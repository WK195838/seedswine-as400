      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : PUR0611                                       *
      * AUTHER       : A1492  TERRY                                  *
      * CREATE DATE  : 101/01/10                                     *
      * UPDATE DATE  :                                               *
      * SYSTEM       :                                             *
      * SUBSYSTEM    :採購系統                                     *
      * REMARK       :採購單彙總表                                 *
      ****************************************************************
     FPOCHPF  IP  E           K        DISK
     FPOCIPF  IF  E           K        DISK
     FPOCDPF  IF  E           K        DISK
     FPOCCLF02IF  E           K        DISK
     FMANBPF  IF  E           K        DISK
     FMANCPF  IF  E           K        DISK
     FSCWK251 UF  E           K        DISK                      A
     ILDA        UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      135 135 $ADD
     I                                      598 5990$PRTID
     I                                      600 600 DOWNL
     I                                      601 602 DCH01
     I                                      603 604 DCH02S
     I                                      605 606 DCH02E
     I                                      607 611 DCH07S
     I                                      612 616 DCH07E
     I                                      617 621 DCH06S
     I                                      622 626 DCH06E
     I                                      627 635 DCI03S
     I                                      636 644 DCI03E
     I                                      645 6500DCD03S
     I                                      651 6560DCD03E
     I                                      657 6620DCH08S
     I                                      663 6680DCH08E
     I                                      669 669 DPRTTP
     I                                      670 6770DATE3S
     I                                      678 6850DATE3E
     I                                      686 6930DATE8S
     I                                      694 7010DATE8E
      *==============================================================*
     C*                    DEFN
     C*==============================================================*
      *                    KLIST
      *==============================================================*
     C           KEYCI1    KLIST
     C                     KFLD           CH04
     C                     KFLD           CH06
     C                     KFLD           DCI03S
     C           KEYCI2    KLIST
     C                     KFLD           CH04
     C                     KFLD           CH06
     C           KEYCC1    KLIST
     C                     KFLD           CH01
     C                     KFLD           CH02
     C                     KFLD           CI03
     C                     KFLD           CC06
     C                     KFLD           CH03
     C           KEYCC2    KLIST
     C                     KFLD           CH01
     C                     KFLD           CH02
     C                     KFLD           CI03
     C                     KFLD           CC06
     C           KEY251    KLIST
     C                     KFLD           W25101
     C                     KFLD           W25102
     C                     KFLD           W25104
     C           KEYNC     KLIST
     C                     KFLD           CH01
     C                     KFLD           CI03
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C*
     C                     EXSR RTN100
     C*
      *==============================================================*
      *                    WRITE SCWK251
      *==============================================================*
01===C           RTN100    BEGSR
|    C*
|    C           CH16      CHAINPOCDPF               40
| 01-C           *IN40     IFEQ *OFF
|   C           CD03      ANDGEDATE3S
|   C           CD03      ANDLEDATE3E
|   C*
|   C           KEYCI1    SETLLPOCIPF
|   C           KEYCI2    READEPOCIPF                   41
| 02-C           *IN41     DOWEQ*OFF
|   C*
| 03-C           CI03      IFGT DCI03E
|   C                     LEAVE
| 03-C                     ENDIF
|   C*
|   C                     EXSR RTN110
|   C*
|   C           KEYCI2    READEPOCIPF                   41
| 02-C                     ENDDO
|   C*
| 01-C                     ENDIF
|    C*
01===C                     ENDSR
      *==============================================================*
      *                    WRITE SCWK251
      *==============================================================*
02===C           RTN110    BEGSR
|    C*
|    C                     CLEARW251
|    C*
|    C                     MOVELCH01      W25101           公司代號
|    C                     MOVELCH06      W25102           供應商代號
|    C           W25102    CHAINMANBPF               40
|    C  N40                MOVELNB03      W25103           供應商簡稱
|    C                     MOVELCI03      W25104           產品代號
|    C           KEYNC     CHAINMANCPF               40
|    C  N40                MOVELNC05      W25105           產品簡稱
|    C*
|    C           KEY251    CHAINSCWK251              40
|    C                     ADD  CI04      W25106           採購數量
|    C*單價取得方式，參考PUC015
|    C*一般及客訂單先取活動價若無再取年度價
|    C                     MOVEL'2'       CC06
|    C                     EXSR RTN111
| 01-C           WCC09     IFEQ 0
|   C                     MOVEL'1'       CC06
|   C                     EXSR RTN111
| 01-C                     ENDIF
|    C                     Z-ADDWCC09     W25107           未稅進價
|    C           CI04      MULT W25107    T25189  90H      未稅金額
|    C                     ADD  T25189    W25108           未稅金額
|    C           T25189    MULT 1.05      T25189    H      含稅金額
|    C                     ADD  T25189    W25109           含稅金額
|    C*
|    C   40                WRITEW251
|    C  N40                UPDATW251
|    C*
02===C                     ENDSR
      *==============================================================*
      *                    取得最新報價單價
      *==============================================================*
03===C           RTN111    BEGSR
|    C*
|    C                     Z-ADD0         WCC09   82
|     *
|    C           KEYCC1    SETGTPOCCLF02
|    C           KEYCC2    REDPEPOCCLF02                 47
| 01-C           *IN47     IFEQ '0'
|    *
| 02-C           CC08      IFNE 0
| 03-C           CH03      IFLE CC08
|   C           CH03      ANDGECC07
|   C                     Z-ADDCC09      WCC09
| 03-C                     ENDIF
| 02*C                     ELSE
| 04-C           CH03      IFGE CC07
|   C                     Z-ADDCC09      WCC09
| 04-C                     ENDIF
| 02-C                     ENDIF
| 01-C                     ENDIF
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*                    INITIAL
     C*==============================================================*
04===C           *INZSR    BEGSR
|    C*
|    C           *NAMVAR   DEFN *LDA      LDA
|    C                     IN   LDA
|    C*
04===C                     ENDSR
     C*==============================================================*
