      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : INR0902                                       *
      * AUTHER       : A1491  EASON                                  *
      * CREATE DATE  : 99/03/11                                      *
      * UPDATE DATE  :                                               *
      * SYSTEM       :西祺進銷存                                   *
      * SUBSYSTEM    :庫存管理系統                                 *
      * REMARK       :銷貨成本表(總表)                           *
      ****************************************************************
      *00A需求單號: 1080438
      *00A108/06/28 CAROL增加篩選條件：供應商、產品歸屬
      *00A               銷售日期改為月/年
      ****************************************************************
     FSCWK22  IP  E           K        DISK
     FMANEPF  IF  E           K        DISK
00A+ FINR090F O   E           K        DISK
     FINR090P O   E             39     PRINTER
     E*
     I*
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      152 161 $USERN
     I                                      111 1160$CHYMD
     I                                      201 2080A8YMD
     I                                      203 2080A6YMD
     I*
     I                                      601 602 DHA01S
     I                                      603 604 DHA01E
     I                                      605 606 DHA02S
     I                                      607 608 DHA02E
     I                                      611 615 DHA04S
     I                                      616 620 DHA04E
     I                                      621 629 DHA05S
     I                                      631 639 DHA05E
     I                                      641 6480WHA03S
     I                                      651 6580WHA03E
     I                                      661 661 DOPT1
     I                                      662 662 DOPT2
 00A-I*                                     671 6760DEK02
 00A+I                                      671 6760DEH02
 00A-I*                                     681 6860DHA03S
 00A-I*                                     691 6960DHA03E
 00A+I                                      681 6840DHA03S
 00A+I                                      686 6890DHA03E
 00A+I                                      701 705 DNB01S
 00A+I                                      706 710 DNB01E
 00A+I                                      711 712 DNC09
     I*
      *==============================================================*
      *                    DEFINE ROUTINE
      *==============================================================*
      *==============================================================*
      *                    KLIST
      *==============================================================*
     C           KEYNE     KLIST
     C                     KFLD           W2201
     C                     KFLD           W2202
     C                     KFLD           W2203
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
      *
     C  N10                EXSR RTN100
      *
     C                     EXSR RTN200
 00A+C                     EXSR RTN500
      *
     CLR 10                EXSR RTN300
     CLRN10                EXSR RTN400
      *
      *===============================================================*
     C*          RTN100....撰寫表頭
      *===============================================================*
01===C           RTN100    BEGSR
|     *
|    C                     SETON                     10
|     *
|    C                     WRITEPH1
|     *
|    C                     SETOF                     39
|     *
01===C                     ENDSR
      *===============================================================*
     C*          RTN200....撰寫報表
      *===============================================================*
02===C           RTN200    BEGSR
|     *
| 01-C           *IN39     IFEQ '1'
|   C                     WRITEPH1
|   C                     SETOF                     39
| 01-C                     ENDIF
|     *
|    C                     MOVEL*BLANK    WK01
|    C                     MOVELW2203     WK01
|    C           KEYNE     CHAINNE0                  40
|    C  N40                MOVELNE05      WK02
|    C   40                MOVEL*BLANK    WK02
|    C                     Z-ADDW2204     WK03
|    C                     Z-ADDW2205     WK04
|    C                     Z-ADDW2206     WK05
|    C                     Z-ADDW2207     WK06
|    C                     Z-ADDW2208     WK07
|    C                     Z-ADDW2209     WK08
|    C                     Z-ADDW2210     WK09
|    C                     WRITEPD1
|     *
|    C                     ADD  W2204     Q1
|    C                     ADD  W2205     Q2
|    C                     ADD  W2206     Q3
|    C                     ADD  W2207     Q4
|    C                     ADD  W2208     Q5
|    C*                    ADD  W2209     Q6
|    C*                    ADD  W2210     Q7
|     *
02===C                     ENDSR
      *===============================================================*
     C*          RTN300....資料結束
      *===============================================================*
03===C           RTN300    BEGSR
|     *
     C           Q5        DIV  Q2        Q6X     54
     C           Q6X       MULT 100       Q6
     C           Q5        DIV  Q4        Q7X     54
     C           Q7X       MULT 100       Q7
|    C                     WRITEPD2
|    C                     WRITEPE2
|     *
03===C                     ENDSR
      *===============================================================*
     C*          RTN400....無資料
      *===============================================================*
04===C           RTN400    BEGSR
|     *
|    C                     WRITEPE9
|     *
04===C                     ENDSR
      *===============================================================*
 00A+C*          RTN500....撰寫INR090F
      *===============================================================*
 00A+C           RTN500    BEGSR
 00A+C*
 00A+C                     MOVELW2203     WF01             分店代號
 00A+C           KEYNE     CHAINNE0                  40
 00A+C  N40                MOVELNE05      WF01A            分店簡稱
 00A+C   40                MOVEL*BLANK    WF01A
 00A+C                     Z-ADDW2204     WF02             銷貨數量
 00A+C                     Z-ADDW2205     WF03             銷貨金額-未稅
 00A+C                     Z-ADDW2206     WF04             銷售佔比
 00A+C                     Z-ADDW2207     WF05             銷貨成本-未稅
 00A+C                     Z-ADDW2208     WF06             銷貨利潤-未稅
 00A+C                     Z-ADDW2209     WF07             獲利率
 00A+C                     Z-ADDW2210     WF08             毛利率
 00A+C                     MOVELDNB01S    WF09             供應商代號起
 00A+C                     MOVELDNB01E    WF10             供應商代號迄
 00A+C                     Z-ADDWHA03S    WF11             銷售起
 00A+C                     Z-ADDWHA03E    WF12             銷售迄
 00A+C                     MOVELDNC09     WF13             產品歸屬
 00A+C*
 00A+C                     WRITEWF0
 00A+C*
 00A+C                     ENDSR
