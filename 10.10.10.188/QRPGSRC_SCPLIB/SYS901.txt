     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : SYS901                                     *
     H*    PROGRAM NAME :網頁人員解除鎖定                          *
     H*    AUTHOR       : NANCY
     H*    CREATE DATE  : 2012.02.10                                 *
     H*    UPDATE DATE  :                                            *
     H****************************************************************
     FMANFPF  UF  E           K        DISK
     FSYS901D CF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN   KSFILE SFLSR
     E*---------------------------------------------------------------*
     E*                   ERR     1  10 60
     E*---------------------------------------------------------------*
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I           SDS
     I                                     *PROGRAM ZZPGM
     I                                      244 253 ZZJOB
     I                                      254 263 ZZUSR
     I           UDS
     I                                      101 110 $USER
     I                                      187 1920DDATE
     I                                      201 2080$A8YMD
     I                                      209 2160ZZYMD
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN RRN       @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN       @PRCD            PAGE REC
     C           *LIKE     DEFN RRN       @LRRN            LAST REC NUM
     C           *LIKE     DEFN RRN       @LPAGE           LAST PAGE
     C           *LIKE     DEFN RRN       @PRRN            PROMPT USE
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           P1KEY     KLIST
     C                     KFLD           ZHNF01
     C           P2KEY     KLIST
     C                     KFLD           Z1NF01
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
     C           *IN03     DOWEQ'0'
     C                     EXSR R1000
     C                     ENDDO
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C                     MOVEL*BLANK    ZHNF01
     C                     MOVEL*BLANK    TPNF01 10        程式代號
     C                     MOVEL*BLANK    Z1FLD   1        登錄狀態
     C*
     C   99                EXSR R1CC0
     C  N99                EXSR R1C00
     C                     ENDSR
     C*==============================================================*
     C*          R1000 ....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C                     WRITEDSPC
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C   03                GOTO E1000
     C*
     C                     EXSR R1E00
     C   98                GOTO E1000
     C*
     C                     EXSR R1B00
     C   99                GOTO E1000
     C*
     C                     EXSR R1D00
     C*
     C           E1000     ENDSR
     C*==============================================================*
     C*          R1B00 ....畫面檢核
     C*==============================================================*
     C           R1B00     BEGSR
     C*
     C           E1B00     ENDSR
     C*==============================================================*
     C*          R1C00 ....MOVE  SCREEN TO SCID
     C*==============================================================*
     C           R1C00     BEGSR
     C*
     C                     EXSR R1CC0                      *INIT SFL
     C                     EXSR R1CD0                      *READ DETAIL
     C*SFLDSP
     C           @LRRN     IFGT 0
     C                     Z-ADD1         DRRN
     C                     SETON                     50    *SFLDSP
     C                     END
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1CC0 ....SUBFILE INIT
     C*=============================================================*
     C           R1CC0     BEGSR
     C*
     C                     Z-ADD15        @PRCD            *PGE RCD
     C                     Z-ADD0         DRRN
     C                     Z-ADD0         RRN
     C                     Z-ADD0         @LRRN            *LAST RRN
     C                     Z-ADD0         @DRCD            *DSP RCD
     C*
     C                     SETOF                     51    *SFLDSPCTL
     C*
     C                     SETON                     52    *SFLCLR
     C                     WRITESFLCR
     C                     SETOF                     52
     C*
     C                     SETON                     5153  *SFLDSPCTL
     C                     SETOF                     50    *SFLDSP
     C*
     C                     ENDSR
     C*==============================================================*
     C*        R1CD0 ....DETAIL DATA TO SFL
     C*==============================================================*
     C           R1CD0     BEGSR
     C*
     C                     SETON                     54    *54
     C                     SETOF                     70    *PROTECT
     C                     MOVEL*BLANK    Z1FLD   1        登錄狀態
     C*
     C           P1KEY     SETLLMANFPF
     C                     READ MANFPF              N    44*NF/EOF
     C           *IN44     DOWEQ'0'
     C           NF08      IFEQ 'Y'                        登錄狀態
     C                     ADD  1         RRN
     C                     MOVELNF08      Z1NF08           登錄狀態
     C                     MOVELNF01      Z1NF01           人員代號
     C                     MOVELNF03      Z1NF03           人員姓名
     C                     WRITESFLSR
     C                     END
     C*
     C                     READ MANFPF              N    44*NF/EOF
     C                     ENDDO
     C*<NO DATA>
     C           RRN       IFEQ *ZERO
     C                     ADD  1         RRN
     C                     MOVEL*BLANK    Z1NF08           登錄狀態
     C                     MOVEL*BLANK    Z1NF01           人員代號
     C                     MOVEL*BLANK    Z1NF03           人員姓名
     C                     SETON                     70    *NON-PROTECT
     C                     WRITESFLSR
     C                     END
     C*
     C                     Z-ADDRRN       @LRRN
     C*
     C                     ENDSR
     C*==============================================================*
     C*        R1D00 ....PROCESS
     C*==============================================================*
     C           R1D00     BEGSR
     C*
     C                     READCSFLSR                  5757
     C           *IN57     DOWEQ'0'
     C           P2KEY     CHAINMANFPF               40
     C           *IN40     IFEQ *OFF
     C           Z1FLD     ANDNEZ1@FLD
     C                     MOVEL*BLANK    NF08             登錄狀態
     C                     UPDATNF0
     C                     END
     C*
     C                     READCSFLSR                    57
     C                     ENDDO
     C*
     C                     EXSR R1C00
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1E00 ....
     C*==============================================================*
     C           R1E00     BEGSR
     C*F9=全選
     C   09                DO
     C                     SETON                     54
     C                     READCSFLSR                  5757
     C           *IN57     DOWEQ'0'
     C   09                MOVEL'N'       Z1FLD            挑選
     C                     UPDATSFLSR
     C                     READCSFLSR                    57
     C                     ENDDO
     C                     SETON                     98
     C                     ENDDO
     C*
     C           TPNF01    IFNE ZHNF01                     程式代號
     C                     EXSR R1C00
     C                     MOVELZHNF01    TPNF01           程式代號
     C                     SETON                     98
     C                     GOTO E1E00
     C                     ENDIF
     C*
     C           E1E00     ENDSR
     C*==============================================================*
