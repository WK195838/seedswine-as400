     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    PROGRAM ID   : INT014                                     *
     A*    AUTHOR       : A1546 VINCENT                              *
     A*    DATE WRITTEN : 97/10/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    REMARK       :現場分店存貨查詢(I)                       *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     FINTOLF01IF  E           K        DISK
     F                                              KINFSR ERRSR
     F            CM0                               KRENAMECM0L
     F            CM0#                              KRENAMECM0#L
     F            TD0                               KRENAMETD0L
     F            TD0#                              KRENAMETD0#L
     F            TD0T                              KRENAMETD0TL
     F            TD0T#                             KRENAMETD0T#L
     F            TF0                               KRENAMETF0L
     F            TH0                               KRENAMETH0L
     F            TL0                               KRENAMETL0L
     F            TJ0                               KRENAMETJ0L
     F            HA0                               KRENAMEHA0L
     F            PI0                               KRENAMEPI0L
     F            TQ0                               KRENAMETQ0L
     FINTOLF02IF  E           K        DISK
     F                                              KINFSR ERRSR
     F                                              KINFDS TORCDN
     F            TQ0                               KRENAMETQ0L2
     FINTBPF  IF  E           K        DISK
     FMANCPF  IF  E           K        DISK
     FMANDPF  IF  E           K        DISK
     FMANEPF  IF  E           K        DISK
     FC#EHPF  IF  E           K        DISK
     FUTL2PF  IF  E           K        DISK
     FUTL3PF  IF  E           K        DISK
     FPA#BPF  IF  E           K        DISK
     FSEHAPF  IF  E           K        DISK
     F            HA0                               KRENAMEHAA
     FINTPLF01IF  E           K        DISK
     FINTQLF01IF  E           K        DISK
     FINT014D CF  E                    WORKSTN
     F                                              KINFDS DSPFDS
     F                                        RRN1  KSFILE SFLSR1
     F                                        RRN2  KSFILE SFLSR2
     E*: FUNCTION DATA :
     E*                   WFUN    6   6  6
     E*: FUNCTION DATA :
     E**                  WTYPE   5   9 10
     E*: A/U/D/I AUT
     E                    WA01        6  1
     E*
     E                    WL303   1   1 10
     I*
     I*SDS        SDS
     I*                                     *PROGRAM APPPGM
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      152 161 $USERN
     I                                      111 1160$CHYMD
 @   I                                      135 135 $ADD
 @   I                                      136 136 $UPD
 @   I                                      137 137 $DLT
 @   I                                      138 138 $INQ
 @   I                                      139 139 $RMK01
 @   I*                                     140 140 $RMK02
 @   I*                                     141 141 $RMK03
     I*                                     162 1620$EVR
     I*                                     125 1270$CPY
     I*                                     598 5990$LPRT
     I*                                     501 505 $WRHUS
     I*                                     506 506 $PURID
     I*                                     507 507 $TRNID
     I*                                     508 512 $APAU1
     I*                                     513 513 $WRHTY
     I*                                     514 521 $#101
     I*                                     522 522 $APAU2
     I            DS
     I                                        1   1 DOPT1
     I                                        1   10WNUM
     I*
     I            DS
     I                                        1   2 TO06
     I                                        1   1 TO06F
     I*
     I            DS
     I                                        1  32 L203
     I                                        1   1 L203F
     I            DS
     I                                        1   60EH02
     I                                        3   40EH02Y
     I                                        5   60EH02M
     I            DS
     I                                        1   40DTB02
     I                                        1   20TB02M
     I                                        3   40TB02Y
     I*
     I* FOR F4 HELP -- GET CURSOR POSITION
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     ITORCDN      DS
     I                                     *RECORD  RCDR
      *==============================================================*
      *                    INITIAL RTN
      *==============================================================*
     C                     MOVE *BLANKS   APPSCR  6
     C                     Z-ADD0         HRRN1   40
     C                     Z-ADD0         HRRN2   40
     C                     MOVE *BLANK    WBOTOM  1
     C                     MOVE *BLANK    WBOTM2  1
      *==============================================================*
      *                    DEFN RTN
      *==============================================================*
     C           *LIKE     DEFN DBGN1     OBGN1            *START KEY
     C           *LIKE     DEFN DBGN2     OBGN2            *START KEY
     C           *LIKE     DEFN DBGN3     OBGN3            *START KEY
     C           *LIKE     DEFN DBGN4     OBGN4            *START KEY
     C           *LIKE     DEFN DBGN1     BBGN1            *SUBFIL BOTTOM
     C           *LIKE     DEFN DBGN2     BBGN2            *SUBFIL BOTTOM
     C           *LIKE     DEFN DBGN3     BBGN3            *SUBFIL BOTTOM
     C           *LIKE     DEFN DBGN4     BBGN4            *SUBFIL BOTTOM
     C**
     C           *LIKE     DEFN DBGN21    OBGN21
     C           *LIKE     DEFN DBGN21    BBGN21+ 2
     C           *LIKE     DEFN DBGN21    WBGN21+ 2
      *==============================================================*
      *                    KLIST
      *==============================================================*
     C           KEYTO     KLIST
     C                     KFLD           DBGN1
     C                     KFLD           DBGN2
     C           KEYTO1    KLIST
     C                     KFLD           DBGN1
     C                     KFLD           DBGN2
     C                     KFLD           DBGN3
     C                     KFLD           DBGN4
     C*
     C           KEYO13    KLIST
     C                     KFLD           BBGN1
     C                     KFLD           BBGN2
     C                     KFLD           BBGN3
     C                     KFLD           BBGN4
     C*
     C           KEYO14    KLIST
     C                     KFLD           TO01
     C                     KFLD           TO02
     C                     KFLD           TO04
     C                     KFLD           TO03
     C*
     C           KEYNC     KLIST
     C                     KFLD           TO01
     C                     KFLD           TO04
     C*
     C           KEYND2    KLIST
     C                     KFLD           DBGN1
     C                     KFLD           DBGN2
     C*
     C           KEYNE     KLIST
     C                     KFLD           TO01
     C                     KFLD           TO02
     C                     KFLD           TO03
     C*
     C           KEYO22    KLIST
     C                     KFLD           TO01
     C                     KFLD           TO02
     C                     KFLD           TO04
     C                     KFLD           TO03
     C                     KFLD           WBGN21           *(YY/MM/DD)
     C*
     C*          KEYR23    KLIST
     C*                    KFLD           BBGN21
     C*
     C           KEYO24    KLIST
     C                     KFLD           TO01
     C                     KFLD           TO02
     C                     KFLD           TO04
     C                     KFLD           TO03
     C*
     C           KEYTB     KLIST
     C                     KFLD           TO01
     C                     KFLD           HTB02
     C                     KFLD           TO02
     C                     KFLD           TO03
     C                     KFLD           TO04
     C*
     C           KEYL2     KLIST
     C                     KFLD           L201
     C                     KFLD           L202
     C*
     C*          KEYME     KLIST
     C*                    KFLD           ME01
     C*                    KFLD           ME02
     C*
     C           KEYL3     KLIST
     C                     KFLD           L301
     C                     KFLD           L302
     C*
     C*          KEYPC     KLIST
     C*                    KFLD           IR13
     C*                    KFLD           IR03
     C           KEYO25    KLIST
     C                     KFLD           TO01
     C                     KFLD           TO02
     C                     KFLD           TO04
     C                     KFLD           TO03
     C                     KFLD           TTO05
     C           KEY1      KLIST
     C                     KFLD           TO01
     C                     KFLD           TO02
     C                     KFLD           TTO05
     C                     KFLD           TO03
     C                     KFLD           TO04
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C*
     C                     Z-ADD0         DTB02                        ENT
     C*
     C                     EXSR RTN010                     *INIT ENVIRMENT
     C                     EXSR RTN191                     *INIT SFL
     C*
     C                     EXSR RTN192                     *READ NEXT PAGE
     C                     EXSR RTN195                     *EXFMT
     C*
  01-C           *IN03     DOWEQ'0'                        *03    OFF
    C*
  02-C           *IN25     CASEQ'1'       RTN192
    C                     CAS            RTN170
  02-C                     END
    C  N03                EXSR RTN195
    C*
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE
     C*==============================================================*
01===C           RTN010    BEGSR
|    C*
|    C**                   MOVE $ADD      WA01,1
|    C**                   MOVE $UPD      WA01,2
|    C**                   MOVE $DLT      WA01,4
|    C                     MOVE $INQ      WA01,5
|    C**                   MOVE $RMK01    WA01,6
|    C*
|    C                     MOVE 'SCR001'  APPSCR
|    C*
01===C                     ENDSR
      *==============================================================*
      *          RTN170....ENTER
      *==============================================================*
02===C           RTN170    BEGSR
|    C*
| 01-C           DBGN1     IFNE OBGN1
|   C           DBGN2     ORNE OBGN2
|   C           DBGN3     ORNE OBGN3
|   C           DBGN4     ORNE OBGN4
|   C                     Z-ADD0         HRRN1
|   C                     MOVEL*BLANK    D#B03
|   C                     MOVEL*BLANK    DND04
|   C                     Z-ADD0         DTB02
|    *新增畫面秀出文字功能
|   C                     EXSR RTN190
|   C                     EXSR RTN191
|   C                     EXSR RTN192
|   C                     MOVE DBGN1     OBGN1
|   C                     MOVE DBGN2     OBGN2
|   C                     MOVE DBGN3     OBGN3
|   C                     MOVE DBGN4     OBGN4
| 01*C                     ELSE
|   C                     EXSR RTN171
| 02-C           *IN99     IFEQ '0'
|   C           *IN03     ANDEQ'0'
|   C                     Z-ADDRRN1      HRRN1
|   C                     EXSR RTN191
|   C                     EXSR RTN192
| 02-C                     END
| 01-C                     END
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTN171....READC SFL
      *==============================================================*
03===C           RTN171    BEGSR
|    C*
|    C                     SETON                     54
|    C*
|    C                     READCSFLSR1                 5757*NF
| 01-C           *IN57     DOWEQ'0'                        *57 LOOP
|   C*
|   C                     SETOF                     60    *DOP *IND
| 02-C           *IN99     IFEQ '0'
|   C*
| 03-C           DOPT1     IFNE *BLANK
|   C                     EXSR RTN180                     *CHK
|   C*
| 04-C           *IN99     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C                     Z-ADDRRN1      DRRN1
|   C                     EXSR RTN200
| 04-C                     END
| 03-C                     END
| 02-C                     END                             *IN99
|   C*
|   C           RRN1      CHAINSFLSR1               40
|   C  N99                MOVE *BLANKS   DOPT1
|   C                     UPDATSFLSR1
|   C                     READCSFLSR1                 5757*NF
|   C*
| 01-C                     END
|    C*
| 05-C           *IN99     IFEQ '0'
|   C                     SETOF                     5412
|   C                     SETON                     95
| 05-C                     END
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          RTN180....CHECK 'SC01' SCREEN
     C*==============================================================*
04===CSR         RTN180    BEGSR
|    C*
|    C* CHK AUT *
| 01-C           WA01,WNUM IFNE 'Y'                         WNUM = DOPT
|   C                     SETON                     609699
|   C                     MOVEL'ERR9999' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 01-C                     END
|    C   99                GOTO END180
|    C*
|    C           END180    TAG
|    C   99                Z-ADDRRN1      DRRN1
|    C*
04===C                     ENDSR
      *==============================================================*
      *          RTN190....READY 'SC01' HEAD SCREEN
      *==============================================================*
05===CSR         RTN190    BEGSR
|    C*
|    C           DBGN1     CHAIN#B0                  40
|    C  N40                MOVEL#B03      D#B03
|    C   40                MOVEL*BLANK    D#B03
|    C*
|    C           KEYND2    CHAINND0                  40
|    C  N40                MOVELND04      DND04
|    C   40                MOVEL*BLANK    DND04
|    C*
|    C           DBGN1     CHAINEH0                  40
| 01-C           *IN40     IFEQ '0'
|   C                     Z-ADDEH02      HTB02
|   C                     Z-ADDEH02Y     TB02Y
|   C                     Z-ADDEH02M     TB02M
| 01*C                     ELSE
|   C                     Z-ADD0         HTB02
|   C                     Z-ADD0         DTB02
| 01-C                     ENDIF
|    C*
05===C                     ENDSR
     C*=========================================================*
     C*          RTN191....INITIAL                              *
     C*=========================================================*
06===C           RTN191    BEGSR
|    C*
|    C                     MOVE *BLANKS   DOPT1
|    C                     Z-ADD0         DRRN1
|    C                     Z-ADD0         RRN1
|    C                     Z-ADD0         WRRN1   40
|    C                     Z-ADD10        WPRCD1  30
|    C                     Z-ADD0         DSPRC1  40
|    C           HRRN1     DIV  WPRCD1    DSPRC1
|    C                     MULT WPRCD1    DSPRC1
|    C                     MOVE 'N'       WBOTOM
|    C                     SETOF                     53
|    C                     SETON                     5254
|    C                     WRITESFLCR1
|    C                     SETOF                     52
|    C           KEYTO1    SETLLINTOLF01
|    C                     MOVE DBGN1     BBGN1
|    C                     MOVE DBGN2     BBGN2
|    C                     MOVE DBGN3     BBGN3
|    C                     MOVE DBGN4     BBGN4
|    C*
06===C                     ENDSR
      *=========================================================*
      *          RTN192 .. ROLLUP MAIN ROUTINE
      *=========================================================*
07===C           RTN192    BEGSR
|    C*
| 01-C           WBOTOM    IFEQ 'Y'
|   C                     SETON                     98    *LST PAG
|   C                     Z-ADDRRN1      DRRN1
| 01*C                     ELSE
| 02-C           BBGN1     IFNE DBGN1
|   C           BBGN2     ORNE DBGN2
|   C           BBGN3     ORNE DBGN3
|   C           BBGN4     ORNE DBGN4
|   C           KEYO13    SETGTINTOLF01
| 02-C                     END
|   C                     EXSR RTN193
|   C                     MOVE TO01      BBGN1
|   C                     MOVE TO02      BBGN2
|   C                     MOVE TO04      BBGN3
|   C                     MOVE TO03      BBGN4
|   C           *LOVAL    SETLLINTOLF01
| 01-C                     END
|    C*
07===C                     ENDSR
      *=========================================================*
      *          RTN193 .. ADD SUBFILE DATA
      *=========================================================*
08===C           RTN193    BEGSR
|    C*
|    C                     SETOF                     54
|    C                     MOVE *BLANKS   DOPT1
|    C                     Z-ADDWRRN1     RRN1
|    C                     ADD  WPRCD1    DSPRC1
|    C*
| 01-C           RRN1      DOWLTDSPRC1
|   C           KEYTO     READEINTOLF01                 46
|   C**
| 02-C           *IN46     IFEQ '0'
|   C* CHAIN 分庫庫存量
|   C           KEYTB     CHAINTB0                  40
| 03-C           *IN40     IFEQ '0'
|   C           TB06      ADD  TB08      TBTT2            帳列庫存量
|   C                     SUB  TB09      TBTT2
|   C                     ADD  TB10      TBTT2
|   C                     SUB  TB11      TBTT2
|   C                     ADD  TB12      TBTT2
|   C                     SUB  TB13      TBTT2
|   C                     ADD  TB14      TBTT2
|   C                     SUB  TB15      TBTT2
|   C           TB16      ADD  TB17      TBTT3            陳列量
     C                     SUB  TB18      TBTT3
     C           TB17      ADD  TB06      TB06X
|   C           TBTT2     ADD  TBTT3     TBTT1            現場庫存量
| 03*C                     ELSE
|   C                     Z-ADD0         TB06
|   C                     Z-ADD0         TBTT1
|   C                     Z-ADD0         TBTT2
|   C                     Z-ADD0         TBTT3
|   C                     Z-ADD0         TB16
| 03-C                     END
|   C*
|   C                     ADD  1         RRN1
|   C                     WRITESFLSR1
|   C           KEYO14    SETGTINTOLF01
|   C*
| 02*C                     ELSE
|   C                     Z-ADDRRN1      DSPRC1
|   C                     MOVE 'Y'       WBOTOM
|   C                     SETON                     53
| 02-C                     END
|   C*
| 01-C                     END
|    C                     Z-ADDRRN1      DRRN1      50
|    C                     Z-ADDRRN1      WRRN1
|    C*
08===C                     ENDSR
      *==============================================================*
      *          RTN195... EXFMT SFL                                 *
      *==============================================================*
09===C           RTN195    BEGSR
|    C*
| 01-C           RRN1      IFLE *ZEROS
| 02-C           DBGN1     IFNE *BLANK                     公司
|   C           DBGN2     ORNE *BLANK                     通路
|   C*
|   C* CHK公司參數檔
| 03-C           DBGN1     IFNE *BLANK
|   C           DBGN1     CHAIN#B0                  40
| 04-C           *IN40     IFEQ '0'
|   C           DBGN1     CHAINEH0                  40
| 05-C           *IN40     IFEQ '1'
|   C                     SETON                     99
|   C                     MOVEL'ERR0049' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 05-C                     ENDIF
| 04-C                     ENDIF
| 03-C                     ENDIF
|   C   99                GOTO END195
|   C*
|   C                     SETON                     99    *NODATA
|   C                     MOVEL'MSG0003' ERRID
|   C                     MOVEL'SCMF'    ERRF
|   C           END195    TAG
| 02-C                     END
|   C*
| 01*C                     ELSE
|   C                     SETON                     50    *SFLDSP
| 01-C                     END
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR1
|    C                     SETOF                     5051
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
09===C                     ENDSR
      *==============================================================*
      *          RTN200....'SC02' SCREEN MAIN ROUTINE
      *==============================================================*
10===C           RTN200    BEGSR
|     *
|    C                     MOVE 'SCR002'  APPSCR
|    C**                   MOVE WFUN,WNUM DFUN
|     *產品名稱
|    C           KEYNC     CHAINNC0                  46
| 01-C           *IN46     IFEQ '0'
|   C                     MOVE NC05      DNC05
|   C                     MOVE NC14      DNC14
| 01*C                     ELSE
|   C                     MOVEL*BLANKS   DNC05
|   C                     MOVE *BLANKS   DNC14
| 01-C                     ENDIF
|     *分店名稱
|    C           KEYNE     CHAINNE0                  46
|    C  N46                MOVELNE05      DNE05
|    C   46                MOVEL*BLANKS   DNE05
|     *
| 02-C           DOPT1     CASEQ'5'       RTN240           *INQ
| 02-C                     END
|    C           *LOVAL    SETLLINTOLF02
|    C*
10===C                     ENDSR
      *==============================================================*
      *          RTN240....INQ DATA
      *==============================================================*
11===C           RTN240    BEGSR
|    C*
|    C                     Z-ADD123129    DBGN21           *日期MDY
|    C                     Z-ADD123129    OBGN21           *日期MDY
|    C                     Z-ADD20291231  WBGN21           *日期YMD
|    C*
|    C                     EXSR RTN291                     *INT SFLSR
|    C                     EXSR RTN292                     *READ DATA TO SFLSR
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           *IN12     ANDEQ'0'                        *CTL LOOP
|   C           APPSCR    ANDEQ'SCR002'
|   C*
| 02-C           RRN2      IFGT 0
|   C                     SETON                     50    *SFLDSP
| 02*C                     ELSE
|   C                     SETON                     94    *NO DTA
| 02-C                     END
|   C*
|   C                     WRITEDSPC2
|   C                     EXFMTSFLCR2
|   C                     SETOF                     50
|   C                     MOVEA*ALL'0'   *IN,60
|   C*
| 03-C           *IN03     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C*
| 04-C           *IN25     CASEQ'1'       RTN292
|   C                     CAS            RTN270
| 04-C                     END
|   C*
| 03*C                     ELSE
|   C                     MOVE 'SCR001'  APPSCR
| 03-C                     END
|   C*
| 01-C                     END                             *IN12 ENDIF
|    C*
11===C                     ENDSR
      *==============================================================*
      *          RTN253....MOVE DATA TO SFLSR2
      *==============================================================*
12===CSR         RTN253    BEGSR
|    C******
|    C*(異動日期-CHANGE TO MM/DD/YY)
|    C******
|    C                     Z-ADDTO05      XTO05   60
|    C                     CALL 'RES001'
|    C                     PARM 'YMD'     S001I1  3
|    C                     PARM XTO05     S001I2  60
|    C           DTO05     PARM           S001O1  60
|    C***
|    C*(CHAIN單別說明)
|    C***
|    C                     MOVE *BLANK    L301
|    C                     MOVE *BLANK    L302
|    C                     MOVELTO06F     L301
|    C                     MOVELTO06      L302
|    C           KEYL3     CHAINL30                  47
|    C   47                MOVE *BLANK    DL303
|    C  N47                MOVELL303      DL303
|    C* 調撥在途
| 01-C           RCDR      IFEQ 'TD0T'
|   C           RCDR      OREQ 'TD0T#'
|   C                     MOVELWL303,1   DL303
| 01-C                     ENDIF
|    C******
|    C*(數量)
|    C******
|    C                     Z-ADD0         DQTY
|    C                     Z-ADD0         DQTY1
|    C                     MOVE *BLANK    DMEMO
|    C                     MOVE *BLANK    L201
|    C                     MOVEL'INTOLF ' L201
|    C                     MOVELRCDR      L202
|    C           KEYL2     CHAINL20                  48
| 02-C           *IN48     IFEQ '0'
| 03-C                     SELEC
|   C           L203F     WHEQ '-'
|   C           TO121     MULT -1        DQTY
|   C           L203F     WHEQ '@'
|   C           TO121     MULT -1        DQTY
|   C                     OTHER
|   C                     Z-ADDTO121     DQTY
| 03-C                     ENDSL
| 04-C           TO06      IFEQ 'E1'
|   C           DQTY      MULT -1        DQTY1
|   C                     MOVEL'陳列'  DMEMO
| 04-C                     ENDIF
| 02-C                     ENDIF
|    C*
     C                     SETON                     61
     C                     Z-ADDTO05      TTO05
     C           TO06      IFEQ 'S1'
     C           KEY1      CHAINHAA                  62
     C           *IN62     IFEQ '0'
     C           HA09      CHAININTPLF01             62
     C           *IN62     IFEQ '0'
     C                     SETOF                     61
     C                     ELSE
     C           HA09      CHAININTQLF01             62
     C           *IN62     IFEQ '0'
     C                     SETOF                     61
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
|    C*
12===CSR                   ENDSR
      *==============================================================*
      *          RTN270....ENTER
      *==============================================================*
13===C           RTN270    BEGSR
|    C*
| 01-C           DBGN21    IFNE OBGN21
| 02-C           DBGN21    IFEQ 0
|   C                     Z-ADD123129    DBGN21
| 02-C                     END
|   C*
|   C                     Z-ADD0         HRRN2
|   C                     EXSR RTN291
|   C                     EXSR RTN292
|   C                     MOVE DBGN21    OBGN21
     C                     ELSE
|    C                     READCSFLSR2                 5757*NF
| 01-C           *IN57     DOWEQ'0'                        *57 LOOP
|   C*
| 03-C           DOPT2     IFNE *BLANK
|   C*
     C                     EXSR RTN271
     C                     EXFMTDSPW
     C                     SETOF                     12
     C                     ENDIF
|   C           RRN2      CHAINSFLSR2               40
|   C  N99                MOVE *BLANKS   DOPT2
|   C                     UPDATSFLSR2
|   C                     READCSFLSR2                 5757*NF
|   C*
| 01-C                     ENDDO
| 01-C                     END
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          RTN291....INIT 'SC02' -- SFLSR2
     C*==============================================================*
14===C           RTN291    BEGSR
|    C**
|    C                     Z-ADD0         DRRN2
|    C                     Z-ADD0         WRRN2   40
|    C                     Z-ADD0         RRN2
|    C                     Z-ADD0         DSPRC2  40       *SFL SIZE
|    C                     Z-ADD10        WPRCD2  40       *PAG RCD
|    C           HRRN2     DIV  WPRCD2    DSPRC2
|    C                     MULT WPRCD2    DSPRC2
|    C                     MOVE 'N'       WBOTM2  1        *BOTTOM
|    C                     SETOF                     53
|    C*
|    C                     SETON                     5254  *SFLCLR
|    C                     WRITESFLCR2
|    C                     SETOF                     525053
|    C                     SETON                     51    *SFLDSPCTL
|    C*
|    C*(CHANGE TO YY/MM/DD FOR SETLL FILE)
|    C                     Z-ADD0         XBGN21  60
|    C                     CALL 'RES001'
|    C                     PARM 'MDY'     S001I1  3
|    C                     PARM DBGN21    S001I2  60
|    C           XBGN21    PARM           S001O1  60
|    C                     MOVE XBGN21    HB2@F6  6
|    C                     EXSR HB@68L
|    C                     MOVE HB2@T8    WBGN21
|    C           KEYO22    SETLLINTOLF02
|    C*
14===C                     ENDSR
     C*==============================================================*
     C*          RTN292....ROLLUP SCREEN2
     C*==============================================================*
15===C           RTN292    BEGSR
|    C*
| 01-C           WBOTM2    IFEQ 'Y'
|   C                     SETON                         98
|   C                     Z-ADDRRN2      DRRN2
| 01*C                     ELSE
|   C                     EXSR RTN293
| 01-C                     END
|    C*
15===C                     ENDSR
     C*==============================================================*
     C*          RTN293....ADD SUBFILE DATA -- SRCREEN2
     C*==============================================================*
16===C           RTN293    BEGSR
|    C*
|    C                     SETOF                         54
|    C****                 MOVE *BLANKS   DOPT2
|    C                     Z-ADDWRRN2     RRN2
|    C                     ADD  WPRCD2    DSPRC2
|    C*
|    C*
| 01-C           RRN2      DOWLTDSPRC2
|   C*
|   C           KEYO24    READEINTOLF02                 44*NF/EOF
| 02-C           *IN44     IFEQ '0'
|   C*
|   C                     EXSR RTN253                     *MOVE->SCRN
|   C                     ADD  1         RRN2
|   C                     WRITESFLSR2
| 02*C                     ELSE
|   C                     Z-ADDRRN2      DSPRC2
|   C                     MOVE 'Y'       WBOTM2
|   C                     SETON                     53
| 02-C                     END
|   C*
| 01-C                     END
|    C*
|    C*
|    C                     Z-ADDRRN2      DRRN2      50
|    C                     Z-ADDRRN2      WRRN2
|    C*
16===C                     ENDSR
     C*==============================================================*
     C*          RTN271....MOVE DATA
     C*==============================================================*
16===C           RTN271    BEGSR
|    C*
|    C                     Z-ADD0         W01
|    C                     Z-ADD0         W02
|    C                     Z-ADD0         W03
|    C*
|    C           KEYO25    SETLLINTOLF02
     C           KEYO25    READEINTOLF02                 62
     C           *IN62     DOWEQ'0'
     C                     ADD  TO121     W01
     C           KEYO25    READEINTOLF02                 62
     C                     ENDDO
|    C*
     C           KEY1      CHAINHAA                  62
     C           *IN62     IFEQ '0'
     C           HA09      SETLLINTPLF01
     C           HA09      READEINTPLF01                 63
     C           *IN63     DOWEQ'0'
     C                     ADD  TP06      W02
     C           HA09      READEINTPLF01                 63
     C                     ENDDO
     C           HA09      SETLLINTQLF01
     C           HA09      READEINTQLF01                 63
     C           *IN63     DOWEQ'0'
     C                     ADD  TQ06      W03
     C           HA09      READEINTQLF01                 63
     C                     ENDDO
     C                     ENDIF
|    C*
     C                     SUB  W02       W01
     C                     SUB  W03       W01
|    C*
16===C                     ENDSR
     C*==============================================================*
     C*          ERRSR ....DATA LOCK ROUTINE
     C*==============================================================*
17===C           ERRSR     BEGSR
|    C*
|    C                     CALL 'P50'
|    C           DMSG      PARM           P5001O 78
|    C                     SETON                     9799
|    C*
17===C                     ENDSR'*DETC'
     C/COPY HBP2CVTR
**
調撥在途
