     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H*    PROGRAM-ID   : PIA0091                                    *
     H*    PROGRAM NAME :計算12/25庫存量                         *
     H*    AUTHOR       : RACHEL                                     *
     H*    CREATE DATE  : 2022/09/20                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :庫存系統作業                              *
     H*    SUBSYSTEM    :盤點系統　　                              *
     H*    REMARK       :計算盤點日系統數量&差異量、盤差金額     *
     H*                 :未稅成本                                  *
     H****************************************************************
     FPIZAPF  IP  E           K        DISK
     FPIZBPF  IF  E           K        DISK
     FMANCPF  IF  E           K        DISK
     FMANEPF  IF  E           K        DISK
     FINTBPF  IF  E           K        DISK
     FPOCCLF08IF  E           K        DISK
     FPIA9WF  O   E           K        DISK                      A
     FPIA9LF01UF  E           K        DISK                      A
     F            PA9                               KRENAMEPA9L
     E*--------------------------------------------------------------*
     IZA0
     I*公司別
     I                                              ZA01  L1
     I*通路代號
     I                                              ZA02  L1
     I*分店代號
     I                                              ZA04  L1
     I*盤點日期
     I                                              ZA03  L1
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      201 2080$A8YMD
     I                                      500 500 $DATA
     I* PROGRAM USE DATA DEFINE ++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DPI01
     I                                      503 504 DPI02
     I                                      505 5080DPI03
     I                                      509 5140DPI04S
     I                                      509 5100DPI4SM
     I                                      511 5120DPI4SD
     I                                      515 5200DPI04E
     I                                      515 5160DPI4EM
     I                                      517 5180DPI4ED
     I                                      521 5260DPIYM
     I                                      527 5340DZA03S
     I                                      535 5420DZA03E
     I           IDS
     I                                        1   80WZA03
     I                                        1   60WZA3YM
     I                                        1   40WZA03Y
     I                                        5   60WZA03M
     I                                        7   80WZA03D
     I           IDS
     I                                        1   80KP905
     I                                        7   80KP905D
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYNC     KLIST
     C                     KFLD           ZA01             公司
     C                     KFLD           P904             產品代號
     C           KEYNE     KLIST
     C                     KFLD           ZA01             公司
     C                     KFLD           ZA02             通路
     C                     KFLD           ZA04             分店
     C           KEYZB     KLIST
     C                     KFLD           ZA01             公司
     C                     KFLD           ZA02             通路
     C                     KFLD           ZA05             盤點單號
     C           KEYTB     KLIST
     C                     KFLD           ZA01             公司
     C                     KFLD           WZA3YM           年月
     C                     KFLD           ZA02             通路
     C                     KFLD           ZA04             分店
     C           KEYCC     KLIST
     C                     KFLD           P904             產品代號
     C                     KFLD           KP905            生效起日
     C           KEYA9     KLIST
     C                     KFLD           TB01             公司
     C                     KFLD           TB03             通路
     C                     KFLD           TB04             分店
     C                     KFLD           TB05             產品代號
     C*==============================================================*
     C*                    DEFN ROUTINE
     C*==============================================================*
     C*
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R0100                      .INIT
     C*
     C   L1                EXSR R7100
     C                     EXSR R1000                                  L
     C*
     CL1 10                EXSR R8100
     C*==============================================================*
     C*          R0100 ... INITIAL
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C                     SETON                     10
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R7100 ... LEVEL BREAK L1 --- INITIAL
     C*==============================================================*
02===C           R7100     BEGSR
|    C*
|    C           KEYNE     CHAINNE0                  40
| 01-C           *IN40     IFEQ '0'
|   C                     MOVELNE04      P903NM    P      分店全稱
| 01*C                     ELSE
|   C                     MOVEL*BLANK    P903NM
| 01-C                     ENDIF
|    C*
02===C                     ENDSR
     C*==============================================================*
     C*          R1000 ... PROCESS
     C*==============================================================*
03===C           R1000     BEGSR
|    C*
|    C                     MOVELZA01      P901      P      公司別
|    C                     MOVELZA02      P902      P      通路代號
|    C                     MOVELZA04      P903      P      分店代號
|    C                     Z-ADDZA03      P905             盤點日期
|    C                     MOVELZA05      P906      P      盤點單號
|    C*
|    C           KEYZB     SETLLZB0
|    C           KEYZB     READEZB0                      46
| 01-C           *IN46     DOWEQ'0'
|   C*
|   C                     MOVELZB05      P904      P      產品代號
|   C           KEYNC     CHAINNC0                  40
| 02-C           *IN40     IFEQ '0'
|   C                     MOVELNC05      P904NM    P      中文簡稱
| 02*C                     ELSE
|   C                     MOVEL*BLANK    P904NM
| 02-C                     ENDIF
|   C*供應商產品報價檔
|   C                     Z-ADD0         P907             未稅成本
|   C*以25日為生效起日查詢報價檔的單價
|   C                     Z-ADDDZA03S    KP905            生效起日
|   C                     Z-ADD25        KP905D
|   C           KEYCC     SETGTCC0
|   C                     READ CC0                      40
| 03-C           *IN40     DOWEQ'0'
| 04-C           P905      IFGE CC07
|   C                     Z-ADDCC09      P907             未稅成本
|   C                     LEAVE
| 04*C                     ELSE
|   C                     READ CC0                      40
| 04-C                     ENDIF
| 03-C                     END
|   C*計算某日庫存量
|   C                     CALL 'MCC710'
|   C                     PARM ZA01      XTB01   2        公司別
|   C                     PARM ZA02      XTB03   2        通路代號
|   C                     PARM ZA04      XTB04   5        分店代號
|   C                     PARM ZB05      XTB05   9        產品代號
|   C                     PARM ZA03      XTX06   80       日期
|   C                     PARM 0         XTB07   90       庫存數量
|   C                     PARM 0         XTB17   90       陳列數量
|   C*
|   C           XTB07     ADD  XTB17     P908             系統數量
|   C                     Z-ADDZB06      P909             盤點量
|   C           P909      SUB  P908      P910             差異量
|   C           P907      MULT P910      P911      H      盤差金額
|   C                     Z-ADD0         P912             銷售數量
|   C                     Z-ADD0         P913             進貨數量
|   C                     Z-ADD0         P914             系統異動調整
|   C                     Z-ADD0         P915             實際庫存量
|   C                     Z-ADD0         P916             12/25系統庫存量
|   C                     Z-ADD0         P917             12/25盤盈虧數量
|   C                     Z-ADD0         P918             12/25盤盈虧金額
|   C                     MOVEL'PIA0091' P9WW
|   C                     Z-ADD$A8YMD    P9XX
|   C                     TIME           P9YY
|   C                     MOVEL$USER     P9ZZ      P
|   C*
|   C                     MOVEL'Y'       $DATA
|   C*
|   C                     WRITEPA9
|   C*
|   C           KEYZB     READEZB0                      46
| 01-C                     ENDDO
|    C*
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          R8100 ... 補缺漏產品
     C*==============================================================*
04===C           R8100     BEGSR
|    C*
|    C                     Z-ADDZA03      WZA03
| 01-C           WZA03D    IFGT 25
| 02-C           WZA03M    IFEQ 12
|   C                     Z-ADD1         WZA03M
|   C                     ADD  1         WZA03Y
| 02*C                     ELSE
|   C                     ADD  1         WZA03M
| 02-C                     ENDIF
| 01-C                     ENDIF
|    C*
|    C           KEYTB     SETLLTB0
|    C           KEYTB     READETB0                      46
| 03-C           *IN46     DOWEQ'0'
|   C           *LOVAL    SETLLPA9L
|   C           KEYA9     CHAINPA9L                 40
| 04-C           *IN40     IFEQ '1'
|   C                     MOVELTB01      P901      P      公司別
|   C                     MOVELTB03      P902      P      通路代號
|   C                     MOVELTB04      P903      P      分店代號
|   C                     Z-ADDZA03      P905             盤點日期
|   C                     MOVEL*BLANK    P906      P      盤點單號
|   C*
|   C                     MOVELTB05      P904      P      產品代號
|   C           KEYNC     CHAINNC0                  40
| 05-C           *IN40     IFEQ '0'
|   C                     MOVELNC05      P904NM    P      中文簡稱
| 05*C                     ELSE
|   C                     MOVEL*BLANK    P904NM
| 05-C                     ENDIF
|   C*供應商產品報價檔
|   C                     Z-ADD0         P907             未稅成本
|   C*以25日為生效起日
|   C           KEYCC     SETGTCC0
|   C                     READ CC0                      40
| 06-C           *IN40     DOWEQ'0'
| 07-C           P905      IFGE CC07
|   C                     Z-ADDCC09      P907             未稅成本
|   C                     LEAVE
| 07*C                     ELSE
|   C                     READ CC0                      40
| 07-C                     ENDIF
| 06-C                     END
|   C*計算某日庫存量
|   C                     CALL 'MCC710'
|   C                     PARM TB01      XTB01   2        公司別
|   C                     PARM TB03      XTB03   2        通路代號
|   C                     PARM TB04      XTB04   5        分店代號
|   C                     PARM TB05      XTB05   9        產品代號
|   C                     PARM ZA03      XTX06   80       日期
|   C                     PARM 0         XTB07   90       庫存數量
|   C                     PARM 0         XTB17   90       陳列數量
|   C*
|   C           XTB07     ADD  XTB17     P908             庫存量
|   C                     Z-ADD0         P909             盤點量
|   C           P909      SUB  P908      P910             差異量
|   C           P907      MULT P910      P911      H      盤差金額
|   C                     Z-ADD0         P912             銷售數量
|   C                     Z-ADD0         P913             進貨數量
|   C                     Z-ADD0         P914             系統異動調整
|   C                     Z-ADD0         P915             實際庫存量
|   C                     Z-ADD0         P916             12/25系統庫存量
|   C                     Z-ADD0         P917             12/25盤盈虧數量
|   C                     Z-ADD0         P918             12/25盤盈虧金額
|   C                     MOVEL'PIA0091' P9WW
|   C                     Z-ADD$A8YMD    P9XX
|   C                     TIME           P9YY
|   C                     MOVEL$USER     P9ZZ      P
|   C*
|   C                     MOVEL'Y'       $DATA
|   C*
|   C                     WRITEPA9L
| 04-C                     ENDIF
|   C*
|   C           KEYTB     READETB0                      46
| 03-C                     ENDDO
|    C*
04===C                     ENDSR
