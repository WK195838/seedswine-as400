     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     H*                                                              *
     H* PROGRAM NAME : INR100                                        *
     H* AUTHER       : DEREK                                         *
     H* CREATE DATE  : 2012/12/07                                    *
     H* UPDATE DATE  :                                               *
     H* SYSTEM       :                                             *
     H* SUBSYSTEM    :庫存系統                                     *
     H* REMARK       :產品異動報表                                 *
     H****************************************************************
     FINTOLF10IF  E           K        DISK
     F                                              KINFDS TORCDN
     FMANCPF  IF  E           K        DISK
     FMANEPF  IF  E           K        DISK
     FUTL2PF  IF  E           K        DISK
     FUTL3PF  IF  E           K        DISK
     FSCWK26  O   E                    DISK
     F*---------------------------------------------------------------*
     E                    WL303   1   1 10
     E*---------------------------------------------------------------*
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      135 135 $ADD
     I                                      598 5990$PRTID
     I                                      600 600 DOWNL
     I                                      601 602 DTO01
     I                                      603 604 DDOC
     I                                      605 606 DTO02S
     I                                      607 608 DTO02E
     I                                      609 613 DTO03S
     I                                      614 618 DTO03E
     I                                      619 6190DATA
     I                                      621 629 DTO04S
     I                                      631 639 DTO04E
     I                                      641 6460DTO05S
     I                                      651 6560DTO05E
     I                                      661 6680DATES
     I                                      671 6780DATEE
     I* FOR F4 HELP -- GET CURSOR POSITION
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     ITORCDN      DS
     I                                     *RECORD  RCDR
     I            DS
     I                                        1   2 TO06
     I                                        1   1 TO06F
     I*
     I            DS
     I                                        1  32 L203
     I                                        1   1 L203F
     C*==============================================================*
     C*                    DEFINE
     C*==============================================================*
     C           *LIKE     DEFN TO01      KTO01            公司別
     C           *LIKE     DEFN TO02      KTO02            通路別
     C           *LIKE     DEFN TO03      KTO03            分店代號
     C           *LIKE     DEFN TO04      KTO04            產品代號
     C           *LIKE     DEFN TO05      KTO05            進出日期
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYTO     KLIST
     C                     KFLD           KTO01            公司別
     C                     KFLD           KTO02            通路別
     C                     KFLD           KTO03            分店代號
     C                     KFLD           KTO04            產品代號
     C                     KFLD           KTO05            進出日期
     C           KEYNC     KLIST
     C                     KFLD           TO01             公司別
     C                     KFLD           TO04             產品代號
     C           KEYNE     KLIST
     C                     KFLD           TO01             公司別
     C                     KFLD           TO02             通路別
     C                     KFLD           TO03             分店代號
     C           KEYL2     KLIST
     C                     KFLD           L201             代碼欄位
     C                     KFLD           L202             代碼內容
     C           KEYL3     KLIST
     C                     KFLD           L301             單別類型
     C                     KFLD           L302             單別
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      .INIT
     C           KEYTO     SETLLINTOLF10
     C           KTO01     READEINTOLF10                 46
     C           *IN46     DOWEQ'0'
     C                     EXSR R1000
     C           KTO01     READEINTOLF10                 46
     C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ... INITIAL
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C                     MOVELDTO01     KTO01     P      公司
     C                     MOVELDTO02S    KTO02     P      通路
     C                     MOVELDTO03S    KTO03     P      分店代號
     C                     MOVELDTO04S    KTO04     P      產品代號
     C                     Z-ADDDATES     KTO05            進出日期
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1000 ... WRITE WORKFILE
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C*通路代號
     C           TO02      IFLT DTO02S
     C           TO02      ORGT DTO02E
     C                     GOTO E1000
     C                     END
     C*分店代號
     C           TO03      IFLT DTO03S
     C           TO03      ORGT DTO03E
     C                     GOTO E1000
     C                     END
     C*產品代號
     C           TO04      IFLT DTO04S
     C           TO04      ORGT DTO04E
     C                     GOTO E1000
     C                     END
     C*進出日期
     C           TO05      IFLT DATES
     C           TO05      ORGT DATEE
     C                     GOTO E1000
     C                     END
     C*
     C                     MOVELTO01      W2601     P      公司代號
     C                     MOVELTO02      W2602     P      通路代號
     C                     MOVELTO03      W2603     P      分店代號
     C*分店簡稱
     C           KEYNE     CHAINNE0                  40
     C           *IN40     IFEQ '0'
     C                     MOVELNE05      W2604     P
     C                     ELSE
     C                     MOVEL*BLANK    W2604
     C                     END
     C*
     C                     MOVELTO04      W2605     P      產品代號
     C*產品簡稱
     C           KEYNC     CHAINNC0                  40
     C           *IN40     IFEQ '0'
     C                     MOVELNC05      W2606     P
     C                     ELSE
     C                     MOVEL*BLANK    W2606
     C                     ENDIF
     C*
     C                     Z-ADDTO05      W2607            進出日期
     C                     MOVELTO06      W2608            單別
     C*單別名稱
     C                     MOVELTO06F     L301      P
     C                     MOVELTO06      L302      P
     C           KEYL3     CHAINL30                  40
     C           *IN40     IFEQ '0'
     C                     MOVELL303      W2609     P
     C                     ELSE
     C                     MOVEL*BLANK    W2609
     C                     END
     C*調撥在途
     C           RCDR      IFEQ 'TD0T'
     C           RCDR      OREQ 'TD0T#'
     C                     MOVELWL303,1   W2609     P
     C                     ENDIF
     C*
     C                     MOVELTO07      W2610     P      單據編號
     C*數量
     C                     Z-ADD0         W2611
     C                     MOVEL'INTOLF ' L201      P
     C                     MOVELRCDR      L202      P
     C           KEYL2     CHAINL20                  40
     C           *IN40     IFEQ '0'
     C                     SELEC
     C           L203F     WHEQ '-'
     C           TO121     MULT -1        W2611
     C           L203F     WHEQ '@'
     C           TO121     MULT -1        W2611
     C                     OTHER
     C                     Z-ADDTO121     W2611
     C                     ENDSL
     C                     ENDIF
     C*
     C           W2611     IFGT 0
     C                     MOVEL'I'       W2690            進
     C                     END
     C*
     C           W2611     IFLT 0
     C                     MOVEL'O'       W2690            出
     C                     END
     C*
     C                     WRITEW26
     C*
     CSR         E1000     ENDSR
     C*===============================================================*
**
調撥在途
