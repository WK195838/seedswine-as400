     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : SER0505                                    *
     H*    PROGRAM NAME :通路銷售明細表-分店                     *
     H*    AUTHOR       : DEREK                                      *
     H*    CREATE DATE  : 2013/06/25                                 *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :採購管理                                  *
     H****************************************************************
     FSCWK072 IP  E           K        DISK
     FMANDPF  IF  E           K        DISK
     FPA#BPF  IF  E           K        DISK
     FSER051P O   E             39     PRINTER
     E*---------------------------------------------------------------
     IWK07
     I                                              W701  L1
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$PRTID
     I                                      550 550 @K01
     I                                      601 602 DHA01
     I                                      603 604 DHA02
     I                                      605 6100DHA03S
     I                                      611 6160DHA03E
     I                                      617 617 DATA
     I                                      618 622 DHA04S
     I                                      623 627 DHA04E
     I                                      628 628 DNC10S
     I                                      629 629 DNC10E
     I                                      630 631 DNC11S
     I                                      632 633 DNC11E
     I                                      634 635 DNC18S
     I                                      636 637 DNC18E
     I                                      641 6480DATES
     I                                      651 6580DATEE
     I                                      661 6680WHA03S
     I                                      671 6780WHA03E
     I                                      681 689 DHA05S
     I                                      691 699 DHA05E
     I                                      701 702 DNC12S
     I                                      703 704 DNC12E
     I                                      705 706 DNC09
     I                                      707 707 DOPT
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYND     KLIST
     C                     KFLD           DHA01            公司代號
     C                     KFLD           DHA02            通路代號
     C           KEY#B     KLIST
     C                     KFLD           DHA01            公司代號
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R0100
     C*
     C   L1                EXSR R7100
     C*
     C                     EXSR R2000
     C*
     CL1 10                EXSR R8100
     CLR 10                EXSR R8900
     CLRN10                EXSR R8990
     C*==============================================================*
     C*          R0100 ... GET HEADING
     C*==============================================================*
01===C           R0100     BEGSR
|    C*
|    C*公司簡稱
|    C                     MOVEL*BLANK    D#B03
|    C           KEY#B     CHAINPA#BPF               40
|    C  N40                MOVEL#B03      D#B03
|    C*通路簡稱
|    C                     MOVEL*BLANK    DND04
|    C           KEYND     CHAINMANDPF               40
|    C  N40                MOVELND04      DND04
|    C*
|    C                     WRITEPH1
|    C*
|    C                     SETOF                     39
|    C                     SETON                     10
|    C                     SETON                     303132
|    C*
|    C                     Z-ADD*ZERO     W703T1           數量
|    C                     Z-ADD*ZERO     W704T1           含稅銷售金額
|    C                     Z-ADD*ZERO     W703T2           數量
|    C                     Z-ADD*ZERO     W704T2           含稅銷售金額
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R7100 ... LEVEL BREAK 1 - INIT
     C*==============================================================*
02===C           R7100     BEGSR
|    C*
|    C                     Z-ADD*ZERO     W703T1           數量
|    C                     Z-ADD*ZERO     W704T1           含稅銷售金額
|    C*
02===C                     ENDSR
     C*==============================================================*
     C*          R2000 ... PROCESS - PRINT DETAIL
     C*==============================================================*
03===C           R2000     BEGSR
|    C*
|    C           *IN39     IFEQ '1'
|    C                     EXSR R9000
|    C                     END
|    C*
|    C                     WRITEPD1
|    C                     SETOF                     30
|    C*
|    C                     ADD  W703      W703T1           數量
|    C                     ADD  W704      W704T1           含稅銷售金額
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          R8100 ... LEVEL BREAK 1 - TOTAL
     C*==============================================================*
04===C           R8100     BEGSR
|    C*
|    C                     WRITEPT1
|    C*
|    C                     ADD  W703T1    W703T2           數量
|    C                     ADD  W704T1    W704T2           含稅銷售金額
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R8900 ....總計
     C*==============================================================*
05===C           R8900     BEGSR
|    C*
|    C                     SETON                     30
|    C                     WRITEPT2
|    C                     WRITEPE2
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R8990 ... (NO DATA)
     C*==============================================================*
06===C           R8990     BEGSR
|    C*
|    C*公司簡稱
|    C                     MOVEL*BLANK    D#B03
|    C           KEY#B     CHAINPA#BPF               40
|    C  N40                MOVEL#B03      D#B03
|    C*通路簡稱
|    C                     MOVEL*BLANK    DND04
|    C           KEYND     CHAINMANDPF               40
|    C  N40                MOVELND04      DND04
|    C*
|    C                     WRITEPH1
|    C                     WRITEPE3
|    C                     WRITEPE2
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          R9000 ... OVERFLOW
     C*==============================================================*
07===C           R9000     BEGSR
|    C*
|    C                     WRITEPE1
|    C                     MOVEL*BLANK    D#B03
|    C           KEY#B     CHAINPA#BPF               40
|    C  N40                MOVEL#B03      D#B03
|    C                     MOVEL*BLANK    DND04
|    C           KEYND     CHAINMANDPF               40
|    C  N40                MOVELND04      DND04
|    C                     WRITEPH1
|    C*
|    C                     SETON                     303132
|    C                     SETOF                     39
|    C*
07===C                     ENDSR
