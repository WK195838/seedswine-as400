     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    PROGRAM ID   : WEB041                                     *
     A*    AUTHOR       : ROBIN LIU                                  *
     A*    CREATE DATE  : 2010/01/28                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :西祺進銷存SQL RPG                         *
     A*    SUBSYSTEM    :前台管理系統                              *
     A*    REMARK       :庫存查詢BY貨架                          *
     A*===============================================================
     FSCWB07PFUF  E           K        DISK
     FSCWB07WFUF  E           K        DISK                      A
     F            WB07                              KRENAMETWB07
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C                     PARM           PARM1   2        公司代號
     C                     PARM           PARM2   2        通路代號
     C                     PARM           PARM3   6        月結年月
     C                     PARM           PARM4   4        貨架代號
     C                     PARM           PARM5   8        系統日期
     C                     PARM           PARM6  24        鍵值
     C                     PARM           ERRRD   3        ERR RCD NO.
     C                     PARM           RTNCD   2        RETURN CODE
     C                     PARM           RTNDSC 60        RETURN DESC.
     C*==============================================================*
     C*                    KLIST
     C*==============================================================*
     C           KEYWB     KLIST
     C                     KFLD           WB0701
     C                     KFLD           WB0703
     C                     KFLD           WB0705
     C                     KFLD           WB0702
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C* RUN SQL STATEMENTS
     C                     EXSR RTNSQL
     C*過濾重複的資料
     C                     EXSR RTNFLR
     C*刪除重複的資料
     C                     EXSR RTNDEL
     C*
     C                     MOVEL'000'     ERRRD
     C                     MOVEL'00'      RTNCD
     C                     MOVEL*BLANKS   RTNDSC
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*                    SQL STATEMENTS
     C*==============================================================*
     C           RTNSQL    BEGSR
     C/EXEC SQL
     C+ DELETE FROM SCFLIB/SCWB07PF WHERE WB0701 = :PARM6
     C/END-EXEC
     C*
     C/EXEC SQL
     C+ INSERT INTO SCFLIB/SCWB07PF
     C+ SELECT :PARM6,'4',TB05,NC05,TB04,NE05,
     C+ TB06+TB08-TB09+TB10-TB11+TB12-TB13+TB14-TB15+TB16+TB17-TB18
     C+ FROM  C#EBPF, C#EGPF, INTBPF, MANCPF, MANEPF
     C+ WHERE EB01 = EG01 AND EB02 = EG02 AND EB04 = EG03
     C+ AND   EB01 = TB01 AND EB02 = TB03 AND EB04 = TB05
     C+ AND   TB01 = NC01 AND TB05 = NC02
     C+ AND   TB01 = NE01 AND TB03 = NE02 AND TB04 = NE03
     C+ AND   EB01 = :PARM1 AND EB02 = :PARM2
     C+ AND   TB02 = :#PARM3 AND EB03 = :PARM4
     C+ AND  (( :#PARM5  >= EB05 AND  :#PARM5  <= EB06)
     C+ OR ( :#PARM5  >= EB05 AND EB06 = 0))
     C+ AND  ((  :#PARM5  >= EG04 AND  :#PARM5  <= EG05)
     C+ OR ( :#PARM5  >= EG04 AND EG05 = 0))
     C/END-EXEC
     C*
     C/EXEC SQL
     C+ INSERT INTO SCFLIB/SCWB07PF
     C+ SELECT :PARM6,'3',TB05,NC05,TB04,NE05,
     C+ TB06+TB08-TB09+TB10-TB11+TB12-TB13+TB14-TB15+TB16+TB17-TB18
     C+ FROM  C#EBPF, C#EGPF, INTBPF, MANCPF, MANEPF
     C+ WHERE EB01 = EG01 AND EB02 = EG02 AND EB04 = EG03
     C+ AND   EB01 = TB01 AND EB02 = TB03 AND EB04 = TB05
     C+ AND   TB01 = NC01 AND TB05 = NC02
     C+ AND   TB01 = NE01 AND TB03 = NE02 AND TB04 = NE03
     C+ AND   EB01 = :PARM1 AND EB02 = :PARM2
     C+ AND   TB02 = :#PARM3 AND EB03 = :PARM4
     C+ AND  NOT (( :#PARM5  >= EB05 AND  :#PARM5  <= EB06)
     C+ OR ( :#PARM5  >= EB05 AND EB06 = 0))
     C+ AND  ((  :#PARM5  >= EG04 AND  :#PARM5  <= EG05)
     C+ OR ( :#PARM5  >= EG04 AND EG05 = 0))
     C+ AND (TB06+TB08-TB09+TB10-TB11+TB12-TB13+TB14-TB15+TB16+TB17-TB18)
     C+ > 0
     C/END-EXEC
     C*
     C/EXEC SQL
     C+ INSERT INTO SCFLIB/SCWB07PF
     C+ SELECT :PARM6,'2',TB05,NC05,TB04,NE05,
     C+ TB06+TB08-TB09+TB10-TB11+TB12-TB13+TB14-TB15+TB16+TB17-TB18
     C+ FROM  C#EBPF, C#EGPF, INTBPF, MANCPF, MANEPF
     C+ WHERE EB01 = EG01 AND EB02 = EG02 AND EB04 = EG03
     C+ AND   EB01 = TB01 AND EB02 = TB03 AND EB04 = TB05
     C+ AND   TB01 = NC01 AND TB05 = NC02
     C+ AND   TB01 = NE01 AND TB03 = NE02 AND TB04 = NE03
     C+ AND   EB01 = :PARM1 AND EB02 = :PARM2
     C+ AND   TB02 = :#PARM3 AND EB03 = :PARM4
     C+ AND  (( :#PARM5  >= EB05 AND  :#PARM5  <= EB06)
     C+ OR ( :#PARM5  >= EB05 AND EB06 = 0))
     C+ AND  NOT ((  :#PARM5  >= EG04 AND  :#PARM5  <= EG05)
     C+ OR ( :#PARM5  >= EG04 AND EG05 = 0))
     C+ AND (TB06+TB08-TB09+TB10-TB11+TB12-TB13+TB14-TB15+TB16+TB17-TB18)
     C+ > 0
     C/END-EXEC
     C*
     C/EXEC SQL
     C+ INSERT INTO SCFLIB/SCWB07PF
     C+ SELECT :PARM6,'1',TB05,NC05,TB04,NE05,
     C+ TB06+TB08-TB09+TB10-TB11+TB12-TB13+TB14-TB15+TB16+TB17-TB18
     C+ FROM  C#EBPF, C#EGPF, INTBPF, MANCPF, MANEPF
     C+ WHERE EB01 = EG01 AND EB02 = EG02 AND EB04 = EG03
     C+ AND   EB01 = TB01 AND EB02 = TB03 AND EB04 = TB05
     C+ AND   TB01 = NC01 AND TB05 = NC02
     C+ AND   TB01 = NE01 AND TB03 = NE02 AND TB04 = NE03
     C+ AND   EB01 = :PARM1 AND EB02 = :PARM2
     C+ AND   TB02 = :#PARM3 AND EB03 = :PARM4
     C+ AND  NOT (( :#PARM5  >= EB05 AND  :#PARM5  <= EB06)
     C+ OR ( :#PARM5  >= EB05 AND EB06 = 0))
     C+ AND  NOT ((  :#PARM5  >= EG04 AND  :#PARM5  <= EG05)
     C+ OR ( :#PARM5  >= EG04 AND EG05 = 0))
     C+ AND (TB06+TB08-TB09+TB10-TB11+TB12-TB13+TB14-TB15+TB16+TB17-TB18)
     C+ > 0
     C/END-EXEC
     C*
     C                     ENDSR
     C*==============================================================*
     C*過濾重複的資料
     C*==============================================================*
     C           RTNFLR    BEGSR
     C*
     C           PARM6     SETLLWB07
     C           PARM6     READEWB07                N    44
     C           *IN44     DOWEQ'0'
     C*
     C*產品代號或分店代號不同時
     C           WB0703    IFNE TB0703
     C           WB0705    ORNE TB0705
     C                     MOVELWB0703    TB0703
     C                     MOVELWB0705    TB0705
     C*產品代號與分店代號相同時，過濾掉此筆資料
     C                     ELSE
     C           KEYWB     CHAINTWB07                45
     C   45                WRITETWB07
     C                     ENDIF
     C*
     C           PARM6     READEWB07                N    44
     C                     ENDDO
     C*
     C                     ENDSR
     C*==============================================================*
     C*刪除重複的資料
     C*==============================================================*
     C           RTNDEL    BEGSR
     C*
     C           *LOVAL    SETLLTWB07
     C                     READ TWB07                    44
     C           *IN44     DOWEQ'0'
     C*
     C           KEYWB     CHAINWB07                 45
     C  N45                DELETWB07
     C*
     C                     READ TWB07                    44
     C                     ENDDO
     C*
     C                     ENDSR
     C*==============================================================*
     C*                    *INZSR-- INITIAL VALUES
     C*==============================================================*
     C           *INZSR    BEGSR
     C*
     C                     MOVELPARM3     #PARM3  60       月結年月
     C                     MOVELPARM5     #PARM5  80       系統日期
     C*
     C                     MOVEL*BLANK    TB0703  9        產品代號
     C                     MOVEL*BLANK    TB0705  5        分店代號
     C*
     C                     ENDSR
