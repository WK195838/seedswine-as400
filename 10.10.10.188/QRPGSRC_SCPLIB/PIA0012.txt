      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : PIA0012                                       *
      * AUTHER       : A1492  TERRY                                  *
      * CREATE DATE  : 100/01/26                                     *
      * UPDATE DATE  :                                               *
      * SYSTEM       :西祺進銷存                                   *
      * SUBSYSTEM    :盤點系統                                     *
      * REMARK       :盤盈虧計算                                   *
      *00A需求單號: 1070130
      *00A 107/02/22 DEREK MABLE說:若無陳列餘量,請抓最後一筆進價
      *00B 111/04/09 DEREK修正BUG :讀取INPIPF要用公司+年月
      ****************************************************************
     FINTBPF  IF  E           K        DISK
     FINTAPF  IF  E           K        DISK
     FINPTPF  IF  E           K        DISK
     FINTNLF06IF  E           K        DISK
 00A+FINTNLF03IF  E           K        DISK
 00A+F            TN0                               KRENAMETNL3
     FINPIPF  UF  E           K        DISK                      A
     FINPSPF  O   E           K        DISK                      A
     E*
 00A+ITNL3
 00A+I              TN01                            TN01L3
 00A+I              TN02                            TN02L3
 00A+I              TN03                            TN03L3
 00A+I              TN04                            TN04L3
 00A+I              TN05                            TN05L3
 00A+I              TN06                            TN06L3
 00A+I              TN07                            TN07L3
 00A+I              TN08                            TN08L3
 00A+I              TN09                            TN09L3
 00A+I              TN10                            TN10L3
 00A+I              TN11                            TN11L3
 00A+I              TN12                            TN12L3
 00A+I              TN13                            TN13L3
 00A+I              TN14                            TN14L3
 00A+I              TN15                            TN15L3
 00A+I              TN16                            TN16L3
 00A+I              TN17                            TN17L3
 00A+I              TN18                            TN18L3
 00A+I              TNVV                            TNVVL3
 00A+I              TNWW                            TNWWL3
 00A+I              TNXX                            TNXXL3
 00A+I              TNYY                            TNYYL3
 00A+I              TNZZ                            TNZZL3
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      152 161 $USERN
     I                                      111 1160$CHYMD
     I                                      201 2080A8YMD
     I                                      203 2080A6YMD
     I                                      500 500 $DATA
     I* PROGRAM USE DATA DEFINE ++++++++++++++++++++++++++++++++++++++
     I                                      501 502 DPI01
     I                                      503 5060DPI02
     I                                      507 5120DPIYM
     I                                      513 5140DPIDD
     I                                      507 5140DPIYMD
      *==============================================================*
      *                    DEFINE ROUTINE
      *==============================================================*
     C*
 00A+C           *LIKE     DEFN TN08      KTN08            到分店日
      *==============================================================*
      *                    KLIST
      *==============================================================*
     C           KEYPI     KLIST
     C                     KFLD           PT01             公司別
     C                     KFLD           DPIYM            年月
     C                     KFLD           PT02             分店代號
     C                     KFLD           PT03             產品代號
     C*
     C           KEYTB     KLIST
     C                     KFLD           DPI01            公司別
     C                     KFLD           DPIYM            年月
     C           KEYTA     KLIST
     C                     KFLD           DPI01            公司別
     C                     KFLD           DPIYM            年月
     C                     KFLD           TB05             產品代號
     C*
     C           KEYPT     KLIST
     C                     KFLD           DPI01            公司別
     C*
     C           KEYTN     KLIST
     C                     KFLD           PI01             公司別
     C                     KFLD           XTN02   2        通路
     C                     KFLD           PI03             分店代號
     C                     KFLD           PI04             產品代號
 00A+C           KEYTN3    KLIST
 00A+C                     KFLD           PI01             公司別
 00A+C                     KFLD           XTN02   2        通路
 00A+C                     KFLD           PI03             分店代號
 00A+C                     KFLD           PI04             產品代號
 00A+C                     KFLD           KTN08            到分店日
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C*
     C                     EXSR RTN010
     C                     EXSR RTN100                     INTBPF
     C                     EXSR RTN200                     INPTPF
     C                     EXSR RTN300                     CALCULATE
     C*
     C                     SETON                     LR
      *===============================================================*
     C*          RTN010....
      *===============================================================*
01===C           RTN010    BEGSR
|    C*
|    C                     MOVEL'C1'      XTN02
|    C                     Z-ADD25        DPIDD
|    C*
|    C           KEYTB     SETLLINPIPF
|    C           KEYTB     READEINPIPF                   41
| 01-C           *IN41     DOWEQ*OFF
|   C                     DELETPI0
|   C           KEYTB     READEINPIPF                   41
| 01-C                     ENDDO
|    C*
01===C                     ENDSR
      *===============================================================*
     C*          RTN100....INTBPF -> INPIPF
      *===============================================================*
02===C           RTN100    BEGSR
|    C*
|    C                     MOVELDPI01     TB01
|    C                     Z-ADDDPIYM     TB02
|    C           KEYTB     SETLLINTBPF
|    C           KEYTB     READEINTBPF                   41
| 01-C           *IN41     DOWEQ*OFF
|   C*
|   C           KEYTA     CHAININTAPF               42
|   C   42                Z-ADD0         TA07
|   C*
|   C                     MOVELDPI01     PI01
|   C                     Z-ADDDPIYM     PI02
|   C                     MOVELTB04      PI03
|   C                     MOVELTB05      PI04
|   C                     Z-ADDTB07      PI05             帳列庫存量
|   C           TB16      ADD  TB17      PI06             　　　
|   C                     SUB  TB18      PI06             陳列量
|   C                     Z-ADD0         PI07             盤點量
|   C                     Z-ADD0         PI08             差異量
|   C                     Z-ADD0         PI08A            差異量＿陳列
|   C           PI08      ADD  PI08A     PI08X            總差異量
|   C                     Z-ADDTA07      PI09             單位成本
|   C                     Z-ADD0         PI10             差異金額
|   C                     Z-ADD0         PI10A            差異金額＿陳列
|   C           PI10      ADD  PI10A     PI10X            總差異金額
|   C                     MOVELTB03      PI11             通路代號
|   C                     MOVEL*BLANK    PI12             盤點單號
|   C                     Z-ADDDPIYMD    PI13             盤點日期
|   C                     MOVEL'A6'      PI14             異動別
|   C                     Z-ADDA8YMD     PIXX             異動日期
|   C                     TIME           PIYY             異動時間
|   C                     MOVEL$USER     PIZZ             異動者　
|   C*
|   C                     MOVEL'Y'       $DATA
|   C                     WRITEPI0
|   C                     CLEARPI0
|   C*
|   C           KEYTB     READEINTBPF                   41
| 01-C                     ENDDO
|    C*
02===C                     ENDSR
      *===============================================================*
     C*          RTN200....INPTPF -> INPIPF
      *===============================================================*
03===C           RTN200    BEGSR
|    C*
|    C           KEYPT     SETLLINPTPF
|    C           KEYPT     READEINPTPF                   41
| 01-C           *IN41     DOWEQ*OFF
|   C*
| 02-C           PT05      IFNE 0
|   C                     MOVELPT01      PI01
|   C                     Z-ADDDPIYM     PI02
|   C                     MOVELPT02      PI03
|   C                     MOVELPT03      PI04
|   C           KEYPI     CHAININPIPF               40
| 03-C           *IN40     IFEQ '0'
|   C                     Z-ADDPT05      PI07             盤點量
|   C                     UPDATPI0
| 03*C                     ELSE
|   C                     CLEARPI0
|   C                     MOVELPT01      PI01             公司別
|   C                     Z-ADDDPIYM     PI02             年月
|   C                     MOVELPT02      PI03             分店代號
|   C                     MOVELPT03      PI04             產品代號
|   C                     Z-ADD0         PI05             帳列庫存量
|   C                     Z-ADD0         PI06             陳列量
|   C                     Z-ADDPT05      PI07             盤點量
|   C                     Z-ADD0         PI08             差異量
|   C                     Z-ADD0         PI08A            差異量＿陳列
|   C           PI08      ADD  PI08A     PI08X            總差異量
     C*
     C                     MOVELPT03      TB05
     C           KEYTA     CHAINTA0                  41
     C           *IN41     IFEQ '0'
|   C                     Z-ADDTA07      PI09             單位成本
     C                     ELSE
|   C                     Z-ADD0         PI09             單位成本
     C                     ENDIF
     C*
|   C                     Z-ADD0         PI10             差異金額
|   C                     Z-ADD0         PI10A            差異金額＿陳列
|   C           PI10      ADD  PI10A     PI10X            總差異金額
|   C                     MOVEL'C1'      PI11             通路代號
|   C                     MOVELPT04      PI12      P      盤點單號
|   C                     Z-ADDDPIYMD    PI13             盤點日期
|   C                     MOVEL'A6'      PI14             異動別
|   C                     Z-ADDA8YMD     PIXX             異動日期
|   C                     TIME           PIYY             異動時間
|   C                     MOVEL$USER     PIZZ             異動者　
|   C*
|   C                     WRITEPI0
|   C*
| 03-C                     ENDIF
| 02-C                     ENDIF
|   C*
|   C           KEYPT     READEINPTPF                   41
|   C*
| 01-C                     ENDDO
|    C*
03===C                     ENDSR
      *===============================================================*
     C*          RTN300....
      *===============================================================*
04===C           RTN300    BEGSR
|    C*
|00B-C*          *LOVAL    SETLLINPIPF
|00B-C*                    READ INPIPF                   42
|00B+C           KEYTB     SETLLINPIPF
|00B+C           KEYTB     READEINPIPF                   42
| 01-C           *IN42     DOWEQ*OFF
| 02-C           PI07      IFGE PI06
|   C           PI07      SUB  PI06      XPI08   60
|   C           XPI08     SUB  PI05      PI08
|   C                     Z-ADD0         PI08A
|   C                     Z-ADD0         PI10A
| 02*C                     ELSE
|   C           PI07      SUB  PI06      PI08A
|   C                     Z-SUBPI05      PI08
|   C                     Z-ADDPI08A     PI08W   60
|   C                     EXSR RTN400
|   C                     Z-SUBPI10W     PI10A
| 02-C                     ENDIF
|   C*
|   C           PI08      MULT PI09      PI10      H
|   C           PI08      ADD  PI08A     PI08X            總差異量
|   C           PI10      ADD  PI10A     PI10X            總差異金額
|   C*
|   C                     UPDATPI0
|   C*
|   C*00B-                READ INPIPF                   42
|00B+C           KEYTB     READEINPIPF                   42
| 01-C                     ENDDO
|    C*
04===C                     ENDSR
      *===============================================================*
     C*          RTN400....
      *===============================================================*
05===C           RTN400    BEGSR
|    C                     Z-ADD0         PI10W   92
|    C                     Z-ADD0         PI10Y   92
|    C                     MULT -1        PI08W
|00A+C           KEYTN     SETLLTN0                      44
|00A+C           *IN44     IFEQ '0'
|00A+C                     Z-ADD*HIVAL    KTN08
|00A+C           KEYTN3    SETGTTNL3
|00A+C           KEYTN     REDPETNL3                     48
|00A+C           *IN48     IFEQ '0'
|00A+C           PI08W     MULT TN06L3    PI10Y     H
|00A+C                     ADD  PI10Y     PI10W     H
|00A+C                     END
|00A+C                     ELSE
|    C           KEYTN     SETLLTN0
|    C           KEYTN     READETN0                      45
| 01-C           *IN45     DOWEQ'0'
|   C           PI08W     ANDGT0
| 02-C           TN07      IFGT 0
| 03-C           PI08W     IFLE TN07
|   C           PI08W     MULT TN06      PI10Y     H
|   C                     ADD  PI10Y     PI10W     H
|   C                     Z-ADDPI08W     XPS06   60
|   C                     EXSR RTN500
|   C                     Z-ADD0         PI08W
| 03*C                     ELSE
|   C           TN07      MULT TN06      PI10Y     H
|   C                     ADD  PI10Y     PI10W     H
|   C                     SUB  TN07      PI08W
|   C                     Z-ADDTN07      XPS06
|   C                     EXSR RTN500
| 03-C                     ENDIF
| 02-C                     ENDIF
|   C*
|   C           KEYTN     READETN0                      45
| 01-C                     ENDDO
|00A+C                     END
05===C                     ENDSR
      *===============================================================*
     C*          RTN500....    WRITE INPSPF
      *===============================================================*
06===C           RTN500    BEGSR
|    C                     CLEARPS0
|    C                     MOVELPI01      PS01             公司別
|    C                     MOVELPI03      PS02             分店代號
|    C                     MOVELPI04      PS03             產品代號
|    C                     MOVELTN09      PS04             陳列單號
|    C                     Z-ADDTN06      PS05             單價
|    C                     Z-ADDXPS06     PS06             借貨量
|    C                     Z-ADDPI10Y     PS07             借貨金額
|    C                     Z-ADDDPIYMD    PS08             借貨日期
|    C                     MOVELTN03      PS09             供應商代號
|   C                     Z-ADDDPIYM     PS10             年月
|    C                     Z-ADDA8YMD     PSXX             異動日期
|    C                     TIME           PSYY             異動時間
|    C                     MOVEL$USER     PSZZ             異動者　
|    C                     WRITEPS0
06===C                     ENDSR
