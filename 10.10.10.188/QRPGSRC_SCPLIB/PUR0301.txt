      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : PUR0301                                       *
      * AUTHER       : WIND                                          *
      * CREATE DATE  : 2008/10/31                                    *
      * UPDATE DATE  :                                               *
      * SYSTEM       :                                               *
      * SUBSYSTEM    :採購系統                                     *
      * REMARK       :分店驗收數量差異表                           *
      ****************************************************************
     FSCWK04  IP  E           K        DISK
     FMANCPF  IF  E           K        DISK
     FMANDPF  IF  E           K        DISK
     FMANEPF  IF  E           K        DISK
     FPA#BPF  IF  E           K        DISK
     FSCWK14  O   E           K        DISK
     FPUR030P O   E             39     PRINTER
     I*----------------------------------------------------------------
     ICM0
     I                                              CL01  L5
     I                                              CL03  L4
     I                                              CL02  L3
     I                                              CL04  L2
     I                                              CM01  L1
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$PRTID
     I                                      601 602 DCL01
     I                                      603 604 DDOC
     I                                      605 6100DCL03S
     I                                      611 6160DCL03E
     I                                      617 617 DATA
     I                                      618 619 DCL02S
     I                                      620 621 DCL02E
     I                                      622 626 DCL04S
     I                                      627 631 DCL04E
     I                                      641 6480DATES
     I                                      651 6580DATEE
      *==============================================================*
      *                    KEY LIST
      *==============================================================*
     C           KEYND     KLIST
     C                     KFLD           CL01
     C                     KFLD           CL02
     C           KEYNE     KLIST
     C                     KFLD           CL01
     C                     KFLD           CL02
     C                     KFLD           CL04
     C           KEY#B     KLIST
     C                     KFLD           CL01
     C           KEYNC     KLIST
     C                     KFLD           CL01
     C                     KFLD           CM03
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C  N10                EXSR RTN010
     C*
     C   39                EXSR RTN900
     C*通路
     C           KEYND     CHAINMANDPF               40
     C   40                MOVEL*BLANK    ND04
     C*分店
     C           KEYNE     CHAINMANEPF               40
     C   40                MOVEL*BLANK    NE05
     C                     Z-ADDCL03      W1402
     C                     MOVELCL02      W1403
     C                     MOVELND04      W1404
     C                     MOVELCL04      W1405
     C                     MOVELNE05      W1406
     C                     MOVELCM01      W1407
     C* GET轉換日期
     C                     CALL 'P31'
     C                     PARM CL03      P3101I  80
     C                     PARM '2'       P3102I  1
     C                     PARM '1'       P3103I  1
     C                     PARM '3'       P3104I  1
     C                     PARM '1'       P3105I  1
     C           CL03      PARM           P3101O  80
     C                     Z-ADDCL03      LCL03
     C* GET 產品簡稱／單位
     C           KEYNC     CHAINMANCPF               40
     C   40                MOVEL*BLANK    NC05
     C*
     C           CM04      SUB  CM05      PCM06
     C                     MOVELCM03      W1408
     C                     MOVELNC05      W1409
     C                     Z-ADDCM04      W1410
     C                     Z-ADDCM05      W1411
     C                     Z-ADDPCM06     W1412
     C                     WRITEW14
     C*
     C                     WRITEPD1
     C                     SETOF                     3031
     C                     SETOF                     3233
     C*
     CL1                   SETON                     33
     CL2                   SETON                     3233
     CL3                   SETON                     313233
     CL4                   SETON                     3031
     CL4                   SETON                     3233
     CL5                   SETON                     3031
     CL5                   SETON                     3233
     CLR                   EXSR RTNLR
     C*==============================================================*
     C*          RTN010....GET HEADING
     C*==============================================================*
01===C           RTN010    BEGSR
|    C* GET公司別
     C                     MOVELCL01      W1401
|    C                     MOVEL*BLANK    D#B03
|    C           KEY#B     CHAINPA#BPF               40
|    C  N40                MOVEL#B03      D#B03
|    C                     WRITEPH1
|    C                     SETOF                     39
|    C                     SETON                     10
|    C                     SETON                     3031
|    C                     SETON                     3233
|    C*
01===C                     ENDSR
      *==============================================================*
      *          RTN900....OVERFLOW
      *==============================================================*
02===C           RTN900    BEGSR
|    C*
|    C                     WRITEPE1
|    C                     WRITEPH1
|    C*
|    C                     SETON                     303132
|    C                     SETOF                     39
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTNLR ....(NO DATA)
      *==============================================================*
03===C           RTNLR     BEGSR
|    C*
| 01-C           *IN10     IFEQ '0'
|   C                     WRITEPH1
|   C                     WRITEPE9
| 01-C                     END
|    C                     WRITEPE2
|    C*
03===C                     ENDSR
