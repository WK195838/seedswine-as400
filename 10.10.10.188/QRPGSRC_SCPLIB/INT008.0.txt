     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    PROGRAM ID   : INT008                                     *
     A*    AUTHOR       : A1546  VINCENT                             *
     A*    DATE WRITTEN : 97/10/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    PROCEDURE    :陳列品退出處理 (U/I)                      *
     A*    REMARK       :                                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     FINTNLF01IF  E           K        DISK
     F            TN0                               KRENAMETN0L
     F                                              KINFSR ERRSR
     FINTNPF  UF  E           K        DISK
     F                                              KINFSR ERRSR
     FINTKLF01UF  E           K        DISK                      A
     F                                              KINFSR ERRSR
     FINTLPF  UF  E           K        DISK                      A
     F                                              KINFSR ERRSR
     FMANBPF  IF  E           K        DISK
     FMANCPF  IF  E           K        DISK
     FMANDPF  IF  E           K        DISK
     FMANEPF  IF  E           K        DISK
     FC#EHPF  IF  E           K        DISK
     FPA#BPF  IF  E           K        DISK
     FINT008D CF  E                    WORKSTN
     F                                              KINFDS DSPFDS
     F                                        RRN1  KSFILE SFLSR1
     F                                        RRN2  KSFILE SFLSR2
     E*: FUNCTION DATA :
     E                    WFUN    5   5  6
     E*: A/U/D/I AUT
     E                    WA01        5  1
     I*
     ISDS        SDS
     I                                     *PROGRAM APPPGM
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      152 161 $USERN
     I                                      111 1160$CHYMD
 @   I                                      135 135 $ADD
 @   I                                      136 136 $UPD
 @   I                                      137 137 $DLT
 @   I                                      138 138 $INQ
 @   I                                      139 139 $RMK01
M001AI                                      201 2080DATE
     I                                      201 2080$EG8
     I                                      187 1920DATES
     I            DS
     I                                        1   1 DOPT1
     I                                        1   10WNUM
     I* FOR F4 HELP -- GET CURSOR POSITION
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I            DS
     I                                        1   80WYMD
     I                                        1   40WYY
     I                                        5   60WMM
     I                                        1   60WYM
      *==============================================================*
      *                    DEFINE ROUTINE
      *==============================================================*
     C                     Z-ADD0         HRRN1   40
     C                     Z-ADD0         W6      60
     C* FOR F4 PARM
     C                     MOVE *BLANKS   APPSCR  6
     C                     MOVE *BLANK    PARMI1 10
     C                     MOVE *BLANK    PARMO1 10
     C*
     C                     MOVE *BLANK    WQUERY  1
     C           *LIKE     DEFN DBGN1     OBGN1            *START KEY
     C           *LIKE     DEFN DBGN2     OBGN2            *START KEY
     C           *LIKE     DEFN DBGN3     OBGN3            *START KEY
     C           *LIKE     DEFN DBGN4     OBGN4            *START KEY
      *MM/DD/YY--->YY/MM/DD
     C           *LIKE     DEFN TK03      WTK03            *DATE CHK
     C           *LIKE     DEFN DTK03     WDTK03           *DATE CHK
     C           *LIKE     DEFN TN07      WQTY
     C           *LIKE     DEFN TN07      WQTY1
      *==============================================================*
      *                    KLIST
      *==============================================================*
     C           KEYTN     KLIST
     C                     KFLD           DBGN1
     C                     KFLD           DBGN2
     C                     KFLD           DBGN3
     C           KEYTN0    KLIST                           FOR CHAIN ND
     C                     KFLD           DBGN1
     C                     KFLD           DBGN2
     C           KEYTN1    KLIST
     C                     KFLD           DBGN1
     C                     KFLD           DBGN2
     C                     KFLD           DBGN3
     C                     KFLD           DBGN4
     C           KEYTN2    KLIST
     C                     KFLD           DTN01
     C                     KFLD           DTN02
     C                     KFLD           DTN03
     C                     KFLD           DTN04
     C           KEYTN3    KLIST
     C                     KFLD           DTN01
     C                     KFLD           DTN02
     C                     KFLD           DTN03
     C                     KFLD           DTN04
     C                     KFLD           DTN05
     C                     KFLD           HTN09
     C           KEYTN4    KLIST
     C                     KFLD           DTN01
     C                     KFLD           DTN02
     C                     KFLD           DTN03
     C           KEYTN5    KLIST
     C                     KFLD           TN01
     C                     KFLD           TN02
     C                     KFLD           TN03
     C                     KFLD           TN04
     C                     KFLD           TN05
     C           KEYNE     KLIST
     C                     KFLD           NE01
     C                     KFLD           NE02
     C                     KFLD           NE03
     C           KEYNC     KLIST
     C                     KFLD           TN01
     C                     KFLD           TN05
     C           KEYTK     KLIST
     C                     KFLD           DTN01
     C                     KFLD           DTN02
     C                     KFLD           DTN03
     C                     KFLD           DTN04
     C                     KFLD           WTK03
     C           KEYTL     KLIST
     C                     KFLD           DTK05
     C                     KFLD           DTN05
     C           KEYTL1    KLIST
     C                     KFLD           DTK05
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C                     EXSR RTN010                     *INIT ENVIRMENT
     C                     EXSR RTN191                     *INIT SFL
      *
     C                     EXSR RTN192                     *READ NEXT PAGE
     C                     EXSR RTN195                     *EXFMT
     C*
  01-C           *IN03     DOWEQ'0'                        *03    OFF
    C*
  02-C           *IN25     CASEQ'1'       RTN192
    C                     CAS            RTN170
  02-C                     END
    C  N03                EXSR RTN195
    C*
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE
     C*==============================================================*
01===C           RTN010    BEGSR
|    C*
|    C                     MOVE $ADD      WA01,1
|    C                     MOVE $UPD      WA01,2
|    C                     MOVE $DLT      WA01,4
|    C                     MOVE $INQ      WA01,5
|    C                     MOVE 'SCR001'  APPSCR
|    C                     MOVE *BLANKS   WEA03   2
|    C*
01===C                     ENDSR
      *==============================================================*
      *          RTN170....ENTER
      *==============================================================*
02===C           RTN170    BEGSR
|    C*
|    C   04                EXSR RTNF4
|    C*
| 01-C           DBGN1     IFNE OBGN1
|   C           DBGN2     ORNE OBGN2
|   C           DBGN3     ORNE OBGN3
|   C           DBGN4     ORNE OBGN4
|   C                     Z-ADD0         HRRN1
|   C                     MOVEL*BLANK    D#B03
|   C                     MOVEL*BLANK    DND04
|   C                     MOVEL*BLANK    DNB03
|    *新增畫面秀出文字功能
|   C                     EXSR RTN190
|   C                     EXSR RTN191
|   C                     EXSR RTN192
|   C                     MOVE DBGN1     OBGN1
|   C                     MOVE DBGN2     OBGN2
|   C                     MOVE DBGN3     OBGN3
|   C                     MOVE DBGN4     OBGN4
| 01*C                     ELSE
|   C                     EXSR RTN171
| 02-C           *IN99     IFEQ '0'
|   C                     Z-ADDRRN1      HRRN1
|   C                     EXSR RTN191
|   C                     EXSR RTN192
| 02-C                     END
| 01-C                     END
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTN171....READC SFL
      *==============================================================*
03===C           RTN171    BEGSR
|    C*
|    C                     SETON                     54
|    C*
|    C                     READCSFLSR1                 5757*NF
| 01-C           *IN57     DOWEQ'0'                        *40 LOOP
|   C*
|   C                     SETOF                     60    *DOP *IND
| 02-C           *IN99     IFEQ '0'
|   C*
| 03-C           DOPT1     IFNE *BLANK
|   C                     EXSR RTN180                     *CHK
| 04-C           *IN99     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C                     Z-ADDRRN1      DRRN1
|   C                     EXSR RTN300
| 04-C                     END
| 03-C                     END
| 02-C                     END                             *IN99
|   C*
|   C           RRN1      CHAINSFLSR1               40
|   C  N99                MOVE *BLANKS   DOPT1
|   C                     UPDATSFLSR1
|   C                     READCSFLSR1                 5757*NF
|   C*
| 01-C                     END                             *40 END
| 05-C           *IN99     IFEQ '0'
|   C                     SETOF                     5412
|   C                     SETON                     97
| 05-C                     END
|    C*
03===C                     ENDSR
      *==============================================================*
      *          RTN180....CHECK 'SC01' SCREEN
      *==============================================================*
04===CSR         RTN180    BEGSR
|    C*
|    C* CHK AUT *
| 01-C           WA01,WNUM IFNE 'Y'                         WNUM = DOPT
|   C                     SETON                     609099
|   C                     Z-ADDRRN1      DRRN1
| 01-C                     END
|    C   99                GOTO END180
|    C*
04===C           END180    ENDSR
      *==============================================================*
      *          RTN190....CHECK 'SC01' SCREEN
      *==============================================================*
05===CSR         RTN190    BEGSR
|    C*
|    C           DBGN1     CHAIN#B0                  40
|    C  N40                MOVEL#B03      D#B03
|    C   40                MOVEL*BLANK    D#B03
|    C           KEYTN0    CHAINND0                  40
|    C  N40                MOVELND04      DND04
|    C   40                MOVEL*BLANK    DND04
|    C           DBGN3     CHAINNB0                  40
|    C  N40                MOVELNB03      DNB03
|    C   40                MOVEL*BLANK    DNB03
|    C*
05===C                     ENDSR
      *=========================================================*
      *          RTN191....INITIAL                              *
      *=========================================================*
06===C           RTN191    BEGSR
|    C*
|    C                     MOVE *BLANKS   DOPT1
|    C                     Z-ADD0         DRRN1
|    C                     Z-ADD0         RRN1
|    C                     Z-ADD0         WRRN1   40
|    C                     Z-ADD10        WPRCD1  30
|    C                     Z-ADD0         DSPRC1  40
|    C           HRRN1     DIV  WPRCD1    DSPRC1
|    C                     MULT WPRCD1    DSPRC1
|    C                     MOVE 'N'       WBOTM1  1
|    C                     SETOF                     53
|    C                     SETON                     5254
|    C                     WRITESFLCR1
|    C                     SETOF                     52
|     *
|    C           KEYTN1    SETLLTN0L
|    C*
06===C                     ENDSR
      *=========================================================*
      *          RTN192 .. ROLLUP MAIN ROUTINE
      *=========================================================*
07===C           RTN192    BEGSR
|    C*
| 01-C           WBOTM1    IFEQ 'Y'
|   C                     SETON                     98    *LST PAG
|   C                     Z-ADDRRN1      DRRN1
| 01*C                     ELSE
|   C                     EXSR RTN193
| 01-C                     END
|    C*
07===C                     ENDSR
      *=========================================================*
      *          RTN193 .. ADD SUBFILE DATA
      *=========================================================*
08===C           RTN193    BEGSR
|    C*
|    C                     MOVE *BLANKS   DOPT1
|    C                     MOVE *BLANKS   WTN04   5
|    C                     SETOF                     54
|    C                     Z-ADDWRRN1     RRN1
|    C                     ADD  WPRCD1    DSPRC1
| 01-C           RRN1      DOWLTDSPRC1
|   C           KEYTN     READETN0L                     46
|   C*
| 02-C           *IN46     IFEQ '0'
|   C*
| 03-C           WTN04     IFNE TN04
|   C*
|   C                     MOVELTN04      WTN04
|   C*
|   C                     MOVE TN01      NE01
|   C                     MOVELTN02      NE02
|   C                     MOVELTN04      NE03
|   C           KEYNE     CHAINNE0                  40
|   C  N40                MOVELNE05      DNE05
|   C   40                MOVEL*BLANK    DNE05
|   C                     ADD  1         RRN1
|   C*
|   C                     WRITESFLSR1
| 03-C                     END
| 02*C                     ELSE
|   C                     Z-ADDRRN1      DSPRC1
|   C                     MOVE 'Y'       WBOTM1
|   C                     SETON                     53
| 02-C                     END
|   C*
| 01-C                     ENDDO
|     *
|    C*再此多做預讀一筆的判斷，去判斷是否已到最後一筆
| 04-C           *IN46     IFEQ '0'
|   C           KEYTN     READETN0L                     46
|   C*在將KEY值帶回來
|   C           KEYTN5    SETLLTN0L
| 05-C           *IN46     IFEQ '1'
|   C                     Z-ADDRRN1      DSPRC1
|   C                     MOVE 'Y'       WBOTM1
|   C                     SETON                     53
| 05-C                     END
| 04-C                     END
|    C*
|    C                     Z-ADDRRN1      DRRN1      50
|    C                     Z-ADDRRN1      WRRN1
|    C*
08===C                     ENDSR
      *==============================================================*
      *          RTN195... EXFMT SFL                                 *
      *==============================================================*
09===C           RTN195    BEGSR
|    C*
| 01-C           RRN1      IFLE *ZEROS
| 02-C           DBGN1     IFNE *BLANK
|   C           DBGN2     ORNE *BLANK
|   C           DBGN3     ORNE *BLANK
|   C                     SETON                     99    *NODATA
|   C                     MOVEL'MSG0003' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 02-C                     END
| 01*C                     ELSE
|   C                     SETON                     50
| 01-C                     END
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR1
|    C                     SETOF                     5051
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD0         D#ROW
|    C                     Z-ADD0         D#COL
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          RTN300....'SC03' SCREEN MAIN ROUTINE --- SFLSR
     C*==============================================================*
10===C           RTN300    BEGSR
|    C*
|    C                     MOVE 'SCR002'  APPSCR
|    C                     MOVE WFUN,WNUM DFUN
|    C*
| 01-C           DOPT1     CASEQ'2'       RTN320           *UPD
|   C           DOPT1     CASEQ'5'       RTN340           *INQ
| 01-C                     END
|    C           *LOVAL    SETLLTN0L
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          RTN320....UPD DATA --- SFLSR
     C*==============================================================*
11===C           RTN320    BEGSR
|    C*
|    C                     SETON                     33
|    C                     SETOF                     32
|    C                     EXSR RTN390                     *INT SFLCR
|    C                     EXSR RTN391                     *INT SFLSR
|    C                     EXSR RTN398                     *READ DATA
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           APPSCR    ANDEQ'SCR002'
|   C*
|   C                     WRITEDSPC3
|   C                     EXFMTSFLCR2
|   C                     MOVEA*ALL'0'   *IN,60
|   C                     Z-ADD0         D#ROW
|   C                     Z-ADD0         D#COL
|   C*
| 02-C           *IN03     IFEQ '0'
| 03-C           *IN12     IFEQ '0'
|   C*
| 04-C           *IN04     CASEQ'1'       RTNF4
|   C                     CAS            RTN380            CHK SFLSR
| 04-C                     END
|   C*
| 05-C           *IN99     IFEQ '0'                         SAVE
|   C           *IN09     ANDEQ'1'                         SAVE
|   C                     EXSR RTN39S                      ADD D
|   C                     MOVE 'SCR001'  APPSCR
| 05-C                     END
|   C*
| 03*C                     ELSE
|   C                     MOVE 'SCR001'  APPSCR
| 03-C                     END                             *IN12 ENDIF
| 02-C                     END                             *IN03 ENDIF
|   C*
| 01-C                     END                             *ENDDO
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          RTN340....INQ DATA --- SFLSR2
     C*==============================================================*
12===C           RTN340    BEGSR
|    C*
|    C                     SETOF                     3233  *NO INPUT
|    C                     EXSR RTN390                     *INT SFLSR
|    C                     EXSR RTN391                     *INT SFLSR
|    C                     EXSR RTN398                     *READ DATA TO SFLSR
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           APPSCR    ANDEQ'SCR002'
|   C*
|   C                     WRITEDSPC3
|   C                     EXFMTSFLCR2
|   C                     MOVEA*ALL'0'   *IN,60
|   C                     Z-ADD0         D#ROW
|   C                     Z-ADD0         D#COL
|   C*
| 02-C           *IN03     IFEQ '0'
|   C                     MOVE 'SCR001'  APPSCR
| 02-C                     END                             *IN03 ENDIF
|   C*
| 01-C                     END                             *ENDDO
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          RTN380....CHECK 'SC03' --- SFLSR
     C*==============================================================*
13===C           RTN380    BEGSR
|    C*
|    C* CHK退出日期
|    C                     EXSR RTN38A
| 01-C           WCHG      IFEQ 'Y'
|   C                     GOTO END380
| 01-C                     ENDIF
|    C* CHKSUBFILE
|    C                     MOVE *BLANK    WERR    1
|    C                     Z-ADD0         WCNT    40
|    C                     SETON                     54
|    C                     READCSFLSR2                 5757
|    C*
| 02-C           *IN57     DOWEQ'0'
|   C                     SETOF                     6061
|   C*
| 03-C           DCHK      IFEQ *BLANK
|   C           DOPT2     ANDEQ*BLANK
|   C           DTL03     ANDEQ0
|   C                     GOTO UPD
| 03-C                     ENDIF
|   C*
| 04-C           DCHK      IFNE *BLANK
| 05-C           DOPT2     IFEQ '1'
|   C                     SETON                     6099
|   C                     MOVEL'UPT2020' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 05-C                     END
| 04*C                     ELSE                            DCHK=' '
| 06-C           DOPT2     IFEQ '2'
|   C           DOPT2     OREQ '4'
|   C                     SETON                     6099
|   C                     MOVEL'UPT2010' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 06-C                     ENDIF
| 04-C                     ENDIF
|   C   99                GOTO DSP
|   C*
| 07-C           DCHK      IFEQ *BLANK
| 08-C           DOPT2     IFEQ *BLANK
|   C           DTL03     ANDNE0
|   C           DOPT2     ORNE *BLANK
|   C           DTL03     ANDEQ0
|   C                     SETON                     606199
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 08-C                     ENDIF
| 07-C                     ENDIF
|   C   99                GOTO DSP
|   C*
|   C* CHK原數量變更但未指定作業選擇
| 09-C           DCHK      IFNE *BLANK
|   C           DOPT2     ANDEQ*BLANK
|   C           DTL03     ANDNEHTL03
|   C                     SETON                     6199
|   C                     MOVEL'ERR0059' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 09-C                     ENDIF
|   C   99                GOTO DSP
|   C*
|   C* CHK退出數量不可大於「可異動陳列量」
|   C           DTN14     SUB  DTT       WQTY
|   C           DTL03     SUB  HTL03     WQTY1
| 10-C           WQTY1     IFGT WQTY
|   C                     SETON                     6199
|   C                     MOVEL'ERR0078' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 10-C                     ENDIF
|   C   99                GOTO DSP
|   C*
|   C* CHK一天只可有一張未庫存日結單據
| 11-C           DOPT1     IFEQ '1'
|   C           KEYTK     CHAINTK0                  40
|   C           *LOVAL    SETLLTK0
| 12-C           *IN40     IFEQ '0'
|   C                     MOVE TK05      DTK05
|   C                     SETON                     99
|   C                     MOVEL'ERR0079' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 12-C                     ENDIF
| 11-C                     ENDIF
|   C   99                GOTO DSP
|   C*
|   C*--檢核完畢--------
|   C           DSP       TAG
| 13-C   99      WERR      IFEQ *BLANK
|   C                     Z-ADDRRN2      DRRN2
|   C                     MOVE 'Y'       WERR
| 13-C                     ENDIF
|   C*
| 14-C           DOPT1     IFEQ '1'
|   C           DOPT2     ANDEQ'1'
|   C           DTL03     ANDGT0
|   C                     ADD  1         WCNT
| 14-C                     ENDIF
|   C*
|   C           UPD       TAG
|   C                     UPDATSFLSR2
|   C                     READCSFLSR2                   57
|   C* READC DOW LOOP
| 02-C                     ENDDO
|    C*
|    C  N99                Z-ADD1         DRRN2
|    C*
|     *判斷一定要輸入明細檔
| 15-C  N99      DOPT1     IFEQ '1'
|   C           WCNT      ANDEQ0
|   C                     SETON                     99
|   C                     MOVEL'ERR0011' ERRID
|   C                     MOVEL'SCMF'    ERRF
|   C           1         CHAINSFLSR2               40
|   C                     UPDATSFLSR2
| 15-C                     ENDIF
|    C*
|    C* FOR DATA LOCK
|    C           END380    TAG
|    C********************
|    C*  GET ROW,COL     *
|    C********************
| 16-C           *IN99     IFEQ '0'
|   C           #CSR      DIV  256       D#ROW            *ROW
|   C                     MVR            D#COL            *COL
| 16-C                     END
|    C** 游標位置即為(D#ROW,D#COL)
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          RTN38A....CHK SFLSCR2
     C*==============================================================*
14===C           RTN38A    BEGSR
|    C*
|    C* CHK 退出日期
|    C                     CALL 'P30'
|    C                     PARM DTK03     P3001I  80
|    C                     PARM '3'       P3002I  1
|    C                     PARM '1'       P3003I  1
|    C                     PARM           P3011O  1
| 01-C           P3011O    IFEQ 'N'
|   C                     SETON                     99
|   C                     MOVEL'ERR0004' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 01-C                     ENDIF
|    C   99                GOTO END38A
|    C*
|    C* CHK公司參數檔
|    C           DTN01     CHAINEH0                  40
| 02-C           *IN40     IFEQ '1'
|   C                     SETON                     99
|   C                     MOVEL'ERR0049' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 02-C                     ENDIF
|    C   99                GOTO END38A
|    C*
|    C*日期年月不得小於月結年月
     C                     Z-ADD0         TTK03   80
|    C                     CALL 'P31'
|    C                     PARM DTK03     P31I1   80
|    C                     PARM '3'       P3112   1
|    C                     PARM '1'       P31I3   1
|    C                     PARM '2'       P31I4   1
|    C                     PARM '1'       P31I5   1
|    C           TTK03     PARM           P31IO   80
| 03-C           TTK03     IFLT EH07
|   C                     SETON                     99
|   C                     MOVEL'ERR0042' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 03-C                     ENDIF
|    C   99                GOTO END38A
|    C*
|    C*日期年月只能跨一個月份
|    C                     Z-ADDEH08      WYMD
|    C                     EXSR RTN38D
| 04-C           TTK03     IFGT WYMD
|   C                     SETON                     6299
|   C                     MOVEL'ERR0128' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 04-C                     ENDIF
|    C   99                GOTO END38A
|    C*
|    C* 日期未變回RTN380
| 05-C           DTK03     IFEQ WDTK03
|   C                     MOVE *BLANK    WCHG    1
|   C                     GOTO END38A
| 05*C                     ELSE
|   C                     MOVE 'Y'       WCHG
|   C                     Z-ADDDTK03     WDTK03
| 05-C                     ENDIF
|    C*CHK 退出單據
|    C                     CALL 'P31'
|    C                     PARM DTK03     P31I1   80
|    C                     PARM '3'       P3112   1
|    C                     PARM '1'       P31I3   1
|    C                     PARM '2'       P31I4   1
|    C                     PARM '1'       P31I5   1
|    C           WTK03     PARM           P31IO   80
|    C           KEYTK     CHAINTK0                  40
|    C           *LOVAL    SETLLTK0
|    C                     EXSR RTN38C
| 06-C           *IN40     IFEQ '1'
|   C                     MOVE '1'       DOPT1            ADD MODE
|   C                     MOVE *BLANK    DTK05
| 06*C                     ELSE
|   C                     MOVE TK05      DTK05
|   C                     EXSR RTN38B                     INTL->SFL2
| 06-C                     ENDIF
|    C*
14===C           END38A    ENDSR
     C*==============================================================*
     C*          RTN38B....INTL -> SFL
     C*==============================================================*
15===C           RTN38B    BEGSR
|    C*
|    C* READ INTL
|    C           DTK05     SETLLTL0
|    C           DTK05     READETL0                      44
| 01-C           *IN44     DOWEQ'0'
|   C* READ SFL2
|   C                     SETON                     54
|   C                     READCSFLSR2                 5757
|   C*
| 02-C           *IN57     DOWEQ'0'
|   C*
| 03-C           DTN05     IFEQ TL02                       產品代號
|   C           HTN09     ANDEQTL04                       驗收單號
|   C                     Z-ADDTL03      DTL03
|   C                     Z-ADDTL03      HTL03
|   C                     MOVE '*'       DCHK
|   C                     GOTO UPD1
| 03-C                     ENDIF
|   C* READ SFLSR2 LOOP
|   C           UPD1      TAG
|   C                     UPDATSFLSR2
|   C                     READCSFLSR2                   57
| 02-C                     ENDDO
|   C* READ INTL LOOP
|   C           DTK05     READETL0                      44
| 01-C                     ENDDO
|    C           *LOVAL    SETLLTL0
|    C*
15===C           END38B    ENDSR
     C*==============================================================*
     C*          RTN38C....CLAER SFL2 INPUT FLD.
     C*==============================================================*
16===C           RTN38C    BEGSR
|    C*
|    C                     SETON                     54
|    C                     READCSFLSR2                 5757
| 01-C           *IN57     DOWEQ'0'
|   C                     MOVE *BLANK    DOPT2
|   C                     Z-ADD0         DTL03
|   C                     Z-ADD0         HTL03
|   C                     MOVE *BLANK    DCHK
|   C                     UPDATSFLSR2
|   C                     READCSFLSR2                   57
| 01-C                     ENDDO
|    C*
16===C                     ENDSR
      *==============================================================*
      *          RTN38D....
      *==============================================================*
17===CSR         RTN38D    BEGSR
|    C*
|    C                     ADD  1         WMM
| 01-C           WMM       IFGT 12
|   C                     ADD  1         WYY
|   C                     Z-ADD1         WMM
| 01-C                     ENDIF
|    C*
17===CSR         END38D    ENDSR
     C*==============================================================*
     C*          RTN390....搬值給第2個畫面需要的KEY值
     C*==============================================================*
18===C           RTN390    BEGSR
|     *
|    C                     MOVELTN01      DTN01
|    C                     MOVELTN02      DTN02
|    C                     MOVELTN03      DTN03
|    C                     MOVELTN04      DTN04
|    C                     Z-ADD0         DTK03
|    C                     MOVE *BLANK    DTK05
|     *
|    C                     Z-ADD0         WDTK03
|     *
18===C                     ENDSR
     C*==============================================================*
     C*          RTN391....INIT 'SC03' -- SFLSR2
     C*==============================================================*
19===C           RTN391    BEGSR
|    C*
|    C                     Z-ADD1         DRRN2
|    C                     Z-ADD0         WRRN2   40
|    C                     Z-ADD0         RRN2
|    C                     Z-ADD999       DSPRC2  40
|    C                     Z-ADD10        WPRCD2  40       *PAG RCD
|    C                     MOVE 'N'       WBOTM2  1        *BOTTOM
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR2
|    C                     SETOF                     525053
|    C                     SETON                     51    *SFLDSPCTL
|    C*
|    C           KEYTN2    SETLLTN0L
|    C*
19===C                     ENDSR
     C*==============================================================*
     C*          RTN398....FILE TO SCREEN --- SFLSR(FOR A,U,D,I)
     C*==============================================================*
20===C           RTN398    BEGSR
|     *
| 01-C           DOPT1     IFEQ '2'
|   C                     SETON                     54
| 01*C                     ELSE
|   C                     SETOF                     54
| 01-C                     END
|     *
|    C           KEYTN2    READETN0L                     44*NF/EOF
|    C*
| 02-C           *IN44     DOWEQ'0'
|   C                     EXSR RTN39B
|   C                     ADD  1         RRN2
|   C                     WRITESFLSR2
|   C           KEYTN2    READETN0L                     44*NF/EOF
| 02-C                     END
|    C*
|    C                     SETON                     53
|    C                     Z-ADDRRN2      WRRN2      50
|    C*
20===C                     ENDSR
     C*==============================================================*
     C*          RTN39B....FILE TO SCREEN --- MOVE
     C*==============================================================*
21===C           RTN39B    BEGSR
|    C*
|    C                     MOVE *BLANKS   DCHK             ! FOR INTLPF
|    C                     MOVE *BLANKS   DOPT2
|    C                     MOVE TN05      DTN05
|    C                     MOVE TN09      DTN09
|    C                     MOVELTN09      HTN09
     C                     Z-ADDTN13      DTN13
     C                     Z-ADDTN14      DTN14
|    C           TN11      ADD  TN12      DTT
|    C                     ADD  TN13      DTT              已借貨量
|    C                     Z-ADD0         DTL03
|    C                     Z-ADD0         HTL03
|    C*
|    C           KEYNC     CHAINNC0                  40
|    C  N40                MOVELNC05      DNC05
|    C   40                MOVEL*BLANK    DNC05
|    C*
21===C                     ENDSR
     C*==============================================================*
     C*          RTN39C....SCREEN TO FILE --- H
     C*==============================================================*
22===C           RTN39C    BEGSR
|    C*
| 01-C           DOPT1     IFEQ '1'
|   C                     MOVE DTN01     TK01
|   C                     MOVE DTN02     TK02
|   C                     MOVE WTK03     TK03
|   C                     MOVE DTN04     TK04
|   C                     MOVE DTK05     TK05
|   C                     MOVE DTN03     TK06
|   C                     MOVE *BLANK    TK07
|   C                     MOVE DATE      TKVV
| 01-C                     END
|    C*
|    C                     MOVEL*BLANK    TKWW
|    C                     MOVEL'INT008'  TKWW
|    C                     Z-ADDDATE      TKXX
|    C                     TIME           TKYY
|    C                     MOVE $USER     TKZZ
|    C*
22===C                     ENDSR
     C*==============================================================*
     C*          RTN39D....SCREEN TO FILE --- D
     C*==============================================================*
23===C           RTN39D    BEGSR
|    C*
| 01-C           DOPT2     IFEQ '1'
|   C                     MOVE DTK05     TL01             轉進貨單號
|   C                     MOVE DTN05     TL02             產品
|   C                     MOVELHTN09     TL04             驗收單號
|   C                     MOVE DTN01     TL05             公司別
|   C                     MOVE DTN02     TL06             通路別
|   C                     MOVE DTN04     TL07             分店
|   C                     Z-ADDWTK03     TL08             退出日期
|   C                     MOVE *BLANK    TL09             狀態碼
|   C                     Z-ADDDATE      TLVV
| 01-C                     ENDIF
|    C*
| 02-C           DOPT2     IFNE *BLANK                     1 OR 2
|   C                     Z-ADDDTL03     TL03             數量
| 02-C                     END
|    C*
|    C                     MOVE *BLANK    TLWW
|    C                     MOVEL'INT008'  TLWW
|    C                     Z-ADDDATE      TLXX
|    C                     TIME           TLYY
|    C                     MOVE $USER     TLZZ
|    C*
23===C                     ENDSR
     C*==============================================================*
     C*          RTN39E....UPDATE INTNPF
     C*==============================================================*
24===C           RTN39E    BEGSR
|    C*
|    C           KEYTN3    CHAINTN0                  41
| 01-C                     SELEC
|   C           DOPT2     WHEQ '1'
|   C                     ADD  DTL03     TN12
|   C           DOPT2     WHEQ '2'
|   C                     SUB  HTL03     TN12
|   C                     ADD  DTL03     TN12
|   C           DOPT2     WHEQ '4'
|   C                     SUB  DTL03     TN12
| 01-C                     ENDSL
|    C*
|    C                     MOVE *BLANK    TNWW
|    C                     MOVEL'INT008'  TNWW
|    C                     Z-ADDDATE      TNXX
|    C                     TIME           TNYY
|    C                     MOVE $USER     TNZZ
|    C                     UPDATTN0
|    C*
24===C                     ENDSR
     C*==============================================================*
     C*          RTN39S....SAVE D
     C*==============================================================*
25===C           RTN39S    BEGSR
|    C*
|    C* GET 陳列品退出單號
| 01-C           DOPT1     IFEQ '1'
|   C                     CALL 'GET001'
|   C                     PARM DTN01     T001I1  2
|   C                     PARM 'B2'      T001I2  2
|   C                     PARM DATES     T001I3  60       西元YYMMDD
|   C           DTK05     PARM           T001O1 13
| 01-C                     ENDIF
|    C*
|    C*****************
|    C* UPDATE INTLPF *
|    C*****************
|    C                     READCSFLSR2                   57
| 02-C           *IN57     DOWEQ'0'
|   C*
| 03-C           DOPT2     IFEQ *BLANK
|   C                     GOTO SKIP
| 03-C                     ENDIF                           DOPT1='1'
|   C*
| 04-C           DOPT1     IFEQ '1'
| 05-C           DOPT2     IFEQ '1'
|   C                     EXSR RTN39D                     SCR -> FILE
|   C                     EXSR RTN39E                     UPD INTNPF
|   C                     WRITETL0
| 05-C                     ENDIF
| 04-C                     ENDIF                           DOPT1='1'
|   C*
| 06-C           DOPT1     IFEQ '2'
|   C*
| 07-C           DOPT2     IFEQ '1'
|   C           KEYTL     CHAINTL0                  40
|   C                     EXSR RTN39D                     SCR -> FILE
|   C                     EXSR RTN39E                     UPD INTNPF
|   C  N40                UPDATTL0
|   C   40                WRITETL0
| 07-C                     END
|   C*
| 08-C           DOPT2     IFEQ '2'
|   C           KEYTL     CHAINTL0                  40
| 09-C           *IN40     IFEQ '0'
|   C                     EXSR RTN39D                     SCR -> FILE
|   C                     EXSR RTN39E                     UPD INTNPF
|   C                     UPDATTL0
| 09-C                     END
| 08-C                     END
|   C*
| 10-C           DOPT2     IFEQ '4'
|   C           KEYTL     CHAINTL0                  40
|   C                     EXSR RTN39E                     UPD INTNPF
|   C                     DELETTL0
| 10-C                     END
| 06-C                     END                             DOPT1='2'
|   C*
|   C           SKIP      TAG
|   C                     READCSFLSR2                   57
| 02-C                     ENDDO
|    C           *LOVAL    SETLLTL0
|    C           *LOVAL    SETLLTN0
|    C*****************
|    C* UPDATE IMTKPF *
|    C*****************
| 11-C           DOPT1     IFEQ '1'
|   C                     EXSR RTN39C
|   C                     WRITETK0
|   C                     SETON                     70
| 11-C                     END
|    C*
| 12-C           DOPT1     IFEQ '2'
|   C           KEYTK     CHAINTK0                  40
|   C                     EXSR RTN39C
|   C                     UPDATTK0
| 12-C                     END
|    C*
|     *判斷如果沒有明細資料就將主檔刪除
|    C           KEYTL1    SETLLTL0
|    C           KEYTL1    READETL0                      44
| 13-C           *IN44     IFEQ '1'
|   C           KEYTK     CHAINTK0                  40
|   C                     DELETTK0
| 13-C                     END
|    C*
25===C                     ENDSR
      *===============================================================*
      *          RTNF4 ....F4 HELP
      *===============================================================*
26===C           RTNF4     BEGSR
|    C*
|    C*                    SETON                     99
|    C*
|    C********************
|    C*  GET ROW,COL     *
|    C********************
|    C           #CSR      DIV  256       D#ROW            *ROW
|    C                     MVR            D#COL            *COL
|    C** 游標位置即為(D#ROW,D#COL)
| 01-C                     SELEC
|   C           APPSCR    WHEQ 'SCR001'
|   C**
| 02-C           D#ROW     IFEQ 5
|   C           D#COL     ANDGE13
|   C           D#COL     ANDLE17
|   C                     CALL 'SWD002'
|   C           DBGN3     PARM DBGN3     PARM01  5
|   C                     GOTO ENDF4
| 02-C                     END
|   C*
|   C*
|   C           APPSCR    WHEQ 'SCR002'
|   C                     Z-ADD0         DTK03
| 03-C           D#ROW     IFEQ 5
|   C           D#COL     ANDGE45
|   C           D#COL     ANDLE52
|   C                     CALL 'SWD009'
|   C                     PARM DTN01     D006I1  2
|   C                     PARM DTN02     D006I2  2
|   C                     PARM DTN03     D006I3  5
|   C                     PARM DTN04     D006I4  5
|   C           WTK03     PARM WTK03     D006I5  80
|   C           DTK05     PARM           D006O1 13
|   C*
|   C                     CALL 'P31'
|   C                     PARM WTK03     P31I1   80
|   C                     PARM '2'       P3112   1
|   C                     PARM '1'       P31I3   1
|   C                     PARM '3'       P31I4   1
|   C                     PARM '1'       P31I5   1
|   C           DTK03     PARM           P31IO   80
|   C*
|   C                     GOTO ENDF4
| 03-C                     ENDIF
| 01-C                     ENDSL
|    C*
26===C           ENDF4     ENDSR
     C*==============================================================*
     C*          ERRSR ....DATA LOCK ROUTINE
     C*==============================================================*
27===C           ERRSR     BEGSR
|    C*
|    C                     CALL 'P50'
|    C           DMSG      PARM           P5001O 78
|    C                     SETON                     8099
|    C*
27===C                     ENDSR'*DETC'
     C/COPY HBP2CVTR
**
新增修改      刪除查詢
