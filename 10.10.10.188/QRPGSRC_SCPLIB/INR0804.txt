      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : INR0804                                       *
      * AUTHER       : NANCY                                         *
      * CREATE DATE  : 100/03/24                                     *
      * UPDATE DATE  :                                               *
      * SUBSYSTEM    :庫存系統                                     *
      * REMARK       :陳列品餘額彙總表－依供應商(產生報表)       *
      ****************************************************************
     FSCRWK08AIP  E           K        DISK
     FMANBPF  IF  E           K        DISK
     FMANCPF  IF  E           K        DISK
     FINR080P O   E             39     PRINTER
     I*---------------------------------------------------------------*
     IWK08A       01
     I*公司別+供應商+產品+進貨單價
     I                                              WCOMP1L3
     I                                              WSUPL1L2
     I                                              WPROD1L1
     I                                              WINPR1L1
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$PRTID
     I*<供應商>
     I                                      603 607 DTN03S
     I                                      608 612 DTN03E
     I*<產品代號>
     I                                      613 621 DTN05S
     I                                      622 630 DTN05E
     I*<迄日>
     I                                      631 6360DTN08
     I                                      631 6320DTOMM
     I                                      633 6340DTODD
     I                                      635 6360DTOYY
     I*<迄日:8碼>
     I                                      641 6480PTN08
     I*<起日:6碼>
     I                                      651 6560DTN08F
     I                                      651 6520DFRMM
     I                                      653 6540DFRDD
     I                                      655 6560DFRYY
     I*<起日:8碼>
     I                                      661 6680PTN08F
     I*---------------------------------------------------------------*
     I*                    KEY LIST
     I*---------------------------------------------------------------*
     C*<MANCPF>
     C           K#NC      KLIST
     C                     KFLD           WCOMP1           公司別
     C                     KFLD           WPROD1           產品
     C*---------------------------------------------------------------*
     C*                    MAIN ROUTINE
     C*---------------------------------------------------------------*
     C  N10                EXSR RTN010
     C   39                EXSR RTN900                     *Overflow
     C*
     C   L2                EXSR $DL2
     C   L1                EXSR $DL1
     C*
     CL1                   EXSR $TL1
     CL2                   EXSR $TL2
     CL3                   EXSR $TL3
     CLR 10                EXSR $TLR
     C*---------------------------------------------------------------*
     C*          RTN010....GET HEADING
     C*---------------------------------------------------------------*
     C           RTN010    BEGSR
     C*
     C                     WRITEPH1
     C                     SETOF                     39
     C                     SETON                     103031
     C*
     C                     ENDSR
     C*---------------------------------------------------------------*
     C* $DTL   :           PROCESS
     C*---------------------------------------------------------------*
     C           $DTL      BEGSR
     C*
     C                     ENDSR
     C*---------------------------------------------------------------*
     C*          $DL1      PROCESS   DETAIL
     C*---------------------------------------------------------------*
     C           $DL1      BEGSR
     C*產品簡稱
     C           K#NC      CHAINMANCPF               90
     C           *IN90     IFEQ *OFF
     C                     MOVELNC05      PNAM2
     C                     ELSE
     C                     MOVEL*BLANK    PNAM2
     C                     END
     C*
     C                     SETON                     31
     C*
     C                     ENDSR
     C*---------------------------------------------------------------*
     C*          $DL2      PROCESS   DETAIL
     C*---------------------------------------------------------------*
     C           $DL2      BEGSR
     C*供應商簡稱
     C           WSUPL1    CHAINMANBPF               90
     C           *IN90     IFEQ *OFF
     C                     MOVELNB03      PNAM1
     C                     ELSE
     C                     MOVEL*BLANK    PNAM1
     C                     END
     C*
     C                     ENDSR
     C*---------------------------------------------------------------*
     C*          $TL1      Leveal Break1
     C*---------------------------------------------------------------*
     C           $TL1      BEGSR
     C*<算小計>
  2  C                     ADD  WINQT1    PINQT1
  3  C                     ADD  WTURN1    PTURN1
  4  C                     ADD  WOUQT1    POUQT1
  5  C                     ADD  WLAQT1    PLAQT1
  6  C                     ADD  WADJQ1    PADJQ1
  7  C                     ADD  WLOSE1    PLOSE1
  8  C                     ADD  WUSEQ1    PUSEQ1
  9  C                     ADD  WUAMT1    PUAMT1
  51 C                     ADD  WXAQT1    PXAQT1
  91 C                     ADD  WXAMT1    PXAMT1
 10  C                     ADD  WFQTY1    PFQTY1
     C*
     C                     WRITEPD1
     C                     SETOF                     3031
     C*
     C                     ENDSR
     C*---------------------------------------------------------------*
     C*          $TL2      Leveal Break1
     C*---------------------------------------------------------------*
     C           $TL2      BEGSR
     C*
     C                     SETON                     3031
     C                     WRITEPE0
     C                     WRITEPD2
     C*<算總計>
  2  C                     ADD  PINQT1    PINQT2
  3  C                     ADD  PTURN1    PTURN2
  4  C                     ADD  POUQT1    POUQT2
  5  C                     ADD  PLAQT1    PLAQT2
  6  C                     ADD  PADJQ1    PADJQ2
  7  C                     ADD  PLOSE1    PLOSE2
  8  C                     ADD  PUSEQ1    PUSEQ2
  9  C                     ADD  PUAMT1    PUAMT2
  51 C                     ADD  PXAQT1    PXAQT2
  91 C                     ADD  PXAMT1    PXAMT2
 10  C                     ADD  PFQTY1    PFQTY2
     C**
  2  C                     Z-ADD*ZERO     PINQT1
  3  C                     Z-ADD*ZERO     PTURN1
  4  C                     Z-ADD*ZERO     POUQT1
  5  C                     Z-ADD*ZERO     PLAQT1
  6  C                     Z-ADD*ZERO     PADJQ1
  7  C                     Z-ADD*ZERO     PLOSE1
  8  C                     Z-ADD*ZERO     PUSEQ1
  9  C                     Z-ADD*ZERO     PUAMT1
  51 C                     Z-ADD*ZERO     PXAQT1
  91 C                     Z-ADD*ZERO     PXAMT1
 10  C                     Z-ADD*ZERO     PFQTY1
     C*
     C                     ENDSR
     C*---------------------------------------------------------------*
     C*          $TL3      Leveal Break3
     C*---------------------------------------------------------------*
     C           $TL3      BEGSR
     C*
     C                     SETON                     3031
     C                     WRITEPE0
     C                     WRITEPD3
     C*
  2  C                     Z-ADD*ZERO     PINQT2
  3  C                     Z-ADD*ZERO     PTURN2
  4  C                     Z-ADD*ZERO     POUQT2
  5  C                     Z-ADD*ZERO     PLAQT2
  6  C                     Z-ADD*ZERO     PADJQ2
  7  C                     Z-ADD*ZERO     PLOSE2
  8  C                     Z-ADD*ZERO     PUSEQ2
  9  C                     Z-ADD*ZERO     PUAMT2
  51 C                     Z-ADD*ZERO     PXAQT2
  91 C                     Z-ADD*ZERO     PXAMT2
 10  C                     Z-ADD*ZERO     PFQTY2
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*          RTN900....OVERFLOW
     C*----------------------------------------------------------------
     C           RTN900    BEGSR
     C*
     C                     WRITEPE1
     C                     WRITEPH1
     C*
     C                     SETON                     303132
     C                     SETOF                     39
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*          $TLR  ....(NO DATA)
     C*----------------------------------------------------------------
     C           $TLR      BEGSR
     C* GET公司名稱
     C           *IN10     IFEQ '0'
     C                     EXSR RTN010
     C                     WRITEPE9
     C                     END
     C*
     C                     WRITEPE2
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
