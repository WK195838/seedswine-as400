     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H* PROGRAM NAME : INR140                                        *
     H* AUTHER       : DEREK                                         *
     H* CREATE DATE  : 2014/04/29                                    *
     H* UPDATE DATE  :                                               *
     H* SYSTEM       :                                               *
     H* SUBSYSTEM    :庫存系統                                     *
     H* REMARK       :陳列借貨明細表                               *
     H*===============================================================
     H* MODI CODE |   DATE   | DESCRIPTION
     H*---------------------------------------------------------------
     H****************************************************************
     FINTPPF  IP  E           K        DISK
     FMANCPF  IF  E           K        DISK
     FINR140F O   E                    DISK
     E*--------------------------------------------------------------*
     ITP0
     I                                              TP13  L1
     I                                              TP05  L1
     I*
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      135 135 $ADD
     I                                      598 5990$PRTID
     I*公司
     I                                      601 602 DTP01
     I*供應商
     I                                      603 607 DTP13S
     I                                      608 612 DTP13E
     I*產品代號
     I                                      613 621 DTP05S
     I                                      622 630 DTP05E
     I*起日:6碼
     I                                      631 6360DTP03S
     I                                      631 6320DFRMM
     I                                      633 6340DFRDD
     I                                      635 6360DFRYY
     I*迄日:6碼
     I                                      637 6420DTP03E
     I                                      637 6380DTOMM
     I                                      639 6400DTODD
     I                                      641 6420DTOYY
     I*起日:8碼
     I                                      643 6500DTP03F
     I*迄日:8碼
     I                                      651 6580DTP03T
     I*含陳列下架
     I                                      659 659 DOPT
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN WF03      WWF03            產品簡稱
     C           *LIKE     DEFN WF04      WWF04            借貨數量
     C           *LIKE     DEFN WF05      WWF05            使用金額
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYNC     KLIST
     C                     KFLD           TP01             公司別
     C                     KFLD           TP05             產品代號
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R0100
     C*
     C   L1                EXSR R7100
     C*
     C                     EXSR R2000
     C*
     CL1 10                EXSR R8100
     C*==============================================================*
     C*          R0100 ... INITIAL
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C                     SETON                     10
     C*
     C           E0100     ENDSR
     C*==============================================================*
     C*          R2000 ... DETAIL
     C*==============================================================*
     C           R2000     BEGSR
     C*
     C           DOPT      IFEQ 'N'
     C           NC25      ANDEQ'Y'
     C                     MOVEL'N'       PRINT
     C                     GOTO E2000
     C                     ENDIF
     C*
     C                     ADD  TP06      WWF04            借貨數量
     C           TP07      MULT TP06      WK01   309       使用金額
     C                     ADD  WK01      WWF05     H      使用金額
     C*
     C           E2000     ENDSR
     C*==============================================================*
     C*          R7100 ... LEVEL BREAK 1 --- INIT.
     C*==============================================================*
     C           R7100     BEGSR
     C*
     C                     MOVEL*BLANK    PRINT   1        是否列印
     C*
     C                     Z-ADD0         WWF04            借貨數量
     C                     Z-ADD0         WWF05            使用金額
     C*
     C           KEYNC     CHAINNC0                  40
     C           *IN40     IFEQ '0'
     C                     MOVELNC05      WWF03     P      中文簡稱
     C                     ELSE
     C                     MOVEL*BLANK    WWF03            中文簡稱
     C                     MOVEL*BLANK    NC25             狀態碼
     C                     END
     C*
     C           E7100     ENDSR
     C*==============================================================*
     C*          R8100 ... LEVEL BREAK 1 --- TOTAL
     C*==============================================================*
     C           R8100     BEGSR
     C*
     C           PRINT     IFEQ 'N'
     C                     GOTO E8100
     C                     END
     C*
     C                     CLEARWF0
     C*
     C                     MOVELTP13      WF01      P      供應商代號
     C                     MOVELTP05      WF02      P      產品代號
     C                     MOVELWWF03     WF03      P      產品簡稱
     C                     Z-ADDWWF04     WF04             借貨數量
     C                     Z-ADDWWF05     WF05             使用金額
     C*
     C                     WRITEWF0
     C*
     C           E8100     ENDSR
