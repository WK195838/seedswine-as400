     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*--------------------------------------------------------------*
     A*    PROGRAM ID   : DBR002                                     *
     A*    AUTHOR       : A1650  DEREK                               *
     A*    CREATE DATRE : 104/01/14                                  *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :銷售系統                                  *
     A*    DESCRIPTION  :抽成獎金品表 (FOR DBQT)                   *
     A*--------------------------------------------------------------*
     A* MODI CODE |   DATE   | DESCRIPTION                           *
     A****************************************************************
     FSEHAPF  IP  E           K        DISK
     FMANCLF03IF  E           K        DISK
     FDBADPF  O   E           K        DISK
     E*---------------------------------------------------------------*
     IHA0
     I                                              HA04  L1
     I                                              HA05  L1
     I           UDS
     I                                      601 6080DATES
     I                                      609 6160DATEE
     I*PRICE :平均價格門檻
     I*BONUS :抽成獎金百分比
     I            DS
     I                                        1   6 DBDA01
     I                                        1   40PRICE
     I                                        5   60BONUS
     C*===============================================================*
     C*                    DEFINE                                     *
     C*===============================================================*
     C           *NAMVAR   DEFN           DBDA01
     C           *LIKE     DEFN AD04      WAD04            含稅銷售金額
     C           *LIKE     DEFN AD05      WAD05            銷售數量
     C*===============================================================*
     C*                    MAIN                                       *
     C*===============================================================*
     C  N10                EXSR R0100                      .INIT
     C   L1                EXSR R7100
     C                     EXSR R2000                      .PROCESS
     CL1 10                EXSR R8100
     C*===============================================================*
     C*          R0100 ... INIT                                       *
     C*===============================================================*
     C           R0100     BEGSR
     C*
     C                     IN   DBDA01
     C                     SETON                     10
     C                     Z-ADDDATES     AD07A            銷售起日
     C                     Z-ADDDATEE     AD07B            銷售迄日
     C*
     C                     ENDSR
     C*===============================================================*
     C*          R7100 ... L1 -- INIT                                 *
     C*===============================================================*
     C           R7100     BEGSR
     C*
     C                     Z-ADD0         WAD04            含稅銷售金額
     C                     Z-ADD0         WAD05            銷售數量
     C                     Z-ADD0         WAD06  309       抽成獎金
     C*取得產品名稱
     C                     MOVEL*BLANK    NC04             產品名稱
     C           HA05      CHAINNC0                  40
     C*
     C                     MOVELHA04      AD01      P      分店代號
     C                     MOVELHA05      AD02      P      產品代號
     C                     MOVELNC04      AD03      P      產品名稱
     C*
     C                     ENDSR
     C*===============================================================*
     C*          R2000 ... PROCESS                                    *
     C*===============================================================*
     C           R2000     BEGSR
     C*
     C                     ADD  HA08      WAD04            含稅銷售金額
     C                     ADD  HA06      WAD05            銷售數量
     C*
     C                     ENDSR
     C*===============================================================*
     C*          R8100 ... L1 -- TOTAL                                *
     C*===============================================================*
     C           R8100     BEGSR
     C*
     C                     Z-ADDWAD04     AD04             含稅銷售金額
     C                     Z-ADDWAD05     AD05             銷售數量
     C*
     C*平均價格=含稅銷售金額/銷售數量
     C*
     C           WAD05     IFNE 0
     C           WAD04     DIV  WAD05     WK01   309       平均價格
     C                     ELSE
     C                     Z-ADD0         WK01             平均價格
     C                     END
     C*
     C*平均價格>=設定條件才給予獎金
     C*
     C           WK01      IFGE PRICE
     C           WAD04     MULT BONUS     WAD06            抽成獎金
     C                     DIV  100       WAD06            抽成獎金
     C                     ENDIF
     C                     Z-ADDWAD06     AD06      H      抽成獎金
     C*
     C                     WRITEAD0
     C*
     C                     ENDSR
