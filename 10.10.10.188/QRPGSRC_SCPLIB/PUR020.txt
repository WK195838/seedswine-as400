      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : PUR020                                        *
      * AUTHER       : A1491  EASON                                  *
      * CREATE DATE  : 97/10/29                                      *
      * UPDATE DATE  : 97/11/17 VINCENT                              *
      * SYSTEM       :                                               *
      * SUBSYSTEM    :採購系統                                     *
      * REMARK       :驗收明細表                                   *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A* 00A 101/03/29 DEREK 增加（產品歸屬）選項                   *
     A* 00B 101/11/26 DEREK 增加（分店代號）選項                   *
     A* 00C需求單號 : 1081472                                      *
     A* 00C 108/12/10 ABBY  F7功能拔除，改執行時產生報表到PC公槽 *
     A*---------------------------------------------------------------
      ****************************************************************
     FPA#BPF  IF  E           K        DISK
     FMANDPF  IF  E           K        DISK
 00A+FUTL2PF  IF  E           K        DISK
     FPUR020D CF  E                    WORKSTN
     F                                              KINFDS DSPFDS
     ILDA        UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      135 135 $ADD
     I                                      598 5990$PRTID
     I                                      600 600 DOWNL
     I                                      601 602 DCL01
     I                                      603 604 DCL02
     I                                      605 609 DCM02S
     I                                      610 614 DCM02E
     I                                      620 6250DCL07S
     I                                      626 6310DCL07E
     I                                      640 648 DCM03S
     I                                      650 658 DCM03E
     I                                      661 6680DATES
     I                                      671 6780DATEE
     I                                      679 679 DOPT
 00A+I                                      680 681 DNC09
 00B+I                                      682 686 DCL04S
 00B+I                                      687 691 DCL04E
     I*
     I* FOR F4 HELP -- GET CURSOR POSITION
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
      *==============================================================*
      *          IDENTIFY A PARAMETER LIST
      *==============================================================*
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C           *IN98     PARM *IN98     IN98    1
     C           *IN97     PARM *IN97     IN97    1
      *==============================================================*
      *                    KLIST
      *==============================================================*
     C           KEYND     KLIST
     C                     KFLD           DCL01
     C                     KFLD           DCL02
 00A+C           KEYL2     KLIST
 00A+C                     KFLD           #L201   8
 00A+C                     KFLD           #L202  10
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C  N97N98             EXSR RTN010                     .INIT SCREEN
  01-C           *IN03     DOUEQ'1'
    C           *IN88     OREQ '1'
    C                     EXFMTDSPD1
    C                     MOVEA*ALL'0'   *IN,60
  02-C           *IN03     IFEQ '0'
    C*
  03-C           *IN04     CASEQ'1'       RTNF4
    C                     CAS            RTN100            .CHECK     ?
  03-C                     ENDCS
 00C-C*  F7 DOWNLOAD*
 00C-C*          *IN99     IFEQ '0'
 00C-C*          *IN07     ANDEQ'1'
 00C-C*                    SETOF                     88
 00C-C*                    CALL 'PUR020C2'
 00C-C*                    ENDIF
     C                     ENDIF
  01-C                     ENDDO
     C*
     C                     EXSR OUTLDA
     C*
     C   03                SETON                     LR
     C                     RETRN
      *===============================================================*
      *          RTN010....INIT SCREEN
      *===============================================================*
01===C           RTN010    BEGSR
|    C*
|    C           *NAMVAR   DEFN *LDA      LDA
|    C                     IN   LDA
|    C                     Z-ADD2         $EVR
|    C*                    MOVEL$USER     AUT     3
|    C*          AUT       IFNE 'D91'
|    C*          AUT       ANDNE'REP'
|    C*                    SETON                     31
|    C*                    END
|    C                     MOVE *BLANK    DCL01
|    C                     MOVE *BLANK    DCL02
|    C                     MOVE *BLANK    DCM02S
|    C                     MOVE *BLANK    DCM02E
|    C                     MOVE *BLANK    DCM03S
|    C                     MOVE *BLANK    DCM03E
|    C                     Z-ADD0         DCL07S
|    C                     Z-ADD0         DCL07E
|    C                     Z-ADD0         W6      60
|    C                     MOVEL'SCR001'  APPSCR  6
|    C                     Z-ADD0         D#ROW
|    C                     Z-ADD0         D#COL
|00A+C                     MOVEL*BLANK    DNC09
|00B+C                     MOVEL*BLANK    DCL04S
|00B+C                     MOVEL*BLANK    DCL04E
|    C*
01===C                     ENDSR
      *===============================================================*
      *          RTN100....CHECK SCREEN
      *===============================================================*
02===C           RTN100    BEGSR
|    C**公司別
| 01-C           DCL01     IFEQ *BLANKS
|   C                     SETON                     6099
|   C                     MOVEL'ERR0001' ERRID
|   C                     MOVEL'SCMF'    ERRF
|   C                     MOVEL*BLANK    D#B03
| 01-C                     END
|    C   99                GOTO END100
|    C**公司別
|    C           DCL01     CHAIN#B0                  40
| 02-C           *IN40     IFEQ '0'
|   C                     MOVEL#B03      D#B03
| 02*C                     ELSE
|   C                     SETON                     6099
|   C                     MOVEL*BLANK    D#B03
|   C                     MOVEL'ERR0018' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 02-C                     END
|    C   99                GOTO END100
|    C**通路
| 03-C           DCL02     IFEQ *BLANKS
|   C                     SETON                     6199
|   C                     MOVEL'ERR0001' ERRID
|   C                     MOVEL'SCMF'    ERRF
|   C                     MOVEL*BLANK    DND04
| 03-C                     END
|    C   99                GOTO END100
|    C**通路別
|    C           KEYND     CHAINND0                  40
| 04-C           *IN40     IFEQ '0'
|   C                     MOVELND04      DND04
| 04*C                     ELSE
|   C                     SETON                     6099
|   C                     MOVEL*BLANK    DND04
|   C                     MOVEL'ERR0018' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 04-C                     END
|    C   99                GOTO END100
|    C**供應商代號
|     *
| 05-C           DCM02S    IFEQ *BLANKS
|   C                     MOVE *LOVAL    DCM02S
| 05-C                     END
|    C*
| 06-C           DCM02E    IFEQ *BLANKS
|   C                     MOVE *HIVAL    DCM02E
| 06-C                     END
|    C*
| 07-C           DCM02S    IFGT DCM02E
|   C                     SETON                     626399
|   C                     MOVEL'ERR0005' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 07-C                     END
|    C   99                GOTO END100
|    C*
|    C**到貨運日
| 08-C           DCL07S    IFEQ 0
|   C                     Z-ADD010192    DCL07S
| 08*C                     ELSE
|   C                     CALL 'P09'
|   C                     PARM DCL07S    P0901I  60
|   C                     PARM           P0901O  1
| 09-C           P0901O    IFEQ 'N'
|   C                     SETON                     6499
|   C                     MOVEL'ERR0004' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 09-C                     END
| 08-C                     END
|    C   99                GOTO END100
|    C*
| 10-C           DCL07E    IFEQ 0
|   C                     Z-ADD123129    DCL07E
| 10*C                     ELSE
|   C                     CALL 'P09'
|   C                     PARM DCL07E    P0901I  60
|   C                     PARM           P0901O  1
| 11-C           P0901O    IFEQ 'N'
|   C                     SETON                     6599
|   C                     MOVEL'ERR0004' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 11-C                     END
| 10-C                     END
|    C   99                GOTO END100
|    C**
|    C                     CALL 'RES001'
|    C                     PARM 'MDY'     S001I1  3
|    C                     PARM DCL07S    S001I2  60
|    C           W6        PARM           S001O1  60
|    C                     MOVE W6        HB2@F6  6
|    C                     EXSR HB@68L
|    C                     MOVE HB2@T8    DATES
|    C*
|    C                     CALL 'RES001'
|    C                     PARM 'MDY'     S001I1  3
|    C                     PARM DCL07E    S001I2  60
|    C           W6        PARM           S001O1  60
|    C                     MOVE W6        HB2@F6  6
|    C                     EXSR HB@68L
|    C                     MOVE HB2@T8    DATEE
|    C*
| 12-C           DATES     IFGT DATEE
|   C                     SETON                     646599
|   C                     MOVEL'ERR0005' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 12-C                     END
|    C   99                GOTO END100
|    C**產品代號
|     *
| 13-C           DCM03S    IFEQ *BLANKS
|   C                     MOVE *LOVAL    DCM03S
| 13-C                     END
|    C*
| 14-C           DCM03E    IFEQ *BLANKS
|   C                     MOVE *HIVAL    DCM03E
| 14-C                     END
|    C*
| 15-C           DCM03S    IFGT DCM03E
|   C                     SETON                     666799
|   C                     MOVEL'ERR0005' ERRID
|   C                     MOVEL'SCMF'    ERRF
| 15-C                     END
|    C   99                GOTO END100
|    C**分店代號
|     *
| 13-C           DCL04S    IFEQ *BLANK
|   C                     MOVE *LOVAL    DCL04S
| 13-C                     END
|    C*
| 14-C           DCL04E    IFEQ *BLANK
|   C                     MOVE *HIVAL    DCL04E
| 14-C                     END
|    C*
| 15-C           DCL04S    IFGT DCL04E
|   C                     SETON                     707199
|   C                     MOVEL'UPT1011' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 15-C                     END
|    C   99                GOTO END100
|    C**
|    C**資料選擇
| 12-C           DTAOPT    IFNE '1'
|   C           DTAOPT    ANDNE'2'
|   C           DTAOPT    ANDNE'3'
|   C                     SETON                     6899
|   C                     MOVEL'UPT0030' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 12-C                     END
|    C   99                GOTO END100
|    C                     MOVELDTAOPT    DOPT
|    C*00A+START**產品歸屬
| 14-C           DNC09     IFNE *BLANK
|   C                     MOVEL'GUISHU  '#L201     P
|   C                     MOVELDNC09     #L202     P
|   C           KEYL2     CHAINL20                  40
| 15-C           *IN40     IFEQ '0'
|   C                     MOVELL203      NC09D
| 15*C                     ELSE
|   C                     SETON                     6999
|   C                     MOVEL'ERR0091' ERRID
|   C                     MOVEL'SCMF'    ERRF
|   C                     MOVEL*BLANK    NC09D
| 15-C                     ENDIF
| 14*C                     ELSE
|   C                     MOVEL*BLANK    NC09D
| 14-C                     ENDIF
|    C   99                GOTO END100
|    C*00A+END
|    C*
|    C                     SETON                     88
02===C           END100    ENDSR
      *===============================================================*
      *          RTNF4 ....F4 HELP
      *===============================================================*
03===C           RTNF4     BEGSR
|    C*
|    C                     SETOF                     88
|    C********************
|    C*  GET ROW,COL     *
|    C********************
|    C           #CSR      DIV  256       D#ROW            *ROW
|    C                     MVR            D#COL            *COL
|    C** 游標位置即為(D#ROW,D#COL)
| 01-C           APPSCR    IFEQ 'SCR001'
|   C*
|   C*供應商
| 02-C           D#ROW     IFEQ 10
|   C           D#COL     ANDGE38
|   C           D#COL     ANDLE42
|   C                     CALL 'SWD002'
|   C                     PARM           DCM02S
|   C                     GOTO ENDF4
| 02-C                     END
|   C*
| 03-C           D#ROW     IFEQ 10
|   C           D#COL     ANDGE48
|   C           D#COL     ANDLE52
|   C                     CALL 'SWD002'
|   C                     PARM           DCM02E
|   C                     GOTO ENDF4
| 03-C                     END
|   C*
| 04-C           D#ROW     IFEQ 14
|   C           D#COL     ANDGE38
|   C           D#COL     ANDLE46
|   C                     CALL 'SWD005'
|   C                     PARM           DCL01
|   C                     PARM           DCM03S
|   C                     GOTO ENDF4
| 04-C                     END
|   C*
| 05-C           D#ROW     IFEQ 14
|   C           D#COL     ANDGE50
|   C           D#COL     ANDLE58
|   C                     CALL 'SWD005'
|   C                     PARM           DCL01
|   C                     PARM           DCM03E
|   C                     GOTO ENDF4
| 05-C                     END
|   C*00A+START產品歸屬
| 04-C           D#ROW     IFEQ 16
|   C           D#COL     ANDGE38
|   C           D#COL     ANDLE39
|   C                     CALL 'SWD001'
|   C                     PARM 'GUISHU'  PARMI1  8
|   C           DNC09     PARM DNC09     PARMO1 10
|   C                     GOTO ENDF4
| 04-C                     END
|   C*00A+END
|   C*
| 06-C           D#ROW     IFEQ 19
|   C           D#COL     ANDGE38
|   C           D#COL     ANDLE39
|   C                     CALL 'WDS011'
|   C           $PRTID    PARM $PRTID    S011O1  20
|   C                     GOTO ENDF4
| 06-C                     END
|   C*
| 01-C                     END
|    C*
03===C           ENDF4     ENDSR
      *===============================================================*
      *          OUTLDA....OUT LDA
      *===============================================================*
04===C           OUTLDA    BEGSR
|    C*
|    C                     OUT  *NAMVAR
|    C*
04===C           ENDLDA    ENDSR
     C/COPY HBP2CVTR
