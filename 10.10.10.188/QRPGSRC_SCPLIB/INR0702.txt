      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : INR0702                                       *
      * AUTHER       : VINCENT                                       *
      * CREATE DATE  : 97/12/18                                      *
      * UPDATE DATE  :                                               *
      * SYSTEM       :                                               *
      * SUBSYSTEM    :庫存系統                                     *
      * REMARK       :陳列品退出表表                               *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* 00A 101/11/27 DEREK增加退出日期
      ****************************************************************
     FSCWK10  IP  E           K        DISK
     FPA#BPF  IF  E           K        DISK
     FINR070P O   E             39     PRINTER
 00A+FSCWK101 O   E                    DISK
      *
     IWK10
     I                                              W1001 L3
     I                                              W1002 L2
     I                                              W1006 L1
 00A+IPTDA01      DS
 |   I                                        1   1 $PFMT
 00A+I                                        2   2 $PTYPE
     I*
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      135 135 $ADD
     I                                      598 5990$PRTID
     I                                      600 600 DOWNL
     I                                      601 602 DTL05
     I                                      603 604 DDOC
     I                                      605 606 DTL06S
     I                                      607 608 DTL06E
     I                                      609 613 DTL07S
     I                                      614 618 DTL07E
     I                                      619 6190DATA
     I                                      621 629 DTL02S
     I                                      631 639 DTL02E
     I                                      641 6460DTL08S
     I                                      651 6560DTL08E
     I                                      661 6680DATES
     I                                      671 6780DATEE
 00A+C*==============================================================*
 |   C*                    DEFN
 |   C*==============================================================*
 00A=C           *NAMVAR   DEFN           PTDA01
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C  N10                EXSR RTN010
     C*
     C   39                EXSR RTN900
      *
 00A+C*退出日期
 |   C                     CALL 'P31'
 |   C                     PARM W1008     P3101I  80       原始日期
 |   C                     PARM '2'       P3102I  1        原始日期格式
 |   C                     PARM '1'       P3103I  1        原始曆別
 |   C                     PARM $PFMT     P3104I  1        轉換日期格式
 |   C                     PARM $PTYPE    P3105I  1        轉換曆別
 00A+C           P1008     PARM           P3111O  80       轉換後日期
     C*
     C                     ADD  W1009     Q1
     C                     ADD  W1009     Q2
     C                     ADD  W1009     Q3
     C                     WRITEPD1
     C                     SETOF                     3031
     C*
 00A+C*WRITE TO WRKFILE (SCWK101)
 00A+C                     EXSR RTN100
      *
     CL1 10                EXSR RTNL1
     CL2 10                EXSR RTNL2
     CL3 10                EXSR RTNL3
     CLR                   EXSR RTNLR
     C*==============================================================*
     C*          RTN010....GET HEADING
     C*==============================================================*
01===C           RTN010    BEGSR
|     *
|00A+C                     IN   PTDA01
|     * GET公司名稱
|    C           DTL05     CHAIN#B0                  40
|    C   40                MOVEL*BLANK    D#B03
|    C  N40                MOVEL#B03      D#B03
|     *
|    C                     WRITEPH1
|     *
|    C                     SETOF                     39
|    C                     SETON                     103031
|     *
|    C                     Z-ADD*ZERO     Q1
|    C                     Z-ADD*ZERO     Q2
|    C                     Z-ADD*ZERO     Q3
|     *
01===C                     ENDSR
 00A+C*==============================================================*
 |   C*          RTN100....WRITE TO WORKFILE(SCWK101)
 |   C*==============================================================*
 |   C           RTN100    BEGSR
 |   C*
 |   C                     MOVELW1001     W1001A    P      公司代號
 |   C                     MOVELW1002     W1002A    P      產品代號
 |   C                     MOVELW1003     W1003A    P      產品簡稱
 |   C                     MOVELW1006     W1006A    P      分店代號
 |   C                     MOVELW1007     W1007A    P      分店簡稱
 |   C                     Z-ADDP1008     W1008A           退出日期
 |   C                     Z-ADDW1009     W1009A           退出數量
 |   C                     MOVELW1010     W1010A    P      驗收單號
 |   C                     MOVELW1011     W1011A    P      陳列退出單號
 |   C                     WRITEWK10A
 |   C*
 00A+C                     ENDSR
      *==============================================================*
      *          RTN900....OVERFLOW
      *==============================================================*
03===C           RTN900    BEGSR
|    C*
|    C                     WRITEPE1
|    C                     WRITEPH1
|    C                     SETOF                     39
|     *
01===C                     ENDSR
      *==============================================================*
      *          RTNL1 ....分店合計
      *==============================================================*
03===C           RTNL1     BEGSR
|    C*
|    C                     SETON                     31
|    C                     WRITEPD2
|    C*
|    C                     Z-ADD*ZERO     Q1
|    C*
03===C                     ENDSR
      *==============================================================*
      *          RTNL2 ....產品合計
      *==============================================================*
03===C           RTNL2     BEGSR
|    C*
|    C                     SETON                     3031
|    C                     WRITEPD3
|    C*
     C                     Z-ADD*ZERO     Q1
|    C                     Z-ADD*ZERO     Q2
|    C*
03===C                     ENDSR
      *==============================================================*
      *          RTNL3 ....總計
      *==============================================================*
02===C           RTNL3     BEGSR
|    C*
|    C                     WRITEPD4
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTNLR ....(NO DATA)
      *==============================================================*
06===C           RTNLR     BEGSR
|     *
| 01-C           *IN10     IFEQ '0'
|   C                     EXSR RTN010
|   C                     WRITEPE9
| 01-C                     END
|     *
|    C                     WRITEPE2
|     *
06===C                     ENDSR
