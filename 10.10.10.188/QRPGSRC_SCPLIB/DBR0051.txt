     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*--------------------------------------------------------------*
     A*    PROGRAM ID   : DBR005                                     *
     A*    AUTHOR       : A1650  DEREK                               *
     A*    CREATE DATRE : 104/02/04                                  *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :銷售系統                                  *
     A*    DESCRIPTION  :供應商進貨彙總表(FOR DBQT)                *
     A*--------------------------------------------------------------*
     A* MODI CODE |   DATE   | DESCRIPTION                           *
     A****************************************************************
     FINTFPF  IP  E           K        DISK
     FDBAKPF  UF  E           K        DISK                      A
     E*---------------------------------------------------------------*
     ITF0
     I                                              TF02  L1
     I                                              TF03  L1
     IDA02        DS
     I                                        1   43DUTY
     C*===============================================================*
     C*                    DEFINE                                     *
     C*===============================================================*
     C           *NAMVAR   DEFN           DA02             稅率REFLIB
     C           *LIKE     DEFN AK05      WAK05            退貨數量
     C           *LIKE     DEFN AK08      WAK08            退貨金額
     C*===============================================================*
     C*                    KEY LIST                                   *
     C*===============================================================*
     C           KEYAK     KLIST                           .DBAKPF
     C                     KFLD           AK01             供應商代號
     C                     KFLD           AK03             產品代號
     C*===============================================================*
     C*                    MAIN                                       *
     C*===============================================================*
     C  N10                EXSR R0100                      .INIT
     C   L1                EXSR R7100
     C                     EXSR R2000                      .PROCESS
     CL1 10                EXSR R8100
     C*===============================================================*
     C*          R0100 ... INIT                                       *
     C*===============================================================*
     C           R0100     BEGSR
     C*
     C                     IN   DA02
     C                     SETON                     10
     C*
     C                     ENDSR
     C*===============================================================*
     C*          R7100 ... L1 -- INIT                                 *
     C*===============================================================*
     C           R7100     BEGSR
     C*
     C                     Z-ADD0         WAK05            退貨數量
     C                     Z-ADD0         WAK08            退貨金額
     C                     MOVELTF02      AK01      P      供應商代號
     C                     Z-ADD0         AK02             編號
     C                     MOVELTF03      AK03      P      產品代號
     C                     Z-ADD0         AK04             進貨數量
     C                     Z-ADD0         AK05             退貨數量
     C                     Z-ADD0         AK06             淨進貨量
     C                     Z-ADD0         AK07             進貨金額
     C                     Z-ADD0         AK08             退貨金額
     C                     Z-ADD0         AK09             進貨淨額
     C*
     C                     ENDSR
     C*===============================================================*
     C*          R2000 ... PROCESS                                    *
     C*===============================================================*
     C           R2000     BEGSR
     C*
     C*退出數量
     C                     ADD  TF04      WAK05            退貨數量
     C*
     C                     CALL 'MCC0021'
     C                     PARM           TF05             公司別
     C                     PARM           TF06             通路別
     C                     PARM           TF03             產品代號
     C                     PARM           TF08             單據日期
     C                     PARM           WTX18   82       單價
     C*
     C           TF04      MULT WTX18     WAMT   110H      金額
     C                     ADD  WAMT      WAK08            退貨金額
     C           WAMT      MULT DUTY      WTAX    90H      稅額
     C                     ADD  WTAX      WAK08            退貨金額
     C*
     C                     ENDSR
     C*===============================================================*
     C*          R8100 ... L1 -- TOTAL                                *
     C*===============================================================*
     C           R8100     BEGSR
     C*
     C           KEYAK     CHAINAK0                  40
     C                     Z-ADDWAK05     AK05             退貨數量
     C                     Z-ADDWAK08     AK08             退貨金額
     C*
     C           *IN40     IFEQ '0'
     C                     UPDATAK0
     C                     ELSE
     C                     WRITEAK0
     C                     END
     C*
     C                     ENDSR
