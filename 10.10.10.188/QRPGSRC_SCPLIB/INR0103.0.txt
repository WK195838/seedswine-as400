      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : INR0103                                       *
      * AUTHER       : A1388  WIND                                   *
      * CREATE DATE  : 2008/11/03                                    *
      * UPDATE DATE  :                                               *
      * SYSTEM       :西祺進銷存                                   *
      * SUBSYSTEM    :庫存系統                                     *
      * REMARK       :帳列產品庫存金額表－依分店                   *
      ****************************************************************
     FINTOLF03IF  E           K        DISK
     F            CM0                               KRENAMECM0L3
     F            TH0                               KRENAMETH0L3
     F            TM0                               KRENAMETM0L3
     F                                              KINFDS TORCD3
     FINTOLF05IF  E           K        DISK
     F            CM0                               KRENAMECM0L5
     F            TF0                               KRENAMETF0L5
     F            TH0                               KRENAMETH0L5
     F            TJ0                               KRENAMETJ0L5
     F            HA0                               KRENAMEHA0L5
     F                                              KINFDS TORCD5
     FINTAPF  IF  E           K        DISK
     FINTBLF02IF  E           K        DISK
     FMANCPF  IF  E           K        DISK
     FMANEPF  IF  E           K        DISK
     FUTL2PF  IF  E           K        DISK
     FPOCCLF02IF  E           K        DISK
     FSCWK08  UF  E           K        DISK                      A
     FSCWK081 UF  E           K        DISK                      A
     ILDA        UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      135 135 $ADD
     I                                      598 5990$PRTID
     I                                      600 600 DOWNL
     I                                      601 602 DTO01
     I                                      603 604 DDOC
     I                                      605 606 DTO02S
     I                                      607 608 DTO02E
     I                                      609 613 DTO03S
     I                                      614 618 DTO03E
     I                                      619 6190DATA
     I                                      621 629 DTO04S
     I                                      631 639 DTO04E
     I                                      641 6460DTO05
     I                                      641 6420DTOYY
     I                                      651 6580DATES
     I                                      651 6560SYYMM
     I                                      661 6680DATEE
     I                                      667 6680DATED
     I*
     I            DS
     I                                        1  32 L203
     I                                        1   1 L203F
     I            DS
     I                                        1   80KCC07
     I                                        1   40KCC07Y
     ITORCD3      DS
     I                                     *RECORD  RCDR3
     ITORCD5      DS
     I                                     *RECORD  RCDR5
      *==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN TA10      WQTY
     C           *LIKE     DEFN TA09      WTA09
     C           *LIKE     DEFN TA08      TTAMT
     C           *LIKE     DEFN TA08      WFLD1
     C           *LIKE     DEFN TA10      WFLD2
     C           *LIKE     DEFN W807      H807
     C           *LIKE     DEFN TO04      KTO04
     C           *LIKE     DEFN CC09      WCC09
     C*==============================================================*
      *                    KLIST
      *==============================================================*
     C           KEYTA     KLIST
     C                     KFLD           DTO01
     C                     KFLD           SYYMM
     C           KEYTB     KLIST
     C                     KFLD           DTO01
     C                     KFLD           SYYMM
     C                     KFLD           TA03
     C           KEYTO     KLIST
     C                     KFLD           TA01
     C                     KFLD           TA03
     C                     KFLD           DATES
     C           KEYTO1    KLIST
     C                     KFLD           TA01
     C                     KFLD           TB03
     C                     KFLD           TB04
     C                     KFLD           TB05
     C                     KFLD           DATES
     C           KEYTO2    KLIST
     C                     KFLD           TA01
     C                     KFLD           TB03
     C                     KFLD           TB04
     C                     KFLD           TB05
     C           KEYW8     KLIST
     C                     KFLD           W801
     C                     KFLD           W802
     C                     KFLD           W803
     C                     KFLD           W805
     C           KEYNE     KLIST
     C                     KFLD           W801
     C                     KFLD           W802
     C                     KFLD           W803
     C           KEYNC     KLIST
     C                     KFLD           W801
     C                     KFLD           W805
     C           KEYL2     KLIST
     C                     KFLD           L201
     C                     KFLD           L202
     C           KEYCC     KLIST
     C                     KFLD           KCC01   2
     C                     KFLD           KCC02   2
     C                     KFLD           KCC04   9
     C                     KFLD           KCC06   1
     C                     KFLD           KCC07
     C           KEYCC1    KLIST
     C                     KFLD           KCC01
     C                     KFLD           KCC02
     C                     KFLD           KCC04
     C                     KFLD           KCC06
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C                     EXSR RTN010
     C*
     C                     SETOF                     54
     C           KEYTA     SETLLINTAPF
  01-C           *IN54     DOUEQ'1'
    C           KEYTA     READEINTAPF                   54
    C   54                LEAVE
    C*產品範圍
  02-C           TA03      IFLT DTO04S
    C                     ITER
  02-C                     ENDIF
    C*
  03-C           TA03      IFGT DTO04E
    C                     ITER
  03-C                     ENDIF
    C*未月結單位成本H807
  04-C           DDOC      IFEQ '02'
    C                     EXSR RTN100
  04-C                     ENDIF
    C*
    C*處理明細工作檔SCWK080
    C                     EXSR RTN200
    C*
  01-C                     ENDDO
     C*
     C*彙總總表工作檔SCWK081
     C                     EXSR RTN300
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INZ.
     C*==============================================================*
01===C           RTN010    BEGSR
|    C*
|    C           *NAMVAR   DEFN *LDA      LDA
|    C                     IN   LDA
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          RTN040....取得最新年度報價單價
     C*==============================================================*
02===C           RTN040    BEGSR
|    C*
     C                     Z-ADD0         WCC09
     C*
|    C           KEYCC     SETGTCC0
|    C           KEYCC1    REDPECC0                      47
| 01-C           *IN47     IFEQ '0'
|   C           KCC07Y    ANDEQCC05
|    *
| 02-C           CC08      IFNE 0
| 03-C           KCC07     IFLE CC08
|   C           KCC07     ANDGECC07
|   C                     Z-ADDCC09      WCC09
| 03-C                     ENDIF
| 02*C                     ELSE
| 04-C           KCC07     IFGE CC07
|   C                     Z-ADDCC09      WCC09
| 04-C                     ENDIF
| 02-C                     ENDIF
| 01-C                     ENDIF
|     *
02===C                     ENDSR
      *===============================================================*
      *          RTN100....未月結-計算單位成本
      *===============================================================*
03===C           RTN100    BEGSR
|    C*
|    C                     SETOF                     53
|    C                     Z-ADD0         WQTY
|    C                     Z-ADD0         WTA09            成本調整金額
|    C                     Z-ADD0         TTAMT
|    C*讀取同產品異動明細
|    C           KEYTO     SETLLINTOLF03
|    C                     READ INTOLF03                 45
| 01-C           *IN45     DOWEQ'0'
|   C           TO01      ANDEQTA01
|   C           TO04      ANDEQTA03
|   C           TO05      ANDLEDATEE
|   C                     SETON                     53
|   C                     EXSR RTN101                     累計單據數量
|   C                     READ INTOLF03                 45
| 01-C                     ENDDO
|    C                     EXSR RTN102                     計算單位成
|    C*
03===C                     ENDSR
      *===============================================================*
      *          RTN101 ...累計單據數量及金額
      *===============================================================*
04===C           RTN101    BEGSR
|    C*
| 01-C           RCDR3     IFEQ 'TM0L3'                    <產品成本調整
|   C                     ADD  TO104     WTA09            調整金額
| 01*C                     ELSE
| 02-C                     SELEC
|   C           RCDR3     WHEQ 'CM0L3'                    <驗收單
|   C           TO101     MULT TO121     WAMT1  112H      當時進價
|   C                     ADD  WAMT1     TTAMT
|   C           RCDR3     WHEQ 'TF0L3'                    <進貨退出單
|   C                     MOVELTO01      KCC01            公司別
|   C                     MOVELTO14      KCC02            通路別
|   C                     MOVELTO04      KCC04            產品代號
|   C                     MOVEL'1'       KCC06            年度報價
|   C                     Z-ADDTO05      KCC07            退出日期
|   C                     EXSR RTN040
|   C           WCC09     MULT TO121     WAMT1     H      最新年度價
|   C                     SUB  WAMT1     TTAMT
|   C           RCDR3     WHEQ 'TH0L3'                    <陳列轉進貨單
|   C           TO101     MULT TO121     WAMT1     H      當時進價
|   C                     ADD  WAMT1     TTAMT
|   C           RCDR3     WHEQ 'TJ0L3'                    <存貨調整單
|   C           TA04      MULT TO121     WAMT1     H      上月單位成本
|   C                     ADD  WAMT1     TTAMT
| 02-C                     ENDSL
|   C*
| 03-C           RCDR3     IFEQ 'TF0L3'                    <進貨退出單
|   C                     SUB  TO121     WQTY
| 03*C                     ELSE
|   C                     ADD  TO121     WQTY
| 03-C                     ENDIF
|   C*
| 01-C                     ENDIF
|    C*
04===C                     ENDSR
      *===============================================================*
      *          RTN102....計算單位成本
      *===============================================================*
05===C           RTN102    BEGSR
|    C*
|    C*無異動
| 01-C           *IN53     IFEQ '0'
|   C                     Z-ADDTA04      H807             上月單位成本
| 01*C                     ELSE
|   C*負庫存量或金額
| 02-C           WQTY      IFLT 0
|   C           TTAMT     ORLT 0
|   C                     Z-ADD0         H807             單位成本
| 02*C                     ELSE
|   C           TA05      ADD  TTAMT     WFLD1
|   C                     ADD  WTA09     WFLD1
|   C           TA06      ADD  WQTY      WFLD2
|   C           WFLD1     DIV  WFLD2     H807      H
| 02-C                     ENDIF
| 01-C                     ENDIF
|    C*
05===C                     ENDSR
      *==============================================================*
      *          RTN200 ... INTBPF BY分店
      *===============================================================*
06===C           RTN200    BEGSR
|    C*
|    C                     SETOF                     55
|    C           KEYTB     SETLLINTBLF02
| 01-C           *IN55     DOUEQ'1'
|   C           KEYTB     READEINTBLF02                 55
|   C   55                LEAVE
|   C*通路範圍
| 02-C           TB03      IFLT DTO02S
|   C                     ITER
| 02-C                     ENDIF
|   C*
| 03-C           TB03      IFGT DTO02E
|   C                     ITER
| 03-C                     ENDIF
|   C*分店範圍
| 04-C           TB04      IFLT DTO03S
|   C                     ITER
| 04-C                     ENDIF
|   C*
| 05-C           TB04      IFGT DTO03E
|   C                     ITER
| 05-C                     ENDIF
|   C*未月結(當期)
| 06-C           DDOC      IFEQ '02'
|   C                     EXSR RTN201                     期初值
|   C                     EXSR RTN202                     截止區間值
|   C*已月結
| 06*C                     ELSE
|   C                     EXSR RTN205
| 06-C                     ENDIF
|   C*
| 01-C                     ENDDO
|    C*
06===C                     ENDSR
      *===============================================================*
      *          RTN201 .. 未月結期初值 WRITE SCWK08
      *===============================================================*
07===C           RTN201    BEGSR
|    C*
|    C                     MOVELTB01      W801      P      公司
|    C                     MOVELTB03      W802      P      通路
|    C                     MOVELTB04      W803      P      分店
|    C                     MOVELTB05      W805      P      產品
|    C           KEYW8     CHAINSCWK08               40
| 01-C           *IN40     IFEQ '1'
|   C*分店簡稱
|   C                     MOVEL*BLANK    W804
|   C           KEYNE     CHAINMANEPF               41
|   C  N41                MOVELNE05      W804
|   C*產品簡稱
|   C                     MOVEL*BLANK    W806
|   C           KEYNC     CHAINMANCPF               41
|   C  N41                MOVELNC05      W806
|   C*
|   C                     Z-ADDH807      W807             單位成本
|   C                     Z-ADDTB06      W808             期初庫存量
|   C           W807      MULT W808      W809      H      庫存金額
|   C                     WRITEWK8
| 01-C                     ENDIF
|    C*
07===C                     ENDSR
      *===============================================================*
      *          RTN202 ..未月結-當期截止區間之產品異動
      *===============================================================*
08===C           RTN202    BEGSR
|    C*
|    C                     SETOF                     56
|    C           KEYTO1    SETLLINTOLF05
| 01-C           *IN56     DOUEQ'1'
|   C           KEYTO2    READEINTOLF05                 56
|   C   56                LEAVE
|   C*異動日期範圍
| 02-C           TO05      IFLT DATES
|   C                     ITER
| 02-C                     ENDIF
|   C*
| 03-C           TO05      IFGT DATEE
|   C                     ITER
| 03-C                     ENDIF
|   C*產品異動數量
|   C                     EXSR RTN203
|   C*
| 01-C                     ENDDO
|    C*
08===C                     ENDSR
      *===============================================================*
      *          RTN203 ..未月結產品異動數量INTOLF
      *===============================================================*
09===C           RTN203    BEGSR
|    C* 數量
|    C                     Z-ADD0         WQTY
|    C                     MOVE *BLANK    L201
|    C                     MOVEL'INTOLF ' L201
|    C                     MOVELRCDR5     L202
|    C           KEYL2     CHAINUTL2PF               40
| 01-C           *IN40     IFEQ '0'
| 02-C                     SELEC
|   C           L203F     WHEQ '-'
|   C           TO121     MULT -1        WQTY             異動數量
|   C           L203F     WHEQ '@'
|   C           TO121     MULT -1        WQTY
|   C                     OTHER
|   C                     Z-ADDTO121     WQTY
| 02-C                     ENDSL
| 01-C                     ENDIF
|    C*
|    C* UPDATE SCWK08 明細工作檔
|    C                     EXSR RTN204
|    C*
09===C                     ENDSR
      *===============================================================*
      *          RTN204 ..UPDATE SCWK08  (明細工作檔)
      *===============================================================*
10===C           RTN204    BEGSR
|    C*
|    C                     MOVELTO01      W801      P
|    C                     MOVELTO02      W802      P
|    C                     MOVELTO03      W803      P
|    C                     MOVELTO04      W805      P
|    C           KEYW8     CHAINSCWK08               40
| 01-C           *IN40     IFEQ '0'
|   C                     ADD  WQTY      W808             本期庫存
|   C           W807      MULT W808      W809      H      庫存金額
|   C                     UPDATWK8
| 01-C                     ENDIF
|    C*
10===C                     ENDSR
      *==============================================================*
      *          RTN205 ..已月結- WRITE SCWK08 (明細工作檔)
      *===============================================================*
11===C           RTN205    BEGSR
|    C*
|    C                     MOVELDTO01     W801      P
|    C                     MOVELTB03      W802      P
|    C                     MOVELTB04      W803      P
|    C                     MOVELTB05      W805      P
|    C*
|    C           KEYW8     CHAINSCWK08               40
| 01-C           *IN40     IFEQ '1'
|   C*分店簡稱
|   C                     MOVEL*BLANK    W804
|   C           KEYNE     CHAINMANEPF               41
|   C  N41                MOVELNE05      W804      P
|   C*產品簡稱
|   C                     MOVEL*BLANK    W806
|   C           KEYNC     CHAINMANCPF               41
|   C  N41                MOVELNC05      W806      P
|   C*
|   C                     Z-ADDTA07      W807             本期單位成本
|   C           TB06      ADD  TB08      W808
|   C                     SUB  TB09      W808
|   C                     ADD  TB10      W808
|   C                     SUB  TB11      W808
|   C                     ADD  TB12      W808
|   C                     SUB  TB13      W808
|   C                     ADD  TB14      W808
|   C                     SUB  TB15      W808             帳列庫存量
|   C           W807      MULT W808      W809      H      庫存金額
|   C                     WRITEWK8
| 01-C                     ENDIF
|    C*
11===C                     ENDSR
      *===============================================================*
      *          RTN300... 總表工作檔SCWK081
      *===============================================================*
12===C           RTN300    BEGSR
|    C*
|    C                     SETOF                     40
|    C           *LOVAL    SETLLSCWK08
| 01-C           *IN40     DOUEQ'1'
|   C                     READ SCWK08              N    40
|   C   40                LEAVE
|   C*
|   C                     EXSR RTN301
|   C*
| 01-C                     ENDDO
|    C*
12===C                     ENDSR
      *===============================================================*
      *          RTN301...WRITE OR UPDATE SCWK081
      *===============================================================*
13===C           RTN301    BEGSR
|    C*
|    C           W805      CHAINSCWK081              41
| 01-C           *IN41     IFEQ '1'
|   C                     MOVELW805      W8105     P
|   C                     MOVELW806      W8106     P
|   C                     Z-ADDW807      W8107
|   C                     Z-ADDW808      W8108            庫存量
|   C                     Z-ADDW809      W8109            庫存金額
|   C                     WRITEWK81
  01*C                     ELSE
|   C                     ADD  W808      W8108
|   C           W8107     MULT W8108     W8109
|   C                     UPDATWK81
| 01-C                     ENDIF
|    C*
13===C                     ENDSR
