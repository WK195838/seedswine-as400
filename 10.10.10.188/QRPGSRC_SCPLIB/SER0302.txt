     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM-ID   : SER0302                                    *
     H*    PROGRAM NAME :銷貨收入分店彙總表                        *
     H*    AUTHOR       : A1491 EASON                                *
     H*    CREATE DATE  : 97/10/28                                   *
     H*    UPDATE DATE  :99/04/29 CAROL(9904)未稅金額用HA07不用計算
     H*    SYSTEM       :採購管理                                  *
     A*---------------------------------------------------------------
     A* 00A需求單號: 1080943
     A* 00A CAROL  2019/08/01新增篩選條件供應商代號&產品代號
     A*---------------------------------------------------------------
     H****************************************************************
     FSCWK07  IP  E           K        DISK
 00A+FMANCPF  IF  E           K        DISK
     FMANDPF  IF  E           K        DISK
     FMANEPF  IF  E           K        DISK
     FPA#BPF  IF  E           K        DISK
     FSCWK18  O   E           K        DISK
     FSER030P O   E             39     PRINTER
     E*---------------------------------------------------------------
     IHA0
     I                                              HA04  L2
     I                                              HA05  L1
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$PRTID
     I                                      550 550 @K01
     I                                      601 602 DHA01
     I                                      603 604 DHA02
     I                                      605 6100DHA03S
     I                                      611 6160DHA03E
     I                                      617 617 DATA
     I                                      618 622 DHA04S
     I                                      623 627 DHA04E
     I                                      628 629 DNC09S
     I                                      630 631 DNC09E
     I                                      641 6480DATES
     I                                      651 6580DATEE
     I                                      661 6680WHA03S
     I                                      671 6780WHA03E
     I                                      679 680 DNC12S
     I                                      681 682 DNC12E
     I                                      683 683 DNC10S
     I                                      684 684 DNC10E
     I                                      685 686 DNC11S
     I                                      687 688 DNC11E
     I                                      689 690 DNC18S
     I                                      691 692 DNC18E
 00A+I                                      693 697 DNB01S
 00A+I                                      698 702 DNB01E
 00A+I                                      703 711 DHA05S
 00A+I                                      712 720 DHA05E
      *==============================================================*
      *                    KEY LIST
      *==============================================================*
 00A+C           KEYNC     KLIST
 00A+C                     KFLD           HA01
 00A+C                     KFLD           HA05
     C           KEYND     KLIST
     C                     KFLD           DHA01
     C                     KFLD           DHA02
     C           KEYNE     KLIST
     C                     KFLD           DHA01
     C                     KFLD           DHA02
     C                     KFLD           HA04
     C           KEY#B     KLIST
     C                     KFLD           DHA01
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C  N10                EXSR RTN010
     C*
     C   39                EXSR RTN900
     C*
 00A+C           KEYNC     CHAINNC0                  41
 00A+C           *IN41     IFEQ '0'
 00A+C           NC02      IFGE DHA05S
 00A+C           NC02      ANDLEDHA05E
 00A+C           NC03      ANDGEDNB01S
 00A+C           NC03      ANDLEDNB01E
 00A+C*
     C           KEYNE     CHAINMANEPF               40
     C   40                MOVEL*BLANK    NE05
     C* GET轉換日期
     C*                    CALL 'P31'
     C*                    PARM HA03      P3101I  80
     C*                    PARM '2'       P3102I  1
     C*                    PARM '1'       P3103I  1
     C*                    PARM '3'       P3104I  1
     C*                    PARM '1'       P3105I  1
     C*          SHA03     PARM           P3101O  80
     C*
     C                     ADD  HA06      PHA06
9904 C                     ADD  HA07      PHA07
9904 C                     ADD  HA07      THA07
     C                     ADD  HA08      PHA08
     C                     ADD  HA08      THA08
     C                     ADD  HA06      Q1
9904 C                     ADD  HA07      Q2
     C                     ADD  HA08      Q3
     C*
 00A+C                     END
 00A+C                     END
     CL1 10                EXSR RTNL0
     CL2 10                EXSR RTNL1
     CLR 10                EXSR RTNL2
     CLR                   EXSR RTNLR
     C*==============================================================*
     C*          RTN010....GET HEADING
     C*==============================================================*
01===C           RTN010    BEGSR
|    C* GET公司別
     C                     MOVELDHA01     W1801
|    C                     MOVEL*BLANK    D#B03
|    C                     MOVEL*BLANK    DND04
|    C           KEY#B     CHAINPA#BPF               40
|    C  N40                MOVEL#B03      D#B03
|    C           KEYND     CHAINMANDPF               40
|    C  N40                MOVELND04      DND04
|    C                     WRITEPH1
|    C                     SETOF                     39
|    C                     SETON                     10
|    C                     SETON                     303132
|    C*
     C                     Z-ADD*ZERO     THA07   90
     C                     Z-ADD*ZERO     THA08   90
|    C                     Z-ADD*ZERO     Q1
|    C                     Z-ADD*ZERO     Q2
|    C                     Z-ADD*ZERO     Q3
|    C                     Z-ADD*ZERO     Q4
|    C*
01===C                     ENDSR
      *==============================================================*
      *          RTNWK .... WRITE WORKFILE
      *==============================================================*
02===C           RTNWK     BEGSR
|    C*
     C                     MOVELHA04      W1802
     C                     MOVELNE05      W1803
     C                     Z-ADDPHA06     W1804
     C                     Z-ADDPHA07     W1805
     C                     Z-ADDPHA08     W1806
     C                     Z-ADDLHA08     W1807
     C                     WRITEW18
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTNL0 ....未稅金額
      *==============================================================*
02===C           RTNL0     BEGSR
|    C*
     C*          THA08     DIV  1.05      THA07     H
     C*                    ADD  THA07     PHA07
     C*                    ADD  THA07     Q2
9904 C* 稅額
     C           THA08     SUB  THA07     HA08H  112
     C                     ADD  HA08H     LHA08
     C                     ADD  HA08H     Q4
     C                     Z-ADD0         THA08
     C                     Z-ADD0         THA07
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTNL1 ....分店合計
      *==============================================================*
02===C           RTNL1     BEGSR
|    C*
 00A+C*顯示分店數量不為0
 00A+C           PHA06     IFNE 0
     C                     EXSR RTNWK
|    C                     WRITEPD1
 00A+C                     END
|    C                     Z-ADD*ZERO     PHA06
|    C                     Z-ADD*ZERO     PHA07
|    C                     Z-ADD*ZERO     PHA08
|    C                     Z-ADD*ZERO     HA08H
|    C                     Z-ADD*ZERO     LHA08
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTNL2 ....總計
      *==============================================================*
03===C           RTNL2     BEGSR
|    C*
 00A+C*總計數量為0，顯示NO DATA
 00A+C           Q1        IFEQ 0
 00A+C                     WRITEPE3
 00A+C                     ELSE
|    C                     SETON                     30
|    C                     WRITEPD2
 00A+C                     END
|    C*
|    C                     Z-ADD*ZERO     Q1
|    C                     Z-ADD*ZERO     Q2
|    C                     Z-ADD*ZERO     Q3
|    C                     Z-ADD*ZERO     Q4
|    C*
03===C                     ENDSR
      *==============================================================*
      *          RTN900....OVERFLOW
      *==============================================================*
04===C           RTN900    BEGSR
|    C*
|    C                     WRITEPE1
|    C           KEY#B     CHAINPA#BPF               40
|    C  N40                MOVEL#B03      D#B03
|    C           KEYND     CHAINMANDPF               40
|    C  N40                MOVELND04      DND04
|    C                     WRITEPH1
|    C*
|    C                     SETON                     303132
|    C                     SETOF                     39
|    C*
04===C                     ENDSR
      *==============================================================*
      *          RTNLR ....(NO DATA)
      *==============================================================*
05===C           RTNLR     BEGSR
|    C*
| 01-C           *IN10     IFEQ '0'
|   C           KEY#B     CHAINPA#BPF               40
|   C  N40                MOVEL#B03      D#B03
|   C           KEYND     CHAINMANDPF               40
|   C  N40                MOVELND04      DND04
|   C                     WRITEPH1
|   C                     WRITEPE3
| 01-C                     END
|    C                     WRITEPE2
|    C*
05===C                     ENDSR
