      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : INT0061                                       *
      * AUTHER       : A1546  VINCENT                                *
      * CREATE DATE  : 97/10/28                                      *
      * UPDATE DATE  :                                               *
      * SYSTEM       :西祺進銷存                                   *
      * SUBSYSTEM    :庫存管理系統                                 *
      * REMARK       :庫存月結批次處理主程式                       *
      ****************************************************************
     FPOCLLF06IF  E           K        DISK
     FINTCLF05IF  E           K        DISK
     FINTCLF06IF  E           K        DISK
     F            TC0                               KRENAMETC0L
     FINTELF03IF  E           K        DISK
     FINTGLF04IF  E           K        DISK
     FINTKLF04IF  E           K        DISK
     FINTILF03IF  E           K        DISK
     FINTMLF03IF  E           K        DISK
     FINTOLF03IF  E           K        DISK
     F            CM0                               KRENAMECM0L3
     F            TH0                               KRENAMETH0L3
     F            TM0                               KRENAMETM0L3
     F            TPW                               KRENAMETPWL3
     F                                              KINFDS TORCD3
     FINTOLF05IF  E           K        DISK
     F            CM0                               KRENAMECM0L5
     F            TF0                               KRENAMETF0L5
     F            TH0                               KRENAMETH0L5
     F            TJ0                               KRENAMETJ0L5
     F            HA0                               KRENAMEHA0L5
     F                                              KINFDS TORCD5
     FINTAPF  UF  E           K        DISK                      A
     FINTBPF  UF  E           K        DISK                      A
     FINTBLF02UF  E           K        DISK
     F            TB0                               KRENAMETB0L2
     FC#EHPF  UF  E           K        DISK
     FPA#BPF  IF  E           K        DISK
     FMANCPF  IF  E           K        DISK
     FMANDPF  UF  E           K        DISK
     FPOCCLF02IF  E           K        DISK
     FUTL3PF  IF  E           K        DISK
     FINTNLF03UF  E           K        DISK
     FINTNWF  UF  E           K        DISK                      A
     FINTPPF  UF  E           K        DISK                      A
     FINTPWF  O   E           K        DISK
     FINTQPF  UF  E           K        DISK                      A
     FSEHALF01UF  E           K        DISK
     FINT006P1O   E             38     PRINTER
     FINT006P2O   E             39     PRINTER
     E*
     I*
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      152 161 $USERN
     I                                      111 1160$CHYMD
     I                                      201 2080A8YMD
     I                                      203 2080A6YMD
     I                                      601 601 MODE
     I                                      602 603 WEH01
     I            DS
     I                                        1   80BDATE
     I                                        3   40BDAYY
     I                                        5   60BDAMM
     I            DS
     I                                        1   80KCC07
     I                                        1   40KCC07Y
     I            DS
     I                                        1  13 KL3
     I                                        3   3 KL301
     I                                        3   4 KL302
     I            DS
     I                                        1   60WNY
     I                                        1   40WYY
     I                                        5   60WMM
     I            DS
     I                                        1   80W1YMD
     I                                        1   40W1YY
     I                                        5   60W1MM
     I                                        1   60W1YM
     IDA02        DS
     I                                        1   43DUTY
     ITORCD3      DS
     I                                     *RECORD  RCDR3
     ITORCD5      DS
     I                                     *RECORD  RCDR5
      *==============================================================*
      *                    DEFINE ROUTINE
      *==============================================================*
     C           *LIKE     DEFN CC09      WCC09
     C           *LIKE     DEFN TO04      KTO04
     C           *LIKE     DEFN TO04      WTO04
     C           *LIKE     DEFN TO05      WTO05
     C           *LIKE     DEFN TA10      WQTY
     C           *LIKE     DEFN TA09      WTA09
     C           *LIKE     DEFN TA08      TTAMT
     C           *LIKE     DEFN TA08      @TA08
     C           *LIKE     DEFN TA08      @TA08W
     C           *LIKE     DEFN TA07      WTA07
     C           *LIKE     DEFN TA08      WFLD1
     C           *LIKE     DEFN TA10      WFLD2
     C           *LIKE     DEFN TA01      KTA01
     C           *LIKE     DEFN TA02      KTA02
     C           *LIKE     DEFN TB01      KTB01
     C           *LIKE     DEFN TB02      KTB02
     C           *LIKE     DEFN TA06      TAQTY
     C           *LIKE     DEFN TA08      TAAMT
     C           *LIKE     DEFN TA06      TBQTY
     C           *LIKE     DEFN TA08      TBAMT
     C           *LIKE     DEFN TN08      KTN08
     C           *LIKE     DEFN TO07      HA09T
     C           *LIKE     DEFN TN07      TNQTY
     C           *LIKE     DEFN TQ07      TQ07U
     C           *LIKE     DEFN DUTY      WDUTY
     C           *LIKE     DEFN TO121     HA06P
     C           *LIKE     DEFN TO121     HA06M
     C           *LIKE     DEFN TO121     HA06R
     C           *LIKE     DEFN TO121     HA06U
     C           *LIKE     DEFN TN13      TN13T
     C           *LIKE     DEFN TNW06     TNW06W
     C           *LIKE     DEFN TP06      @TP06
     C           *LIKE     DEFN TP08      @TP08
     C           *LIKE     DEFN HA07      KHA07
     C           *LIKE     DEFN TP01      KTP01
     C           *LIKE     DEFN TQ01      KTQ01
     C*
     C* 總庫庫存檔重算欄位
     C           *LIKE     DEFN TA11      TA11X            進貨量
     C           *LIKE     DEFN TA12      TA12X            退出量
     C           *LIKE     DEFN TA13      TA13X            調整入庫量
     C           *LIKE     DEFN TA14      TA14X            調整出庫量
     C           *LIKE     DEFN TA15      TA15X            銷售入庫量
     C           *LIKE     DEFN TA16      TA16X            銷售出庫量
     C*
     C* 分庫庫存檔重算欄位
     C           *LIKE     DEFN TB08      TB08X            進貨量
     C           *LIKE     DEFN TB09      TB09X            退出量
     C           *LIKE     DEFN TB10      TB10X            調整入庫量
     C           *LIKE     DEFN TB11      TB11X            調整出庫量
     C           *LIKE     DEFN TB12      TB12X            銷售入庫量
     C           *LIKE     DEFN TB13      TB13X            銷售出庫量
      *==============================================================*
      *                    KLIST
      *==============================================================*
     C           KEYZZ     KLIST
     C                     KFLD           EH01
     C                     KFLD           BDATE
     C           KEYL3     KLIST
     C                     KFLD           KL301
     C                     KFLD           KL302
     C           KEYTO     KLIST
     C                     KFLD           EH01             公司別
     C                     KFLD           KTO04            產品代號
     C                     KFLD           BDATE            起日
     C           KEYTO1    KLIST
     C                     KFLD           TB01             公司別
     C                     KFLD           TB03             通路代號
     C                     KFLD           TB04             分店代號
     C                     KFLD           TB05             產品代號
     C                     KFLD           BDATE            起日
     C           KEYTO2    KLIST
     C                     KFLD           TB01             公司別
     C                     KFLD           TB03             通路代號
     C                     KFLD           TB04             分店代號
     C                     KFLD           TB05             產品代號
     C           KEYTO3    KLIST
     C                     KFLD           EH01             公司別
     C                     KFLD           KTO04            產品代號
     C           KEYHA     KLIST
     C                     KFLD           KHA07            未稅金額
     C           KEYTA     KLIST
     C                     KFLD           TA01
     C                     KFLD           TA02
     C                     KFLD           TA03
     C           KEYTA1    KLIST
     C                     KFLD           KTA01
     C                     KFLD           KTA02
     C           KEYTB1    KLIST
     C                     KFLD           KTB01
     C                     KFLD           KTB02
     C           KEYTB2    KLIST
     C                     KFLD           TA01             公司別
     C                     KFLD           TA02             年月
     C                     KFLD           TA03             產品代號
     C           KEYCC     KLIST
     C                     KFLD           KCC01   2
     C                     KFLD           KCC02   2
     C                     KFLD           KCC04   9
     C                     KFLD           KCC06   1
     C                     KFLD           KCC07
     C           KEYCC1    KLIST
     C                     KFLD           KCC01
     C                     KFLD           KCC02
     C                     KFLD           KCC04
     C                     KFLD           KCC06
     C           KEYNC     KLIST
     C                     KFLD           TA01
     C                     KFLD           TA03
     C           KEYND     KLIST
     C                     KFLD           EH01
     C           KEYTN     KLIST
     C                     KFLD           TB01             公司別
     C                     KFLD           TB03             通路代號
     C                     KFLD           TB04             分店代號
     C                     KFLD           TB05             產品代號
     C                     KFLD           KTN08            到分店日
     C           KEYTN1    KLIST
     C                     KFLD           TB01             公司別
     C                     KFLD           TB03             通路代號
     C                     KFLD           TB04             分店代號
     C                     KFLD           TB05             產品代號
     C           KEYTN2    KLIST
     C                     KFLD           TB01             公司別
     C                     KFLD           TB03             通路代號
     C                     KFLD           TB04             分店代號
     C                     KFLD           TB05             產品代號
     C                     KFLD           TN09             陳列驗收單號
     C           KEYTP     KLIST
     C                     KFLD           KTP01            公司別
     C           KEYTQ     KLIST
     C                     KFLD           KTQ01            公司別
     C           KEYEA     KLIST
     C                     KFLD           TB01             公司別
     C                     KFLD           TB02             年月
     C                     KFLD           TB05             產品代號
     C                     KFLD           TB03             通路代號
     C                     KFLD           TB04             分店代號
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C*
     C                     EXSR RTN010
     C*目前不使用MODE2
  01-C           MODE      CASEQ'1'       RTN100           1指定公司
    C*          MODE      CASEQ'2'       RTN200           2全部公司
  01-C                     END
     C*
     C                     SETON                     LR
      *===============================================================*
     C*          RTN010....INITIAL VALUE
      *===============================================================*
01===C           RTN010    BEGSR
|    C*
|    C                     MOVEL'INT0061' PGWW   10
|    C           *NAMVAR   DEFN           DA02             稅率REFLIB
|    C                     IN   DA02
|    C*
| 01-C           MODE      IFEQ '1'                        指定公司
|   C                     MOVE WEH01     EH01
|   C           WEH01     CHAIN#B0                  40
|   C  N40                MOVEL#B03      P#B03
|   C   40                MOVEL*BLANK    P#B03
| 01-C                     ENDIF
|    C*
|    C* 先清除以重算當月月結月份內( OPEN QUERY FILE )
|    C* 可能已存在的銷售借貨及銷售無庫存異動記錄
|    C*
|    C                     MOVEL*LOVAL    KTP01
|    C           KEYTP     SETLLTP0
|    C                     READ TP0                      40
| 02-C           *IN40     DOWEQ'0'
|   C                     DELETTP0
|   C                     READ TP0                      40
| 02-C                     ENDDO
|    C*
|    C                     MOVEL*LOVAL    KTQ01
|    C           KEYTQ     SETLLTQ0
|    C                     READ TQ0                      40
| 03-C           *IN40     DOWEQ'0'
|   C                     DELETTQ0
|   C                     READ TQ0                      40
| 03-C                     ENDDO
|    C*
01===C                     ENDSR
      *===============================================================*
     C*          RTN020....處理起訖日期
      *===============================================================*
02===C           RTN020    BEGSR
|    C*
|    C           EH01      CHAINEH0                  40
|    C                     Z-ADDEH07      BDATE            帳務區間起日
|    C                     Z-ADDEH08      EDATE   80       帳務區間訖日
|    C*
|    C                     MOVE EH01      PEH01            指定公司
|    C           BDAMM     MULT 100       PMMYY
|    C                     ADD  BDAYY     PMMYY
|    C*
|    C                     WRITEP1H1
|    C                     WRITEP2H1
|    C*
02===C                     ENDSR
      *===============================================================*
     C*          RTN030....取得單別名稱
      *===============================================================*
03===C           RTN030    BEGSR
|    C*
|    C                     MOVE PDNO      KL3
|    C           KEYL3     CHAINL30                  47
|    C   47                MOVE *BLANK    PDNM
|    C  N47                MOVELL303      PDNM
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          RTN040....取得最新年度報價單價
     C*==============================================================*
04===C           RTN040    BEGSR
|     *
|    C                     Z-ADD0         WCC09
|     *
|    C           KEYCC     SETGTCC0
|    C           KEYCC1    REDPECC0                      47
| 01-C           *IN47     IFEQ '0'
|   C           KCC07Y    ANDEQCC05
|    *
| 02-C           CC08      IFNE 0
| 03-C           KCC07     IFLE CC08
|   C           KCC07     ANDGECC07
|   C                     Z-ADDCC09      WCC09
| 03-C                     ENDIF
| 02*C                     ELSE
| 04-C           KCC07     IFGE CC07
|   C                     Z-ADDCC09      WCC09
| 04-C                     ENDIF
| 02-C                     ENDIF
| 01-C                     ENDIF
|     *
04===C                     ENDSR
      *===============================================================*
      *          RTN080....錯誤訊息列表
      *===============================================================*
05===C           RTN080    BEGSR
|    C*
|    C                     WRITEP1D1
|    C                     SETON                     10
| 01-C           *IN38     IFEQ '1'
|   C                     WRITEP1E1
|   C                     WRITEP1H1
|   C                     SETOF                     38
| 01-C                     ENDIF
|    C*
05===C                     ENDSR
      *===============================================================*
      *          RTN090....錯誤訊息列表
      *===============================================================*
06===C           RTN090    BEGSR
|    C*
|    C                     WRITEP2D1
|    C                     SETON                     11
| 01-C           *IN39     IFEQ '1'
|   C                     WRITEP2E1
|   C                     WRITEP2H1
|   C                     SETOF                     39
| 01-C                     ENDIF
|    C*
06===C                     ENDSR
      *===============================================================*
     C*          RTN100....MODE 1 指定公司
      *===============================================================*
07===C           RTN100    BEGSR
|    C*
|    C                     EXSR RTN020
|    C*
|    C*檢核是否有未庫存日結處理單據
|    C                     EXSR RTN310
|    C                     EXSR RTN320
|    C                     EXSR RTN330
|    C                     EXSR RTN340
|    C                     EXSR RTN350
|    C                     EXSR RTN360
|    C                     EXSR RTN370
|    C                     EXSR RTN380
|    C*
| 01-C           *IN10     IFEQ '0'
|   C                     WRITEP1E31
| 01-C                     ENDIF
|    C                     WRITEP1E2
|    C*
|    C   99                GOTO END100
|    C*
|    C*處理陳列借貨
|    C                     EXSR RTN400
|    C   99                GOTO END100
|    C*
|    C*處理本月單位成本
|    C                     EXSR RTN450
|    C   99                GOTO END100
|    C*
|    C*處理帳列庫存金額（含無庫存盤盈記錄)
|    C                     EXSR RTN500
|    C   99                GOTO END100
|    C*
|    C*產生新月份產品庫存記錄
|    C                     EXSR RTN600
|    C*
|    C*更新控制欄位
|    C                     EXSR RTN700
|    C*
07===C           END100    ENDSR
      *===============================================================*
      *          RTN310....採購驗收單據
      *===============================================================*
08===C           RTN310    BEGSR
|    C*
|    C           KEYZZ     SETLLCL0
|    C                     READ CL0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
| 02-C           CL01      IFEQ EH01
|   C           CL07      ANDLEEDATE                      到貨運日
|   C                     SETON                     99
|   C                     EXSR RTN080
|   C                     READ CL0                      44
| 02*C                     ELSE
|   C                     SETON                     44
| 02-C                     ENDIF
| 01-C                     ENDDO
|    C*
08===C                     ENDSR
      *===============================================================*
      *          RTN320....調撥單－後台單
      *===============================================================*
09===C           RTN320    BEGSR
|    C*
|    C           KEYZZ     SETLLTC0
|    C                     READ TC0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
| 02-C           TC01      IFEQ EH01
|   C           TC06      ANDLEEDATE
|   C                     SETON                     99
|   C                     MOVELTC02      PCNO
|   C                     MOVELTC05      PDNO
|   C                     EXSR RTN030
|   C                     EXSR RTN080
|   C                     READ TC0                      44
| 02*C                     ELSE
|   C                     SETON                     44
| 02-C                     ENDIF
| 01-C                     ENDDO
|    C*
09===C                     ENDSR
      *===============================================================*
      *          RTN330....調撥單－前台單
      *===============================================================*
10===C           RTN330    BEGSR
|    C*
|    C           KEYZZ     SETLLTC0L
|    C                     READ TC0L                     44
| 01-C           *IN44     DOWEQ'0'
|   C*
| 02-C           TC01      IFEQ EH01
|   C           TC03      ANDLEEDATE                      調出日期
|   C                     SETON                     99
|   C                     MOVELTC02      PCNO
|   C                     MOVELTC05      PDNO
|   C                     EXSR RTN030
|   C                     EXSR RTN080
|   C                     READ TC0L                     44
| 02*C                     ELSE
|   C                     SETON                     44
| 02-C                     ENDIF
| 01-C                     ENDDO
|    C*
10===C                     ENDSR
      *===============================================================*
      *          RTN340....進貨退出單
      *===============================================================*
11===C           RTN340    BEGSR
|    C*
|    C           KEYZZ     SETLLTE0
|    C                     READ TE0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
| 02-C           TE01      IFEQ EH01
|   C           TE03      ANDLEEDATE                      退出日期
|   C                     SETON                     99
|   C                     MOVELTE02      PCNO
|   C                     MOVELTE05      PDNO
|   C                     EXSR RTN030
|   C                     EXSR RTN080
|   C                     READ TE0                      44
| 02*C                     ELSE
|   C                     SETON                     44
| 02-C                     ENDIF
| 01-C                     ENDDO
|    C*
11===C                     ENDSR
      *===============================================================*
      *          RTN350....陳列轉進貨單
      *===============================================================*
12===C           RTN350    BEGSR
|    C*
|    C           KEYZZ     SETLLTG0
|    C                     READ TG0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
| 02-C           TG01      IFEQ EH01
|   C           TG03      ANDLEEDATE                      進貨日期
|   C                     SETON                     99
|   C                     MOVELTG02      PCNO
|   C                     MOVELTG05      PDNO
|   C                     EXSR RTN030
|   C                     EXSR RTN080
|   C                     READ TG0                      44
| 02*C                     ELSE
|   C                     SETON                     44
| 02-C                     ENDIF
| 01-C                     ENDDO
|    C*
12===C                     ENDSR
      *===============================================================*
      *          RTN360....陳列品退出單
      *===============================================================*
13===C           RTN360    BEGSR
|    C*
|    C           KEYZZ     SETLLTK0
|    C                     READ TK0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
| 02-C           TK01      IFEQ EH01
|   C           TK03      ANDLEEDATE                      退出日期
|   C                     SETON                     99
|   C                     MOVELTK02      PCNO
|   C                     MOVELTK05      PDNO
|   C                     EXSR RTN030
|   C                     EXSR RTN080
|   C                     READ TK0                      44
| 02*C                     ELSE
|   C                     SETON                     44
| 02-C                     ENDIF
| 01-C                     ENDDO
|    C*
13===C                     ENDSR
      *===============================================================*
      *          RTN370....存貨調整單
      *===============================================================*
14===C           RTN370    BEGSR
|    C*
|    C           KEYZZ     SETLLTI0
|    C                     READ TI0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
| 02-C           TI01      IFEQ EH01
|   C           TI03      ANDLEEDATE                      調整日期
|   C                     SETON                     99
|   C                     MOVELTI02      PCNO
|   C                     MOVELTI05      PDNO
|   C                     EXSR RTN030
|   C                     EXSR RTN080
|   C                     READ TI0                      44
| 02*C                     ELSE
|   C                     SETON                     44
| 02-C                     ENDIF
| 01-C                     ENDDO
|    C*
14===C                     ENDSR
      *===============================================================*
      *          RTN380....產品成本調整單
      *===============================================================*
15===C           RTN380    BEGSR
|    C*
|    C           KEYZZ     SETLLTM0
|    C                     READ TM0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
| 02-C           TM01      IFEQ EH01
|   C           TM02      ANDLEEDATE                      調整日期
|   C                     SETON                     99
|   C                     MOVEL*BLANK    PCNO
|   C                     MOVELTM08      PDNO
|   C                     EXSR RTN030
|   C                     EXSR RTN080
|   C                     READ TM0                      44
| 02*C                     ELSE
|   C                     SETON                     44
| 02-C                     ENDIF
| 01-C                     ENDDO
|    C*
15===C                     ENDSR
      *===============================================================*
      *          RTN400....處理　銷售陳列借貨
      *===============================================================*
16===C           RTN400    BEGSR
|    C*
|    C                     MOVE EH01      KTA01
|    C                     MOVE EH02      KTA02
|    C                     Z-ADD0         HA06P            銷售數量
|    C                     Z-ADD0         HA06M            銷貨退回量
|    C*
|    C*讀取所有「總庫」產品(KEYTA1:EH01+EH02)
|    C*
|    C           KEYTA1    SETLLTA0
|    C           KEYTA1    READETA0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*~~<< INTA LOOP START >>~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
|   C*讀取同產品之「各通路分庫」
|   C* KEYTB2:TA01+TA02+TA03
|   C*
|   C           KEYTB2    SETLLTB0L2
|   C           KEYTB2    READETB0L2                    46
| 02-C           *IN46     DOWEQ'0'
|   C*--<< INTB LOOP START >>-------------------------------------
|   C*
|   C*新分庫INZ.
|   C                     Z-ADDTB06      TBQTY            起始庫存期初
|   C                     Z-ADD0         WTO05            日期BREAK
|   C                     SETOF                     54    OFF第一筆異動
|   C*
|   C* KEYTO1:TB01+TB03+TB04+TB05+BDATE（+起始日)
|   C* KEYTO2:TB01+TB03+TB04+TB05（同分庫、同產品）
|   C*同分庫同產品，累算「帳務日期區間內」異動明細
|   C           KEYTO1    SETLLINTOLF05                   起始日
|   C           KEYTO2    READEINTOLF05                 45同分庫同產品
| 03-C           *IN45     DOWEQ'0'
|   C           TO05      ANDLEEDATE
|   C*++<< INTOLF LOOP START >>+++++++++++++++++++++++++++++++++++
|   C*  同庫第一筆異動設定
| 04-C  N54                DO
|   C                     MOVELTO05      WTO05
|   C                     SETON                     54
| 04-C                     ENDDO
|   C*
|   C*  單日結算－銷貨成本及銷貨入出量及金額
| 05-C           TO05      IFNE WTO05                      日期BREAK
|   C                     EXSR RTN402                     陳列借貨處理
|   C                     Z-ADDTO05      WTO05
| 05-C                     ENDIF
|   C*
|   C*  累算一般異動量及金額
|   C                     EXSR RTN401                     累算一般異動
|   C*
|   C           KEYTO2    READEINTOLF05                 45同分庫產品期間
| 03-C                     ENDDO
|   C*++++++++++++++++++++++++++++++++++++++++++++++<< INTOLF >>++
|   C*同分庫最後一筆記錄
|   C                     EXSR RTN402                     陳列借貨處理
|   C*讀取下一分庫
|   C           KEYTB2    READETB0L2                    46 NEXT分庫
| 02-C                     ENDDO
|   C*------------------------------------------------<< INTB >>--
|   C*
|   C           KEYTA1    READETA0                      44 NEXT產品
| 01-C                     ENDDO
|    C* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~<< INTA >>~~
|    C*
16===C                     ENDSR
      *===============================================================*
      *          RTN401....推算銷售陳借－累計一般異動庫存量
      *===============================================================*
17===C           RTN401    BEGSR
|    C*
|    C*累算一般異動明細
|    C*
|    C* 帳列採購驗收單
| 01-C                     SELEC
|   C           RCDR5     WHEQ 'CM0L5'
|   C                     ADD  TO121     TBQTY
|   C* 帳列進貨退出單
|   C           RCDR5     WHEQ 'TF0L5'
|   C                     SUB  TO121     TBQTY
|   C* 陳列轉進貨單
|   C           RCDR5     WHEQ 'TH0L5'
|   C                     ADD  TO121     TBQTY
|   C* 存貨調整單
|   C           RCDR5     WHEQ 'TJ0L5'
|   C                     ADD  TO121     TBQTY
|   C* 銷售資料
|   C           RCDR5     WHEQ 'HA0L5'
| 02-C           TO121     IFLT 0
|   C*   銷售退回
|   C                     ADD  TO121     HA06M            存負值
| 02*C                     ELSE
|   C*   銷售出庫
|   C                     ADD  TO121     HA06P
| 02-C                     ENDIF
| 01-C                     ENDSL
|    C*
17===C                     ENDSR
      *===============================================================*
      *          RTN402....推算銷售陳列品（借貨）
      *===============================================================*
18===C           RTN402    BEGSR
|    C*
| 01-C           HA06M     IFEQ 0
|   C           HA06P     ANDEQ0
|   C                     GOTO END402
| 01-C                     ENDIF
|    C*
|    C*優先處理－銷貨退回
|    C*
| 02-C           HA06M     IFLT 0                          有銷貨退回
|   C                     SUB  HA06M     TBQTY            補回帳列庫存
| 02-C                     ENDIF
|    C*
|    C*接續處理－銷貨出庫推算判斷銷售品的庫存類別
|    C*
| 03-C           HA06P     IFGT 0                          有銷貨數量
|   C*
|   C                     Z-ADD0         HA06R            尚餘不足量
|   C*銷售帳列庫存品---------------------------------------------
|   C*
|   C*帳列量足夠銷售
| 04-C           HA06P     IFLE TBQTY                      銷售<=帳列量
|   C                     SUB  HA06P     TBQTY            扣減帳列庫存
|   C                     GOTO END402                      END-RTN
| 04*C                     ELSE
|   C*帳列量不足夠銷售，先處理帳列量
|   C*   KEEP尚不足量HA06R
|   C           HA06P     SUB  TBQTY     HA06R            尚餘不足量
|   C                     Z-ADD0         TBQTY            帳列全被銷售
| 04-C                     ENDIF                            TBQTY > 0
|   C*-----------------------------------------<<銷售帳列庫存>>---
|   C*
|   C* 再處理陳列可借貨量，BY到分店日排序－作先進先出
|   C                     MOVEL*BLANKS   KTN08            到分店日排序
|   C           KEYTN     SETLLTN0
|   C           KEYTN1    READETN0                      48
| 05-C           *IN48     DOWEQ'0'
|   C*---<< INTNLF03 LOOP START >>-----------------------------
|   C                     Z-ADD0         TNW06W
|   C           KEYTN2    CHAINTNW                  40    借貨量工作檔
| 06-C           *IN40     IFEQ '0'
|   C                     Z-ADDTNW06     TNW06W
| 06-C                     ENDIF
|   C*  計算可借貨量                                    TN07陳列餘量
|   C           TN07      SUB  TN13      TNQTY            檔案內已借貨量
|   C                     SUB  TNW06W    TNQTY            程式中已借貨量
| 07-C           TNQTY     IFGT 0                          有可借貨量
|   C*  有可借貨量
| 08-C           HA06R     IFLE TNQTY
|   C*  可借量足夠
|   C                     Z-ADDHA06R     HA06U            FOR UPDATE
|   C                     Z-ADD0         HA06R            可借量全足夠
| 08*C                     ELSE
|   C*  可借量不足，先借現有「可借量」
|   C                     Z-ADDTNQTY     HA06U            FOR UPDATE
|   C                     SUB  TNQTY     HA06R            續算尚餘不足量
| 08-C                     ENDIF
|   C*
|   C*   WRITE INTPWF, INTNWF銷售陳列借貨工作檔
|   C                     EXSR RTN40A
| 07-C                     ENDIF                           TNQTY > 0
|   C* 陳列量已足夠銷售
| 09-C           HA06R     IFEQ 0
|   C                     GOTO END402                      END-RTN
| 09-C                     ENDIF
|   C*
|   C           KEYTN1    READETN0                      48 NEXT INTN
| 05-C                     ENDDO
|   C*-----------------------------------<< INTNLF03 END >>--
|   C*
| 03-C                     ENDIF                           銷貨出庫處理
|    C*
|    C           END402    TAG
|    C*
|    C                     Z-ADD0         HA06P            銷售數量
|    C                     Z-ADD0         HA06M            銷貨退回量
|    C*
18===C                     ENDSR
      *===============================================================*
      *          RTN40A.... WRITE INTPWF,INTNWF 銷售陳列借貨工作檔
      *===============================================================*
19===C           RTN40A    BEGSR
|    C*
|    C* WRITE INTPWF ( FOR 單位成本計算)
|    C                     CLEARTPW
|    C                     MOVELTB01      TPW01            公司別
|    C                     MOVELTB05      TPW02            產品代號
|    C                     MOVELWTO05     TPW03            銷售日期
|    C                     Z-ADDTN06      TPW05            陳列進價
|    C                     Z-ADDHA06U     TPW06            借貨數量
|     *   取得銷售借貨工作單號W1
|    C                     CALL 'GET001'
|    C                     PARM TB01      T001I1  2
|    C                     PARM 'W1'      T001I2  2
|    C                     PARM A6YMD     T001I3  60       西元YYMMDD
|    C           TPW04     PARM           T001O1 13        借貨單號
|    C                     WRITETPW
|    C*
|    C* WRITE INTNWF ( FOR KEEP推算中的已借貨量)
|    C                     CLEARTNW
|    C*
|    C           KEYTN2    CHAINTNW                  40
| 01-C           *IN40     IFEQ '1'
|   C                     MOVELTB01      TNW01            公司別
|   C                     MOVELTB03      TNW02            通路代號
|   C                     MOVELTB04      TNW03            分店代號
|   C                     MOVELTB05      TNW04            產品代號
|   C                     MOVELTN09      TNW05            陳列驗收單號
|   C                     Z-ADDHA06U     TNW06            借貨數量
|   C                     WRITETNW
| 01*C                     ELSE
|   C                     ADD  HA06U     TNW06            借貨數量
|   C                     UPDATTNW
| 01-C                     ENDIF
|    C*
19===C                     ENDSR
      *===============================================================*
      *          RTN450....處理本月單位成本
      *===============================================================*
20===C           RTN450    BEGSR
|    C*
|    C                     MOVE EH01      KTA01
|    C                     MOVE EH02      KTA02
|    C*                    SETOF                     52    ON異常
|    C*          OK400     TAG
|    C*
|    C*讀取每一筆總庫產品
|    C           KEYTA1    SETLLTA0
|    C           KEYTA1    READETA0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
|   C                     Z-ADD0         WQTY
|   C                     Z-ADD0         WTA09            成本調整金額
|   C                     Z-ADD0         TTAMT
|   C                     SETOF                     53
|   C*讀取同產品異動明細
|   C                     MOVELTA03      KTO04
|   C           KEYTO     SETLLINTOLF03
|   C           KEYTO3    READEINTOLF03                 45
| 02-C           *IN45     DOWEQ'0'
|   C*          TO01      IFEQ EH01
|   C*          TO04      ANDEQKTO04
| 03-C           TO05      IFLE EDATE
|   C                     SETON                     53
|   C                     EXSR RTN451                     累算單據
| 03-C                     ENDIF
|   C           KEYTO3    READEINTOLF03                 45
| 02-C                     ENDDO
|    *
|   C                     EXSR RTN453                     UPDATE INTAP
|    *
|   C           KEYTA1    READETA0                      44
| 01-C                     ENDDO
|    C*
|    C*          *IN99     IFEQ '0'
|    C*          *IN52     ANDEQ'0'
|    C*                    SETON                     52
|    C*                    GOTO OK400
|    C*                    ENDIF
|    C*
| 04-C           *IN11     IFEQ '0'
|   C                     WRITEP2E31
| 04-C                     ENDIF
|    C                     WRITEP2E2
|    C*
20===C                     ENDSR
      *===============================================================*
      *          RTN451....累算異動單據明細
      *===============================================================*
21===C           RTN451    BEGSR
|    C*
| 01-C           RCDR3     IFEQ 'TM0L3'                    <產品成本調整
|   C                     ADD  TO104     WTA09            調整金額
| 01*C                     ELSE
|   C           TO101     MULT TO121     WAMT1  112H      當時進價
|   C                     ADD  WAMT1     TTAMT            累計進貨金額
|   C                     ADD  TO121     WQTY             累計進貨量
| 01-C                     ENDIF                           EQ 'TM0L3'
|    C*
21===C                     ENDSR
      *===============================================================*
      *          RTN452....檢核異常庫存量及金額
      *===============================================================*
22===C           RTN452    BEGSR
|    C*
|    C* 期初庫存金額＋本月異動金額
|    C           TA05      ADD  TTAMT     WFLD1
|    C                     ADD  WTA09     WFLD1
|    C* 期初庫存量＋本月異動量
|    C           TA06      ADD  WQTY      WFLD2
|    C*
|    C* CHK庫存量及金額異常
| 01-C           WFLD1     IFLT 0
|   C           WFLD2     ORLT 0
|   C                     SETON                     99    REMARK
|   C           KEYNC     CHAINNC0                  41
|   C   41                MOVEL*BLANK    NC05
|   C                     Z-ADDWFLD2     PQTY
|   C                     Z-ADDWFLD1     PAMT
|   C                     EXSR RTN090
|   C                     GOTO R452E
| 01-C                     ENDIF
|    C*
22===C           R452E     ENDSR
      *===============================================================*
      *          RTN453....UPDATE INTAPF單位成本
      *===============================================================*
23===C           RTN453    BEGSR
|    C*
|    C*本月有異動
| 01-C           *IN53     IFEQ '1'
|   C*單位成本計算
|   C           TA05      ADD  TTAMT     WFLD1
|   C                     ADD  WTA09     WFLD1            庫存金額
|   C           TA06      ADD  WQTY      WFLD2            庫存量
| 02-C           WFLD2     IFNE 0
|   C           WFLD1     DIV  WFLD2     TA07      H      本月單位成本
| 02*C                     ELSE
|   C                     Z-ADD0         TA07
| 02-C                     ENDIF
| 01*C                     ELSE
|   C*本月無異動上月->本月單位成本
|   C                     Z-ADDTA04      TA07             本月單位成本
|   C                     Z-ADDWTA09     TA09             成本調整金額
| 01-C                     ENDIF
|    C*
|    C                     MOVE *BLANK    TAWW
|    C                     MOVELPGWW      TAWW
|    C                     Z-ADDA8YMD     TAXX
|    C                     TIME           TAYY
|    C                     MOVEL$USER     TAZZ
|    C                     UPDATTA0
|    C*
23===C                     ENDSR
      *===============================================================*
      *          RTN500....處理　帳列庫存金額
      *                         （含銷售之陳列借貨及無庫存盤盈記錄)
      *===============================================================*
24===C           RTN500    BEGSR
|    C*
|    C                     MOVE EH01      KTA01
|    C                     MOVE EH02      KTA02
|    C                     SETOF                     52    ON異常
|    C                     Z-ADD0         HA06P            銷售數量
|    C                     Z-ADD0         HA06M            銷貨退回量
|    C*
|    C*先累計各產品帳列零庫存差額，最後再一次處理
|    C                     Z-ADD0         @BAL    92       零庫存量差額
|    C*
|    C*讀取所有「總庫」產品(KEYTA1:EH01+EH02)
|    C*
|    C           KEYTA1    SETLLTA0
|    C           KEYTA1    READETA0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*~~<< INTA LOOP START >>~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
|   C                     MOVEL'0'       R50D    1
|   C                     EXSR RTN50D                     CLR TA QTY
|   C                     Z-ADD0         TAQTY            帳列庫存量
|   C                     Z-ADD0         TAAMT            帳列庫存金額
|   C                     Z-ADD0         @TP06            累計銷借量
|   C                     Z-ADD0         @TP08            累計銷借金額
|   C*讀取同產品之「各通路分庫」
|   C* KEYTB2:TA01+TA02+TA03
|   C*
|   C           KEYTB2    SETLLTB0L2
|   C           KEYTB2    READETB0L2                    46
| 02-C           *IN46     DOWEQ'0'
|   C*--<< INTB LOOP START >>-------------------------------------
|   C*
|   C*新分庫INZ.
|   C                     MOVEL'0'       R50E    1
|   C                     EXSR RTN50E                     CLR TB QTY
|   C                     Z-ADDTB06      TBQTY            起始庫存期初
|   C                     Z-ADD0         TBAMT            異動金額
|   C                     Z-ADD0         WTO05            日期BREAK
|   C                     SETOF                     54    OFF第一筆異動
|   C*
|   C* KEYTO1:TB01+TB03+TB04+TB05+BDATE（+起始日)
|   C* KEYTO2:TB01+TB03+TB04+TB05（同分庫、同產品）
|   C*同分庫同產品，累算「帳務日期區間內」異動明細
|   C           KEYTO1    SETLLINTOLF05                   起始日
|   C           KEYTO2    READEINTOLF05                 45同分庫同產品
| 03-C           *IN45     DOWEQ'0'
|   C           TO05      ANDLEEDATE
|   C*++<< INTOLF LOOP START >>+++++++++++++++++++++++++++++++++++
|   C*  同庫第一筆異動設定
| 04-C  N54                DO
|   C                     MOVELTO05      WTO05
|   C                     SETON                     54
| 04-C                     ENDDO
|   C*
|   C*  單日結算－銷貨成本及銷貨入出量及金額
| 05-C           TO05      IFNE WTO05                      日期BREAK
|   C                     EXSR RTN502                     銷貨成本
|   C                     Z-ADDTO05      WTO05
|   C                     MOVEL*BLANKS   HA09T            銷售序號
| 05-C                     ENDIF
|   C*
|   C*  累算一般異動量及金額
|   C                     EXSR RTN501                     累算一般異動
|   C*
|   C           KEYTO2    READEINTOLF05                 45同分庫產品期間
| 03-C                     ENDDO
|   C*++++++++++++++++++++++++++++++++++++++++++++++<< INTOLF >>++
|   C*同分庫最後一筆記錄
|   C                     EXSR RTN502                     銷貨成本
|   C*累計各分庫庫存金額
|   C                     ADD  TBAMT     TAAMT
|   C                     ADD  TBQTY     TAQTY
|   C* UPDATE INTBPF
|   C                     MOVEL'1'       R50E
|   C                     EXSR RTN50E
|   C*讀取下一分庫
|   C           KEYTB2    READETB0L2                    46 NEXT分庫
| 02-C                     ENDDO
|   C*------------------------------------------------<< INTB >>--
|   C*單一產品結算處理
|   C                     EXSR RTN503
|   C* UPDATE INTAPF
|   C                     MOVEL'1'       R50D
|   C                     EXSR RTN50D
|   C*
|   C           KEYTA1    READETA0                      44 NEXT產品
| 01-C                     ENDDO
|    C* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~<< INTA >>~~
|    C* 將差額加入最大一筆出貨
|    C                     EXSR RTN504
|    C*
24===C                     ENDSR
      *===============================================================*
      *          RTN501....計算一般異動庫存量及庫存金額
      *===============================================================*
25===C           RTN501    BEGSR
|    C*
|    C*累算一般異動明細
|    C*
|    C* 帳列採購驗收單
| 01-C                     SELEC
|   C           RCDR5     WHEQ 'CM0L5'
|   C                     ADD  TO121     TB08X
|   C                     ADD  TO121     TBQTY
|   C           TO101     MULT TO121     WAMT1  112H       當時進價
|   C                     ADD  WAMT1     TBAMT
|   C* 帳列進貨退出單
|   C           RCDR5     WHEQ 'TF0L5'
|   C                     ADD  TO121     TB09X
|   C                     SUB  TO121     TBQTY
|   C           TA07      MULT TO121     WAMT1     H       當月單位成本
|   C                     SUB  WAMT1     TBAMT
|   C* 陳列轉進貨單
|   C           RCDR5     WHEQ 'TH0L5'
|   C                     ADD  TO121     TB08X
|   C                     ADD  TO121     TBQTY
|   C           TO101     MULT TO121     WAMT1     H       當時進價
|   C                     ADD  WAMT1     TBAMT
|   C* 存貨調整單
|   C           RCDR5     WHEQ 'TJ0L5'
| 02-C           TO121     IFGT 0
|   C                     ADD  TO121     TB10X            調整入庫
| 02*C                     ELSE
|   C                     SUB  TO121     TB11X            調整出庫
| 02-C                     ENDIF
|   C                     ADD  TO121     TBQTY
|   C           TA07      MULT TO121     WAMT1     H      本月單位成本
|   C                     ADD  WAMT1     TBAMT
|   C* 銷售資料
|   C           RCDR5     WHEQ 'HA0L5'
| 03-C           TO121     IFLT 0
|   C*   銷售退回
|   C                     ADD  TO121     HA06M             FOR銷貨成本
|   C                     SUB  TO121     TB12X            銷售入庫
| 03*C                     ELSE
|   C*   銷售出庫
|   C                     ADD  TO121     HA06P
|   C                     MOVELTO07      HA09T            銷售序號
| 03-C                     ENDIF
| 01-C                     ENDSL
|    C*
25===C                     ENDSR
      *===============================================================*
      *          RTN502....銷貨成本及銷貨入出庫存量及金額
      *===============================================================*
26===C           RTN502    BEGSR
|    C*
| 01-C           HA06M     IFEQ 0
|   C           HA06P     ANDEQ0
|   C                     GOTO END502
| 01-C                     ENDIF
|    C*
|    C*
|    C*優先處理－銷貨退回
|    C*
| 02-C           HA06M     IFLT 0                          有銷貨退回
|   C*
|   C* 取價：當月單位成本->最新報價->最近陳列進價（上線前庫存）
|   C*
| 03-C           TA07      IFGT 0                          當月單位成本
|   C* 取當月單位成本
|   C                     SUB  HA06M     TBQTY            補回帳列庫存
|   C           HA06M     MULT TA07      WAMT1     H      當月單位成本
|   C                     SUB  WAMT1     TBAMT            庫存金額
| 03*C                     ELSE
|   C* 取最新年度報價
|   C                     MOVELTB01      KCC01             公司別
|   C                     MOVELTB03      KCC02             通路別
|   C                     MOVELTB05      KCC04             產品代號
|   C                     MOVEL'1'       KCC06             年度報價
|   C                     Z-ADDWTO05     KCC07             退回日期
|   C                     EXSR RTN040
| 04-C           WCC09     IFNE 0                           最新年度價
|   C                     SUB  HA06M     TBQTY            補回帳列庫存
|   C           HA06M     MULT WCC09     WAMT1     H      當月單位成本
|   C                     SUB  WAMT1     TBAMT            庫存金額
|   C*
| 04*C                     ELSE
|   C* 取最近陳列進價
|   C                     MOVEL*HIVAL    KTN08            到分店日排序
|   C           KEYTN     SETGTTN0
|   C           KEYTN1    REDPETN0                      48
| 05-C           *IN48     IFEQ '0'
|   C                     SUB  HA06M     TBQTY            補回帳列庫存
|   C           HA06M     MULT TN06      WAMT1     H      當月單位成本
|   C                     SUB  WAMT1     TBAMT            庫存金額
| 05-C                     ENDIF                           最近陳列進價
| 04-C                     ENDIF                           最新年度報價
| 03-C                     ENDIF                           當月單位成本
|   C*
| 02-C                     ENDIF                           銷貨退回處理
|    C*
|    C*接續處理－銷貨出庫推算銷售品庫存類別及銷售成本
|    C*
| 06-C           HA06P     IFGT 0                          有銷貨數量
|   C*
|   C                     Z-ADD0         HA06R            尚餘不足量
|   C*銷售帳列庫存品---------------------------------------------
|   C*
|   C*帳列量足夠銷售
| 07-C           HA06P     IFLE TBQTY                      銷售<=帳列量
|   C                     SUB  HA06P     TBQTY            扣減帳列庫存
|   C                     ADD  HA06P     TB13X            銷售出庫
|   C           HA06P     MULT TA07      WAMT1     H      當月單位成本
|   C                     SUB  WAMT1     TBAMT            庫存金額
|   C                     GOTO END502                      END-RTN
| 07*C                     ELSE
|   C*帳列量不足夠銷售，先處理帳列量
|   C*   KEEP尚不足量HA06R
|   C           HA06P     SUB  TBQTY     HA06R            尚餘不足量
|   C*  註：務必先處理帳列量及金額相關處理
|   C           TBQTY     MULT TA07      WAMT1     H      當月單位成本
|   C                     SUB  WAMT1     TBAMT            庫存金額
|   C                     ADD  TBQTY     TB13X            銷售出庫
|   C*  註：最後才將累算帳列量TBQTY清零
|   C                     Z-ADD0         TBQTY            帳列全被銷售
|   C*
| 07-C                     ENDIF                            TBQTY > 0
|   C*-----------------------------------------<<銷售帳列庫存>>---
|   C* 帳列量已足夠銷售
| 08-C           HA06R     IFEQ 0
|   C                     GOTO END502                      END-RTN
| 08-C                     ENDIF
|   C*
|   C* 再處理陳列可借貨量，BY到分店日排序－作先進先出
|   C                     MOVEL*BLANKS   KTN08            到分店日排序
|   C           KEYTN     SETLLTN0
|   C           KEYTN1    READETN0                      48
| 09-C           *IN48     DOWEQ'0'
|   C*---<< INTNLF03 LOOP START >>-----------------------------
|   C*  計算可借貨量                                    TN07陳列餘量
|   C           TN07      SUB  TN11      TNQTY            已預轉進貨量
|   C                     SUB  TN12      TNQTY            已預退出量
|   C                     SUB  TN13      TNQTY            已借貨量
| 10-C           TNQTY     IFGT 0                          有可借貨量
|   C*  有可借貨量
| 11-C           HA06R     IFLE TNQTY
|   C*  可借量足夠
|   C                     Z-ADDHA06R     HA06U            FOR UPDATE
|   C                     Z-ADD0         HA06R            可借量全足夠
| 11*C                     ELSE
|   C*  可借量不足，先借現有「可借量」
|   C                     Z-ADDTNQTY     HA06U            FOR UPDATE
|   C                     SUB  TNQTY     HA06R            續算尚餘不足量
| 11-C                     ENDIF
|   C*
|   C*   WRITE 銷售陳列品（借貨）異動記錄FOR銷貨成本（傳票）BY銷售序號
|   C                     EXSR RTN50A
|   C*   UPDATE借貨量INTNPF ( ONLY FOR 銷貨成本)
|   C                     EXSR RTN50B
| 10-C                     ENDIF                           TNQTY > 0
|   C* 陳列量已足夠銷售
| 12-C           HA06R     IFEQ 0
|   C                     GOTO END502                      END-RTN
| 12-C                     ENDIF
|   C*
|   C           KEYTN1    READETN0                      48 NEXT INTN
| 09-C                     ENDDO
|   C*-----------------------------------<< INTNLF03 END >>--
|   C* 其餘不足量HA06R皆列入產生銷售無庫量（盤盈）異動記錄
|   C* 取價：當月單位成本->最新報價->最近陳列進價（上線前庫存）
|   C*
| 13-C           TA07      IFGT 0                          當月單位成本
|   C* 取當月單位成本
|   C                     Z-ADDTA07      TQ07U
|   C                     EXSR RTN50C
| 13*C                     ELSE
|   C* 取最新年度報價
|   C                     MOVELTB01      KCC01             公司別
|   C                     MOVELTB03      KCC02             通路別
|   C                     MOVELTB05      KCC04             產品代號
|   C                     MOVEL'1'       KCC06             年度報價
|   C                     Z-ADDWTO05     KCC07             銷售日期
|   C                     EXSR RTN040
| 14-C           WCC09     IFNE 0                           最新年度價
|   C                     Z-ADDWCC09     TQ07U
|   C                     EXSR RTN50C
| 14*C                     ELSE
|   C* 取最近陳列進價
|   C                     MOVEL*HIVAL    KTN08            到分店日排序
|   C           KEYTN     SETGTTN0
|   C           KEYTN1    REDPETN0                      48
| 15-C           *IN48     IFEQ '0'
|   C                     Z-ADDTN06      TQ07U
|   C                     EXSR RTN50C
| 15-C                     ENDIF                           最近陳列進價
| 14-C                     ENDIF                           最新年度報價
| 13-C                     ENDIF                           當月單位成本
|   C*
| 06-C                     ENDIF                           銷貨成本處理
|    C*
|    C           END502    TAG
|    C*
|    C                     Z-ADD0         HA06P            銷售數量
|    C                     Z-ADD0         HA06M            銷貨退回量
|    C*
26===C                     ENDSR
      *===============================================================*
      *          RTN503....單一產品結算處理
      *===============================================================*
27===C           RTN503    BEGSR
|     *
|    C* 結算總庫產品帳列庫存量及金額
|    C                     Z-ADDTAAMT     TA08             帳列庫存金額
|    C                     ADD  TA09      TA08             加入調整金額
|    C                     ADD  @TP08     TA08             加入累計借貨金額
|    C                     Z-ADDTAQTY     TA10             帳列庫存量
|    C                     ADD  @TP06     TA10             加入累計借貨數量
|    C*
| 01-C           TA10      IFEQ 0
|   C* 總庫「零庫存量」的金額差額，先累計KEEP所有產品，
|   C* 最後再一次處理。
| 02-C           TA08      IFNE 0
|   C                     ADD  TA08      @BAL             累計零庫存量差額
|   C                     Z-ADD0         TA08
| 02-C                     ENDIF
|   C*
| 01*C                     ELSE
|   C* 處理「當月單位成本ｘ數量」及「不同單位成本ｘ數量」之差額
|   C* 以「當月單位成本ｘ數量」為基準，先累計KEEP所有產品，
|   C* 最後再一次處理。
|   C           TA07      MULT TA10      @TA08            BY當月單位成本
|   C           @TA08     SUB  TA08      @TA08W           差額
|   C                     ADD  @TA08W    @BAL
| 01-C                     ENDIF
|    C*
27===C                     ENDSR
      *===============================================================*
      *          RTN504....差額加入最大一筆出貨
      *===============================================================*
28===C           RTN504    BEGSR
|     *
|    C                     Z-ADD*HIVAL    KHA07
|    C           KEYHA     SETGTHA0
|    C                     READPHA0                      44
| 01-C           *IN44     IFEQ '0'
|   C                     ADD  @BAL      HA07
|   C                     UPDATHA0
| 01-C                     ENDIF
|     *
28===C                     ENDSR
      *===============================================================*
      *          RTN50A.... WRITE INTPPF 銷售陳列借貨異動檔
      *===============================================================*
29===C           RTN50A    BEGSR
|    C*
|    C                     CLEARTP0
|    C                     MOVELTB01      TP01             公司別
|    C                     MOVELTB03      TP02             通路代號
|    C                     Z-ADDWTO05     TP03             銷售日期
|    C                     MOVELTB04      TP04             分店代號
|    C                     MOVELTB05      TP05             產品代號
|    C                     Z-ADDHA06U     TP06             借貨數量
|    C                     Z-ADDTN06      TP07             陳列進價
|    C           TP06      MULT TP07      WTP08   90H
|    C                     Z-ADDWTP08     TP08             未稅金額
|    C           DUTY      ADD  1         WDUTY            含稅率
|    C           TP08      MULT WDUTY     WTP09   90H
|    C                     Z-ADDWTP09     TP09             未稅金額
|     *取得銷售借貨單號S2
|    C                     CALL 'GET001'
|    C                     PARM TB01      T001I1  2
|    C                     PARM 'S2'      T001I2  2
|    C                     PARM A6YMD     T001I3  60       西元YYMMDD
|    C           TP10      PARM           T001O1 13        借貨單號
|    C*
|    C                     MOVELHA09T     TP11             銷售序號
|    C                     MOVELTN09      TP12             陳列驗收單號
|    C                     MOVELTN03      TP13             供應商代號
|    C                     MOVEL*BLANKS   TP14             狀態碼
|    C                     Z-ADDA8YMD     TPVV             建檔日期
|    C                     MOVELPGWW      TPWW             異動程式
|    C                     Z-ADDA8YMD     TPXX             異動日期
|    C                     TIME           TPYY             異動時間
|    C                     MOVE $USER     TPZZ             異動者　
|    C                     WRITETP0
|    C* 累計借貨量價
|    C                     ADD  TP06      @TP06            借貨量
|    C                     ADD  TP08      @TP08            借貨金額
|    C*
29===C                     ENDSR
      *===============================================================*
      *          RTN50B.... UPDATE借貨量INTNPF ( ONLY FOR 銷貨成本)
      *===============================================================*
30===C           RTN50B    BEGSR
|    C*
|    C*累加借貨量UPDATE INTNPF,INTAPF,INTBPF
|    C*
|    C* UPDATE INTNPF
|    C                     ADD  HA06U     TN13             累計借貨量
|    C                     MOVELPGWW      TNWW      P
|    C                     Z-ADDA8YMD     TNXX
|    C                     TIME           TNYY
|    C                     MOVE $USER     TNZZ
|    C                     UPDATTN0
|    C*
|    C* UPDATE INTBPF
|    C                     ADD  HA06U     TB18             +陳列借貨量
|    C                     MOVELPGWW      TBWW      P
|    C                     Z-ADDA8YMD     TBXX
|    C                     TIME           TBYY
|    C                     MOVE $USER     TBZZ
|    C                     UPDATTB0L2
|    C*
|    C* UPDATE INTAPF
|    C                     ADD  HA06U     TA19             +陳列借貨量
|    C                     MOVELPGWW      TAWW      P
|    C                     Z-ADDA8YMD     TAXX
|    C                     TIME           TAYY
|    C                     MOVE $USER     TAZZ
|    C                     UPDATTA0
|    C*
30===C                     ENDSR
      *===============================================================*
      *          RTN50C.... WRITE INTQPF 銷售無庫存（盤盈）異動檔
      *===============================================================*
31===C           RTN50C    BEGSR
|    C*
|    C                     CLEARTQ0
|    C                     MOVELTB01      TQ01             公司別
|    C                     MOVELTB03      TQ02             通路代號
|    C                     MOVELWTO05     TQ03             銷售日期
|    C                     MOVELTB04      TQ04             分店代號
|    C                     MOVELTB05      TQ05             產品代號
|    C                     MOVELHA06R     TQ06             銷售數量
|    C                     Z-ADDTQ07U     TQ07             單價
|    C           TQ06      MULT TQ07      WTQ08   90H
|    C                     Z-ADDWTQ08     TQ08             未稅金額
|    C           DUTY      ADD  1         WDUTY            含稅率
|    C           TQ08      MULT WDUTY     WTQ09   90H
|    C                     Z-ADDWTQ09     TQ09             含稅金額
|     *取得銷售無庫單號S3
|    C                     CALL 'GET001'
|    C                     PARM TB01      T001I1  2
|    C                     PARM 'S3'      T001I2  2
|    C                     PARM A6YMD     T001I3  60       西元YYMMDD
|    C           TQ10      PARM           T001O1 13        銷售無庫單號
|     *
|    C                     MOVELHA09T     TQ11             銷售序號
|    C                     MOVEL*BLANKS   TQ12             狀態碼
|    C                     Z-ADDA8YMD     TQVV             建檔日期
|    C                     MOVELPGWW      TQWW             異動程式
|    C                     Z-ADDA8YMD     TQXX             異動日期
|    C                     TIME           TQYY             異動時間
|    C                     MOVE $USER     TQZZ             異動者　
|    C                     WRITETQ0
|    C*
31===C                     ENDSR
      *===============================================================*
      *          RTN50D....CLEAR TA QTY
      *===============================================================*
32===C           RTN50D    BEGSR
|     *
| 01-C           R50D      IFEQ '0'
|   C                     Z-ADD0         TA11X            進貨量
|   C                     Z-ADD0         TA12X            退出量
|   C                     Z-ADD0         TA13X            調整入庫量
|   C                     Z-ADD0         TA14X            調整出庫量
|   C                     Z-ADD0         TA15X            銷售入庫量
|   C                     Z-ADD0         TA16X            銷售出庫量
| 01*C                     ELSE
|   C                     Z-ADDTA08      XTA08  112
|   C                     Z-ADDTA10      XTA10   60
|   C           KEYTB2    CHAINTA0                  40
|   C                     Z-ADDXTA08     TA08
|   C                     Z-ADDXTA10     TA10
|   C                     Z-ADDTA11X     TA11             進貨量
|   C                     Z-ADDTA12X     TA12             退出量
|   C                     Z-ADDTA13X     TA13             調整入庫量
|   C                     Z-ADDTA14X     TA14             調整出庫量
|   C                     Z-ADDTA15X     TA15             銷售入庫量
|   C                     Z-ADDTA16X     TA16             銷售出庫量
|   C                     MOVELPGWW      TAWW      P
|   C                     Z-ADDA8YMD     TAXX
|   C                     TIME           TAYY
|   C                     MOVE $USER     TAZZ
|   C                     UPDATTA0
| 01-C                     ENDIF
|    C*
32===C                     ENDSR
      *===============================================================*
      *          RTN50E....CLEAR TB QTY
      *===============================================================*
33===C           RTN50E    BEGSR
|    C*
| 01-C           R50E      IFEQ '0'
|   C                     Z-ADD0         TB08X            進貨量
|   C                     Z-ADD0         TB09X            退出量
|   C                     Z-ADD0         TB10X            調整入庫量
|   C                     Z-ADD0         TB11X            調整出庫量
|   C                     Z-ADD0         TB12X            銷售入庫量
|   C                     Z-ADD0         TB13X            銷售出庫量
| 01*C                     ELSE
|   C                     ADD  TB08X     TA11X            進貨量
|   C                     ADD  TB09X     TA12X            退出量
|   C                     ADD  TB10X     TA13X            調整入庫量
|   C                     ADD  TB11X     TA14X            調整出庫量
|   C                     ADD  TB12X     TA15X            銷售入庫量
|   C                     ADD  TB13X     TA16X            銷售出庫量
|   C           KEYEA     CHAINTB0L2                40
|   C                     Z-ADDTB08X     TB08             進貨量
|   C                     Z-ADDTB09X     TB09             退出量
|   C                     Z-ADDTB10X     TB10             調整入庫量
|   C                     Z-ADDTB11X     TB11             調整出庫量
|   C                     Z-ADDTB12X     TB12             銷售入庫量
|   C                     Z-ADDTB13X     TB13             銷售出庫量
|   C                     MOVELPGWW      TBWW      P
|   C                     Z-ADDA8YMD     TBXX
|   C                     TIME           TBYY
|   C                     MOVE $USER     TBZZ
|   C                     UPDATTB0L2
| 01-C                     ENDIF
|    C*
33===C                     ENDSR
      *===============================================================*
      *          RTN600....產生新月份產品庫存記錄
      *===============================================================*
34===C           RTN600    BEGSR
|    C*
|    C* 下一月庫存年月份
|    C                     Z-ADDEH02      WNY
| 01-C           WMM       IFEQ 12
|   C                     ADD  1         WYY
|   C                     Z-ADD1         WMM
| 01*C                     ELSE
|   C                     ADD  1         WMM
| 01-C                     ENDIF
|    C*
|    C                     MOVE EH01      KTA01
|    C                     MOVE EH02      KTA02
|    C*新年度總庫產品記錄
|    C           KEYTA1    SETLLTA0
|    C           KEYTA1    READETA0                      44
| 02-C           *IN44     DOWEQ'0'
|   C                     EXSR RTN610
|   C                     WRITETA0
|   C           KEYTA1    READETA0                      44
| 02-C                     ENDDO
|    C           *LOVAL    SETLLTA0
|    C*
|    C                     MOVE EH01      KTB01
|    C                     MOVE EH02      KTB02
|    C*新年度分庫產品記錄
|    C           KEYTB1    SETLLTB0
|    C           KEYTB1    READETB0                      44
| 03-C           *IN44     DOWEQ'0'
|   C                     EXSR RTN620
|   C                     WRITETB0
|   C           KEYTB1    READETB0                      44
| 03-C                     ENDDO
|    C           *LOVAL    SETLLTB0
|    C*
34===C                     ENDSR
      *===============================================================*
      *          RTN610....產品總庫欄位
      *===============================================================*
35===C           RTN610    BEGSR
|    C*
|    C                     Z-ADDWNY       TA02             新年月份
|    C                     Z-ADDTA07      TA04             上月單位成本
|    C* 期初庫存金額TA05+TA08(TA08已含TA09)
|    C                     ADD  TA08      TA05             +帳列庫存金額
|    C* 期初庫存量
|    C                     ADD  TA11      TA06
|    C                     SUB  TA12      TA06
|    C                     ADD  TA13      TA06
|    C                     SUB  TA14      TA06
|    C                     ADD  TA15      TA06
|    C                     SUB  TA16      TA06
|    C* 陳列期初量
|    C                     ADD  TA17      TA18             +陳列異動
|    C*
|    C                     Z-ADD0         TA07
|    C                     Z-ADD0         TA08
|    C                     Z-ADD0         TA09
|    C                     Z-ADD0         TA10
|    C                     Z-ADD0         TA11
|    C                     Z-ADD0         TA12
|    C                     Z-ADD0         TA13
|    C                     Z-ADD0         TA14
|    C                     Z-ADD0         TA15
|    C                     Z-ADD0         TA16
|    C                     Z-ADD0         TA17             陳列量
|    C*
|    C                     Z-ADDA8YMD     TAVV
|    C                     MOVE *BLANK    TAWW
|    C                     MOVELPGWW      TAWW
|    C                     Z-ADDA8YMD     TAXX
|    C                     TIME           TAYY
|    C                     MOVEL$USER     TAZZ
|    C*
35===C                     ENDSR
      *===============================================================*
      *          RTN620....產品分庫欄位
      *===============================================================*
36===C           RTN620    BEGSR
|    C*
|    C*新月份期初值，調撥在途量直接移轉
|    C*
|    C                     Z-ADDWNY       TB02             新年月份
|    C*期初庫存量不含調撥在途量
|    C                     ADD  TB08      TB06             期初庫存量
|    C                     SUB  TB09      TB06
|    C                     ADD  TB10      TB06
|    C                     SUB  TB11      TB06
|    C                     ADD  TB12      TB06
|    C                     SUB  TB13      TB06
|    C*
|    C                     ADD  TB16      TB17             陳列期初量
|    C*
|    C                     Z-ADD0         TB07
|    C                     Z-ADD0         TB08
|    C                     Z-ADD0         TB09
|    C                     Z-ADD0         TB10
|    C                     Z-ADD0         TB11
|    C                     Z-ADD0         TB12
|    C                     Z-ADD0         TB13
|    C                     Z-ADD0         TB16             陳列量
|    C*
|    C                     Z-ADDA8YMD     TBVV
|    C                     MOVE *BLANK    TBWW
|    C                     MOVELPGWW      TBWW
|    C                     Z-ADDA8YMD     TBXX
|    C                     TIME           TBYY
|    C                     MOVEL$USER     TBZZ
|    C*
36===C                     ENDSR
      *===============================================================*
      *          RTN700....更新控制欄位
      *===============================================================*
37===C           RTN700    BEGSR
|    C*
|    C*清除銷售通路主檔/銷售確認欄位
|    C*
|    C           KEYND     SETLLND0
|    C           KEYND     READEND0                      45
| 01-C           *IN45     DOWEQ'0'
|   C                     MOVE *BLANK    ND22             銷售確認
|   C                     MOVEL*BLANK    NDWW
|   C                     MOVELPGWW      NDWW
|   C                     Z-ADDA8YMD     NDXX
|   C                     TIME           NDYY
|   C                     MOVEL$USER     NDZZ
|   C                     UPDATND0
|   C           KEYND     READEND0                      45
| 01-C                     ENDDO
|    C           *LOVAL    SETLLND0
|    C*
|    C*公司參數檔更新年月份
|    C           EH01      CHAINEH0                  40
| 02-C           *IN40     IFEQ '0'
|   C                     Z-ADDWNY       EH02             下一月結年月份
|   C                     MOVEL*BLANK    EH03
|   C                     MOVEL*BLANK    EH04
|   C                     MOVEL*BLANK    EH05
|   C                     MOVEL*BLANK    EH06
|   C                     Z-ADDEH07      W1YMD
|   C                     EXSR RTN70A
|   C                     Z-ADDW1YMD     EH07             下月區間起日
|   C                     Z-ADDEH08      W1YMD
|   C                     EXSR RTN70A
|   C                     Z-ADDW1YMD     EH08             下月區間訖日
|   C                     MOVEL*BLANK    EHWW
|   C                     MOVELPGWW      EHWW
|   C                     Z-ADDA8YMD     EHXX
|   C                     TIME           EHYY
|   C                     MOVEL$USER     EHZZ
|   C                     UPDATEH0
| 02-C                     ENDIF
|    C           *LOVAL    SETLLEH0
|    C*
37===C                     ENDSR
      *==============================================================*
      *          RTN70A....推算下一月份
      *==============================================================*
38===CSR         RTN70A    BEGSR
|    C*
|    C                     ADD  1         W1MM
| 01-C           W1MM      IFGT 12
|   C                     ADD  1         W1YY
|   C                     Z-ADD1         W1MM
| 01-C                     ENDIF
|    C*
38===CSR         END70A    ENDSR
