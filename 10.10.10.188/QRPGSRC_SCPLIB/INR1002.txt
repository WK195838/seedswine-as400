     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H* PROGRAM NAME : INR1002                                       *
     H* AUTHER       : DEREK                                         *
     H* CREATE DATE  : 101/12/10                                     *
     H* UPDATE DATE  :                                               *
     H* SYSTEM       :                                               *
     H* SUBSYSTEM    :庫存系統                                     *
     H* REMARK       :產品異動報表                                 *
     H*===============================================================
     H* MODI CODE |   DATE   | DESCRIPTION
     H*---------------------------------------------------------------
     H****************************************************************
     FSCWK26  IP  E           K        DISK
     FPA#BPF  IF  E           K        DISK
     FINR100P O   E             39     PRINTER
     FSCWK261 O   E                    DISK
     E*--------------------------------------------------------------*
     E                    TXT     1   2 10
     E*--------------------------------------------------------------*
     IW26
     I                                              W2603 L2
     I                                              W2605 L1
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      135 135 $ADD
     I                                      598 5990$PRTID
     I                                      600 600 DOWNL
     I                                      601 602 DTO01
     I                                      603 604 DDOC
     I                                      605 606 DTO02S
     I                                      607 608 DTO02E
     I                                      609 613 DTO03S
     I                                      614 618 DTO03E
     I                                      619 6190DATA
     I                                      621 629 DTO04S
     I                                      631 639 DTO04E
     I                                      641 6460DTO05S
     I                                      651 6560DTO05E
     I                                      661 6680DATES
     I                                      671 6780DATEE
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN           PTDA01
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR R0100
     C*
     C   L1                EXSR R7100
     C   L2                EXSR R7200
     C*
     C           *IN39     IFEQ '1'
     C                     EXSR R9000
     C                     END
     C*
     C                     EXSR R2000
     C*
     C*WRITE TO WRKFILE (SCWK261)
     C                     EXSR R3000
     C*
     CL1 10                EXSR R8100
     CLR 10                EXSR R8900
     CLR                   EXSR R8999
     C*==============================================================*
     C*          R0100 ... INITIAL
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C                     IN   PTDA01
     C* GET公司名稱
     C           DTO01     CHAIN#B0                  40
     C           *IN40     IFEQ '0'
     C                     MOVEL#B03      D#B03
     C                     ELSE
     C                     MOVEL*BLANK    D#B03
     C                     END
     C*
     C                     WRITEPH1
     C*
     C                     SETOF                     39
     C                     SETON                     103031
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R2000 ... WRITE DETAIL
     C*==============================================================*
     C           R2000     BEGSR
     C*
     C*進出日期
     C                     CALL 'P31'
     C                     PARM W2607     P3101I  80       原始日期
     C                     PARM '2'       P3102I  1        原始日期格式
     C                     PARM '1'       P3103I  1        原始曆別
     C                     PARM $PFMT     P3104I  1        轉換日期格式
     C                     PARM $PTYPE    P3105I  1        轉換曆別
     C           P2607     PARM           P3111O  80       轉換後日期
     C*期末數量
     C                     ADD  W2611     PQTY2
     C*
     C           *IN39     IFEQ '1'
     C                     EXSR R9000
     C                     END
     C*
     C                     WRITEPD1
     C                     SETOF                     3031
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R3000 ... WRITE TO WORKFILE(SCWK261)
     C*==============================================================*
     C           R3000     BEGSR
     C*
     C                     CLEARW261
     C                     MOVELW2601     W26101    P      公司代號
     C                     MOVELW2602     W26102    P      通路代號
     C                     MOVELW2603     W26103    P      分店代號
     C                     MOVELW2604     W26104    P      分店簡稱
     C                     MOVELW2605     W26105    P      產品代號
     C                     MOVELW2606     W26106    P      產品簡稱
     C                     Z-ADDP2607     W26107           進出日期
     C                     MOVELW2608     W26108    P      單別
     C                     MOVELW2609     W26109    P      單別名稱
     C                     MOVELW2610     W26110    P      單據編號
     C                     Z-ADDW2611     W26111           數量
     C                     WRITEW261
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R7100 ... LEVEL BREAK 1 --- INIT.
     C*==============================================================*
     C           R7100     BEGSR
     C*
     C                     SETON                     31
     C*期初數量
     C                     CALL 'INR1003'
     C                     PARM W2601     P001    2        公司別
     C                     PARM W2602     P002    2        通路別
     C                     PARM W2603     P003    5        分店代號
     C                     PARM W2605     P004    9        產品代號
     C           PQTY1     PARM           P005    90       期初數量
     C*
     C           *IN39     IFEQ '1'
     C                     EXSR R9000
     C                     END
     C*
     C                     WRITEPD0
     C*
     C                     Z-ADDPQTY1     PQTY2
     C*
     C                     CLEARW261
     C                     MOVELTXT,1     W26106    P
     C                     Z-ADDPQTY1     W26111
     C                     WRITEW261
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R7200 ... LEVEL BREAK 2 --- INIT.
     C*==============================================================*
     C           R7200     BEGSR
     C*
     C                     SETON                     30
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R9000 ... OVERFLOW
     C*==============================================================*
     C           R9000     BEGSR
     C*
     C                     WRITEPE1
     C                     WRITEPH1
     C                     SETOF                     39
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8100 ... LEVEL BREAK 2 --- TOTAL
     C*==============================================================*
     C           R8100     BEGSR
     C*
     C           *IN39     IFEQ '1'
     C                     EXSR R9000
     C                     END
     C*列印期末數量
     C                     WRITEPD2
     C*
     C                     CLEARW261
     C                     MOVELTXT,2     W26106    P
     C                     Z-ADDPQTY2     W26111
     C                     WRITEW261
     C*
     C                     Z-ADD0         PQTY2
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8900 ... LR --- HAS DATA
     C*==============================================================*
     C           R8900     BEGSR
     C*
     C           *IN39     IFEQ '1'
     C                     EXSR R9000
     C                     END
     C*
     C                     WRITEPE2
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R8999 ... LR --- NO DATA
     C*==============================================================*
     C           R8999     BEGSR
     C*
     C                     EXSR R0100
     C                     WRITEPE9
     C                     WRITEPE2
     C*
     C                     ENDSR
**
期初數量
期末數量
