     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM NAME : S#A120                                     *
     H*    AUTHOR       : A1062   JEANNY LIN                         *
     H*    CREATE DATE  : 79/06/27                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :程式安全管制系統                          *
     H*    PURPOSE      :使用者管制資料維護                        *
     H****************************************************************
     FS#SDPF  UF  E           K        DISK                      A
     FS#SPPF  IF  E           K        DISK
     FS#SUPF  IF  E           K        DISK
     FS#A120D CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
     E                    WFUN    5   5  6               *OPTION
     E                    WARY    1   1 10               *DSD022 INTVAL
     IS#DA01      DS
     I                                        1  10 CENTER
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
     C*==============================================================*
     C*                    KEY LIST FOR SDPF
     C*==============================================================*
     C           KEYSD     KLIST
     C                     KFLD           WSD01  10
     C                     KFLD           WSD02  10
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR RTN010                     *INITIAL
     C           *IN03     DOWEQ'0'
     C*
     C           SCID      CASEQ'SC01'    RTN100
     C           SCID      CASEQ'SC02'    RTN200
     C                     END
     C*
     C                     END
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE
     C*==============================================================*
     C           RTN010    BEGSR
     C           *NAMVAR   DEFN           S#DA01
     C                     IN   *NAMVAR
     C*
     C           USER      CHAINSU0                  40    *NF
     C   40                MOVE *BLANK    DUSER
     C  N40                MOVE SU02      DUSER
     C*
     C                     MOVE 'SC01'    SCID    4
     C                     MOVE *BLANK    DSD01
     C                     MOVE *BLANK    DSD021
     C                     MOVE WARY,1    DSD022
     C                     MOVE '1'       DOPT
     C                     ENDSR
     C*==============================================================*
     C*          RTN100....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           RTN100    BEGSR
     C                     EXFMTDSPC1
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'                        *03 OFF
     C*
     C           *IN18     IFEQ '1'                        *18 OFF
     C                     CALL 'S#S110'
     C           DSD01     PARM DSD01     S1101I 10
     C           *IN03     PARM *IN03     S1101O  1
     C                     END                             *18 END
     C*
     C                     EXSR RTN110                     *CHK SRC
     C  N99                MOVE 'SC02'    SCID
     C                     END                             *03 END
     C*
     C           END100    ENDSR
     C*==============================================================*
     C*          RTN110....CHECK SCR1
     C*==============================================================*
     C           RTN110    BEGSR
     C           USER      IFNE CENTER
     C*執行者基本資料須存在
     C           USER      CHAINSU0                  40
     C   40                SETON                     9599
     C   99                GOTO END110
     C*執行者管制資料須存在
     C           USER      SETLLSD0                      42*EQ
     C  N42                SETON                     9499
     C           *LOVAL    SETLLSD0
     C   99                GOTO END110
     C*
     C                     END
     C*須輸入資料
     C           DSD01     IFEQ *BLANK
     C                     SETON                     609899
     C                     END
     C   99                GOTO END110
     C*
     C           USER      IFNE CENTER
     C*非管制中心無法使用管制中心資料
     C           DSD01     IFEQ CENTER
     C                     SETON                     609799
     C                     END
     C   99                GOTO END110
     C*使用者基本資料須存在
     C           DSD01     CHAINSU0                  40    *NF
     C   40                SETON                     609399
     C   99                GOTO END110
     C*去除無權使用者
     C           USER      IFNE SU03
     C           USER      ANDNESU01
     C                     SETON                     609799
     C                     END
     C   99                GOTO END110
     C*
     C                     END
     C*指定程式範圍值
     C           DSD022    IFEQ *BLANK
     C                     MOVE *HIVAL    DSD022
     C                     END
     C*
     C           DSD021    IFGT DSD022
     C                     SETON                     616289
     C                     SETON                     99
     C                     END
     C   99                GOTO END110
     C*
     C           DSD01     IFNE CENTER                     ******
     C*檢查使用者之管制程式範圍資料是否存在
     C                     MOVE DSD01     WSD01
     C                     MOVE DSD021    WSD02
     C           KEYSD     SETLLSD0
     C           DSD01     READESD0                      44*EOF
     C           DOPT      IFNE '1'                        *DOPT<>1
     C           *IN44     IFEQ '1'
     C           *IN44     OREQ '0'
     C           SD02      ANDGTDSD022
     C                     SETON                     606162
     C                     SETON                     9099
     C                     END
     C                     END
     C           *LOVAL    SETLLSD0
     C   99                GOTO END110
     C*本人僅能查詢
     C           USER      IFEQ DSD01
     C           DOPT      ANDNE'5'
     C                     SETON                     609699
     C                     END
     C*
     C                     END                             ******
     C*
     C           END110    ENDSR
     C*==============================================================*
     C*                    SUBFILE MAIN ROUTINE
     C*==============================================================*
     C           RTN200    BEGSR
     C                     EXSR RTN210                     *INIT SFL
     C           *IN03     DOWEQ'0'                        *03 LOOP
     C           SCID      ANDEQ'SC02'
     C*
     C                     EXSR RTN220                     *EXFMTSFLCR
     C*
     C           DOPT      IFLE '2'
     C   05                EXSR RTN210
     C                     END
     C*
     C           *IN25     IFEQ '1'                        *25 ON
     C                     SETON                     9799  *LAST PAGE
     C                     Z-ADDRRN       DRRN
     C                     END
     C*
     C           *IN26     IFEQ '1'                        *26 ON
     C                     SETON                     9899  *FIRST PAGE
     C                     Z-ADD1         DRRN
     C                     END                             *26 OFF
     C*
     C           *IN99     IFEQ '0'
     C           DSD01     IFNE CENTER
     C  N03N05N12          EXSR RTN300                     *SAVE
     C                     END
     C  N03N05N12          MOVE 'SC01'    SCID             *ENTER
     C                     END
     C*
     C   12                MOVE 'SC01'    SCID
     C*
     C                     END                             *03 LOOP
     C                     ENDSR
     C*==============================================================*
     C*          RTN210....INITIAL
     C*==============================================================*
     CSR         RTN210    BEGSR
     C                     MOVE DOPT      WNUM    10
     C                     MOVE WFUN,WNUM DFUN
     C*
     C                     Z-ADD0         RRN
     C                     Z-ADD1         DRRN
     C*
     C                     SETON                     52
     C                     WRITESFLCR
     C                     SETOF                     5253
     C*
     C           DOPT      IFLE '2'
     C                     SETON                     31    *CAN INPUT
     C                     ELSE
     C                     SETOF                     31    *CAN'T INPUT
     C                     END
     C*
     C                     SETON                     505154*SFLDSP,CTL
     C                     EXSR RTN211
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN211....ADD SUBFILE DATA
     C*==============================================================*
     CSR         RTN211    BEGSR
     C*帶出使用者基本資料
     C                     MOVE *BLANK    DSD01C
     C                     MOVE *BLANK    DSU03
     C                     MOVE *BLANK    DSU03C
     C           DSD01     CHAINSU0                  40    *NF
     C           *IN40     IFEQ '0'
     C                     MOVE SU02      DSD01C
     C                     MOVE SU03      DSU03
     C                     END
     C           SU03      CHAINSU0                  40    *NF
     C  N40                MOVE SU02      DSU03C
     C           DSD01     CHAINSU0                  40    *NF
     C*帶出程式管制資料
     C* 0.*IN05 = '1'
     C   05                EXSR RTN213
     C   05                GOTO END211
     C* 1.執行者為管制中心且新增∣中心異動中心
     C           USER      IFEQ CENTER                     *1
     C           DOPT      ANDEQ'1'
     C           DSD01     OREQ CENTER
     C           DSD021    SETLLSP0
     C                     READ SP0                      46*EOF
     C           *IN46     DOWEQ'0'                        *46
     C           SP01      ANDLEDSD022
     C                     MOVE DSD01     WSD01
     C                     MOVE SP01      WSD02
     C           KEYSD     CHAINSD0                  40    *NF
     C           *IN40     IFEQ '1'                        *40
     C*
     C                     MOVE 'Y'       DUSE
     C                     MOVE SP01      SD02
     C                     MOVE 'Y'       SD03
     C                     MOVE 'Y'       SD04
     C                     MOVE 'Y'       SD05
     C                     MOVE 'Y'       SD06
     C                     MOVE 'Y'       SD07
     C                     MOVE 'Y'       SD08
     C                     MOVE 'Y'       SD09
     C                     ADD  1         RRN
     C                     WRITESFLSR
     C*
     C                     END                             *40
     C                     READ SP0                      46*EOF
     C                     END                             *46
     C                     END                             *1
     C* 2.執行者非管制中心且新增
     C           USER      IFNE CENTER                     *2
     C           DOPT      ANDEQ'1'
     C           DSD021    SETLLSP0
     C                     READ SP0                      46*EOF
     C           *IN46     DOWEQ'0'                        *46
     C           SP01      ANDLEDSD022
     C                     MOVE DSD01     WSD01
     C                     MOVE SP01      WSD02
     C           KEYSD     CHAINSD0                  40    *NF
     C           *IN40     IFEQ '1'                        *40 1
     C*
     C                     MOVE USER      WSD01
     C                     MOVE 'Y'       DUSE
     C           KEYSD     CHAINSD0                  40    *NF
     C           *IN40     IFEQ '0'                        *40 2
     C                     ADD  1         RRN
     C                     EXSR RTN212                     *->PROTECT
     C                     WRITESFLSR
     C                     END                             *40 2
     C*
     C                     END                             *40 1
     C                     READ SP0                      46*EOF
     C                     END                             *46
     C                     END                             *2
     C* 3.異動
     C           DOPT      IFNE '1'
     C                     MOVE DSD01     WSD01
     C                     MOVE DSD021    WSD02
     C           KEYSD     SETLLSD0
     C           WSD01     READESD0                      44*EOF
     C           *IN44     DOWEQ'0'                        *44
     C           SD02      ANDLEDSD022
     C*
     C                     ADD  1         RRN
     C                     MOVE 'Y'       DUSE
     C           SD02      CHAINSP0                  40    *NF
     C   40                MOVE *BLANK    SP02
     C           DOPT      IFEQ '2'                        *DOPT 2
     C                     EXSR RTN212                     *->PROTECT
     C                     END                             *DOPT 2
     C                     WRITESFLSR
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           WSD01     READESD0                      44*EOF
     C                     END                             *44
     C                     END                             *3
     C******
     C           END211    TAG
     C           *LOVAL    SETLLSD0
     C                     SETON                     53
     C                     ENDSR
     C*==============================================================*
     C*          RTN212....PROTECE SUBFILE DATA
     C*==============================================================*
     CSR         RTN212    BEGSR
     C           SD03      IFEQ ' '
     C                     SETON                     60
     C                     END
     C*
     C           SD04      IFEQ ' '
     C                     SETON                     61
     C                     END
     C*
     C           SD05      IFEQ ' '
     C                     SETON                     62
     C                     END
     C*
     C           SD06      IFEQ ' '
     C                     SETON                     63
     C                     END
     C*
     C           SD07      IFEQ ' '
     C                     SETON                     64
     C                     END
     C*
     C           SD08      IFEQ ' '
     C                     SETON                     65
     C                     END
     C*
     C           SD09      IFEQ ' '
     C                     SETON                     66
     C                     END
     C                     ENDSR
     C*==============================================================*
     C*          RTN213    *IN05 ON (重新授權)
     C*==============================================================*
     C           RTN213    BEGSR
     C                     SETON                     32    *FOR DLT OLD
     C*1.執行者為管制中心
     C           USER      IFEQ CENTER                     *=CENTER
     C           DSD021    SETLLSP0
     C                     READ SP0                      46*EOF
     C                     MOVE 'Y'       DUSE
     C                     MOVE 'Y'       SD03
     C                     MOVE 'Y'       SD04
     C                     MOVE 'Y'       SD05
     C                     MOVE 'Y'       SD06
     C                     MOVE 'Y'       SD07
     C                     MOVE 'Y'       SD08
     C                     MOVE 'Y'       SD09
     C           *IN46     DOWEQ'0'                        *46
     C           SP01      ANDLEDSD022
     C                     MOVE SP01      SD02
     C                     ADD  1         RRN
     C                     WRITESFLSR
     C                     READ SP0                      46*EOF
     C                     END                             *46
     C*
     C                     ELSE                            *<>CENTER
     C*2.執行者非管制中心
     C                     MOVE 'Y'       DUSE
     C           DSD021    SETLLSP0
     C                     READ SP0                      46*EOF
     C           *IN46     DOWEQ'0'                        *46
     C           SP01      ANDLEDSD022
     C                     MOVE USER      WSD01
     C                     MOVE SP01      WSD02
     C           KEYSD     CHAINSD0                  40    *NF
     C           *IN40     IFEQ '0'                        *40 2
     C                     ADD  1         RRN
     C                     EXSR RTN212                     *->PROTECT
     C                     WRITESFLSR
     C                     END                             *40 2
     C                     READ SP0                      46*EOF
     C                     END                             *46
     C******
     C                     END                             *CENTER
     C                     ENDSR
     C*==============================================================*
     C*          RTN220    EXFMT SFL
     C*==============================================================*
     C           RTN220    BEGSR
     C*
     C                     SETOF                     5051
     C           RRN       IFGT *ZEROS
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     9699
     C                     END
     C*
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC2
     C                     EXFMTSFLCR
     C*
     C                     SETOF                     5051
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN300....CHK & SAVE  DATA MAIN ROTUINE
     C*==============================================================*
     CSR         RTN300    BEGSR
     C*
     C           DOPT      CASEQ'1'       RTN310
     C           DOPT      CASEQ'2'       RTN320
     C  N05      DOPT      CASEQ'4'       RTN340
     C                     END
     C*
     C  N05      DOPT      IFEQ '5'
     C                     MOVE 'SC01'    SCID
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN310....SAVE DATA -- ADD MODE
     C*==============================================================*
     C           RTN310    BEGSR
     C   32                EXSR RTN340
     C                     SETOF                     32
     C*
     C                     MOVE DSD01     SD01             *SFL->PF
     C                     READCSFLSR                    57
     C           *IN57     DOWEQ'0'                        *57 DO
     C                     UPDATSFLSR
     C*WRITE SD0
     C           DUSE      IFEQ 'Y'                        *'Y'
     C                     WRITESD0
     C                     END
     C*
     C                     READCSFLSR                    57
     C                     END                             *57 END
     C           *LOVAL    SETLLSD0
     C                     ENDSR
     C*==============================================================*
     C*          RTN320....SAVE DATA -- UPD MODE
     C*==============================================================*
     C           RTN320    BEGSR
     C                     EXSR RTN340                     *SAVE - DLT
     C                     EXSR RTN310                     *SAVE - ADD
     C                     ENDSR
     C*==============================================================*
     C*          RTN340....SAVE DATA --DLT MODE
     C*==============================================================*
     CSR         RTN340    BEGSR
     C*DELETE SD0
     C                     MOVE DSD01     WSD01
     C                     MOVE DSD021    WSD02
     C           KEYSD     SETLLSD0
     C           DSD01     READESD0                      44
     C           *IN44     DOWEQ'0'
     C           SD02      ANDLEDSD022
     C           SD01      CHAINSU0                  40    *NF
     C  N40      USER      IFEQ CENTER                     *IF
     C           USER      OREQ SU03
     C                     DELETSD0
     C                     END                             *IF
     C           DSD01     READESD0                      44
     C                     END
     C*
     C                     ENDSR
**
新增修改      刪除查詢
**
9999999999
