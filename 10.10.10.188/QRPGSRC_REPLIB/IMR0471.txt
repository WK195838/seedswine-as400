      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : IMR0471                                       *
      * AUTHER       : A1139  JANE                                   *
      * CREATE DATE  : 81/02/28                                      *
      * UPDATE DATE  :                                               *
      * SYSTEM       :人頭馬匯東                                   *
      * SUBSYSTEM    :庫存系統                                     *
      * REMARK       :產品送貨明細表                               *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.12.10 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
      ****************************************************************
     FREWF09  IP  E           K        DISK
     FMTMAPF  IF  E           K        DISK
     FMTMEPF  IF  E           K        DISK
     FIMR047P O   E             39     PRINTER
     ISE0
     I                                              SE05  L1
     I                                              SE04  L2
     I                                              SF03  L3
     I                                              SE21  L4
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$LPRT
     I                                      601 602 DSE01
     I                                      603 607 DSE07S
     I                                      608 612 DSE07E
     I                                      613 621 DSF03S
     I                                      622 630 DSF03E
     I*(MM/DD/YY)送貨日期
     I                                      637 6420DSE21S
     I                                      643 6480DSE21E
     I*(MM/DD/YY)確認日期
     I                                      650 6550DSE24S
     I                                      656 6610DSE24E
     I*(YY/MM/DD)
M001MI*                                     670 6750YMDS
M001MI*                                     676 6810YMDE
M001MI                                      670 6770YMDS
M001MI                                      678 6850YMDE
     I*送貨日期及確認日期控制碼
     I                                      750 750 IN20
      *==============================================================*
      *                    KEY LIST
      *==============================================================*
     C           KEYSE     KLIST
     C                     KFLD           SE04
     C                     KFLD           SE05
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C           *IN10     IFEQ '0'
     C                     EXSR RTN010
|    C                     SETON                         10
     C                     END
     C*
     C   39                EXSR RTN900                     *OVRFLW-THE SAME SALE
     C                     EXSR RTN200                     *READE DETAIL
     C                     SETOF                     293031
     C*
     CL1 10                SETON                     29
     CL3 10                SETON                     2930
     CL4 10                SETON                     293031
     CLR                   EXSR RTN999
      *==============================================================*
      *          RTN010....CHECK HEADING
      *==============================================================*
01===C           RTN010    BEGSR
|    C           IN20      IFEQ '1'
|    C                     MOVE DSE24S    DMDYS
|    C                     MOVE DSE24E    DMDYE
|    C                     SETON                     20
|    C                     ELSE
|    C                     MOVE DSE21S    DMDYS
|    C                     MOVE DSE21E    DMDYE
|    C                     SETOF                     20
|    C                     END
|    C                     WRITEPH1
|    C                     SETON                     293031
01===C                     ENDSR
      *==============================================================*
      *          RTN200....READE DETAIL
      *==============================================================*
02===C           RTN200    BEGSR
|    C*
|    C                     EXSR RTN300                     *CHAIN DATA
|    C                     EXSR RTN400                     *JUDGE TOT  L
|    C                     EXSR RTN500                     *WRITE DETAIL
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTN300....CHAIN DATA
      *==============================================================*
03===C           RTN300    BEGSR
|    C*客戶簡稱
|    C           KEYSE     CHAINME0                  42
| 01-C           *IN42     IFEQ '1'
|   C                     MOVE *BLANK    ME04
| 01-C                     END
|    C*
|    C*產品簡稱
|    C           SF03      CHAINMA0                  44
| 02-C           *IN44     IFEQ '1'
|   C                     MOVE *BLANK    MA03
| 02-C                     END
|    C*
03===C                     ENDSR
      *==============================================================*
      *          RTN400....JUDGE TOT
      *==============================================================*
04===C           RTN400    BEGSR
|    C*
| 01-C           SE03      IFEQ 'D4'
|   C*試飲數量
|   C                     Z-ADD0         PSF041
|   C                     Z-ADDSF04      PSF042
| 01*C                     ELSE
|   C*訂購數量
|   C                     Z-ADD0         PSF042
|   C                     Z-ADDSF04      PSF041
| 01-C                     END
|    C*送貨總數
|    C           SF04      ADD  SF05      PTOT
|    C*
04===C                     ENDSR
      *==============================================================*
      *          RTN500....WRITE DETAIL
      *==============================================================*
05===C           RTN500    BEGSR
M001AC                     Z-ADDSE21      PSE21
|    C                     WRITEPD1
|    C                     SETOF                     293031
05===C                     ENDSR
      *==============================================================*
      *          RTN900....OVERFLOW
      *==============================================================*
06===C           RTN900    BEGSR
|    C*
|    C                     WRITEPE1
|    C                     WRITEPH1
|    C                     SETON                     293031
|    C                     SETOF                     39
|    C*
06===C                     ENDSR
      *==============================================================*
      *          RTN999....(NO DATA)
      *==============================================================*
07===C           RTN999    BEGSR
| 01-C           *IN10     IFEQ '0'
|   C                     EXSR RTN010
|    C                     WRITEPE31
| 01-C                     END
|    C                     WRITEPE2
07===C                     ENDSR
