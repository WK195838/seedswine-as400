     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM NAME : S#M010                                     *
     H*    AUTHOR       : A1034   STEPHANIE                          *
     H*    CREATE DATE  : 79/06/28                                   *
     H*    UPDATE DATE  :                                            *
     F*    SYSTEM       :程式安全管制系統                          *
     F*    PURPOSE      :程式安全管制系統主畫面                    *
     H****************************************************************
     FS#SDPF  IF  E           K        DISK
     FS#SPPF  IF  E           K        DISK
     FS#M010D CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
     I           UDS
     I                                      101 110 $USER
     I                                      111 1160$CHYMD
     I            DS
     I                                        1  12 DSP01
     I                                        1   1 DSP011
     I                                        2  11 SP01
     I                                       12  12 DSP012
     C*==============================================================*
     C*                    KEY  LIST
     C*==============================================================*
     C           KEYSD     KLIST
     C                     KFLD           $USER
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR RTN100                     *INITIAL
     C                     EXSR RTN810                     *ADD SFLDTA
     C*
     C           DOP       DOUEQ90
     C           DOP       IFNE 0
     C           RRN       ANDNE0
     C                     EXSR RTN400
     C                     END
     C                     EXSR RTN300                     *EXFMT
     C                     END
     C*
     C                     SETON                     LR
     C*=============================================================*
     C*          RTN100....INITIAL
     C*=============================================================*
     C           RTN100    BEGSR
     C*: INITIAL VALUE
     C                     Z-ADD1         DRRN
     C                     Z-ADD0         RRN
     C                     Z-ADD0         DOP
     C                     MOVE '('       DSP011
     C                     MOVE ')'       DSP012
     C*: CLEAR SUBFILE DATA :
     C                     SETON                     52    *SFLCLR
     C                     WRITESFLCR
     C                     SETOF                     52
     C*
     C           KEYSD     SETLLS#SDPF
     C                     ENDSR
     C*=============================================================*
     C*          RTN300....EXFMT DSPF
     C*=============================================================*
     C           RTN300    BEGSR
     C*
     C           RRN       IFGT 0
     C                     SETON                     50    *SFLDSP
     C                     END
     C*
     C                     SETON                     51    *SFLCTL
     C                     WRITEDSPC1
     C                     EXFMTSFLCR
     C                     SETOF                     5051
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C                     ENDSR
     C*=============================================================*
     C*          RTN400....OPT
     C*=============================================================*
     C           RTN400    BEGSR
     C*
     C           DOP       CHAINSFLSR                40
     C           *IN40     IFEQ '1'
     C                     SETON                     609599
     C                     Z-ADD1         DRRN
     C                     END
     C   99                GOTO END400
     C*
     C                     CALL 'S#S01'
     C                     PARM $USER     S0101I 10
     C                     PARM SP01      S0102I 10
     C                     PARM           S0101O  1
     C*
     C                     CALL SP01
     C                     Z-ADDDOP       DRRN
     C*
     C           END400    ENDSR
     C*=============================================================*
     C*          RTN810....ADD SUBFILE DATA
     C*=============================================================*
     C           RTN810    BEGSR
     C*
     C           KEYSD     READES#SDPF                   44*NF/EOF
     C           *IN44     DOWEQ'0'
     C           *IN53     ANDEQ'0'
     C*
     C           SD02      CHAINS#SPPF               40
     C           *IN40     IFEQ '1'
     C                     MOVE *BLANKS   SP02
     C                     END
     C*
     C                     ADD  1         RRN
     C                     WRITESFLSR                    53
     C*
     C           KEYSD     READES#SDPF                   44*NF/EOF
     C                     END
     C*
     C                     SETON                     53
     C                     ENDSR
