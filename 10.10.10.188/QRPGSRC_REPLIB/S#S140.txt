     F****************************************************************
     F*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     F*                                                              *
     F*    PROGRAM NAME : S#S140                                     *
     F*    AUTHOR       : A1062   JEANNY LIN                         *
     F*    CREATE DATE  : 79/07/03                                   *
     F*    UPDATE DATE  :                                            *
     F*    SYSTEM       :程式安全管制系統                          *
     F*    PURPOSE      :程式基本資料全頁查詢                      *
     F****************************************************************
     FS#SPPF  IF  E           K        DISK
     FS#SUPF  IF  E           K        DISK
     FS#S140D CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
     E                    A03     3   3  8               *PROGRAM TYPE
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
     C*==============================================================*
     C*                    LIST PARAMETERS
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DSP01     PARM DSP01     S1401I 10
     C           *IN03     PARM *IN03     S1401O  1
     C*=============================================================*
     C*                    MAIN ROUTINE
     C*=============================================================*
     C           USER      CHAINSU0                  40    *NF
     C   40                MOVE *BLANK    DUSER
     C  N40                MOVE SU02      DUSER
     C*
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
     C*
     C           *IN02     IFEQ '0'
     C           *IN25     ANDEQ'0'
     C           *IN26     ANDEQ'0'
     C                     EXSR RTN100                     *INIT RTN.
     C                     EXSR RTN200                     *ADD SFL
     C                     END
     C*
     C           *IN25     IFEQ '1'                        *25 ON
     C                     SETON                     97    *LAST PAGE
     C                     Z-ADDRRN       DRRN
     C                     END
     C*
     C           *IN26     IFEQ '1'                        *26 ON
     C                     SETON                     98    *FIRST PAGE
     C                     Z-ADD1         DRRN
     C                     END                             *26 OFF
     C*
     C                     EXSR RTN300                     *EXFMT
     C   02                EXSR RTN400
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
     C*=============================================================*
     C*          RTN100....INITIAL
     C*=============================================================*
     C           RTN100    BEGSR
     C*INITIAL VALUES
     C                     Z-ADD*ZEROS    RRN
     C                     Z-ADD*ZEROS    DRRN
     C*SFL CLEAR
     C                     SETON                     52
     C                     WRITESFLCR
     C                     SETOF                     5253
     C*SETLL DATA
     C           DSP01     SETLLSP0
     C                     ENDSR
     C*=============================================================*
     C*          RTN200....ADD SUBFILE DATA
     C*=============================================================*
     C           RTN200    BEGSR
     C           *IN53     DOWEQ'0'                        *53 DO
     C                     READ SP0                      46*NF
     C*
     C           *IN46     IFEQ '0'                        *46 IF
     C*
     C           SP03      IFEQ 'M'
     C                     MOVE A03,1     DSP03
     C                     ELSE
     C           SP03      IFEQ 'P'
     C                     MOVE A03,2     DSP03
     C                     ELSE
     C                     MOVE A03,3     DSP03
     C                     END
     C                     END
     C*
     C                     ADD  1         RRN
     C                     WRITESFLSR
     C                     ELSE                            *46 ELSE
     C                     SETON                     53
     C                     END                             *46 END
     C*
     C                     END                             *53 END
     C                     Z-ADD1         DRRN
     C                     ENDSR
     C*=============================================================*
     C*          RTN300....EXFMT DSPF
     C*=============================================================*
     C           RTN300    BEGSR
     C           RRN       IFGT *ZEROS
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     96    *NO DATA
     C                     END
     C*
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC1
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C                     SETOF                     5051
     C                     ENDSR
     C*=============================================================*
     C*          RTN400....SELECT SEQUENCE NUMBER
     C*=============================================================*
     C           RTN400    BEGSR
     C*
     C           DSLT      IFEQ 0
     C                     SETON                     609599
     C                     END
     C   99                GOTO END400
     C*
     C           DSLT      CHAINSFLSR                40    *NF
     C   40                SETON                     609499
     C   99                GOTO END400
     C*
     C                     MOVE SP01      DSP01
     C                     SETON                     12
     C*
     C           END400    ENDSR
**
主畫面主程式副程式
