     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    PROGRAM ID   : KIS009                                     *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    DATE WRITTEN : 98/03/12                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :茂世進銷存                                *
     A*    SUBSYSTEM    :                                          *
     A*    REMARK       :採購單查詢                                *
     A*===============================================================
     F* 後台採購主檔
     FW2BELF01IF  E           K        DISK
     F* 後台採購明細檔
     FW2BFPF  IF  E           K        DISK
     F* 後台採購驗收檔
     FW2BGLF02IF  E           K        DISK
     FW2BHPF  IF  E           K        DISK
     FMTMBPF  IF  E           K        DISK
     FMTMAPF  IF  E           K        DISK
     FMTMEPF  IF  E           K        DISK
     FKIS009D CF  E                    WORKSTN
     F                                        RRN1  KSFILE SFLSR1
     F                                        RRN2  KSFILE SFLSR2
     E*: FUNCTION DATA :
     E                    WFUN    5   5  6
     E*: A/U/D/I AUT
     E                    WA01        5  1
     I*
     ISDS        SDS
     I                                     *PROGRAM APPPGM
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      152 161 $USERN
     I                                      111 1160$CHYMD
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      139 139 $RMK01
     I                                      201 2080DATE
     I                                      201 2080$EG8
     I            DS
     I                                        1   1 DOPT1
     I                                        1   10WNUM
     I                                    B 378 3790#DRRN
      *==============================================================*
      *                    DEFINE ROUTINE
      *==============================================================*
     C                     Z-ADD0         HRRN1   40
     C                     MOVE *BLANKS   APPSCR  6
     C*
     C           *LIKE     DEFN DBGN1     OBGN1            *START KEY
     C           *LIKE     DEFN DBGN2     OBGN2            *START KEY
     C           *LIKE     DEFN DBGN3     OBGN3            *START KEY
     C           *LIKE     DEFN DBGN4     OBGN4            *START KEY
     C           *LIKE     DEFN RRN2      @PRRN
      *  FOR SUBFILE (SYS+PGM CONTROL)
      *==============================================================*
      *                    KLIST
      *==============================================================*
     C           KEYBE     KLIST
     C                     KFLD           KBGN1
     C                     KFLD           DBGN2
     C                     KFLD           DBGN3
     C                     KFLD           DBGN4
     C           KEYBE1    KLIST
     C                     KFLD           BE01
     C                     KFLD           BE03
     C           KEYBF     KLIST
     C                     KFLD           DBE03
     C           KEYBG     KLIST
     C                     KFLD           SBE01H
     C                     KFLD           SBE02
     C                     KFLD           SBE03
     C           KEYBH     KLIST
     C                     KFLD           BG03
     C                     KFLD           SBF02
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C                     EXSR RTN010                     *INIT ENVIRMENT
     C                     EXSR RTN191                     *INIT SFL
      *
     C                     EXSR RTN192                     *READ NEXT PAGE
     C                     EXSR RTN195                     *EXFMT
     C*
  01-C           *IN03     DOWEQ'0'                        *03    OFF
    C*
  02-C           *IN25     CASEQ'1'       RTN192
    C                     CAS            RTN170
  02-C                     END
    C  N03                EXSR RTN195
    C*
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE
     C*==============================================================*
01===C           RTN010    BEGSR
|    C*
|    C                     MOVE $ADD      WA01,1
|    C                     MOVE $UPD      WA01,2
|    C                     MOVE $DLT      WA01,4
|    C                     MOVE $INQ      WA01,5
|    C                     MOVE 'SCR001'  APPSCR
|    C                     Z-ADD0         KBGN1   80
|    C                     Z-ADD123129    OBGN1
|    C*
01===C                     ENDSR
      *==============================================================*
      *          RTN170....ENTER
      *==============================================================*
02===C           RTN170    BEGSR
|    C*
| 01-C           DBGN1     IFNE OBGN1
|   C           DBGN2     ORNE OBGN2
|   C           DBGN3     ORNE OBGN3
|   C           DBGN4     ORNE OBGN4
|   C                     Z-ADD0         HRRN1
|   C                     EXSR RTN191
|   C                     EXSR RTN192
|   C                     Z-ADDDBGN1     OBGN1
|   C                     MOVE DBGN2     OBGN2
|   C                     MOVE DBGN3     OBGN3
|   C                     MOVE DBGN4     OBGN4
| 01*C                     ELSE
|   C                     EXSR RTN171
| 02-C           *IN99     IFEQ '0'
|   C                     Z-ADDRRN1      HRRN1
|   C                     EXSR RTN191
|   C                     EXSR RTN192
| 02-C                     END
| 01-C                     END
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTN171....READC SFL
      *==============================================================*
03===C           RTN171    BEGSR
|    C*
|    C                     SETON                     54
|    C*
|    C                     READCSFLSR1                 5757*NF
| 01-C           *IN57     DOWEQ'0'                        *40 LOOP
|   C*
|   C                     SETOF                     60    *DOP *IND
| 02-C           *IN99     IFEQ '0'
|   C*
| 03-C           DOPT1     IFNE *BLANK
|   C                     EXSR RTN180                     *CHK
| 04-C           *IN99     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C                     Z-ADDRRN1      DRRN1
|   C                     EXSR RTN300
| 04-C                     END
| 03-C                     END
| 02-C                     END                             *IN99
|   C*
|   C           RRN1      CHAINSFLSR1               40
|   C  N99                MOVE *BLANKS   DOPT1
|   C                     UPDATSFLSR1
|   C                     READCSFLSR1                 5757*NF
|   C*
| 01-C                     END                             *40 END
| 05-C           *IN99     IFEQ '0'
|   C                     SETOF                     5412
|   C                     SETON                     97
| 05-C                     END
|    C*
03===C                     ENDSR
      *==============================================================*
      *          RTN180....CHECK 'SC01' SCREEN
      *==============================================================*
04===CSR         RTN180    BEGSR
|    C*
|    C* CHK AUT *
| 01-C           WA01,WNUM IFNE 'Y'                         WNUM = DOPT
|   C                     SETON                     6099
|   C                     Z-ADDRRN1      DRRN1
| 01-C                     END
|    C*
|    C   99                GOTO END180
|    C*
04===C           END180    ENDSR
      *=========================================================*
      *          RTN191....INITIAL                              *
      *=========================================================*
05===C           RTN191    BEGSR
|    C*
|    C                     MOVE *BLANKS   DOPT1
|    C                     Z-ADD0         DRRN1
|    C                     Z-ADD0         RRN1
|    C                     Z-ADD0         WRRN1   40
|    C                     Z-ADD10        WPRCD1  30
|    C                     Z-ADD0         DSPRC1  40
|    C           HRRN1     DIV  WPRCD1    DSPRC1
|    C                     MULT WPRCD1    DSPRC1
|    C                     MOVE 'N'       WBOTM1  1
|    C                     SETOF                     53
|    C                     SETON                     5254
|    C                     WRITESFLCR1
|    C                     SETOF                     52
|     *
     C           DBGN1     IFEQ 0
     C                     Z-ADD123129    DBGN1
     C                     ENDIF
|     *
|    C                     Z-ADDDBGN1     P3101I
|    C                     CALL 'P31'
|    C                     PARM           P3101I  80
|    C                     PARM '3'       P3102I  1
|    C                     PARM '1'       P3103I  1
|    C                     PARM '2'       P3104I  1
|    C                     PARM '1'       P3105I  1
|    C           KBGN1     PARM           P31110  80
|    C*
|    C           KEYBE     SETLLBE0
|    C*
05===C                     ENDSR
      *=========================================================*
      *          RTN192 .. ROLLUP MAIN ROUTINE
      *=========================================================*
06===C           RTN192    BEGSR
|    C*
| 01-C           WBOTM1    IFEQ 'Y'
|   C                     SETON                     98    *LST PAG
|   C                     Z-ADDRRN1      DRRN1
| 01*C                     ELSE
|   C                     EXSR RTN193
| 01-C                     END
|    C*
06===C                     ENDSR
      *=========================================================*
      *          RTN193 .. ADD SUBFILE DATA
      *=========================================================*
07===C           RTN193    BEGSR
|    C*
|    C                     MOVE *BLANKS   DOPT1
|    C                     SETOF                     54
|    C                     Z-ADDWRRN1     RRN1
|    C                     ADD  WPRCD1    DSPRC1
| 01-C           RRN1      DOWLTDSPRC1
|   C                     READ BE0                 N    46
|   C*
| 02-C           *IN46     IFEQ '0'
|   C*
|   C                     Z-ADDBE01      SBE01H
|   C                     CALL 'P31'
|   C                     PARM BE01      P31I1   80
|   C                     PARM '2'       P3112   1
|   C                     PARM '1'       P31I3   1
|   C                     PARM '3'       P31I4   1
|   C                     PARM '1'       P31I5   1
|   C           SBE01     PARM           P31IO   80
|   C                     MOVELBE03      SBE03
|   C                     MOVELBE06      SBE06
|   C                     MOVEL*BLANK    SMB03
|   C           BE06      CHAINMB0                  40
|   C  N40                MOVELMB03      SMB03
|   C                     MOVELBE02      SBE02
|   C                     MOVEL*BLANK    SME04
|   C           BE02      CHAINME0                  40
|   C  N40                MOVELME04      SME04
|   C*
|   C                     ADD  1         RRN1
|   C                     WRITESFLSR1
| 02*C                     ELSE
|   C                     Z-ADDRRN1      DSPRC1
|   C                     MOVE 'Y'       WBOTM1
|   C                     SETON                     53
| 02-C                     ENDIF
| 01-C                     ENDDO
|    C*
|    C*再此多做預讀一筆的判斷，去判斷是否已到最後一筆
| 03-C           *IN46     IFEQ '0'
|   C                     READ BE0                 N    46
|   C*在將KEY值帶回來
|   C           KEYBE1    SETLLBE0
| 04-C           *IN46     IFEQ '1'
|   C                     Z-ADDRRN1      DSPRC1
|   C                     MOVE 'Y'       WBOTM1
|   C                     SETON                     53
| 04-C                     END
| 03-C                     END
|    C*
|    C                     Z-ADDRRN1      DRRN1      50
|    C                     Z-ADDRRN1      WRRN1
|    C*
07===C                     ENDSR
      *==============================================================*
      *          RTN195... EXFMT SFL                                 *
      *==============================================================*
08===C           RTN195    BEGSR
|    C*
| 01-C           RRN1      IFLE *ZEROS
| 02-C           DBGN1     IFNE 0
|   C                     SETON                     99    *NODATA
|   C                     MOVEL'UPT2050' ERRID
|   C                     MOVEL'PTMF'    ERRF
| 02-C                     END
| 01*C                     ELSE
|   C                     SETON                     50
| 01-C                     END
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR1
|    C                     SETOF                     5051
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
08===C                     ENDSR
     C*==============================================================*
     C*          RTN300....'SC03' SCREEN MAIN ROUTINE --- SFLSR
     C*==============================================================*
09===C           RTN300    BEGSR
|    C*
|    C                     MOVE 'SCR002'  APPSCR
|    C                     MOVE WFUN,WNUM DFUN
|    C*
| 01-C           DOPT1     CASEQ'5'       RTN340           *INQ
| 01-C                     END
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          RTN340....INQ DATA --- SFLSR2
     C*==============================================================*
10===C           RTN340    BEGSR
|    C*
|    C                     SETOF                     323334*NO INPUT
|    C                     EXSR RTN390                     *INT SFLSR
|    C                     EXSR RTN391                     *INT SFLSR
|    C                     EXSR RTN398                     *READ DATA TO SFLSR
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           APPSCR    ANDEQ'SCR002'
|   C*
|   C                     WRITEDSPC2
|   C                     EXFMTSFLCR2
|   C                     MOVEA*ALL'0'   *IN,60
|   C*
| 02-C           *IN03     IFEQ '0'
| 03-C           *IN12     IFEQ '0'
|   C*
| 04-C           *IN99     IFEQ '0'                         SAVE
|   C                     MOVE 'SCR001'  APPSCR
| 04-C                     END
|   C*
| 03*C                     ELSE
|   C                     MOVE 'SCR001'  APPSCR
| 03-C                     END                             *IN12 ENDIF
| 02-C                     END                             *IN03 ENDIF
|   C*
| 01-C                     END                             *ENDDO
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          RTN390....搬值給第2各畫面需要的KEY值
     C*==============================================================*
11===C           RTN390    BEGSR
|    C*
|    C                     MOVELSBE03     DBE03
|    C                     MOVELSBE02     DBE02
|    C                     MOVEL*BLANK    DME04
|    C           SBE02     CHAINME0                  40
|    C  N40                MOVELME04      DME04
|    C                     Z-ADDSBE01     DBE01
|    C                     MOVELSBE06     DBE06
|    C                     MOVEL*BLANK    DMB03
|    C           SBE06     CHAINMB0                  40
|    C  N40                MOVELMB03      DMB03
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          RTN391....INIT 'SC03' -- SFLSR2
     C*==============================================================*
12===C           RTN391    BEGSR
|    C*
|    C                     Z-ADD1         DRRN2
|    C                     Z-ADD0         WRRN2   40
|    C                     Z-ADD0         RRN2
|    C                     Z-ADD999       DSPRC2  40
|    C                     Z-ADD10        WPRCD2  40       *PAG RCD
|    C                     MOVE 'N'       WBOTM2  1        *BOTTOM
|    C*
|    C                     Z-ADD9         @SFTOP  20
|    C                     Z-ADD18        @SFBOT  20
|    C                     Z-ADD1         @LNUM   20
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR2
|    C                     SETOF                     525053
|    C                     SETON                     51    *SFLDSPCTL
|    C*
|    C           KEYBF     SETLLBF0
|    C*
12===C                     ENDSR
     C*==============================================================*
     C*          RTN398....FILE TO SCREEN --- SFLSR(FOR A,U,D,I)
     C*==============================================================*
13===C           RTN398    BEGSR
|     *
|    C                     SETOF                     54
|     *
|    C           KEYBF     READEBF0                 N    44*NF/EOF
| 01-C           *IN44     DOWEQ'0'
|   C                     EXSR RTN39B
|   C                     ADD  1         RRN2
|   C                     WRITESFLSR2
|   C           KEYBF     READEBF0                 N    44*NF/EOF
| 01-C                     END
|    C*
|    C                     SETON                     53
|    C                     Z-ADDRRN2      WRRN2      50
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          RTN39B....FILE TO SCREEN --- MOVE
     C*==============================================================*
14===C           RTN39B    BEGSR
|    C*
|    C                     MOVELBF02      SBF02
|    C                     MOVEL*BLANK    SMA03
|    C           BF02      CHAINMA0                  40
|    C  N40                MOVELMA03      SMA03
|    C                     Z-ADDBF04      SBF04
|    C                     Z-ADDBF03      SBF03
|    C                     Z-ADD0         SBH03
|    C           KEYBG     CHAINBG0                  40
| 01-C           *IN40     IFEQ '0'
|   C           KEYBH     CHAINBH0                  40
| 02-C           *IN40     IFEQ '0'
| 03-C           BH06      IFEQ 0
|   C                     MOVEL'未驗收'STATUS
| 03*C                     ELSE
|   C                     MOVEL'已驗收'STATUS
|   C                     Z-ADDBH03      SBH03
| 03-C                     ENDIF
| 02-C                     ENDIF
| 01-C                     ENDIF
|    C*
14===C                     ENDSR
**
新增修改      刪除查詢
