      ****************************************************************
      *   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
      * PROGRAM NAME : SOR0662                                       *
      * AUTHER       : AN MING HSIA                                  *
      * CREATE DATE  : 81/12/08                                      *
      * UPDATE DATE  :                                               *
      * SYSTEM       :人頭馬匯東                                   *
      * SUBSYSTEM    :銷售訂單系統                                 *
      * REMARK       :客戶銷售明細表                               *
     /*=============================================================*/
     /*  CODE       AUTHOR.     DATE    DESCRIPTION
     /*-------------------------------------------------------------
     /*  M001      MICHELLE   98.12.19  Y2K MODIFY
     F*----------------------------------------------------------------
     F*  M002     | MICHELLE  | 99.11.10 | ADD CUSGRP OPTION
     F*----------------------------------------------------------------
     F*  M003     | MICHELLE  | 00.11.08 |增加大區域選項
     F*----------------------------------------------------------------
     F*  M004     | MICHELLE  | 02.01.30 |增加型態BREAK
     F*----------------------------------------------------------------
     /*=============================================================*/
      ****************************************************************
     FREWF55  IP  E           K        DISK
     FPA#CPF  IF  E           K        DISK
     FMTMAPF  IF  E           K        DISK
     FMTMCPF  IF  E           K        DISK
     FMTMEPF  IF  E           K        DISK
M002AFPA#APF  IF  E           K        DISK
M003AFPA#LPF  IF  E           K        DISK
     FSOR066P O   E             39     PRINTER
     E*
     IWF550
M004MI*                                             WF5502L3
M004MI                                              WF5502L4
M004AI                                              WF5503L3
     I                                              WF5504L2
     I                                              WF5505L2
     I                                              WF5506L1
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$LPRT
     I*辦事處
     I                                      601 601 COMP
     I*產品代號
     I                                      630 638 DSD03S
     I                                      640 648 DSD03E
     I*日期
     I                                      650 6550DSC39S
     I                                      656 6610DSC39E
     I                                      662 6670DSC08S
     I                                      668 6730DSC08E
     I                                      701 706 DSC27S
     I                                      707 712 DSC27E
M003A *大區域別
M003AI                                      730 731 D#L01S
M003AI                                      732 733 D#L01E
     I                                      750 750 IN20
      *產品組別
     I                                      797 797 DMA11
      *禮盒碼
M002AI                                      800 800 DMA54
M002A *集團代號
M002AI                                      810 811 DMD06S
M002AI                                      812 813 DMD06E
      *==============================================================*
      *                    KEY LIST
      *==============================================================*
     C           KEY#A     KLIST
     C                     KFLD           #A01
     C                     KFLD           #A02
     C           KEY#C     KLIST
     C                     KFLD           #C01
     C                     KFLD           #C02
     C           KEYME     KLIST
     C                     KFLD           ME01
     C                     KFLD           ME02
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
  01-C           *IN10     IFEQ '0'
    C                     EXSR RTN010                     *HEADING
    C                     SETON                     10
  01*C                     ELSE
  02-C*M004M     *INL3     IFEQ '1'
M004MC           *INL4     IFEQ '1'
    C                     WRITEPE1
    C                     EXSR RTN010                     *HEADING
    C                     SETOF                     303132
  02-C                     END
  01-C                     END
     C*
     C   39                EXSR RTN900
     C                     EXSR RTN100                     *READE DETAIL
     C*
     CL1 10                EXSR RTN200
     CL2 10                EXSR RTN300
     CL3 10                EXSR RTN500
M004MC*L3 10                EXSR RTN400
M004MCL4 10                EXSR RTN400
     CLR                   EXSR RTNLR
      *==============================================================*
      *          RTN010....CHECK HEADING
      *==============================================================*
01===C           RTN010    BEGSR
|    C*
| 03-C           IN20      IFEQ '0'
|   C                     SETOF                     20
| 03*C                     ELSE
|   C                     SETON                     20
| 03-C                     END
|    C*產品組別
     C                     MOVEL*BLANKS   GROUP
| 02-C           DMA11     IFEQ '1'
|   C                     MOVEL'白蘭地'GROUP
| 02*C                     ELSE
| 03-C           DMA11     IFEQ '2'
|   C                     MOVEL'威士忌'GROUP
| 03*C                     ELSE
| 04-C           DMA11     IFEQ '3'
|   C                     MOVEL'葡萄酒'GROUP
| 04*C                     ELSE
|   C                     MOVEL'全　部'GROUP
| 04-C                     END
| 03-C                     END
| 02-C                     END
|    C*禮盒碼
     C                     MOVEL*BLANKS   LMA54
| 02-C           DMA54     IFEQ 'A'
|   C                     MOVEL'全　部'LMA54
| 02*C                     ELSE
| 03-C           DMA54     IFEQ 'B'
|   C                     MOVEL'非禮盒'LMA54
| 03*C                     ELSE
| 04-C           DMA54     IFEQ 'C'
|   C                     MOVEL'禮　盒'LMA54
| 04-C                     END
| 03-C                     END
| 02-C                     END
|    C*集團代號
|    C                     MOVEL'CUSTGRP' #A01
|    C                     MOVELDMD06S    #A02
|    C           KEY#A     CHAIN#A0                  40
| 01-C           *IN40     IFEQ '0'
M002AC                     MOVEL#A03      D#A03S
     C                     ELSE
|   C                     MOVEL*BLANKS   D#A03S
| 01-C                     END
M002AC                     MOVEL'CUSTGRP' #A01
M002AC                     MOVELDMD06E    #A02
M002AC           KEY#A     CHAIN#A0                  42
M002AC           *IN42     IFEQ '0'
M002AC                     MOVEL#A03      D#A03E
M002AC                     ELSE
M002AC                     MOVE *BLANKS   D#A03E
M002AC                     END
|    C*
M003AC           D#L01S    CHAIN#L0                  50
M003AC           *IN50     IFEQ '0'
M003AC                     MOVEL#L02      D#L02S
M003AC                     ELSE
M003AC                     MOVE *BLANKS   D#L02S
M003AC                     END
     C*
M003AC           D#L01E    CHAIN#L0                  51
M003AC           *IN51     IFEQ '0'
M003AC                     MOVEL#L02      D#L02E
M003AC                     ELSE
M003AC                     MOVE *BLANKS   D#L02E
M003AC                     END
|    C                     WRITEPH1
|    C*
01===C                     ENDSR
      *==============================================================*
      *          RTN100....READE DETAIL
      *==============================================================*
02===C           RTN100    BEGSR
|    C*
|    C*型態
|    C                     MOVELWF5502    #C01
|    C                     MOVELWF5503    #C02
|    C           KEY#C     CHAIN#C0                  40
| 02-C           *IN40     IFEQ '1'
|   C*M004M               MOVE *BLANKS   #C02
M004MC                     MOVE *BLANKS   #C03
| 02-C                     END
|    C*
|    C*客戶
|    C                     MOVELWF5504    ME01
|    C                     MOVELWF5505    ME02
|    C           KEYME     CHAINME0                  40
| 03-C           *IN40     IFEQ '1'
|   C                     MOVE *BLANKS   ME04
| 03-C                     END
|    C*產品
|    C           WF5506    CHAINMA0                  40
| 04-C           *IN40     IFEQ '1'
|   C                     MOVEL*BLANKS   MA03
| 04-C                     END
|    C*業務員
|    C           WF5513    CHAINMC0                  40
| 05-C           *IN40     IFEQ '1'
|   C                     MOVE *BLANKS   MC02
| 05-C                     END
|    C*日期轉換
M001AC                     Z-ADDWF5507    W6      60
|    C                     CALL 'RES001'
|    C                     PARM 'YMD'     S001I1  3
B2MODC*M001M               PARM WF5507    S001I2  80
M001MC                     PARM W6        S001I2  60
|    C           PSC07     PARM           S001O1  60
|    C*
M001AC                     Z-ADDWF5516    W6      60
|    C                     CALL 'RES001'
|    C                     PARM 'YMD'     S001I1  3
B2MODC*M001M               PARM WF5516    S001I2  80
M001MC                     PARM W6        S001I2  60
|    C           PSC08     PARM           S001O1  60
|    C*
M001AC                     Z-ADDWF5514    W6      60
|    C                     CALL 'RES001'
|    C                     PARM 'YMD'     S001I1  3
B2MODC*M001M               PARM WF5514    S001I2  80
M001MC                     PARM W6        S001I2  60
|    C           PSC39     PARM           S001O1  60
|    C*
|    C   39                EXSR RTN900
|    C                     WRITEPD1
|    C                     SETOF                     21
|    C                     SETON                     303132
|    C                     ADD  WF5509    T15509
|    C                     ADD  WF5510    T15510
|    C                     ADD  WF5512    T15512
|    C                     ADD  WF5515    T15515
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTN200....WRITE PT1   SUBTOTAL
      *==============================================================*
03===C           RTN200    BEGSR
|    C*
|    C                     WRITEPT1
|    C                     SETOF                     32
|    C                     SETON                     21
|    C                     ADD  T15509    T25509
|    C                     ADD  T15510    T25510
|    C                     ADD  T15512    T25512
|    C                     ADD  T15515    T25515
|    C                     Z-ADD0         T15509
|    C                     Z-ADD0         T15510
|    C                     Z-ADD0         T15512
|    C                     Z-ADD0         T15515
|    C*
03===C                     ENDSR
      *==============================================================*
      *          RTN300....WRITE PT2   SUBTOTAL
      *==============================================================*
04===C           RTN300    BEGSR
|    C*
|    C   39                EXSR RTN900
|    C                     WRITEPT2
|    C                     SETOF                     31
|    C                     ADD  T25509    T35509
|    C                     ADD  T25510    T35510
|    C                     ADD  T25512    T35512
|    C                     ADD  T25515    T35515
|    C                     Z-ADD0         T25509
|    C                     Z-ADD0         T25510
|    C                     Z-ADD0         T25512
|    C                     Z-ADD0         T25515
|    C*
04===C                     ENDSR
      *==============================================================*
      *          RTN400....WRITE PT3   SUBTOTAL
      *==============================================================*
05===C           RTN400    BEGSR
|    C*
|    C   39                EXSR RTN900
|    C                     WRITEPT3
|    C                     SETOF                     30
|    C                     Z-ADD0         T35509
|    C                     Z-ADD0         T35510
|    C                     Z-ADD0         T35512
|    C                     Z-ADD0         T35515
|    C*
05===C                     ENDSR
      *==============================================================*
      *          RTN500....
      *==============================================================*
04===C           RTN500    BEGSR
|    C*
M004AC           *INL3     IFEQ '1'
M004AC                     SETOF                     30
M004AC                     END
|    C*
04===C                     ENDSR
      *==============================================================*
      *          RTN900....OVERFLOW
      *==============================================================*
06===C           RTN900    BEGSR
|    C*
|    C                     WRITEPE1
|    C                     WRITEPH1
|    C                     SETOF                     39
|    C                     SETOF                     303132
|    C                     SETOF                     21
|    C*
06===C                     ENDSR
      *==============================================================*
      *          RTNLR  ...LR
      *==============================================================*
09===C           RTNLR     BEGSR
|    C*
| 01-C           *IN10     IFEQ '0'
|   C                     WRITEPH1
|   C                     WRITEPE9
| 01-C                     END
|    C                     WRITEPE2
|    C*
09===C                     ENDSR
