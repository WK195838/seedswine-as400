      ****************************************************************
      *   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
      *                                                              *
      * PROGRAM NAME : IMR066                                        *
      * AUTHER       : AN MING HSIA                                  *
      * CREATE DATE  : 82/03/15                                      *
      * UPDATE DATE  :                                               *
      * SYSTEM       :人頭馬匯東                                   *
      * SUBSYSTEM    :庫存資料子系統                               *
      * REMARK       :產品調撥明細／彙總表                         *
     /*=============================================================*/
     /* CODE   AUTHOR.     DATE    DESCRIPTION                   */
     /*-------------------------------------------------------------*/
     /* M001  MICHELLE   99.01.04  Y2K MODIFY                    */
     /*=============================================================*/
      ****************************************************************
     FIMR066D CF  E                    WORKSTN
     F                                              KINFDS DSPFDS
     I*
     ISDS        SDS
     I                                     *PROGRAM APPPGM
     ILDA        UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$PRTID
     I*產品代號，出貨物流點，收貨
     I                                      601 609 DIJ03S
     I                                      611 619 DIJ03E
     I                                      620 624 DIJ07S
     I                                      625 629 DIJ07E
     I                                      630 634 DIJ08S
     I                                      635 639 DIJ08E
     I*調撥日期(MM/DD/YY)
     I                                      640 6450DII05S
     I                                      646 6510DII05E
M001MI*                                     652 6570DATES
M001MI*                                     658 6630DATEE
M001MI                                      681 6880DATES
M001MI                                      691 6980DATEE
     I                                      664 6690DII06S
     I                                      670 6750DII06E
     I                                      676 676 DSORT
     I                                      677 677 DSEL
     I                                      678 678 DYES
     I                                      750 750 IN20
     I*
     I* FOR F4 HELP -- GET CURSOR POSITION
     IDSPFDS      DS
     I                                    B 370 3710#CSR
      *==============================================================*
      *          IDENTIFY A PARAMETER LIST
      *==============================================================*
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C           *IN98     PARM *IN98     IN98    1
     C           *IN97     PARM *IN97     IN97    1
      *==============================================================*
      *                    KLIST
      *==============================================================*
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C  N97N98             EXSR RTN010                     .INIT SCREEN
  01-C           *IN03     DOUEQ'1'
    C           *IN99     OREQ '0'
    C                     EXFMTDSPD1
    C                     MOVEA*ALL'0'   *IN,60
    C                     Z-ADD0         D#ROW
    C                     Z-ADD0         D#COL
  02-C           *IN03     IFEQ '0'
  03-C           *IN04     IFEQ '1'
    C**********************
    C*  F4 PROMPT PROCESS *
    C**********************
    C                     EXSR RTNF4
  03*C                     ELSE
    C                     EXSR RTN100                     .CHECK      ?
  03-C                     END
  02-C                     END
  01-C                     END
     C*
     C                     EXSR OUTLDA
     C*
     C   03                SETON                     LR
     C                     RETRN
      *===============================================================*
      *          RTN010....INIT SCREEN
      *===============================================================*
01===C           RTN010    BEGSR
|    C*
|    C           *NAMVAR   DEFN *LDA      LDA
|    C                     IN   LDA
|    C                     Z-ADD2         $EVR
|    C                     MOVEL$USER     AUT     3
|    C           AUT       IFNE 'D91'
|    C           AUT       ANDNE'REP'
|    C                     SETON                     31
|    C                     END
|    C                     MOVE *BLANK    DIJ03S
|    C                     MOVE *BLANK    DIJ03E
|    C                     MOVE *BLANK    DIJ07S
|    C                     MOVE *BLANK    DIJ07E
|    C                     MOVE *BLANK    DIJ08S
|    C                     MOVE *BLANK    DIJ08E
|    C                     Z-ADD0         DII05S
|    C                     Z-ADD0         DII05E
|    C                     Z-ADD0         DII06S
|    C                     Z-ADD0         DII06E
|    C                     Z-ADD0         DATES
|    C                     Z-ADD0         DATEE
M001AC                     Z-ADD0         W6      60
|    C                     MOVE 'A'       DSEL
|    C                     MOVE 'Y'       DYES
|    C                     MOVE 'A'       DSORT
|    C                     MOVEL'SCR001'  APPSCR  6
|    C*
01===C                     ENDSR
      *===============================================================*
      *          RTN100....CHECK SCREEN
      *===============================================================*
02===C           RTN100    BEGSR
|    C*產品代號
| 01-C           DIJ03S    IFEQ *BLANKS
|   C                     MOVE *LOVAL    DIJ03S
| 01-C                     END
|    C*
| 02-C           DIJ03E    IFEQ *BLANKS
|   C                     MOVE *HIVAL    DIJ03E
| 02-C                     END
|    C*
| 03-C           DIJ03S    IFGT DIJ03E
|   C                     SETON                     6061
|   C                     SETON                     9399
| 03-C                     END
|    C   99                GOTO END100
|    C*出貨物流點
| 04-C           DIJ07S    IFEQ *BLANK
|   C                     MOVE *LOVAL    DIJ07S
| 04-C                     END
|    C*
| 05-C           DIJ07E    IFEQ *BLANK
|   C                     MOVE *HIVAL    DIJ07E
| 05-C                     END
|    C*
| 06-C           DIJ07S    IFGT DIJ07E
|   C                     SETON                     6263
|   C                     SETON                     9399
| 06-C                     END
|    C   99                GOTO END100
|    C*收貨物流點
| 07-C           DIJ08S    IFEQ *BLANK
|   C                     MOVE *LOVAL    DIJ08S
| 07-C                     END
|    C*
| 08-C           DIJ08E    IFEQ *BLANK
|   C                     MOVE *HIVAL    DIJ08E
| 08-C                     END
|    C*
| 09-C           DIJ08S    IFGT DIJ08E
|   C                     SETON                     6465
|   C                     SETON                     9399
| 09-C                     END
|    C   99                GOTO END100
|    C*
|    C*調撥日期或入庫日期只輸入一項
| 10-C           DII05S    IFNE 0
|   C           DII05E    ORNE 0
| 11-C           DII06S    IFNE 0
|   C                     SETON                     688999
| 11-C                     END
| 12-C           DII06E    IFNE 0
|   C                     SETON                     798999
| 12-C                     END
| 10-C                     END
|    C   99                GOTO END100
|    C*
|    C* CHK銷售日期
| 13-C           DII05S    IFNE 0
|   C                     CALL 'P09'
|   C                     PARM DII05S    P0901I  60
|   C                     PARM           P0901O  1
| 14-C           P0901O    IFEQ 'N'
|   C                     SETON                     669399
| 14-C                     END
|   C   99                GOTO END100
|   C*
|   C                     CALL 'P09'
|   C                     PARM DII05E    P0901I  60
|   C                     PARM           P0901O  1
| 15-C           P0901O    IFEQ 'N'
|   C                     SETON                     679399
| 15-C                     END
|   C   99                GOTO END100
|   C*日期轉換
|   C                     CALL 'RES001'
|   C                     PARM 'MDY'     S001I1  3
|   C                     PARM DII05S    S001I2  60
|   C*M001M     DATES     PARM           S001O1  60
M001MC           W6        PARM           S001O1  60
M001AC                     MOVE W6        HB2@F6  6
M001AC                     EXSR HB@68L                     + CENTURY 6->8 LEFT
M001AC                     MOVE HB2@T8    DATES
|   C*
|   C                     CALL 'RES001'
|   C                     PARM 'MDY'     S001I1  3
|   C                     PARM DII05E    S001I2  60
|   C*M001M     DATEE     PARM           S001O1  60
M001MC           W6        PARM           S001O1  60
M001AC                     MOVE W6        HB2@F6  6
M001AC                     EXSR HB@68L                     + CENTURY 6->8 LEFT
M001AC                     MOVE HB2@T8    DATEE
|   C**
| 16-C           DATES     IFGT DATEE
|   C                     SETON                     6667
|   C                     SETON                     9299
| 16-C                     END
|   C   99                GOTO END100
|   C                     MOVEL'0'       IN20
| 13*C                     ELSE
|   C                     CALL 'P09'
|   C                     PARM DII06S    P0901I  60
|   C                     PARM           P0901O  1
| 17-C           P0901O    IFEQ 'N'
|   C                     SETON                     689399
| 17-C                     END
|   C   99                GOTO END100
|   C*
|   C                     CALL 'P09'
|   C                     PARM DII06E    P0901I  60
|   C                     PARM           P0901O  1
| 18-C           P0901O    IFEQ 'N'
|   C                     SETON                     699399
| 18-C                     END
|   C   99                GOTO END100
|   C*日期轉換
|   C                     CALL 'RES001'
|   C                     PARM 'MDY'     S001I1  3
|   C                     PARM DII06S    S001I2  60
|   C*M001M     DATES     PARM           S001O1  60
M001MC           W6        PARM           S001O1  60
M001AC                     MOVE W6        HB2@F6  6
M001AC                     EXSR HB@68L                     + CENTURY 6->8 LEFT
M001AC                     MOVE HB2@T8    DATES
|   C*
|   C                     CALL 'RES001'
|   C                     PARM 'MDY'     S001I1  3
|   C                     PARM DII06E    S001I2  60
|   C*M001M     DATEE     PARM           S001O1  60
M001MC           W6        PARM           S001O1  60
M001AC                     MOVE W6        HB2@F6  6
M001AC                     EXSR HB@68L                     + CENTURY 6->8 LEFT
M001AC                     MOVE HB2@T8    DATEE
|   C**
| 19-C           DATES     IFGT DATEE
|   C                     SETON                     6869
|   C                     SETON                     9299
| 19-C                     END
|   C   99                GOTO END100
|   C                     MOVEL'1'       IN20
| 13-C                     END
|    C*
02===C           END100    ENDSR
      *===============================================================*
      *          RTNF4 ....F4 HELP
      *===============================================================*
03===C           RTNF4     BEGSR
|    C*
|    C                     SETON                     99
|    C********************
|    C*  GET ROW,COL     *
|    C********************
|    C           #CSR      DIV  256       D#ROW            *ROW
|    C                     MVR            D#COL            *COL
|    C** 游標位置即為(D#ROW,D#COL)
| 01-C           APPSCR    IFEQ 'SCR001'
|   C*
| 02-C           D#ROW     IFEQ 4
| 03-C           D#COL     IFGE 36
|   C           D#COL     ANDLE44
|   C                     CALL 'WDS001'
|   C                     PARM           DIJ03S
|   C                     GOTO ENDF4
| 03-C                     END
|   C*
| 04-C           D#COL     IFGE 48
|   C           D#COL     ANDLE56
|   C                     CALL 'WDS001'
|   C                     PARM           DIJ03E
|   C                     GOTO ENDF4
| 04-C                     END
| 02-C                     END
|   C*
| 05-C           D#ROW     IFEQ 18
|   C           D#COL     ANDGE38
|   C           D#COL     ANDLE39
|   C                     CALL 'WDS011'
|   C           $PRTID    PARM $PRTID    S011O1  20
|   C                     GOTO ENDF4
| 05-C                     END
|   C*
| 01-C                     END
|    C*
03===C           ENDF4     ENDSR
      *===============================================================*
      *          OUTLDA....OUT LDA
      *===============================================================*
04===C           OUTLDA    BEGSR
|    C*
|    C                     OUT  *NAMVAR
|    C*
04===C           ENDLDA    ENDSR
M001AC/COPY HBP2CVTR
