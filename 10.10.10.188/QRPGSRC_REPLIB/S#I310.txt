     F****************************************************************
     F*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     F*                                                              *
     F*    PROGRAM NAME : S#I310                                     *
     F*    AUTHOR       : A1062   JEANNY LIN                         *
     F*    CREATE DATE  : 79/07/02                                   *
     F*    UPDATE DATE  :                                            *
     F*    SYSTEM       :程式安全管制系統                          *
     F*    PURPOSE      :程式基本資料全頁查詢                      *
     F****************************************************************
     FS#SPPF  IF  E           K        DISK
     FS#SUPF  IF  E           K        DISK
     FS#SDPF  IF  E           K        DISK
     FS#I310D CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
     E                    A03     3   3  8               *PROGRAM TYPE
     IS#DA01      DS
     I                                        1  10 CENTER
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
     C*==============================================================*
     C*                    LIST PARAMETERS
     C*==============================================================*
     C           *ENTRY    PLIST
     C           DSP01     PARM DSP01     S1101I 10
     C           *IN03     PARM *IN03     S1101O  1
     C*==============================================================*
     C*                    KEY LIST FOR SDPF
     C*==============================================================*
     C           KEYSD     KLIST
     C                     KFLD           WSD01  10
     C                     KFLD           WSD02  10
     C*=============================================================*
     C*                    MAIN ROUTINE
     C*=============================================================*
     C                     EXSR RTN010
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
     C*
     C           *IN02     IFEQ '0'
     C           *IN25     ANDEQ'0'
     C           *IN26     ANDEQ'0'
     C                     EXSR RTN100                     *INIT RTN.
     C                     EXSR RTN200                     *ADD SFL
     C                     END
     C*
     C           *IN25     IFEQ '1'                        *25 ON
     C                     SETON                     97    *LAST PAGE
     C                     Z-ADDRRN       DRRN
     C                     END
     C*
     C           *IN26     IFEQ '1'                        *26 ON
     C                     SETON                     98    *FIRST PAGE
     C                     Z-ADD1         DRRN
     C                     END                             *26 OFF
     C*
     C                     EXSR RTN300                     *EXFMT
     C   02                EXSR RTN400
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
     C*=============================================================*
     C*          RTN010....INITIAL ROUTINE
     C*=============================================================*
     C           RTN010    BEGSR
     C           *NAMVAR   DEFN           S#DA01
     C                     IN   *NAMVAR
     C*
     C           USER      CHAINSU0                  40    *NF
     C   40                MOVE *BLANK    DUSER
     C  N40                MOVE SU02      DUSER
     C                     MOVE USER      WSD01
     C                     SETOF                     03
     C                     ENDSR
     C*=============================================================*
     C*          RTN100....INITIAL SFL
     C*=============================================================*
     C           RTN100    BEGSR
     C*INITIAL VALUES
     C                     Z-ADD*ZEROS    RRN
     C                     Z-ADD*ZEROS    DRRN
     C*SFL CLEAR
     C                     SETON                     52
     C                     WRITESFLCR
     C                     SETOF                     5253
     C*SETLL DATA
     C           DSP01     SETLLSP0
     C                     ENDSR
     C*=============================================================*
     C*          RTN200....ADD SUBFILE DATA
     C*=============================================================*
     C           RTN200    BEGSR
     C           *IN53     DOWEQ'0'                        *53
     C                     READ SP0                      46*NF
     C*
     C           *IN46     IFEQ '0'                        *N46
     C                     SETOF                     30    *CAN DISPLAY
     C*
     C           USER      IFNE CENTER                     *CENTER
     C                     MOVE SP01      WSD02
     C           KEYSD     CHAINSD0                  40    *NF
     C   40                SETON                     30
     C                     END                             *CENTER
     C*
     C           *IN30     IFEQ '0'                        *30
     C           SP03      IFEQ 'M'                        *M
     C                     MOVE A03,1     DSP03
     C                     ELSE
     C           SP03      IFEQ 'P'                        *P
     C                     MOVE A03,2     DSP03
     C                     ELSE                            *S
     C                     MOVE A03,3     DSP03
     C                     END                             *P
     C                     END                             *M
     C                     ADD  1         RRN
     C                     WRITESFLSR
     C                     END                             *30
     C*
     C                     ELSE                            *46
     C                     SETON                     53
     C                     END                             *46
     C*
     C                     END                             *53 END
     C                     Z-ADD1         DRRN
     C                     ENDSR
     C*=============================================================*
     C*          RTN300....EXFMT DSPF
     C*=============================================================*
     C           RTN300    BEGSR
     C           RRN       IFGT *ZEROS
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     96    *NO DATA
     C                     END
     C*
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC1
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C                     SETOF                     5051
     C                     ENDSR
     C*=============================================================*
     C*          RTN400....SELECT SEQUENCE NUMBER
     C*=============================================================*
     C           RTN400    BEGSR
     C*
     C           DSLT      IFEQ 0
     C                     SETON                     609599
     C                     END
     C   99                GOTO END400
     C*
     C           DSLT      CHAINSFLSR                40    *NF
     C   40                SETON                     609499
     C   99                GOTO END400
     C*
     C                     MOVE SP01      DSP01
     C                     SETON                     12
     C*
     C           END400    ENDSR
**
主畫面主程式副程式
