     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    PROGRAM ID   : POA007                                     *
     A*    AUTHOR       : A1034  STEPHANIE                           *
     A*    DATE WRITTEN : 81/01/18                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購資料建檔子系統                        *
     A*    REMARK       :採購備註資料維護
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.09.11 | PJYY = TIME
     A*---------------------------------------------------------------
     A* M002      | 99.01.07 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     FPOPJPF  UF  E           K        DISK                      A
     F                                              KINFSR ERRSR
     FPOPJLF  IF  E           K        DISK
     F            PJ0                               KRENAMEPJ0L
     FPOA007D CF  E                    WORKSTN
     F                                              KINFDS DSPFDS
     F                                        RRN1  KSFILE SFLSR1
     F                                        RRN2  KSFILE SFLSR2
     E*: FUNCTION DATA :
     E                    WFUN    5   5  6
     E*: A/U/D/I AUT
     E                    WA01        5  1
     I*
     ISDS        SDS
     I                                     *PROGRAM APPPGM
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      152 161 $USERN
     I                                      111 1160$CHYMD
 @   I                                      135 135 $ADD
 @   I                                      136 136 $UPD
 @   I                                      137 137 $DLT
 @   I                                      138 138 $INQ
 @   I                                      139 139 $RMK01
M002AI                                      201 2080EG8
 @   I*                                     140 140 $RMK02
 @   I*                                     141 141 $RMK03
     I*                                     162 1620$EVR
     I*                                     125 1270$CPY
     I*                                     598 5990$LPRT
     I*                                     501 505 $WRHUS
     I*                                     506 506 $PURID
     I*                                     507 508 $TRNID
     I*                                     509 513 $APAUT
     I*                                     514 521 $#101
     I            DS
     I                                        1   1 DOPT1
     I                                        1   10WNUM
     I* FOR F4 HELP -- GET CURSOR POSITION
     IDSPFDS      DS
     I                                    B 370 3710#CSR
      *==============================================================*
      *                    DEFINE ROUTINE
      *==============================================================*
     C                     MOVE *BLANKS   APPSCR  6
     C                     MOVE *BLANK    WQUERY  1
     C           *LIKE     DEFN PJ01      OBGN1            *START KEY
      *  FOR SUBFILE (SYS+PGM CONTROL)
     C           *LIKE     DEFN PJ01      BPJ01            *SUBFIL BUTTOM
     C*
      *==============================================================*
      *                    KLIST
      *==============================================================*
     C           KEYPJ1    KLIST
     C                     KFLD           DBGN1
     C           KEYPJ2    KLIST
     C                     KFLD           BPJ01
     C           KEYPJ4    KLIST
     C                     KFLD           PJ01
     C           KEYPJ     KLIST
     C                     KFLD           DPJ01
     C                     KFLD           DPJ02
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C                     EXSR RTN010                     *INIT ENVIRMENT
     C                     EXSR RTN191                     *INIT SFL
      *
     C                     EXSR RTN192                     *READ NEXT PAGE
     C                     EXSR RTN195                     *EXFMT
     C*
  01-C           *IN03     DOWEQ'0'                        *03    OFF
    C*
  02-C           *IN06     CASEQ'1'       RTN110
    C           *IN25     CASEQ'1'       RTN192
    C                     CAS            RTN170
  02-C                     END
    C  N03                EXSR RTN195
    C*
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE
     C*==============================================================*
01===C           RTN010    BEGSR
|    C*
|    C                     MOVE $ADD      WA01,1
|    C                     MOVE $UPD      WA01,2
|    C                     MOVE $DLT      WA01,4
|    C                     MOVE $INQ      WA01,5
|    C                     MOVE 'SCR001'  APPSCR
|    C*
01===C                     ENDSR
      *==============================================================*
      *          RTN110....F6(ADD)
      *==============================================================*
02===C           RTN110    BEGSR
|    C*
|    C                     MOVE '1'       DOPT1
| 01-C           WA01,WNUM IFNE 'Y'                         WNUM = DOPT
|   C                     SETON                     9099
     C                     ELSE
|    C                     EXSR RTN200
|    C                     EXSR RTN191
|    C                     EXSR RTN192
| 01-C                     END
|    C                     SETOF                     12
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTN170....ENTER
      *==============================================================*
03===C           RTN170    BEGSR
|    C*
| 01-C           DBGN1     IFNE OBGN1
|   C                     EXSR RTN191
|   C                     EXSR RTN192
|   C                     MOVE DBGN1     OBGN1
| 01*C                     ELSE
| 02-C           WRRN1     IFGT 0
|   C                     EXSR RTN171
| 03-C           *IN99     IFEQ '0'
|   C                     EXSR RTN191
|   C                     EXSR RTN192
| 03-C                     END
| 02-C                     END
| 01-C                     END
|    C*
03===C                     ENDSR
      *==============================================================*
      *          RTN171....READC SFL
      *==============================================================*
04===C           RTN171    BEGSR
|    C*
|    C                     SETON                     54
|    C*
|    C                     READCSFLSR1                 5757*NF
| 01-C           *IN57     DOWEQ'0'                        *40 LOOP
|   C*
|   C                     SETOF                     60    *DOP *IND
| 02-C           *IN99     IFEQ '0'
|   C*
| 03-C           DOPT1     IFNE *BLANK
|   C                     EXSR RTN180                     *CHK
| 04-C           *IN99     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C                     Z-ADDRRN1      DRRN1
|   C                     EXSR RTN200
|   C                     MOVE *BLANKS   DOPT1
| 04-C                     END
| 03-C                     END
| 02-C                     END                             *IN99
|   C           RRN1      CHAINSFLSR1               40
|   C  N99                MOVE *BLANKS   DOPT1
|   C                     UPDATSFLSR1
|   C                     READCSFLSR1                   57*NF
|   C*
| 01-C                     END                             *40 END
| 05-C           *IN99     IFEQ '0'
|   C                     SETOF                     5412
|   C                     SETON                     97
| 05-C                     END
|    C*
04===C                     ENDSR
      *==============================================================*
      *          RTN180....CHECK 'SC01' SCREEN
      *==============================================================*
05===CSR         RTN180    BEGSR
|    C*
|    C* CHK AUT *
| 01-C           WA01,WNUM IFNE 'Y'                         WNUM = DOPT
|   C                     SETON                     609099
| 01-C                     END
|    C   99                GOTO END180
|    C*
05===C           END180    ENDSR
      *=========================================================*
      *          RTN191....INITIAL                              *
      *=========================================================*
06===C           RTN191    BEGSR
|    C*
|    C                     MOVE *BLANKS   DOPT1
|    C                     Z-ADD0         DRRN1
|    C                     Z-ADD0         RRN1
|    C                     Z-ADD0         WRRN1   40
|    C                     Z-ADD10        WPRCD1  30
|    C                     Z-ADD0         DSPRC1  40
|    C                     MOVE 'N'       WBOTM1  1
|    C                     SETOF                     53
|    C                     SETON                     5254
|    C                     WRITESFLCR1
|    C                     SETOF                     52
|    C           KEYPJ1    SETLLPJ0L
|    C                     MOVE DBGN1     BPJ01
|    C*
06===C                     ENDSR
      *=========================================================*
      *          RTN192 .. ROLLUP MAIN ROUTINE
      *=========================================================*
07===C           RTN192    BEGSR
|    C*
| 01-C           WBOTM1    IFEQ 'Y'
|   C                     SETON                     98    *LST PAG
|   C                     Z-ADDRRN1      DRRN1
| 01*C                     ELSE
| 02-C           BPJ01     IFNE DBGN1
|   C           KEYPJ2    SETGTPJ0L
| 02-C                     END
|   C                     EXSR RTN193
|   C                     MOVE PJ01      BPJ01
|   C           *LOVAL    SETLLPJ0L
| 01-C                     END
|    C*
07===C                     ENDSR
      *=========================================================*
      *          RTN193 .. ADD SUBFILE DATA
      *=========================================================*
08===C           RTN193    BEGSR
|    C*
|    C                     MOVE *BLANKS   DOPT1
|    C                     SETOF                     54
|    C                     Z-ADDWRRN1     RRN1
|    C                     ADD  WPRCD1    DSPRC1
| 01-C           RRN1      DOWLTDSPRC1
|   C                     READ PJ0L                     46
|   C*
| 02-C           *IN46     IFEQ '0'
|   C                     ADD  1         RRN1
|   C                     WRITESFLSR1
|   C           KEYPJ4    SETGTPJ0L
| 02*C                     ELSE
|   C                     Z-ADDRRN1      DSPRC1
|   C                     MOVE 'Y'       WBOTM1
|   C                     SETON                     53
| 02-C                     END
|   C*
| 01-C                     END
|    C*
|    C                     Z-ADDRRN1      DRRN1      50
|    C                     Z-ADDRRN1      WRRN1
|    C*
08===C                     ENDSR
      *==============================================================*
      *          RTN195... EXFMT SFL                                 *
      *==============================================================*
09===C           RTN195    BEGSR
|    C*
| 01-C           RRN1      IFLE *ZEROS
|   C                     SETON                     96    *NODATA
| 01*C                     ELSE
|   C                     SETON                     50
| 01-C                     END
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR1
|    C                     SETOF                     5051
|    C                     MOVEA*ALL'0'   *IN,60
|    C*
09===C                     ENDSR
      *==============================================================*
      *          RTN200....'SC02' SCREEN MAIN ROUTINE
      *==============================================================*
10===C           RTN200    BEGSR
|    C*
|    C                     EXSR RTN205                     *INIT
|    C*
| 01-C           *IN03     DOWEQ'0'                        *CTL LOOP
|   C           APPSCR    ANDEQ'SCR002'
|   C*
|   C                     WRITEDSPC2
|   C                     EXFMTSFLCR2
|   C                     MOVEA*ALL'0'   *IN,60
|   C*
| 02-C           *IN03     IFEQ '0'
|   C           *IN12     ANDEQ'0'
|   C                     EXSR RTN210                      *POST ENTER
| 02*C                     ELSE
|   C                     MOVE 'SCR001'  APPSCR
| 02-C                     END
|   C*
| 01-C                     END
|    C*
10===C                     ENDSR
      *==============================================================*
      *          RTN205....INIT SFL
      *==============================================================*
11===C           RTN205    BEGSR
|    C*
|    C                     MOVE 'SCR002'  APPSCR
|    C                     MOVE WFUN,WNUM DFUN
|    C*INIT SFL
|    C                     Z-ADD0         DRRN2
|    C                     Z-ADD0         RRN2
|    C                     SETOF                     53    *EOF
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     WRITESFLCR2
|    C                     SETOF                     52
|    C                     SETON                     51    *SFLDSPCTL
|    C*SET HEAD DATA
| 01-C           DOPT1     IFEQ '1'
|   C                     MOVE *BLANKS   DPJ01
|   C                     SETON                     31    *UL
| 01*C                     ELSE
|   C                     MOVE PJ01      DPJ01
|   C                     SETOF                     31    *PR
| 01-C                     END
|    C*WRITE TO SFL
|    C                     MOVE *BLANKS   DOPT2
|    C*-------------------------------------------*
|    C*  FROM FILE TO SCREEN
|    C*-------------------------------------------*
|    C*  新增或修改時-> SETON 33 FOR (UL)
|    C           DOPT1     COMP '2'                    3333*DATA (UL)
|    C                     SETOF                     54    *SFLNXTCHG
|    C                     SETOF                     32    *KEY   (PR)
|    C                     MOVE 'Y'       DCHK             *WILL UPD
|    C           DPJ01     SETLLPJ0
|    C           DPJ01     READEPJ0                      44
| 02-C           *IN44     DOWEQ'0'
|   C**START : MOVE DATA TO SFL
|   C                     Z-ADDPJ02      DPJ02
|   C                     MOVE PJ03      DPJ03
|   C**END   : MOVE DATA TO SFL
|   C                     ADD  1         RRN2
|   C                     WRITESFLSR2
|   C           DPJ01     READEPJ0                      44
| 02-C                     END
|    C*
|    C*-------------------------------------------*
|    C*  INIT SCREEN
|    C*-------------------------------------------*
|    C*  新增或修改時-> SETON 32 FOR (UL) -> DO
|    C           DOPT1     COMP '2'                    3232*DATA (UL)
|    C*
| 03-C           *IN32     IFEQ '1'
|   C           RRN2      ANDLT15
|   C                     SETOF                     54    *SFLNXTCHG
|   C                     MOVE *BLANKS   DCHK             *WILL WRT
|   C**INIT FIELD
|   C                     Z-ADD0         DPJ02
|   C                     MOVE *BLANKS   DPJ03
|   C*
|   C           RRN2      ADD  1         WRRN2   40
| 04-C           WRRN2     DO   15        RRN2
|   C                     WRITESFLSR2
| 04-C                     END
|   C*
| 03-C                     END
|    C*
|    C                     SETON                     51     *SFLDSPCTL
|    C                     SETON                     53     *SFLEND
|    C           RRN2      COMP 0                    50     *SFLDSP
|    C                     Z-ADD1         DRRN2
11===C                     ENDSR
      *==============================================================*
      *          RTN210....'SCR002' POST-ENTER
      *==============================================================*
12===C           RTN210    BEGSR
|    C*
| 01-C           DOPT1     CASLE'2'       RTN211           *ADD OR UPD
|   C           DOPT1     CASEQ'4'       RTN214           *DLT
| 01-C                     END
|    C*
12===C                     ENDSR
      *==============================================================*
      *          RTN211....ADD OR UPD (CHK & SAVE)
      *==============================================================*
13===C           RTN211    BEGSR
|    C*
|    C                     EXSR RTN810                     *CHK
| 01-C           *IN99     IFEQ '0'
|   C                     EXSR RTN820                     *SAVE
|   C                     EXSR RTN205                     *INIT
| 01-C                     END
|    C*
13===C                     ENDSR
      *==============================================================*
      *          RTN214....DLT
      *==============================================================*
14===C           RTN214    BEGSR
|    C*
|    C                     CALL 'WDS000'
|    C                     PARM 'N'       DLTFLG  1
| 01-C           DLTFLG    IFEQ 'Y'
|   C           DPJ01     SETLLPJ0
|   C           DPJ01     READEPJ0                      57
| 02-C           *IN57     DOWEQ'0'
|   C                     DELETPJ0
|   C           DPJ01     READEPJ0                      57
| 02-C                     END
|   C                     MOVE 'SCR001'  APPSCR
| 01-C                     END
|    C*
14===C                     ENDSR
      *==============================================================*
      *          RTN810....CHK SREEN2
      *==============================================================*
15===C           RTN810    BEGSR
|    C*
| 01-C           DOPT1     IFEQ '1'
| 02-C           DPJ01     IFEQ *BLANKS
|   C                     SETON                     639599
| 02*C                     ELSE
|   C           DPJ01     SETLLPJ0                      42
| 03-C           *IN42     IFEQ '1'
|   C                     SETON                     639399
| 03-C                     END
| 02-C                     END
| 01-C                     END
|    C   99                GOTO END810
|    C*
|    C                     SETON                     54
|    C                     READCSFLSR2                   57
| 04-C           *IN57     DOWEQ'0'
|   C                     SETOF                     606162
|   C*
| 05-C           DCHK      IFEQ 'Y'
|   C                     SETOF                     32
| 05*C                     ELSE
|   C                     SETON                     32
| 05-C                     END
|   C*
|   C   99                GOTO UPD
|   C*
| 06-C           DOPT2     IFEQ '1'
| 07-C           DCHK      IFEQ 'Y'
|   C                     SETON                     609399
|   C                     Z-ADDRRN2      DRRN2
| 07*C                     ELSE
| 08-C           DPJ02     IFEQ 0
|   C                     SETON                     619599
|   C                     Z-ADDRRN2      DRRN2
| 08-C                     END
| 07-C                     END
| 06-C                     END
|   C   99                GOTO UPD
|   C*
| 09-C           DOPT2     IFNE '1'
|   C           DOPT2     ANDNE*BLANKS
|   C           DCHK      ANDEQ*BLANKS
|   C                     SETON                     609499
|   C                     Z-ADDRRN2      DRRN2
| 09-C                     END
|   C   99                GOTO UPD
|   C*
| 10-C           DPJ02     IFNE 0
|   C           DPJ03     ANDEQ*BLANKS
|   C                     SETON                     629599
|   C                     Z-ADDRRN2      DRRN2
| 10-C                     END
|   C*
|   C           UPD       TAG
|   C                     UPDATSFLSR2
|   C                     READCSFLSR2                   57
| 04-C                     END
|    C*
15===C           END810    ENDSR
      *==============================================================*
      *          RTN820....SAVE
      *==============================================================*
16===C           RTN820    BEGSR
|    C*
|    C                     READCSFLSR2                   57
| 01-C           *IN57     DOWEQ'0'
|   C*
| 02-C           DOPT2     IFEQ '4'
|   C                     CALL 'WDS000'
|   C                     PARM 'N'       DLTFLG  1
| 03-C           DLTFLG    IFEQ 'Y'
|   C           KEYPJ     CHAINPJ0                  40
|   C                     DELETPJ0
| 03-C                     END
|   C*
|   C                     SETON                     37
|   C                     WRITEDSPC2
|   C                     WRITESFLCR2
|   C                     SETOF                     37
|   C*
| 02*C                     ELSE
|   C*
| 04-C           DOPT2     IFNE *BLANKS
|   C           KEYPJ     CHAINPJ0                  40
|   C                     EXSR RTN821
| 05-C           *IN40     IFEQ '1'
|   C                     WRITEPJ0
| 05*C                     ELSE
|   C                     UPDATPJ0
| 05-C                     END
| 04-C                     END
| 02-C                     END
|   C*
|   C                     READCSFLSR2                   57
| 01-C                     END
|    C*
16===C                     ENDSR
      *==============================================================*
      *          RTN821....MOVE DATA TO FILE
      *==============================================================*
17===C           RTN821    BEGSR
|    C*
|    C                     MOVE DPJ01     PJ01
|    C                     Z-ADDDPJ02     PJ02
|    C                     MOVE DPJ03     PJ03
|    C*
|    C*M002D               CALL 'RES001'
|    C*M002D               PARM 'MDY'     S001I1  3
|    C*M002D               PARM $EGMDY    S001I2  60
B2MODC*M002D     PJXX      PARM           S001O1  80
M002AC                     Z-ADDEG8       PJXX
|    C*
M001MC*                    TIME           PJXX
M001MC                     TIME           PJYY
|    C                     MOVE $USER     PJZZ
|    C*
17===CSR                   ENDSR
     C*==============================================================*
     C*          ERRSR ....DATA LOCK ROUTINE
     C*==============================================================*
18===C           ERRSR     BEGSR
|    C*
|    C                     CALL 'P50'
|    C           DMSG      PARM           P5001O 78
|    C                     SETON                     8099
|    C*
18===C                     ENDSR'*DETC'
**
新增修改      刪除查詢
