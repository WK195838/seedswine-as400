     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250
     H*
     H*    PROGRAM ID   : JLB023
     H*    PROGRAM NAME : 試飲退回傳票批次
     H*    AUTHOR       : AN MING HSIA
     H*    CREATE DATE  : 1993.06.16
     H*    UPDATE DATE  :
     H*    SYSTEM       : 進銷存收TO總帳
     A*===============================================================
     A* CODE | AUTHOR |  DATE  | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001 |MICHELLE|98.11.17| MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M2A  |MICHELLE|01.11.13|產品收回A&P CODE,設為999
     A*---------------------------------------------------------------
     A* M002 |MICHELLE|08.28.02| USED NEW AMP CODE
     A*---------------------------------------------------------------
     F****************************************************************
     FWFJL10PFUF  E           K        DISK                      A
     FWFJL10LFIF  E           K        DISK
     F            WFJL10R                           KRENAMEWFJL10L
     FWFJL13PFUF  E           K        DISK                      A
     FPA#GPF  UF  E           K        DISK                      A
     FSOSNPF  IF  E           K        DISK
M2A  F*SOSNPFX IF  E           K        DISK
     FSOSOPF  IF  E           K        DISK
M002MF*MTMAPF  IF  E           K        DISK
M002MFMTMALF05IF  E           K        DISK
     FGLAAPF  IF  E           K        DISK
     FGLAFPF  IF  E           K        DISK
     FPA#3PF  IF  E           K        DISK
     FPA#4PF  IF  E           K        DISK
M2A  F*PA#5PF  IF  E           K        DISK
M003AFPA#PPF  IF  E           K        DISK
"    FNPNGPF  IF  E           K        DISK
M003DF*PA#6PF  IF  E           K        DISK
     FGLAHPF  O   E           K        DISK
     F*WF#GPF  O   E           K        DISK
     E*---------------------------------------------------------------*
     E*輸入欄位代碼 ... EX.( 10 )=>AH05A
     E                    CHK1        7  2
     E*輸入欄位內容 ... EX.( R&D )=>AH05
     E                    CHK2        4 20
     E*輸入欄位說明 EX.(研發部)=>CALL 檢核程式&AA04得之.
     E                    CHK3        7 20
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$ADMDY
B2MODI                                      187 1920$ADYMD
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      152 161 $USERN
B2MODI                                      201 2080ADYYMD
      *確認年月
B2MODI                                      601 6060F01
     I            DS
B2MODI                                        1   80ADYMD
B2MODI                                        1   60YM
B2MODI                                        7   80DD
     I            DS
     I                                        1   2 SN01
     I                                        1   1 WSN01
     I            DS
     I                                        1   9 SO03
     I                                        3   4 SO03A
     I            DS
     I                                        1   5 SN03
     I                                        1   1 SN03A
     I*檢核程式之參數.....................................
     ICHKPGM      DS
     I                                        1   2 AH01
     I                                        3  22 WPARM1
     I                                       23  32 WPARM2
     I                                       33  33 WCHK
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN #G01      W#G01
     C           *LIKE     DEFN AH03      WAH03
B2000C           *LIKE     DEFN JL1001    WL1001
     C           *LIKE     DEFN JL1002    WL1002
     C           *LIKE     DEFN J1002$    W1002$
     C           *LIKE     DEFN JL1003    WL1003
     C           *LIKE     DEFN SO03A     SO03AA
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYAA     KLIST
     C                     KFLD           AH01
     C                     KFLD           CHK1,I
     C           KEY#4     KLIST
     C                     KFLD           #401
     C                     KFLD           #402
     C           CUSK      KLIST
     C                     KFLD           SN03
     C                     KFLD           SN11
     C           KEYW10    KLIST
     C                     KFLD           JL1001           *確認日期
     C                     KFLD           JL1002           *公司別2
     C                     KFLD           J1002$           *辦事處1
     C                     KFLD           JL1003           *傳票別
     C                     KFLD           JL1004           *會計科目
     C                     KFLD           JL1005           *成本中心
     C                     KFLD           JL1006           *對項別
     C                     KFLD           JL1007           *相關號碼１
     C                     KFLD           JL1008           *相關號碼２
     C                     KFLD           JL1009           *日期
     C                     KFLD           JL1010           *數量
     C                     KFLD           JL1011           *其它
     C           KEYW13    KLIST
     C                     KFLD           JL1301           *確認日期
     C                     KFLD           JL1302           *公司別2
     C                     KFLD           J1302$           *辦事處1
     C                     KFLD           JL1303           *等級
     C                     KFLD           JL1304           *品牌
     C                     KFLD           JL1305           * A&P CODE
     C                     KFLD           JL1306           *產品代號
     C           KEY#G     KLIST
     C                     KFLD           W#G01
     C           KEYAF     KLIST
     C                     KFLD           AH01             *公司別
     C                     KFLD           AH04             *會計科目
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C*
     C                     EXSR R2000
     C                     EXSR R4000
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C*--------------------------------------------------------------*
|    C                     Z-ADD*ZERO     W01    132
|    C                     Z-ADD*ZERO     W02    132
|    C                     Z-ADD*ZERO     W03    132
|    C                     Z-ADD*ZERO     W04    132
|    C                     Z-ADD*ZERO     W05    132
|    C                     Z-ADD0         WCOST  132
M002AC                     Z-ADD0         WCOST1 132
|    C                     Z-ADD0         WAMT   154
|    C*
|    C*計算確認年月最後一天ADYMD =YM+DD
|    C                     Z-ADD0         ADYMD
B2000C                     Z-ADDF01       YM
|    C                     CALL 'P34'
B2MODC                     PARM F01       P3401I  60
M001MC                     PARM '2'       P3402I  1
|    C                     PARM '1'       P3403I  1
|    C                     PARM           P3401O  1
|    C           DD        PARM           P3402O  20
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R2000 .計算試飲退回檔           P3-2.3-(4)
     C*==============================================================*
02===C           R2000     BEGSR
|    C*
|    C           *LOVAL    SETLLSN0
|    C                     READ SN0                      45
| 01-C           *IN45     DOWEQ'0'
|   C*
|   C                     MOVELSN02      W#G01
|   C           KEY#G     SETLL#G0                      42
|   C   42                GOTO TAG#2
|   C*
|   C                     SETOF                     88
      *INITIAL檔案共同欄位WFJL10PF/WFJL13 KEY VALUES
|   C                     EXSR RINT
|   C*
|   C           SN02      SETLLSO0
|   C           SN02      READESO0                      46
| 02-C           *IN46     DOWEQ'0'
|   C*傳票WFJL10PF
     C                     MOVELSO03A     SO03AA
     C           SN03      IFGE '10001'
     C           SN03      ANDLE'19999'
     C           SN03      CHAIN#30                  49
     C  N49                MOVEL#302      SO03AA
     C   49                MOVELSO03A     SO03AA
     C                     END
|   C                     EXSR R2A00
|   C*試飲成本彙總WFJL13PF
|   C                     EXSR R2D00
|   C           SN02      READESO0                      46
| 02-C                     END
|   C*
|   C                     EXSR R2E00
|   C*
|   C           TAG#2     TAG
|   C                     READ SN0                      45
| 01-C                     END
|    C*
02===C           E2000     ENDSR
     C*==============================================================*
     C*          RINT  ....INITIAL KEY VALUES
     C*INITIAL檔案共同欄位WFJL10PF/WFJL13PF
     C*==============================================================*
03===C           RINT      BEGSR
|    C*--------------------------------------------------------------*
|    C*INITIAL檔案共同欄位WFJL10PF
B2000C                     Z-ADDADYMD     JL1001
|    C                     MOVE SN01      J1002$           辦事處
|    C                     MOVEL'3'       JL1003
|    C                     MOVEL*BLANKS   JL1005
|    C                     MOVEL*BLANKS   JL1006
|    C                     MOVEL*BLANKS   JL1007
|    C                     MOVEL*BLANKS   JL1008
|    C                     Z-ADD0         JL1009
|    C                     Z-ADD0         JL1010
|    C                     MOVEL*BLANKS   JL1011
|    C*INITIAL檔案共同欄位WFJL13PF
B2000C                     Z-ADDF01       JL1301
|    C*公司R1/R2/R3/C1/C2/C3
|    C                     MOVE SN01      JL1302
|    C                     MOVE SN01      J1302$           辦事處
|    C*
|    C*--------------------------------------------------------------*
03===C                     ENDSR
     C*==============================================================*
     C*          R2E00 ... READ SOSNPF=>WRITE PA#GPF
     C*==============================================================*
04===C           R2E00     BEGSR
|    C*
|    C                     MOVELSN02      #G01
B2000C                     Z-ADDSN06      #G02
B2000C                     Z-ADDADYYMD    #GXX
B2000C                     TIME           #GYY
|    C                     MOVEL$USER     #GZZ
|    C                     WRITE#G0
|    C*                    WRITEWF#GR
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R2A00 ... 計算試飲退回檔                 P3-2.3-(4)
     C*                    (SOSNPF/SOSOPF)
     C*==============================================================*
05===C           R2A00     BEGSR
|    C*公司別RE/CN
| 01-C           WSN01     IFEQ 'R'
|   C                     MOVEL'RE'      JL1002
| 01*C                     ELSE
| 02-C           WSN01     IFEQ 'C'
|   C                     MOVEL'CN'      JL1002
| 02*C                     ELSE
| 03-C           WSN01     IFEQ 'W'
|   C                     MOVEL'WL'      JL1002
| 03-C                     END
| 02-C                     END
| 01-C                     END
|    C*WCOST = (SO16+SO17+SO18)
|    C           SO16      ADD  SO17      WCOST
|    C                     ADD  SO18      WCOST
|    C*----------------------------------------------------------------
M002AC*WCOST1= (SO13+SO14+SO15)
"    C           SO13      ADD  SO14      WCOST1
"    C                     ADD  SO15      WCOST1
|    C*----------------------------------------------------------------
|    C*會計科目－存貨
M002 C* WCOST1為成本，WCOST為費用金額
      *存貨應以成本入帳，要用欄位：WCOST1
      *
|    C                     MOVEL'1141    'JL1004
|    C                     Z-ADD0         W01
|    C                     Z-ADD0         W02
| 04-C           WCOST1    IFGT 0
|   C                     Z-ADDWCOST1    W01
| 04*C                     ELSE
|   C           WCOST1    MULT -1        W02
| 04-C                     END
|    C*
|    C           KEYW10    CHAINWFJL10R              41
|    C                     EXSR RWF10
|    C*
      *----------------------------------------------------------------
      *先取得AMP CODE TYPE
M002AC           SN16      CHAIN#P0                  41
"    C           *IN41     IFEQ '0'
"    C           #P02      CHAINNG0                  42
"    C           *IN42     IFEQ '0'
"    C                     MOVE NG04      ATYPE   1
"    C                     ELSE
"    C                     MOVE *BLANKS   ATYPE
"    C                     END
"    C                     ELSE
"    C                     MOVE *BLANKS   ATYPE
"    C                     END
     C*
      *----------------------------------------------------------------
| 04-C           SN03      IFGE 'Y0000'
| 04-C           SN03      ANDLE'Y9999'
|   C                     MOVEL'611201  'JL1004
|   C                     MOVEL'000000  'JL1006
|   C                     MOVEL*BLANKS   JL1007
|   C*確認年月
|   C*M001D               Z-ADD0         WSC39   80
|   C*M001D               CALL 'P31'
B2MODC*M001D               PARM SN06      P310I1 100
|   C*M001D               PARM '1'       P310I2  1
|   C*M001D               PARM '1'       P310I3  1
|   C*M001D               PARM '2'       P310I4  1
|   C*M001D               PARM '1'       P310I5  1
|   C*M001D     WSC39     PARM           P311O1  80
|   C*
|   C*M001M               MOVE WSC39     LSC39   60
M001MC                     MOVE SN06      LSC39   60
|   C                     MOVELLSC39     LSC391  40
|   C                     MOVE LSC391    JL1009
|   C*
| 04*C                     ELSE
|    C*會計科目－產品促銷－搭贈－人頭馬／其他品牌
M002DC*          SN03      IFGE 'Z0000'
"    C*          SN03      ANDLE'Z9999'
"    C*          SO03AA    IFEQ 'RM'
"    C*                    MOVEL'619901'  JL1004
"    C*                    MOVELSO03      JL1006
"    C*                    ELSE
"    C*                    MOVEL'619902'  JL1004
"    C*                    MOVELSO03A     JL1006
"    C*                    END
"    C*                    ELSE
" 05-C*          SO03AA    IFEQ 'RM'
"   C*RM系列
"   C*                    MOVEL'51010501'JL1004
"    C*          SN03      IFGE '10001'
"    C*          SN03      ANDLE'19999'
"    C*                    MOVEL#302      JL1006
"    C*                    ELSE
"   C*                    MOVEL'RM'      JL1006
"    C*                    END
" 05*C*                    ELSE
"   C*其他品牌
"   C*                    MOVEL'51010502'JL1004
"    C*          SN03      IFGE '10001'
"    C*          SN03      ANDLE'19999'
"    C*                    MOVEL#302      JL1006
"    C*                    ELSE
"   C*                    MOVELSO03A     JL1006
" 05-C*                    END
" 05-C*                    END
" 05-C*                    END
M002AC           ATYPE     IFEQ 'S'
"    C                     MOVEL*BLANKS   JL1004
"    C                     MOVEL*BLANKS   JL1006
"    C                     MOVEL'51010503'JL1004
"    C                     MOVELSO03AA    JL1006
"    C                     ELSE
"    C           SO03AA    IFEQ 'RM'
"    C                     MOVEL*BLANKS   JL1004
"    C                     MOVEL'51010501'JL1004
"    C                     MOVELSO03      JL1006
"    C                     ELSE
"    C                     MOVEL*BLANKS   JL1004
"    C                     MOVEL'51010502'JL1004
"    C                     MOVELSO03AA    JL1006
"    C                     END
"    C                     END
| 05-C                     END
|    C**MA11 FOR A&P --------
|    C                     MOVEL*BLANKS   JL1303
|    C                     MOVEL*BLANKS   JL1305
M002AC                     MOVEL*BLANKS   JL1007
|    C                     MOVEL*BLANKS   MA11
|    C*M002M     SO03      CHAINMA0                  40
M002MC           SO03AA    CHAINMA0                  40
| 06-C           *IN40     IFEQ '0'
|   C                     MOVELMA11      JL1303
| 07-C           SN03      IFGE '10000'
| 07-C           SN03      ANDLE'19999'
M002AC           SN16      CHAIN#P0                  42
"   -C           *IN42     IFEQ '0'
"    C                     MOVEL*BLANKS   JL1007
"    C                     MOVEL*BLANKS   JL1305
"    C                     MOVEL#P02      JL1007
"    C                     MOVEL#P02      JL1305
"    C                     ELSE
" || C                     MOVEL'BTL999'  JL1007
" || C                     MOVEL'BTL999'  JL1305
"    C                     END
     C*
| 07*C                     ELSE
| 07-C           SN03      IFGE 'Y0000'
| 07-C           SN03      ANDLE'Y9999'
|   C                     MOVEL'      '  JL1007
|   C                     MOVEL'      '  JL1305
M002DC*                    ELSE
"    C*          SO03AA    IFEQ 'RM'
"    C*                    MOVELSO03      JL1007
"    C*                    MOVEL'   '     JL1305
"    C*                    ELSE
"    C*                    MOVELSO03A     JL1007
"    C*                    MOVEL'   '     JL1305
"    C*                    END
| 07-C                     END
| 07-C                     END
| 06-C                     END
|    C*----------------------
| 26-C           SN03      IFGE 'Y0000'
     C           SN03      ANDLE'Y9999'
     C           CUSK      CHAIN#40                  40
     C  N40                MOVEL#403      JL1005
|   C                     MOVEL*BLANKS   JL1305
| 27*C                     ELSE
M002DC*          SN03      IFGE 'Z0000'
"    C*          SN03      ANDLE'Z9999'
"    C*          SN03      CHAIN#60                  40
"    C*                    MOVEL#602      #401
"    C*                    MOVEL#603      #402
"    C*          KEY#4     CHAIN#40                  40
"    C* N40                MOVEL#403      JL1005
"   C*                    MOVEL*BLANKS   JL1305
"    C*                    ELSE
"   C*                    MOVEL*BLANKS   JL1005
" 26-C*                    END
| 26-C                     END
|    C                     Z-ADD0         W01
|    C                     Z-ADD0         W02
M002AC           WCOST     SUB  WCOST1    WBAL   132
| 08-C           WCOST     IFLE 0
|   C           WCOST     MULT -1        W01
| 08*C                     ELSE
|   C                     Z-ADDWCOST     W02
| 08-C                     END
|    C*
|    C           KEYW10    CHAINWFJL10R              41
|    C                     EXSR RWF10
      *
     C*與成本之間的差異數的處理段
      *
M002AC                     Z-ADD0         W01
"    C                     Z-ADD0         W02
     C*
     C           WBAL      IFNE 0
| 28-C           WBAL      IFLT 0
|   C                     Z-ADDWBAL      W02
|   C                     MULT -1        W02
| 28*C                     ELSE
|   C                     Z-ADDWBAL      W01
| 28-C                     END
M002AC                     MOVEL*BLANKS   JL1004
"    C                     MOVEL*BLANKS   JL1006
"    C                     MOVEL*BLANKS   JL1007
"    C*                    MOVEL*BLANKS   JL1305
"    C                     MOVEL'BTL999'  JL1007
"    C*                    MOVEL'BTL999'  JL1305
M002AC           SO03A     IFEQ 'RM'
      *RM系列
"    C                     MOVEL'51010501'JL1004
"    C                     MOVELSO03      JL1006
"    C                     ELSE
      *其他品牌
"    C                     MOVEL'51010502'JL1004
M002AC                     MOVELSO03A     JL1006
"    C                     END
|    C           KEYW10    CHAINWFJL10R              41
|    C                     EXSR RWF10
| 28-C                     END
     C*
|    C                     MOVEL*BLANKS   JL1006
|    C                     MOVEL*BLANKS   JL1007
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R2D00 ... 寫入暫存檔－試飲成本彙總
     C*==============================================================*
06===C           R2D00     BEGSR
      *產品代號
M002MC*                    MOVELSO03A     JL1304
M002MC                     MOVELSO03AA    JL1304
|    C                     MOVELSO03      JL1306
M002AC           SO03AA    CHAINMA0                  40
"    C           *IN40     IFEQ '0'
"    C                     MOVELMA11      JL1303
"    C                     END
      *----------------------------------------------------------------
|    C*W01數量
|    C                     Z-ADDSO05      W01
|    C*W03成本ＦＯＢ
|    C                     Z-ADDSO16      W03
|    C*W04成本ＦＨＩ
|    C                     Z-ADDSO17      W04
|    C*W05成本ＤＵＴＹ
|    C                     Z-ADDSO18      W05
|    C*
|    C           KEYW13    CHAINWFJL13R              41
| 01-C           *IN41     IFEQ '1'
|   C                     Z-ADDW01       JL1307
|   C                     Z-ADDW03       JL1308
|   C                     Z-ADDW04       JL1309
|   C                     Z-ADDW05       JL1310
|   C                     WRITEWFJL13R
| 01*C                     ELSE
|   C                     ADD  W01       JL1307
|   C                     ADD  W03       JL1308
|   C                     ADD  W04       JL1309
|   C                     ADD  W05       JL1310
|   C                     UPDATWFJL13R
| 01-C                     END
|    C*
|    C*
06===C                     ENDSR
     C*==============================================================*
     C*          RWF10 ... 寫入．更新傳票暫存檔WFJL10PF
     C*==============================================================*
07===C           RWF10     BEGSR
|    C*
| 01-C           *IN41     IFEQ '1'
|   C                     Z-ADDW01       JL1012
|   C                     Z-ADDW02       JL1013
|   C*判別借貸方
|   C           JL1012    SUB  JL1013    WAMT
| 02-C           WAMT      IFGT 0
|   C                     MOVEL'1'       J1003$
| 02-C                     END
| 03-C           WAMT      IFLT 0
|   C                     MOVEL'2'       J1003$
| 03-C                     END
|   C*金額不為０才寫入
| 04-C           WAMT      IFNE 0
|   C                     WRITEWFJL10R
| 04-C                     END
|   C*
| 01*C                     ELSE
|   C                     ADD  W01       JL1012
|   C                     ADD  W02       JL1013
|   C*判別借貸方
|   C           JL1012    SUB  JL1013    WAMT
| 05-C           WAMT      IFGT 0
|   C                     MOVEL'1'       J1003$
|   C                     Z-ADDWAMT      JL1012
|   C                     Z-ADD0         JL1013
| 05-C                     END
| 06-C           WAMT      IFLT 0
|   C                     MOVEL'2'       J1003$
|   C                     Z-ADD0         JL1012
|   C           WAMT      MULT -1        JL1013
| 06-C                     END
|   C*金額為０不產生傳票
| 07-C           WAMT      IFNE 0
|   C                     UPDATWFJL10R
| 07*C                     ELSE
|   C                     DELETWFJL10R
| 07-C                     END
| 01-C                     END
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R4000 .產生銷售傳票
     C*==============================================================*
08===C           R4000     BEGSR
|    C*
B2000C                     MOVEL*BLANKS   WL1001
|    C                     MOVEL*BLANKS   WL1002
|    C                     MOVEL*BLANKS   W1002$
|    C                     MOVEL*BLANKS   WL1003
|    C*
B2000C           *LOVAL    SETLLWFJL10L
|    C                     READ WFJL10L                  46
| 01-C           *IN46     DOWEQ'0'
|   C                     EXSR R4100
|   C*
|   C                     READ WFJL10L                  46
| 01-C                     END
|    C*
08===C           E4000     ENDSR
     C*==============================================================*
     C*          R4100 .  產生銷售傳票
     C*==============================================================*
09===C           R4100     BEGSR
|    C*
|    C* <<清除傳票檔部份欄位 >>
|    C                     EXSR R40XX
|    C*----------------------------------------------------------------
|    C*傳票日期
|    C                     Z-ADD*ZEROS    AH10
M001AC                     Z-ADDJL1001    AH10
|    C*M001D               CALL 'P31'
B2MODC*                    PARM JL1001    P310I1 100
|    C*                    PARM '1'       P310I2  1
|    C*                    PARM '1'       P310I3  1
|    C*                    PARM '2'       P310I4  1
|    C*                    PARM '1'       P310I5  1
|    C*M001D     AH10      PARM           P311O1  80
      *1994年6月前公司別不變，以後ＲＥ－＞ＲＭ，ＷＬ－＞ＷＮ
| 01-C           AH10      IFGE 19940601
| 02-C           JL1002    IFEQ 'RE'
|   C                     MOVEL'RM'      JL1002
| 02-C                     END
| 03-C           JL1002    IFEQ 'WL'
|   C                     MOVEL'WH'      JL1002
| 03-C                     END
| 01-C                     END
|    C                     MOVELJL1002    AH01
      *1994年6月前公司別不變，以後ＲＥ－＞ＲＭ，ＷＬ－＞ＷＮ
|    C*
|    C*傳票號碼
B2000C           JL1001    IFNE WL1001
|   C           JL1002    ORNE WL1002
|   C           J1002$    ORNE W1002$
|   C           JL1003    ORNE WL1003
|   C                     MOVEL*BLANKS   AH02
|   C                     CALL 'GLS002'
|   C                     PARM AH01      P002I1  2
|   C                     PARM AH10      P002I2  80
|   C                     PARM 'F'       P002I3  1
|   C           AH02      PARM           P002O1  9
B2000C                     MOVELJL1001    WL1001
|   C                     MOVELJL1002    WL1002
|   C                     MOVELJ1002$    W1002$
|   C                     MOVELJL1003    WL1003
|   C                     Z-ADD0         WAH03
| 04-C                     END
      *
|    C                     MOVELJL1004    AH04
|    C           KEYAF     CHAINAF0                  40
| 05-C           *IN40     IFEQ '0'
      *
| 06-C           AF11      IFNE *BLANK
|   C                     MOVELJL1005    AH05
| 06-C                     END
      *
| 07-C           AF12      IFNE *BLANK
|   C                     MOVELJL1006    AH06
| 07-C                     END
      *
| 08-C           AF13      IFNE *BLANK
|   C                     MOVELJL1007    AH07
| 08-C                     END
      *
| 09-C           AF14      IFNE *BLANK
|   C                     MOVELJL1008    AH08
| 09-C                     END
      *
| 10-C           AF15      IFNE *BLANK
|   C                     MOVELJL1009    AH09
| 10-C                     END
      *
|   C                     Z-ADDJL1012    AH12
|   C                     Z-ADDJL1013    AH13
      *
| 11-C           AF16      IFNE *BLANK
|   C                     MOVELJL1010    AH14
| 11-C                     END
     C*
| 12-C           AF17      IFNE *BLANK
|   C                     MOVELJL1011    AH15
| 12-C                     END
      *
|   C                     MOVEL'R1'      AH17
|   C                     MOVEL*BLANKS   AH18
|   C                     MOVEL'V'       AH19
|   C                     MOVEL*BLANKS   AH20
|   C                     Z-ADD*ZEROS    AH21
|   C                     MOVEL*BLANKS   AH22
|   C                     Z-ADD*ZEROS    AH23
|   C                     MOVEL*BLANKS   AH24
|   C                     Z-ADDADYYMD    AH25
|   C                     MOVEL$USER     AH26
B2000C                     TIME           AH97
|   C                     Z-ADDADYYMD    AH98
|   C                     MOVEL$USER     AH99
|   C* <<取得會計科目資料 >>
|   C                     EXSR R40YY
|   C* <<寫入傳票檔 >>
|   C                     EXSR R40ZZ
| 05-C                     END
|    C*
09===C           E4A00     ENDSR
     C*==============================================================*
     C*          R40XX ...清除傳票檔部份欄位
     C*==============================================================*
10===C           R40XX     BEGSR
|    C*
|    C                     MOVEL*BLANKS   AH04
|    C                     MOVEL*BLANKS   AH05
|    C                     MOVEL*BLANKS   AH05A
|    C                     MOVEL*BLANKS   AH06
|    C                     MOVEL*BLANKS   AH06A
|    C                     MOVEL*BLANKS   AH07
|    C                     MOVEL*BLANKS   AH07A
|    C                     MOVEL*BLANKS   AH08
|    C                     MOVEL*BLANKS   AH08A
|    C                     Z-ADD*ZEROS    AH09
|    C                     MOVEL*BLANKS   AH09A
|    C                     Z-ADD*ZEROS    AH12
|    C                     Z-ADD*ZEROS    AH13
|    C                     MOVEL*BLANKS   AH14
|    C                     MOVEL*BLANKS   AH14A
|    C                     MOVEL*BLANKS   AH15
|    C                     MOVEL*BLANKS   AH15A
|    C                     MOVEL*BLANKS   AH16
|    C                     MOVEL'R1'      AH17
|    C                     MOVEL*BLANKS   AH18
|    C                     MOVEL'V'       AH19
|    C                     MOVEL*BLANKS   AH20
|    C                     Z-ADD*ZEROS    AH21
|    C                     MOVEL*BLANKS   AH22
|    C                     Z-ADD*ZEROS    AH23
|    C                     MOVEL*BLANKS   AH24
|    C                     Z-ADDADYYMD    AH25
|    C                     MOVEL$USER     AH26
B2000C                     TIME           AH97
|    C                     Z-ADDADYYMD    AH98
|    C                     MOVEL$USER     AH99
|    C*
10===C                     ENDSR
     C*==============================================================*
     C*          R40YY ...取得會計科目資料
     C*==============================================================*
11===C           R40YY     BEGSR
|    C*
|    C                     MOVEA*BLANKS   CHK1
|    C                     MOVEA*BLANKS   CHK2
|    C                     MOVEA*BLANKS   CHK3
|    C*<<取得會計科目各欄位名稱>>
|    C           KEYAF     CHAINAF0                  40
| 01-C           *IN40     IFEQ '0'
|   C                     MOVELAF11      AH05A
|   C                     MOVELAF12      AH06A
|   C                     MOVELAF13      AH07A
|   C                     MOVELAF14      AH08A
|   C                     MOVELAF15      AH09A
|   C                     MOVELAF16      AH14A
|   C                     MOVELAF17      AH15A
|   C*
|   C                     MOVELAF11      CHK1,1
|   C                     MOVELAF12      CHK1,2
|   C                     MOVELAF13      CHK1,3
|   C                     MOVELAF14      CHK1,4
|   C                     MOVELAF15      CHK1,5
|   C                     MOVELAF16      CHK1,6
|   C                     MOVELAF17      CHK1,7
| 01-C                     END
|    C* << 輸入欄位說明CHK3 >>
|    C*
|    C                     MOVELAH05      CHK2,1
|    C                     MOVELAH06      CHK2,2
|    C                     MOVELAH07      CHK2,3
|    C                     MOVELAH08      CHK2,4
| 02-C                     DO   4         I       10
| 03-C           CHK2,I    IFNE *BLANKS
|   C           KEYAA     CHAINAA0                  40
| 04-C           *IN40     IFEQ '0'
|   C           AA04      ANDNE*BLANKS
|   C*
|   C                     MOVELCHK2,I    WPARM1
|   C                     MOVELAA05      WPARM2
|   C                     MOVEL'0'       WCHK
|   C                     CALL AA04
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C           CHK3,I    PARM           DESC   20
| 04*C                     ELSE
|   C                     MOVELCHK2,I    CHK3,I
| 04-C                     END
| 03-C                     END
| 02-C                     END
|    C*
| 05-C           AH09      IFNE 0
|   C                     CALL 'P56'                      *P56(GET'/')
|   C                     PARM AH09      P5601I  80
|   C                     PARM           P5601O 10
|   C                     MOVELP5601O    CHK3,5
| 05-C                     END
|    C*
| 06-C           AH14      IFNE 0
|   C                     CALL 'P55'                      *P55(GET,.)
|   C                     PARM AH14      P5501I 185
|   C                     PARM 2         P5502I  10
|   C                     PARM           P5501O 24
|   C                     MOVELP5501O    CHK3,6
| 06-C                     END
|    C*
| 07-C           AH15      IFNE *BLANK
|   C                     Z-ADD7         I
|   C           KEYAA     CHAINAA0                  40
| 08-C           *IN40     IFEQ '0'
|   C           AA04      ANDNE*BLANKS
|   C*
|   C                     MOVELAH15      WPARM1
|   C                     MOVELAA05      WPARM2
|   C                     MOVEL'0'       WCHK
|   C                     CALL AA04
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C           CHK3,7    PARM           DESC   20
| 08*C*                    ELSE
|   C                     MOVELAH15      CHK3,7
| 08-C                     END
| 07-C                     END
|    C*<<摘要 >>
| 09-C                     DO   7         K       10
| 10-C           CHK3,K    IFNE *BLANKS
|   C                     CALL 'GLS001'
|   C                     PARM AH16      S001I1 80
|   C                     PARM CHK3,K    S001I2 80
|   C                     PARM 49        S001I3  30
|   C           AH16      PARM           S001O1 80
| 10-C                     END
| 09-C                     END
|    C*
11===C                     ENDSR
     C*==============================================================*
     C*          R40ZZ ... WRITE GLAHPF
     C*==============================================================*
12===C           R40ZZ     BEGSR
|    C*
|    C                     ADD  1         WAH03
|    C                     Z-ADDWAH03     AH03
|    C                     WRITEAH0
|    C*
12===C                     ENDSR
