     H****************************************************************
     H*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     H*                                                              *
     H*    PROGRAM ID   : JLB0422                                    *
     H*    PROGRAM NAME :完稅傳票批次                              *
     H*    AUTHOR       : AN MING HSIA                               *
     H*    CREATE DATE  : 06/05/93                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       : 進銷存收TO總帳                         *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.18 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     H****************************************************************
     FWFJL10PFUF  E           K        DISK                      A
     FWFJL10LFIF  E           K        DISK
     F            WFJL10R                           KRENAMEWFJL10L
     FPA#GPF  UF  E           K        DISK                      A
     FPOPFPF  IF  E           K        DISK
     FPOPGPF  IF  E           K        DISK
     FGLAAPF  IF  E           K        DISK
     FGLAFPF  IF  E           K        DISK
     FGLAHPF  O   E           K        DISK
     F*WF#GPF  O   E           K        DISK
     FWFJL18PFO   E           K        DISK
     E*---------------------------------------------------------------*
     E*輸入欄位代碼 ... EX.( 10 )=>AH05A
     E                    CHK1        7  2
     E*輸入欄位內容 ... EX.( R&D )=>AH05
     E                    CHK2        4 20
     E*輸入欄位說明 EX.(研發部)=>CALL 檢核程式&AA04得之.
     E                    CHK3        7 20
     I*CURSOR POSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$ADMDY
M001MI                                      187 1920$ADYMD
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      152 161 $USERN
M001MI                                      201 2080ADYYMD
     I            DS
     I                                        1   2 PF01
     I                                        1   1 WPF01
     I            DS
     I                                        1   9 SD03
     I                                        3   4 SD03A
     I                                        1   4 SD03B
     I*檢核程式之參數.....................................
     ICHKPGM      DS
     I                                        1   2 AH01
     I                                        3  22 WPARM1
     I                                       23  32 WPARM2
     I                                       33  33 WCHK
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *LIKE     DEFN #G01      W#G01
     C           *LIKE     DEFN AH03      WAH03
B2000C           *LIKE     DEFN JL1001    WL1001
     C           *LIKE     DEFN JL1002    WL1002
     C           *LIKE     DEFN J1002$    W1002$
     C           *LIKE     DEFN JL1003    WL1003
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYAA     KLIST
     C                     KFLD           AH01
     C                     KFLD           CHK1,I
     C           KEYW10    KLIST
     C                     KFLD           JL1001           *確認日期
     C                     KFLD           JL1002           *公司別2
     C                     KFLD           J1002$           *辦事處1
     C                     KFLD           JL1003           *傳票別
     C                     KFLD           JL1004           *會計科目
     C                     KFLD           JL1005           *成本中心
     C                     KFLD           JL1006           *對項別
     C                     KFLD           JL1007           *相關號碼１
     C                     KFLD           JL1008           *相關號碼２
     C                     KFLD           JL1009           *日期
     C                     KFLD           JL1010           *數量
     C                     KFLD           JL1011           *其它
     C           KEY#G     KLIST
     C                     KFLD           W#G01
     C           KEYAF     KLIST
     C                     KFLD           AH01             *公司別
     C                     KFLD           AH04             *會計科目
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C*
     C                     EXSR R2000
     C                     EXSR R4000
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R2000 .計算銷售訂單檔           P3-2.3-(4)
     C*==============================================================*
01===C           R2000     BEGSR
|    C*
|    C           *LOVAL    SETLLPF0
|    C                     READ PF0                      45
| 01-C           *IN45     DOWEQ'0'
|   C*
|   C                     MOVELPF02      W#G01
|   C           KEY#G     SETLL#G0                      42
| 02-C           *IN42     IFEQ '0'
|   C                     EXSR R2A00                      產生傳票
|   C                     EXSR R2B00                      寫入PA#GP
| 02-C                     END
|   C                     READ PF0                      45
| 01-C                     END
|    C*
01===C           E2000     ENDSR
     C*==============================================================*
     C*          R2A00 ... POPFPF 完稅驗收檔
     C*==============================================================*
02===C           R2A00     BEGSR
|    C*
|    C*共同資料
B2000C                     Z-ADDPF11      JL1001
| 01-C           WPF01     IFEQ 'R'
|   C                     MOVEL'RE'      JL1002
| 01-C                     END
| 02-C           WPF01     IFEQ 'W'
|   C                     MOVEL'WL'      JL1002
| 02-C                     END
| 03-C           WPF01     IFEQ 'C'
|   C                     MOVEL'CN'      JL1002
| 03-C                     END
|    C                     MOVELPF01      J1002$
|    C                     MOVEL'8'       JL1003
|    C*
|    C*完稅成本傳票
|    C*壹..會計科目－存貨
|    C*
|    C                     EXSR R2A10                      INIT
|    C*
|    C                     MOVE '1141    'JL1004
|    C*金額
| 04-C           PF10      IFGE 0
|   C                     Z-ADDPF10      W01
| 04*C                     ELSE
|   C           PF10      MULT -1        W02
| 04-C                     END
|    C*
|    C           KEYW10    CHAINWFJL10R              41
|    C                     EXSR RWF10
|    C*
|    C*貳...會計科目－預付貨款－公賣利益
|    C*
|    C                     EXSR R2A10
|    C*
|    C                     MOVE '115101  'JL1004
|    C                     MOVELPF02      JL1007
|    C*金額
| 05-C           PF10      IFLE 0
|   C           PF10      MULT -1        W01
| 05*C                     ELSE
|   C                     Z-ADDPF10      W02
| 05-C                     END
|    C*
|    C           KEYW10    CHAINWFJL10R              41
|    C                     EXSR RWF10
|    C*
|    C*
02===C                     ENDSR
     C*==============================================================*
     C*          R2A10 ... INITIAL WFJL10PF
     C*==============================================================*
03===C           R2A10     BEGSR
|    C*
|    C                     MOVE *BLANK    JL1003
|    C                     MOVE *BLANK    JL1004
|    C                     MOVE *BLANK    JL1005
|    C                     MOVE *BLANK    JL1006
|    C                     MOVE *BLANK    JL1007
|    C                     MOVE *BLANK    JL1008
|    C                     MOVE *BLANK    JL1009
|    C                     Z-ADD0         JL1010
|    C                     MOVE *BLANK    JL1011
|    C                     Z-ADD0         JL1012
|    C                     Z-ADD0         W01    120
|    C                     Z-ADD0         W02    120
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          R2B00 ... READ POPFPF=>WRITE PA#GPF WFJL18PF
     C*==============================================================*
04===C           R2B00     BEGSR
|    C*
|    C                     MOVELPF02      #G01
B2000C                     Z-ADDPF11      #G02
B2000C                     Z-ADDADYYMD    #GXX
B2000C                     TIME           #GYY
|    C                     MOVEL$USER     #GZZ
|    C                     WRITE#G0
|    C*                    WRITEWF#GR
|    C*
B2000C                     MOVELPF11      JL1801
|    C                     MOVELPF01      JL1802
|    C                     MOVELPF02      JL1803
|    C*
|    C           PF02      SETLLPG0
|    C           PF02      READEPG0                      44
| 01-C           *IN44     DOWEQ'0'
|   C*
|   C                     MOVELPG03      JL1804
|   C                     Z-ADDPG05      JL1805
|   C                     Z-ADDPG06      JL1806
|   C                     WRITEWFJL18R
|   C*
|   C           KEY#G     READEPG0                      44
| 01-C                     END
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          RWF10 ... 寫入．更新傳票暫存檔WFJL10PF
     C*==============================================================*
05===C           RWF10     BEGSR
|    C*
| 01-C           *IN41     IFEQ '1'
|   C                     Z-ADDW01       JL1012
|   C                     Z-ADDW02       JL1013
|   C*借貸別
| 02-C           JL1012    IFGT JL1013
|   C                     MOVEL'1'       J1003$
| 02*C                     ELSE
|   C                     MOVEL'2'       J1003$
| 02-C                     END
|   C                     WRITEWFJL10R
|   C*
| 01*C                     ELSE
|   C*
|   C                     ADD  W01       JL1012
|   C                     ADD  W02       JL1013
|   C*借貸別
| 03-C           JL1012    IFGT JL1013
|   C                     MOVEL'1'       J1003$
| 03*C                     ELSE
|   C                     MOVEL'2'       J1003$
| 03-C                     END
|   C                     UPDATWFJL10R
| 01-C                     END
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R4000 .產生銷售傳票
     C*==============================================================*
06===C           R4000     BEGSR
|    C*
B2000C                     MOVEL*BLANKS   WL1001
|    C                     MOVEL*BLANKS   WL1002
|    C                     MOVEL*BLANKS   W1002$
|    C                     MOVEL*BLANKS   WL1003
|    C*
B2000C           *LOVAL    SETLLWFJL10L
|    C                     READ WFJL10L                  46
| 01-C           *IN46     DOWEQ'0'
|   C*借－貸＝０者不寫入
|   C           JL1012    SUB  JL1013    WAMT   132
| 02-C           WAMT      IFNE 0
|   C                     EXSR R4100
| 02-C                     END
|   C*
|   C                     READ WFJL10L                  46
| 01-C                     END
|    C*
06===C           E4000     ENDSR
     C*==============================================================*
     C*          R4100 .  產生銷售傳票
     C*==============================================================*
07===C           R4100     BEGSR
|    C*
|    C* <<清除傳票檔部份欄位 >>
|    C                     EXSR R40XX
|    C*----------------------------------------------------------------
|    C*傳票日期
|    C                     Z-ADD*ZEROS    AH10
M001AC                     Z-ADDJL1001    AH10
|    C*M001D               CALL 'P31'
B2MODC*                    PARM JL1001    P310I1 100
|    C*                    PARM '1'       P310I2  1
|    C*                    PARM '1'       P310I3  1
|    C*                    PARM '2'       P310I4  1
|    C*                    PARM '1'       P310I5  1
|    C*M001D     AH10      PARM           P311O1  80
      *1994年6月前公司別不變，以後ＲＥ－＞ＲＭ，ＷＬ－＞ＷＮ
| 01-C           AH10      IFGE 19940601
| 02-C           JL1002    IFEQ 'RE'
|   C                     MOVEL'RM'      JL1002
| 02-C                     END
| 03-C           JL1002    IFEQ 'WL'
|   C                     MOVEL'WH'      JL1002
| 03-C                     END
| 01-C                     END
|    C                     MOVELJL1002    AH01
      *1994年6月前公司別不變，以後ＲＥ－＞ＲＭ，ＷＬ－＞ＷＮ
|    C*傳票號碼
B2000C           JL1001    IFNE WL1001
|   C           JL1002    ORNE WL1002
|   C           J1002$    ORNE W1002$
|   C           JL1003    ORNE WL1003
|   C                     MOVEL*BLANKS   AH02
|   C                     CALL 'GLS002'
|   C                     PARM AH01      P002I1  2
|   C                     PARM AH10      P002I2  80
|   C                     PARM 'F'       P002I3  1
|   C           AH02      PARM           P002O1  9
B2000C                     MOVE JL1001    WL1001
|   C                     MOVE JL1002    WL1002
|   C                     MOVE J1002$    W1002$
|   C                     MOVE JL1003    WL1003
|   C                     Z-ADD0         WAH03
| 04-C                     END
|    C*
|    C                     MOVELJL1004    AH04
|    C           KEYAF     CHAINAF0                  40
| 05-C           *IN40     IFEQ '0'
      *
| 06-C           AF11      IFNE *BLANK
|   C                     MOVELJL1005    AH05
| 06-C                     END
      *
| 07-C           AF12      IFNE *BLANK
|   C                     MOVELJL1006    AH06
| 07-C                     END
      *
| 08-C           AF13      IFNE *BLANK
|   C                     MOVELJL1007    AH07
| 08-C                     END
      *
| 09-C           AF14      IFNE *BLANK
|   C                     MOVELJL1008    AH08
| 09-C                     END
      *
| 10-C           AF15      IFNE *BLANK
|   C                     MOVELJL1009    AH09
| 10-C                     END
      *
|   C                     Z-ADDJL1012    AH12
|   C                     Z-ADDJL1013    AH13
      *
| 11-C           AF16      IFNE *BLANK
|   C                     MOVELJL1010    AH14
| 11-C                     END
      *
| 12-C           AF17      IFNE *BLANK
|   C                     MOVELJL1011    AH15
| 12-C                     END
      *
|   C                     MOVEL'P2'      AH17
|   C                     MOVEL*BLANKS   AH18
|   C                     MOVEL'V'       AH19
|   C                     MOVEL*BLANKS   AH20
|   C                     Z-ADD*ZEROS    AH21
|   C                     MOVEL*BLANKS   AH22
|   C                     Z-ADD*ZEROS    AH23
|   C                     MOVEL*BLANKS   AH24
|   C                     Z-ADDADYYMD    AH25
|   C                     MOVEL$USER     AH26
B2000C                     TIME           AH97
|   C                     Z-ADDADYYMD    AH98
|   C                     MOVEL$USER     AH99
|   C* <<取得會計科目資料 >>
|   C                     EXSR R40YY
|   C* <<寫入傳票檔 >>
|   C                     EXSR R40ZZ
| 05-C                     END
|    C*
07===C           E4A00     ENDSR
     C*==============================================================*
     C*          R40XX ...清除傳票檔部份欄位
     C*==============================================================*
08===C           R40XX     BEGSR
|    C*
|    C                     MOVEL*BLANKS   AH04
|    C                     MOVEL*BLANKS   AH05
|    C                     MOVEL*BLANKS   AH05A
|    C                     MOVEL*BLANKS   AH06
|    C                     MOVEL*BLANKS   AH06A
|    C                     MOVEL*BLANKS   AH07
|    C                     MOVEL*BLANKS   AH07A
|    C                     MOVEL*BLANKS   AH08
|    C                     MOVEL*BLANKS   AH08A
|    C                     Z-ADD*ZEROS    AH09
|    C                     MOVEL*BLANKS   AH09A
|    C                     Z-ADD*ZEROS    AH12
|    C                     Z-ADD*ZEROS    AH13
|    C                     MOVEL*BLANKS   AH14
|    C                     MOVEL*BLANKS   AH14A
|    C                     MOVEL*BLANKS   AH15
|    C                     MOVEL*BLANKS   AH15A
|    C                     MOVEL*BLANKS   AH16
|    C                     MOVEL'P2'      AH17
|    C                     MOVEL*BLANKS   AH18
|    C                     MOVEL'V'       AH19
|    C                     MOVEL*BLANKS   AH20
|    C                     Z-ADD*ZEROS    AH21
|    C                     MOVEL*BLANKS   AH22
|    C                     Z-ADD*ZEROS    AH23
|    C                     MOVEL*BLANKS   AH24
|    C                     Z-ADDADYYMD    AH25
|    C                     MOVEL$USER     AH26
B2000C                     TIME           AH97
|    C                     Z-ADDADYYMD    AH98
|    C                     MOVEL$USER     AH99
|    C*
08===C                     ENDSR
     C*==============================================================*
     C*          R40YY ...取得會計科目資料
     C*==============================================================*
09===C           R40YY     BEGSR
|    C*
|    C                     MOVEA*BLANKS   CHK1
|    C                     MOVEA*BLANKS   CHK2
|    C                     MOVEA*BLANKS   CHK3
|    C*<<取得會計科目各欄位名稱>>
|    C           KEYAF     CHAINAF0                  40
| 01-C           *IN40     IFEQ '0'
|   C                     MOVELAF11      AH05A
|   C                     MOVELAF12      AH06A
|   C                     MOVELAF13      AH07A
|   C                     MOVELAF14      AH08A
|   C                     MOVELAF15      AH09A
|   C                     MOVELAF16      AH14A
|   C                     MOVELAF17      AH15A
|   C*
|   C                     MOVELAF11      CHK1,1
|   C                     MOVELAF12      CHK1,2
|   C                     MOVELAF13      CHK1,3
|   C                     MOVELAF14      CHK1,4
|   C                     MOVELAF15      CHK1,5
|   C                     MOVELAF16      CHK1,6
|   C                     MOVELAF17      CHK1,7
| 01-C                     END
|    C* << 輸入欄位說明CHK3 >>
|    C*
|    C                     MOVELAH05      CHK2,1
|    C                     MOVELAH06      CHK2,2
|    C                     MOVELAH07      CHK2,3
|    C                     MOVELAH08      CHK2,4
| 02-C                     DO   4         I       10
| 03-C           CHK2,I    IFNE *BLANKS
|   C           KEYAA     CHAINAA0                  40
| 04-C           *IN40     IFEQ '0'
|   C           AA04      ANDNE*BLANKS
|   C*
|   C                     MOVELCHK2,I    WPARM1
|   C                     MOVELAA05      WPARM2
|   C                     MOVEL'0'       WCHK
|   C                     CALL AA04
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C           CHK3,I    PARM           DEPF   20
| 04*C                     ELSE
|   C                     MOVELCHK2,I    CHK3,I
| 04-C                     END
| 03-C                     END
| 02-C                     END
|    C*
| 05-C           AH09      IFNE 0
|   C                     CALL 'P56'                      *P56(GET'/')
|   C                     PARM AH09      P5601I  80
|   C                     PARM           P5601O 10
|   C                     MOVELP5601O    CHK3,5
| 05-C                     END
|    C*
| 06-C           AH14      IFNE 0
|   C                     CALL 'P55'                      *P55(GET,.)
|   C                     PARM AH14      P5501I 185
|   C                     PARM 2         P5502I  10
|   C                     PARM           P5501O 24
|   C                     MOVELP5501O    CHK3,6
| 06-C                     END
|    C*
| 07-C           AH15      IFNE *BLANK
|   C                     Z-ADD7         I
|   C           KEYAA     CHAINAA0                  40
| 08-C           *IN40     IFEQ '0'
|   C           AA04      ANDNE*BLANKS
|   C*
|   C                     MOVELAH15      WPARM1
|   C                     MOVELAA05      WPARM2
|   C                     MOVEL'0'       WCHK
|   C                     CALL AA04
|   C                     PARM           CHKPGM
|   C                     PARM           TORN    1
|   C           CHK3,7    PARM           DEPF   20
| 08*C*                    ELSE
|   C                     MOVELAH15      CHK3,7
| 08-C                     END
| 07-C                     END
|    C*<<摘要 >>
| 09-C                     DO   7         K       10
| 10-C           CHK3,K    IFNE *BLANKS
|   C                     CALL 'GLS001'
|   C                     PARM AH16      S001I1 80
|   C                     PARM CHK3,K    S001I2 80
|   C                     PARM 49        S001I3  30
|   C           AH16      PARM           S001O1 80
| 10-C                     END
| 09-C                     END
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          R40ZZ ... WRITE GLAHPF
     C*==============================================================*
10===C           R40ZZ     BEGSR
|    C*
|    C                     ADD  1         WAH03
|    C                     Z-ADDWAH03     AH03
|    C                     WRITEAH0
|    C*
10===C                     ENDSR
