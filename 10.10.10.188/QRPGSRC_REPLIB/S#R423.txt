     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM NAME : S#R4201                                    *
     H*    AUTHOR       : A1062   JEANNY LIN                         *
     H*    CREATE DATE  : 79/07/03                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :程式安全管制系統                          *
     H*    PURPOSE      :依程式列印管制資料                        *
     H****************************************************************
     FS#SDLF  IP  E           K        DISK
     FS#SUPF  IF  E           K        DISK
     FS#SPPF  IF  E           K        DISK
     FS#R420P O   E             39     PRINTER
     E                    A03     3   3  8               *PROGRAM TYPE
     ISDL1
     I                                              SD02  L1
     IS#DA01      DS
     I                                        1  10 CENTER
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
     I                                      125 1270@CPY
     I                                      128 1280@PRTID
     I                                      501 510 DSD011
     I                                      511 520 DSD012
     I                                      521 530 DSD021
     I                                      531 540 DSD022
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR RTN100                     *INIT. RTN.
     C   39                EXSR RTN200                     *OVERFLOW
     C*
     C           SD01      CHAINSU0                  40    *NF
     C           *IN40     IFEQ '0'                        *40 1
     C           USER      IFEQ CENTER                     *CENTER
     C           USER      OREQ SU03
     C           USER      OREQ SU01
     C  N38                EXSR RTN300
     C                     SETON                     1038
     C                     WRITEPD1
     C                     END
     C                     END
     C*
     CL1                   SETOF                     38    *L1
     CLR 10                EXSR RTN400
     C*==============================================================*
     C*          RTN100....INITIAL ROUTINE
     C*==============================================================*
     C           RTN100    BEGSR
     C           CENTER    IFEQ *BLANK
     C           *NAMVAR   DEFN           S#DA01
     C                     IN   *NAMVAR
     C                     END
     C                     ENDSR
     C*==============================================================*
     C*          RTN200....NEXR PAGE
     C*==============================================================*
     CSR         RTN200    BEGSR
     C                     WRITEPH1
     C                     SETOF                     39
     C                     ENDSR
     C*==============================================================*
     C*          RTN300....LEVER BREAK
     C*==============================================================*
     CSR         RTN300    BEGSR
     C   10                WRITEPE1
     C           SD02      CHAINSP0                  40    *NF
     C           *IN40     IFEQ '1'                        *40
     C                     MOVE *BLANK    SP02
     C                     MOVE *BLANK    SP04
     C                     MOVE *BLANK    SP05
     C                     MOVE *BLANK    SP06
     C                     ELSE                            *N40
     C*
     C           SP03      IFEQ 'M'                        *3
     C                     MOVE A03,1     DSP03
     C                     ELSE
     C           SP03      IFEQ 'P'
     C                     MOVE A03,2     DSP03
     C                     ELSE
     C                     MOVE A03,3     DSP03
     C                     END
     C                     END                             *3
     C*
     C                     END                             *40
     C*
     C                     WRITEPH1
     C                     ENDSR
     C*==============================================================*
     C*          RTN400....SET ON LR
     C*==============================================================*
     CSR         RTN400    BEGSR
     C                     WRITEPE2
     C                     MOVEL'S#R420P' PRTF
     C                     Z-ADD@CPY      CPY
     C                     Z-ADD@PRTID    PRTID
     C                     CALL 'P68'
     C                     PARM           PRTF   10
     C                     PARM           CPY     30
     C                     PARM           PRTID   20
     C                     ENDSR
**
主畫面主程式副程式
