     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250
     H*
     H*    PROGRAM ID   : JLB021
     H*    PROGRAM NAME : 銷退傳票批次
     H*    AUTHOR       : D910121 CHARLES
     H*    CREATE DATE  : 1993.06.22
     H*    UPDATE DATE  :
     H*    SYSTEM       : 進銷存收TO總帳
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.17 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A*  M004     | 00.04.28 |菸酒稅法實施，增加銷退稅額
     A*---------------------------------------------------------------
     F****************************************************************
     FWFJL21PFIP  E           K        DISK
     FPA#BPF  IF  E           K        DISK
     FMTMEPF  IF  E           K        DISK
     FJLB0213PO   E             39     PRINTER
     IWFJL21R
     I                                              JL2102L3
     I                                              JL2101L2
     I                                              JL2103L1
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$LPRT
      *確認日期
M001MI                                      601 6080F01
M001MI                                      609 6160F02
M001MI                                      617 6220D05
M001MI                                      623 6280D06
      *公司別
M001MI                                      629 629 D01
M001MI                                      630 630 D02
      *辦事處
M001MI                                      631 631 D03
M001MI                                      632 632 D04
      *==============================================================*
      *                    KEY LIST
      *==============================================================*
     C           KEYME     KLIST
     C                     KFLD           ME01
     C                     KFLD           ME02
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
  01-C           *IN10     IFEQ '0'
    C                     EXSR RTN010                     *HEADING
    C                     SETON                     10
  01*C                     ELSE
  02-C           *INL3     IFEQ '1'
    C                     WRITEPE1
    C                     EXSR RTN010                     *HEADING
    C                     SETOF                     39
  02-C                     END
  01-C                     END
     C*
     C   39                EXSR RTN900
     C                     EXSR RTN100                     *READE DETAIL
     C*
     CL1 10                EXSR RTN150
     CL2 10                EXSR RTN200
     CL3 10                EXSR RTN300
     CLR                   EXSR RTN999
      *==============================================================*
      *          RTN010....CHECK HEADING
      *==============================================================*
01===C           RTN010    BEGSR
|    C*公司
|    C                     MOVEL*BLANKS   #B03
|    C           JL2102    CHAIN#B0                  40
|    C                     WRITEPH1
|    C                     SETOF                     39
|    C*
01===C                     ENDSR
      *==============================================================*
      *          RTN100....READE DETAIL
      *==============================================================*
02===C           RTN100    BEGSR
|    C*客戶
|    C                     MOVEL*BLANKS   ME04
|    C                     MOVELJL2105    ME01
|    C                     MOVEL*BLANKS   ME02
|    C           KEYME     CHAINME0                  40
      *日期轉換
M001AC                     Z-ADDJL2101    W6      60
|    C                     CALL 'RES001'
|    C                     PARM 'YMD'     S001I1  3
B2MODC                     PARM W6        S001I2  60
|    C           PL2101    PARM           S001O1  60
      *
|    C   39                EXSR RTN900
|    C                     WRITEPD1
|    C                     SETOF                     20
|    C                     SETON                     3031
|    C                     ADD  JL2106    T12106
M004AC                     ADD  JL2107    T12107
|    C*
02===C                     ENDSR
      *==============================================================*
      *          RTN150....WRITE PT1   SUBTOTAL
      *==============================================================*
03===C           RTN150    BEGSR
|    C*
|    C   39                EXSR RTN900
|    C                     WRITEPT1
|    C                     SETON                     20
|    C                     SETOF                     30
|    C                     ADD  T12106    T22106
|    C                     Z-ADD0         T12106
M004AC                     ADD  T12107    T22107
M004AC                     Z-ADD0         T12107
|    C*
03===C                     ENDSR
      *==============================================================*
      *          RTN200....WRITE PT2   SUBTOTAL
      *==============================================================*
04===C           RTN200    BEGSR
|    C*
|    C   39                EXSR RTN900
|    C                     WRITEPT2
|    C                     SETOF                     31
|    C                     ADD  T22106    T32106
|    C                     Z-ADD0         T22106
M004AC                     ADD  T22107    T32107
M004AC                     Z-ADD0         T22107
|    C*
04===C                     ENDSR
      *==============================================================*
      *          RTN300....WRITE PT3   SUBTOTAL
      *==============================================================*
05===C           RTN300    BEGSR
|    C*
|    C   39                EXSR RTN900
|    C                     WRITEPT3
|    C                     SETOF                     31
|    C                     Z-ADD0         T32106
M004AC                     Z-ADD0         T32107
|    C*
05===C                     ENDSR
      *==============================================================*
      *          RTN900....OVERFLOW
      *==============================================================*
06===C           RTN900    BEGSR
      *
|    C                     WRITEPE1
|    C                     WRITEPH1
|    C                     SETOF                     39
|    C                     SETOF                     203031
      *
06===C                     ENDSR
      *==============================================================*
      *          RTN999....END
      *==============================================================*
07===C           RTN999    BEGSR
      *
| 01-C           *IN10     IFEQ '0'
|   C                     WRITEPH1
|   C                     WRITEPE9
| 01-C                     END
|    C                     WRITEPE2
      *
07===C                     ENDSR
