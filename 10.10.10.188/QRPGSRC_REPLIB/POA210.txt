     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM NAME : POA210                                     *
     H*    AUTHOR       : A1034  STEPHANIE                           *
     H*    CREATE DATE  : 79/07/10                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :銷售系統                                  *
     H*    SUBSYSTEM    :車輛採購                                  *
     H*    REMARK       :車型請購單建立                            *
     F****************************************************************
     FPOZFPF  UF  E           K        DISK                      A
     F                                              KINFDS POZFDS
     F                                              KINFSR ERRSR
     FPOZGPF  UF  E           K        DISK                      A
     F                                              KINFDS POZGDS
     F                                              KINFSR ERRSR
     FPOZHPF  UF  E           K        DISK
     F                                              KINFDS POZHDS
     F                                              KINFSR ERRSR
     FPOZILF01UF  E           K        DISK
     F                                              KINFDS POZIDS
     F                                              KINFSR ERRSR
     FPT#APF  IF  E           K        DISK
     FPT#CPF  IF  E           K        DISK
     FPOZBPF  IF  E           K        DISK
     FPOZCPF  IF  E           K        DISK
     FPOA210D CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
     F            DSPC2                             KIGNORE
     F            DSPC5                             KIGNORE
     E*: FUNCTION DATA :
     E                    WFUN    5   5  6
     E*: A/U/D/I AUT
     E                    WA01        5  1
     E*配備
     E                    WA02       12  3
     E*配備 TEMPARY
     E                    WA03       12  3
     I*
 @   ILDA        UDS
     I                                      101 110 $USER
     I                                      152 161 $USERN
     I                                      111 1160$CHYMD
 @   I                                      135 135 $ADD
 @   I                                      136 136 $UPD
 @   I                                      137 137 $DLT
 @   I                                      138 138 $INQ
 @   I**                                    139 139 $RMK01
 @   I**                                    140 140 $RMK02
 @   I**                                    141 141 $RMK03
 @   I**                                    142 147 $DEP
 @   I**                                    148 151 $STORE
     IPOZFDS      DS
     I                                     *STATUS  WCODE1
     IPOZGDS      DS
     I                                     *STATUS  WCODE2
     IPOZHDS      DS
     I                                     *STATUS  WCODE3
     IPOZIDS      DS
     I                                     *STATUS  WCODE4
     I            DS
     I                                        1  36 WA02
     I                                        1   3 DZG07
     I                                        4   6 DZG08
     I                                        7   9 DZG09
     I                                       10  12 DZG10
     I                                       13  15 DZG11
     I                                       16  18 DZG12
     I                                       19  21 DZG13
     I                                       22  24 DZG14
     I                                       25  27 DZG15
     I                                       28  30 DZG16
     I                                       31  33 DZG17
     I                                       34  36 DZG18
     I            DS
     I                                        1  36 WA03
     I                                        1   3 TZG07
     I                                        4   6 TZG08
     I                                        7   9 TZG09
     I                                       10  12 TZG10
     I                                       13  15 TZG11
     I                                       16  18 TZG12
     I                                       19  21 TZG13
     I                                       22  24 TZG14
     I                                       25  27 TZG15
     I                                       28  30 TZG16
     I                                       31  33 TZG17
     I                                       34  36 TZG18
     I            DS
     I                                        1   7 DZF01
     I                                        1   1 DZF011
     I                                        2   20DZF012
     I                                        3   40DZF013
     I                                        5   70DZF014
     I            DS
     I                                        1   60DZF02
     I                                        1   20DZF021
     I                                        2   20DZF02Y
     I                                        3   40DZF022
     I                                        5   60DZF023
 @   I            DS
 @   I                                        1   1 DOPT
 @   I                                        1   10WNUM
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C           *NAMVAR   DEFN *LDA      LDA
     C*
     C           *LIKE     DEFN DZF02Y    WZF022
     C           *LIKE     DEFN ZG02      WZG02
     C           *LIKE     DEFN ZG03      TZG03
     C           *LIKE     DEFN ZI02      WZI02
     C* FOR KEEP KEY
     C           *LIKE     DEFN ZG02      WZG02K
     C* WZF021 =請購年（西元年個位）+請購月
     C                     Z-ADD0         WZF021  30
     C* FOR WINDOW(?)
     C                     MOVE *BLANKS   WQUERY  1
     C* FOR ARRAY INDEX
     C                     Z-ADD0         W       40
     C* FOR KEYZI1
     C                     MOVE *HIVAL    WZI02
     C*==============================================================*
     C*                    KLIST
     C*==============================================================*
     C           KEY#A     KLIST
     C                     KFLD           #A01
     C                     KFLD           #A02
     C           KEYZG     KLIST
     C                     KFLD           DZF01
     C                     KFLD           WZG02
     C           KEYZH     KLIST
     C                     KFLD           DZF01
     C                     KFLD           WZI02
     C           KEYZI1    KLIST
     C                     KFLD           DZF01
     C                     KFLD           WZG02
     C                     KFLD           WZI02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N99                EXSR RTN010                     *INITIAL
     C           *IN03     DOWEQ'0'
     C*
     C           SCID      CASEQ'SC01'    RTN100
     C           SCID      CASEQ'SC02'    RTN200
     C           SCID      CASEQ'SC03'    RTN300
     C                     END
     C*
     C                     END
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE
     C*==============================================================*
     C           RTN010    BEGSR
     C                     MOVE 'SC01'    SCID    4
     C                     MOVE 'A'       DZF011
     C                     Z-ADD0         DZF012
     C                     Z-ADD0         DZF013
     C                     Z-ADD0         DZF014
     C                     Z-ADD0         DZF02
     C                     MOVE '1'       DOPT
 @   C* FUN AUT
 @   C                     IN   LDA
 @   C                     MOVE $ADD      WA01,1
 @   C                     MOVE $UPD      WA01,2
 @   C                     MOVE $DLT      WA01,4
 @   C                     MOVE $INQ      WA01,5
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN100....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           RTN100    BEGSR
     C*
     C                     EXFMTDSPD1
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C           *IN18     IFEQ '0'
     C                     EXSR RTN110                     *SCR CHK
     C  N99                MOVE 'SC02'    SCID
     C                     ELSE
@    C                     MOVEL'POS210'  S0102I           *FOR RTN990
@    C                     EXSR RTN990                     *AUT CHK
@    C  N99                EXSR RTN180                     *CALL SUBPGM
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN110....SCREEN CHECK
     C*==============================================================*
     C           RTN110    BEGSR
     C*
@    C* CHK AUT *
@ DS C           WA01,WNUM IFNE 'Y'                         WNUM = DOPT
@    C                     SETON                     9099
@    C                     END
@    C   99                GOTO END110
     C*
STARTC*新增時－－＞請購日期需輸入，自動編號
     C*
     C           DOPT      IFEQ '1'                        *ADD
     C***請購日期需輸入
     C           DZF02     IFEQ 0
     C                     SETON                     619899
     C                     ELSE
     C                     CALL 'P04'
     C                     PARM DZF02     P0401I  60
     C                     PARM           P0401O  1
     C           P0401O    IFEQ 'N'
     C                     SETON                     619599
     C                     END
     C                     END
     C   99                GOTO END110
     C*
     C****自動編號
     C* WZF021 =請購年（西元年個位）+請購月
     C           DZF02Y    ADD  1         WZF022
     C           WZF022    MULT 100       WZF021
     C                     ADD  DZF022    WZF021
     C                     MOVE WZF021    S001I1
     C                     MOVEL'A'       S001I1
     C                     CALL 'PTS001'
     C                     PARM           S001I1  4
     C                     PARM           S001O1  30
     C                     MOVELS001I1    DZF01
     C                     MOVE S001O1    DZF01
     C*
END  C                     END
     C*
     C           DOPT      IFNE '1'
     C           DZF01     SETLLZF0                      42
     C           *IN42     IFEQ '0'
     C                     SETON                     609699
     C                     END
     C                     END
     C   99                GOTO END110
     C*刪除時，該請購單不得存於計劃請購變更檔（一）中
     C           DOPT      IFEQ '4'
     C           DZF01     SETLLZH0                      42
     C           *IN42     IFEQ '1'
     C                     SETON                     609499
     C                     END
     C                     END
     C*
     C           END110    ENDSR
     C*==============================================================*
     C*          RTN180....CALL SUBPGM
     C*==============================================================*
     C           RTN180    BEGSR
     C*
     C                     CALL 'POS210'
     C           DZF01     PARM DZF01     S210I1  7
     C                     PARM           S210O1  2
     C*
     C           S210O1    IFEQ '03'
     C                     SETON                     03
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN200....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           RTN200    BEGSR
     C*
     C                     MOVE WFUN,WNUM DFUN
     C                     EXSR RTN205                      *CHK AUT
     C*
     C           DOPT      CASEQ'1'       RTN210           *ADD
     C           DOPT      CASEQ'2'       RTN220           *UPD
     C           DOPT      CASEQ'4'       RTN230           *DEL
     C           DOPT      CASEQ'5'       RTN240           *INQ
     C                     END
     C                     ENDSR
     C*==============================================================*
     C*          RTN205....CHK SUBPGM AUT
     C*==============================================================*
     C           RTN205    BEGSR
     C*
     C*使用者若有使用＜車型資料查詢＞權利時--> SETON IN32
     C*          --> 可輸入明細查詢
     C*
     C                     MOVEL'POI120'  S0102I           *FOR RTN990
     C                     EXSR RTN990                     *AUT CHK
     C           S0101O    IFEQ 'Y'
     C                     SETON                     32
     C                     END
     C*
     C*使用者若有使用＜配備資料查詢＞權利時--> SETON IN33
     C*          --> 可輸入明細查詢
     C*
     C                     MOVEL'POS140'  S0102I           *FOR RTN990
     C                     EXSR RTN990                     *AUT CHK
     C           S0101O    IFEQ 'Y'
     C                     SETON                     33
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN210....ADD DATA --- DSPD2
     C*==============================================================*
     C           RTN210    BEGSR
     C*
     C                     SETON                     31
     C                     SETOF                     343529
     C                     EXSR RTN800                     *INIT SC02 DSPD2
     C*
     C           *IN03     DOWEQ'0'                        *CTL LOOP
     C           SCID      ANDEQ'SC02'                     *CTL LOOP
     C*
     C                     WRITEDSPC1
     C                     WRITEDSPC3
     C                     EXFMTDSPD2
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C*
     C           *IN19     CASEQ'1'       RTN400           訪問記錄
     C                     CAS            RTN810            CHK SCR DSPD2
     C                     END
     C*
     C  N99                MOVE 'SC03'    SCID
     C*  *IN12 ON
     C                     ELSE
     C                     MOVE 'SC01'    SCID
     C                     END                             *IN12 ENDIF
     C                     END                             *IN03 ENDIF
     C*
     C                     END                              ENDDO
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN220....UPD DATA --- DSPD2
     C*==============================================================*
     C           RTN220    BEGSR
     C*
     C                     SETON                     31    *CAN INPUT
     C                     SETOF                     343529*NO INPUT
     C                     EXSR RTN801                     *INT SFLSR
     C                     EXSR RTN830                     *PF-->DSPD2
     C                     EXSR RTN833                     *ADD SFLSR
     C*
     C           *IN03     DOWEQ'0'                        *CTL LOOP
     C           SCID      ANDEQ'SC02'
     C*
     C                     WRITEDSPC1
     C                     WRITESFLCR
     C                     WRITEDSPC3
     C                     EXFMTDSPD2
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C*
     C           *IN19     CASEQ'1'       RTN400           計劃變更
     C                     CAS            RTN810            CHK SCR
     C                     END
     C*
     C  N99                MOVE 'SC03'    SCID
     C* *IN12 ON
     C                     ELSE
     C           *LOVAL    SETLLZF0
     C                     MOVE 'SC01'    SCID
     C                     END                             *IN03 ENDIF
     C                     END                             *IN12 ENDIF
     C*
     C                     END                             *ENDDO
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN230....DEL DATA --- DSPD2 & SFLSR
     C*==============================================================*
     C           RTN230    BEGSR
     C*
     C                     SETOF                     313435*NO INPUT
     C                     SETON                     29    *CAN INPUT
     C                     EXSR RTN801                     *INT SFLSR
     C                     EXSR RTN830                     *PF-->DSPD2
     C                     EXSR RTN833                     *ADD SFLSR
     C*
     C           *IN03     DOWEQ'0'                        *CTL LOOP
     C           SCID      ANDEQ'SC02'
     C*
     C                     WRITEDSPC1
     C                     WRITEDSPC4
     C                     WRITEDSPD2
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C*
     C           *IN25     CASEQ'1'       RTN833         99下一頁
     C           *IN19     CASEQ'1'       RTN400           計劃變更
     C           DOP1      CASNE0         RTN410           車型明細
     C           DZB01     CASNE*BLANKS   RTN420           配備明細
     C                     END
     C*
     C           *IN99     IFEQ '0'                         SAVE
     C                     DELETZF0
     C                     EXSR RTN841                      DLT D
     C                     MOVE 'SC01'    SCID
     C                     END
     C*
     C                     ELSE
     C           *LOVAL    SETLLZF0
     C                     MOVE 'SC01'    SCID
     C                     END                             *IN03 ENDIF
     C                     END                             *IN12 ENDIF
     C*
     C                     END                             *ENDDO
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN240....INQ DATA
     C*==============================================================*
     C           RTN240    BEGSR
     C*
     C* LDA UPDATE TO *LDA
     C                     OUT  LDA
     C*
     C                     CALL 'POI210'
     C           DZF01     PARM DZF01     I210I1  7
     C                     PARM           I210O1  2
     C*
     C           I210O1    IFEQ '03'
     C                     SETON                     03
     C                     END
     C*
     C                     MOVE 'SC01'    SCID
     C                     ENDSR
     C*==============================================================*
     C*          RTN300....'SC03' SCREEN MAIN ROUTINE --- SFLSR
     C*==============================================================*
     C           RTN300    BEGSR
     C*
     C           DOPT      CASEQ'1'       RTN310           *ADD
     C           DOPT      CASEQ'2'       RTN320           *UPD
     C                     END
     C                     ENDSR
     C*==============================================================*
     C*          RTN310....ADD DATA --- SFLSR
     C*==============================================================*
     C           RTN310    BEGSR
     C*
     C                     SETOF                     31
     C                     SETON                     343529
     C                     EXSR RTN801                     *INT SFLSR
     C                     EXSR RTN832                     *ADD BLANKS
     C*
     C           *IN03     DOWEQ'0'                        *CTL LOOP
     C           SCID      ANDEQ'SC03'
     C*
     C                     WRITEDSPC1
     C                     WRITEDSPD2
     C                     WRITEDSPC4
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C*
     C           *IN25     CASEQ'1'       RTN832         99 ADD BLANK
     C           *IN19     CASEQ'1'       RTN400           計劃變更
     C           DOP1      CASNE0         RTN410           車型明細
     C           DZB01     CASNE*BLANKS   RTN420           配備明細
     C                     CAS            RTN811            CHK SFLSR
     C                     END
     C*
     C           *IN99     IFEQ '0'                         SAVE
     C           *IN05     ANDEQ'0'                         SAVE
     C                     EXSR RTN820                      H -> FILE
     C                     WRITEZF0
     C                     EXSR RTN842                      ADD D
     C                     MOVE 'SC01'    SCID
     C                     END
     C*
     C                     ELSE
     C           *LOVAL    SETLLZF0
     C                     MOVE 'SC01'    SCID
     C                     END                             *IN03 ENDIF
     C                     END                             *IN12 ENDIF
     C*
     C                     END                             *ENDDO
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN320....UPD DATA --- SFLSR
     C*==============================================================*
     C           RTN320    BEGSR
     C*
     C                     SETOF                     31
     C                     SETON                     343529
     C                     EXSR RTN801                     *INT SFLSR
     C                     EXSR RTN831                     *ADD SFLSR
     C*
     C           *IN03     DOWEQ'0'                        *CTL LOOP
     C           SCID      ANDEQ'SC03'
     C*
     C                     WRITEDSPC1
     C                     WRITEDSPD2
     C                     WRITEDSPC4
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C*
     C           *IN25     CASEQ'1'       RTN831         99 ADD SFLSR
     C           *IN19     CASEQ'1'       RTN400           計劃變更
     C           DOP1      CASNE0         RTN410           車型明細
     C           DZB01     CASNE*BLANKS   RTN420           配備明細
     C                     CAS            RTN811            CHK SFLSR
     C                     END
     C*
     C           *IN99     IFEQ '0'                         SAVE
     C           *IN05     ANDEQ'0'                         SAVE
     C                     EXSR RTN820                      H -> FILE
     C                     UPDATZF0
     C                     EXSR RTN840                      ADD ZH0
     C                     EXSR RTN842                      ADD D
     C                     MOVE 'SC01'    SCID
     C                     END
     C*
     C                     ELSE
     C           *LOVAL    SETLLZF0
     C                     MOVE 'SC01'    SCID
     C                     END                             *IN03 ENDIF
     C                     END                             *IN12 ENDIF
     C*
     C                     END                             *ENDDO
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN400....計劃變更(PF19)
     C*==============================================================*
     C           RTN400    BEGSR
     C*
     C* SETON 99 --> NOT A/U/D FILE
     C*
     C                     MOVEL'POS220'  S0102I           *FOR RTN990
     C                     EXSR RTN990                     *AUT CHK
     C*
     C           *IN99     IFEQ '0'
     C                     CALL 'POS220'
     C                     PARM DZF01     S220O1  7
     C                     PARM           S220O2  60
     C                     PARM           S220O3  2
     C                     SETON                     99
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN410....車型明細 DOP1
     C*==============================================================*
     C           RTN410    BEGSR
     C*
     C                     MOVE DOP1      WZG02
     C           KEYZG     CHAINZG0                  40
-----C           *IN40     IFEQ '1'
|    C                     SETON                     799599
|----C                     ELSE
|    C                     CALL 'POI120'
|    C                     PARM ZG03      I120I1 15        車型編號
|    C                     PARM           I120O1  2         FUN KEY
|    C                     Z-ADD0         DOP1
-----C                     END                              *IN40END
     C*
     C                     MOVE WZG02K    WZG02
     C           KEYZG     SETGTZG0
     C                     SETON                     99
     C                     ENDSR
     C*==============================================================*
     C*          RTN420....配備明細 DZB01
     C*==============================================================*
     C           RTN420    BEGSR
     C*
     C                     CALL 'POS140'
     C                     PARM DZB01     S140O1  3
     C                     PARM           S140O2  2
     C*
     C                     MOVE *BLANKS   DZB01
     C                     SETON                     99
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN800....INIT 'SC02' -- DSPD2
     C*==============================================================*
     C           RTN800    BEGSR
     C*
     C                     Z-ADD0         DZF03
     C                     MOVE *BLANKS   DZF04
     C                     MOVE *BLANKS   D#A03
     C                     MOVE *BLANKS   DZF05
     C                     MOVE *BLANKS   #C02
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN801....INIT 'SC02' -- SFLSR
     C*==============================================================*
     C           RTN801    BEGSR
     C*
     C                     Z-ADD0         DRRN
     C                     Z-ADD0         WRRN    40
     C                     Z-ADD0         RRN
     C                     Z-ADD0         DSPRCD  40
@    C                     Z-ADD12        WPRCD   40       *PAG RCD
     C                     MOVE 'N'       WBOTOM  1        *BOTTOM
     C*
     C                     SETON                     52    *SFLCLR
     C                     WRITESFLCR
     C                     SETOF                     525053
     C                     SETON                     51    *SFLDSPCTL
     C*
     C           DZF01     SETLLZG0
     C                     ENDSR
     C*==============================================================*
     C*          RTN802....INIT 'SC02' -- SFLSR(DTA)
     C*==============================================================*
     C           RTN802    BEGSR
     C*
     C                     MOVE *BLANKS   DOP
     C                     Z-ADD0         DZG02
     C                     MOVE *BLANKS   DZG03
     C                     MOVE *BLANKS   DZC02
     C                     MOVE *BLANKS   DZG04
     C                     MOVE *BLANKS   D#A031
     C                     Z-ADD0         DZG05
     C                     Z-ADD0         DZG06
     C                     MOVE *BLANKS   DZG07
     C                     MOVE *BLANKS   DZG08
     C                     MOVE *BLANKS   DZG09
     C                     MOVE *BLANKS   DZG10
     C                     MOVE *BLANKS   DZG11
     C                     MOVE *BLANKS   DZG12
     C                     MOVE *BLANKS   DZG13
     C                     MOVE *BLANKS   DZG14
     C                     MOVE *BLANKS   DZG15
     C                     MOVE *BLANKS   DZG16
     C                     MOVE *BLANKS   DZG17
     C                     MOVE *BLANKS   DZG18
     C                     MOVE *BLANKS   ZG19
     C                     MOVE *BLANKS   DCHK
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN810....CHECK 'SC02' --- DSPD2
     C*==============================================================*
     C           RTN810    BEGSR
     C*
     C*CHK SCREEN
     C*
     C           DZF05     IFNE *BLANK
     C                     MOVEL'#C04'    #A01
     C                     MOVE *BLANKS   #A02
     C                     MOVELDZF05     #A02
     C           KEY#A     CHAIN#A0                  40
     C           *IN40     IFEQ '1'
     C                     SETON                     609899
     C                     MOVE *BLANKS   D#A03
     C                     ELSE
     C                     MOVEL#A03      D#A03
     C                     END
     C                     END
     C   99                GOTO END810
     C*
     C                     CALL 'P04'
     C                     PARM DZF02     P0401I  60
     C                     PARM           P0401O  1
     C           P0401O    IFEQ 'N'
     C                     SETON                     619799
     C                     END
     C   99                GOTO END810
     C*
     C           DZF03     IFNE 0
     C                     CALL 'P04'
     C                     PARM DZF03     P0401I  60
     C                     PARM           P0401O  1
     C           P0401O    IFEQ 'N'
     C                     SETON                     629799
     C                     END
     C                     END
     C   99                GOTO END810
     C*
     C           DZF04     IFNE *BLANK
     C           DZF04     CHAIN#C0                  40
     C           *IN40     IFEQ '1'
     C                     SETON                     639899
     C                     MOVE *BLANKS   #C02
     C                     END
     C                     END
     C*
     C           END810    ENDSR
     C*==============================================================*
     C*          RTN811....CHECK 'SC02' --- SFLSR
     C*==============================================================*
     C           RTN811    BEGSR
     C*
     C*CHK SCREEN
     C                     SETOF                     3839
     C                     SETON                     54
     C                     READCSFLSR                    57
     C*
+----C           *IN57     DOWEQ'0'
|    C*
|    C                     SETOF                     636465
|    C                     SETOF                     666768
|    C                     SETOF                     697071
|    C                     SETOF                     727374
|    C                     SETOF                     757677
|    C                     SETOF                     7880
|    C*                                  DATA ENTRY & *IN99 OFF ->CHECK
| +--C   99                GOTO UPD
| |  C*
| |  C           DCHK      IFEQ 'Y'
| |  C           DOP       IFEQ '1'
| |  C                     SETON                     649499
| |  C                     Z-ADDRRN       DRRN
| |  C                     END
| |  C                     ELSE
| |  C           DOP       IFEQ '2'
| |  C           DOP       OREQ '4'
| |  C                     SETON                     649399
| |  C                     Z-ADDRRN       DRRN
| |  C                     END
| |  C                     END
| |  C   99                GOTO UPD
| |  C*
| |  C           DOP       IFEQ '4'
| |  C           DZG06     ANDNE0
| |  C                     SETON                     649299
| |  C                     Z-ADDRRN       DRRN
| |  C                     END
| |  C   99                GOTO UPD
| |  C*
| |  C           DOP       IFEQ '4'
| |  C                     Z-ADDDZG02     WZG02
| |  C           KEYZG     SETLLZI0                      42
| |  C           *IN42     IFEQ '1'
| |  C                     SETON                     648999
| |  C                     Z-ADDRRN       DRRN
| |  C                     END
| |  C                     END
| |  C   99                GOTO UPD
| |  C*
| |  C           DZG03     IFNE *BLANK
| |  C           DZG03     CHAINZC0                  40
| |  C           *IN40     IFEQ '1'
| |  C                     SETON                     659599
| |  C                     Z-ADDRRN       DRRN
| |  C                     MOVE *BLANKS   DZC02
| |  C                     ELSE
| |  C                     MOVELZC02      DZC02
| |  C                     END
| |  C                     ELSE
| |  C           DOP       IFEQ '1'
| |  C                     MOVE TZG03     DZG03
| |  C                     DO   12        W
| |  C                     MOVE WA03,W    WA02,W
| |  C                     END
| |  C                     END
| |  C                     END
| |  C   99                GOTO UPD
| |  C*
| |  C*挑選車色說明
| |  C                     MOVELDZG04     WQUERY
| |  C           WQUERY    IFEQ '?'
| |  C                     CALL 'PTS111'
| |  C                     PARM 'YA03'    S111I1 10
| |  C           DZG04     PARM           S111O1 10
| |  C                     SETON                     38
| |  C                     END
| |  C* CHK 車色
| |  C           DZG04     IFEQ *BLANKS
| |  C                     SETON                     669199
| |  C                     ELSE
| |  C                     MOVEL'YA03'    #A01
| |  C                     MOVE *BLANKS   #A02
| |  C                     MOVELDZG04     #A02
| |  C           KEY#A     CHAIN#A0                  40
| |  C           *IN40     IFEQ '0'
| |  C                     MOVEL#A03      D#A031
| |  C                     ELSE
| |  C                     SETON                     669599
| |  C                     MOVE *BLANKS   D#A031
| |  C                     END
| |  C                     END
| |  C   99                GOTO UPD
| |  C* CHK 數量
| |  C           DZG05     IFEQ 0
| |  C                     SETON                     809199
| |  C                     END
| |  C   99                GOTO UPD
| |  C*
| |  C* CHK 配備
| |  C                     DO   12        W
| |  C*
| |  C           WA02,W    IFNE *BLANKS
| |  C           WA02,W    SETLLZB0                      42
| |  C           *IN42     IFEQ '0'
| |  C                     ADD  66        W
| |  C                     MOVE '1'       *IN,W
| |  C                     SETON                     9599
| |  C                     Z-ADDRRN       DRRN
| |  C                     END
| |  C                     END
| |  C*
| |  C  N99                END
| |  C*
| |  C*
| +--C           UPD       TAG
|    C                     UPDATSFLSR
|    C           *IN38     IFEQ '1'
|    C                     SETON                     3739
|    C                     WRITEDSPC1
|    C                     WRITEDSPD2
|    C                     WRITEDSPC4
|    C                     WRITESFLCR
|    C                     SETOF                     3738
|    C                     END
|    C                     MOVE DZG03     TZG03
|    C                     DO   12        W
|    C                     MOVE WA02,W    WA03,W
|    C                     END
|    C                     READCSFLSR                    57
|    C* READC DOW LOOP
+----C                     END
     C*
     C   39                SETON                     99
     C           END820    ENDSR
     C*==============================================================*
     C*          RTN820....SCREEN TO FILE --- H
     C*==============================================================*
     C           RTN820    BEGSR
     C*
     C                     MOVE DZF01     ZF01
     C                     Z-ADDDZF02     ZF02
     C                     Z-ADDDZF03     ZF03
     C                     MOVE DZF04     ZF04
     C                     MOVE DZF05     ZF05
     C                     TIME           ZFXX
     C                     Z-ADD$CHYMD    ZFYY
     C                     MOVE $USER     ZFZZ
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN821....SCREEN TO FILE --- D
     C*==============================================================*
     C           RTN821    BEGSR
     C*
     C                     MOVE DZF01     ZG01
     C                     Z-ADDDZG02     ZG02
     C                     MOVE DZG03     ZG03
     C                     MOVE DZG04     ZG04
     C                     Z-ADDDZG05     ZG05
     C                     Z-ADDDZG06     ZG06
     C                     MOVE DZG07     ZG07
     C                     MOVE DZG08     ZG08
     C                     MOVE DZG09     ZG09
     C                     MOVE DZG10     ZG10
     C                     MOVE DZG11     ZG11
     C                     MOVE DZG12     ZG12
     C                     MOVE DZG13     ZG13
     C                     MOVE DZG14     ZG14
     C                     MOVE DZG15     ZG15
     C                     MOVE DZG16     ZG16
     C                     MOVE DZG17     ZG17
     C                     MOVE DZG18     ZG18
     C                     TIME           ZGXX
     C                     Z-ADD$CHYMD    ZGYY
     C                     MOVE $USER     ZGZZ
     C           DOP       IFEQ '1'
     C                     MOVE *BLANK    ZG19
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN830....FILE TO SCREEN --- DSPD2
     C*==============================================================*
     C           RTN830    BEGSR
     C*
     C           DZF01     CHAINZF0                  40
     C                     Z-ADDZF02      DZF02
     C                     Z-ADDZF03      DZF03
     C                     MOVE ZF04      DZF04
     C                     MOVE ZF05      DZF05
     C*
     C* GET 部門說明
     C           DZF05     IFNE *BLANKS
     C                     MOVEL'#C04'    #A01
     C                     MOVE *BLANKS   #A02
     C                     MOVELDZF05     #A02
     C           KEY#A     CHAIN#A0                  40
     C           *IN40     IFEQ '1'
     C                     MOVE *BLANKS   D#A03
     C                     ELSE
     C                     MOVEL#A03      D#A03
     C                     END
     C                     END
     C*
     C* GET 員工姓名
     C           DZF04     IFNE *BLANKS
     C           DZF04     CHAIN#C0                  40
     C           *IN40     IFEQ '1'
     C                     MOVE *BLANKS   #C02
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN831....FILE TO SCREEN --- SFLSR(FOR ADD OR UPD)
     C*==============================================================*
     C           RTN831    BEGSR
     C                     SETOF                     54
     C                     Z-ADDWRRN      RRN
     C                     ADD  WPRCD     DSPRCD
     C           RRN       DOWLTDSPRCD
     C           DZF01     READEZG0                      44*NF/EOF
     C*
     C           *IN44     IFEQ '0'
     C           ZG19      COMP *BLANKS                  34
     C           ZG06      COMP 0                        35
     C                     EXSR RTN83A
     C                     MOVE DZG03     TZG03
     C                     DO   12        W
     C                     MOVE WA02,W    WA03,W
     C                     END
     C                     ELSE
     C                     EXSR RTN802                     *INIT SFLDTA
     C                     SETON                     3435
     C                     END
     C*
     C                     ADD  1         RRN
     C                     WRITESFLSR
     C                     END
     C*
     C                     MOVE ZG02      WZG02K
     C                     Z-ADDRRN       WRRN
     C                     Z-ADDRRN       DRRN       50
     C                     ENDSR
     C*==============================================================*
     C*          RTN832....BLANK TO SCREEN --- SFLSR
     C*==============================================================*
     C           RTN832    BEGSR
     C*
     C                     EXSR RTN802                     *INIT SFLDTA
     C                     SETOF                     54
     C                     SETON                     3435
     C*
     C                     Z-ADDWRRN      RRN
     C                     ADD  WPRCD     DSPRCD
     C           RRN       DOWLTDSPRCD
     C                     ADD  1         RRN
     C                     WRITESFLSR
     C                     END
     C*
     C                     Z-ADDRRN       WRRN
     C                     Z-ADDRRN       DRRN       50
     C                     ENDSR
     C*==============================================================*
     C*          RTN833....FILE TO SCREEN --- SFLSR(FOR INQ)
     C*==============================================================*
     C           RTN833    BEGSR
     C           WBOTOM    IFEQ 'Y'
     C                     SETON                         97*LST PAG
     C                     Z-ADDWRRN      DRRN
     C                     ELSE
     C                     EXSR RTN83B                     *ADD SFL DTA
     C                     END
     C                     ENDSR
     C*==============================================================*
     C*          RTN83A....FILE TO SCREEN --- MOVE
     C*==============================================================*
     C           RTN83A    BEGSR
     C*
     C                     MOVE *BLANKS   DOP
     C                     Z-ADDZG02      DZG02
     C                     MOVE ZG03      DZG03
     C                     MOVE ZG04      DZG04
     C                     Z-ADDZG05      DZG05
     C                     Z-ADDZG06      DZG06
     C                     MOVE ZG07      DZG07
     C                     MOVE ZG08      DZG08
     C                     MOVE ZG09      DZG09
     C                     MOVE ZG10      DZG10
     C                     MOVE ZG11      DZG11
     C                     MOVE ZG12      DZG12
     C                     MOVE ZG13      DZG13
     C                     MOVE ZG14      DZG14
     C                     MOVE ZG15      DZG15
     C                     MOVE ZG16      DZG16
     C                     MOVE ZG17      DZG17
     C                     MOVE ZG18      DZG18
     C                     MOVE 'Y'       DCHK
     C*
     C* GET 車型名稱
     C           ZG03      CHAINZC0                  40
     C           *IN40     IFEQ '0'
     C                     MOVELZC02      DZC02
     C                     ELSE
     C                     MOVE *BLANKS   DZC02
     C                     END
     C*
     C* GET 車色
     C                     MOVEL'YA03'    #A01
     C                     MOVE *BLANKS   #A02
     C                     MOVELDZG04     #A02
     C           KEY#A     CHAIN#A0                  40
     C           *IN40     IFEQ '1'
     C                     MOVE *BLANKS   D#A031
     C                     ELSE
     C                     MOVEL#A03      D#A031
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN83B....FILE TO SCREEN --- SFLSR(FOR INQ)
     C*==============================================================*
     C           RTN83B    BEGSR
     C                     SETOF                     54
     C                     Z-ADDWRRN      RRN
     C                     ADD  WPRCD     DSPRCD
     C           RRN       DOWLTDSPRCD
     C           DZF01     READEZG0                      44*NF/EOF
     C*
     C           *IN44     IFEQ '0'
     C                     EXSR RTN83A
     C                     ADD  1         RRN
     C                     WRITESFLSR
     C                     ELSE
     C                     Z-ADDRRN       DSPRCD
     C                     MOVE 'Y'       WBOTOM
     C                     SETON                         53
     C                     END
     C*
     C                     END
     C*
     C                     MOVE ZG02      WZG02K
     C                     Z-ADDRRN       WRRN
     C                     Z-ADDRRN       DRRN       50
     C                     ENDSR
     C*==============================================================*
     C*          RTN840....UPDAT ZH0
     C*==============================================================*
     C           RTN840    BEGSR
     C*
     C           KEYZH     SETGTZH0
     C           DZF01     REDPEZH0                      45
     C           *IN45     IFEQ '0'
     C                     Z-ADDDZF03     ZH06
     C                     MOVE DZF04     ZH07
     C                     MOVE DZF05     ZH08
     C                     TIME           ZHXX
     C                     Z-ADD$CHYMD    ZHYY
     C                     MOVE $USER     ZHZZ
     C                     UPDATZH0
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN841....DLT D
     C*==============================================================*
     C           RTN841    BEGSR
     C*
     C           DZF01     SETLLZG0
     C           DZF01     READEZG0                      44
     C           *IN44     DOWEQ'0'
     C                     DELETZG0
     C           DZF01     READEZG0                      44
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN842....SAVE D
     C*==============================================================*
     C           RTN842    BEGSR
     C*
     C                     READCSFLSR                    57
     C           *IN57     DOWEQ'0'
     C*
     C           DOP       IFEQ '1'
     C                     MOVE *HIVAL    WZG02
     C           KEYZG     SETGTZG0
     C           DZF01     REDPEZG0                      45
     C           *IN45     IFEQ '1'
     C                     Z-ADD1         DZG02
     C                     ELSE
     C           ZG02      ADD  1         DZG02
     C                     END
     C                     EXSR RTN821
     C                     WRITEZG0
     C                     END
     C*
     C* KEYZG  = DZF01 + WZG02
     C* KEYZI1 = DZF01 + WZG02 + WZI02
     C           DOP       IFEQ '2'
     C                     Z-ADDDZG02     WZG02
     C           KEYZG     CHAINZG0                  40
     C                     EXSR RTN821
     C                     UPDATZG0
     C           KEYZI1    SETGTZI0
     C           KEYZG     REDPEZI0                      45
     C           *IN45     IFEQ '0'
     C                     Z-ADDDZG05     ZI05
     C                     UPDATZI0
     C                     END
     C                     END
     C*
     C* KEYZG = DZF01 + WZG02
     C           DOP       IFEQ '4'
     C                     Z-ADDDZG02     WZG02
     C           KEYZG     CHAINZG0                  40
     C                     DELETZG0
     C                     END
     C*
     C                     READCSFLSR                    57
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN990....AUT CHK (CALL SUBPGM)
     C*==============================================================*
     C           RTN990    BEGSR
     C*
     C                     CALL 'S#S01'
     C                     PARM $USER     S0101I 10
     C                     PARM           S0102I 10
     C                     PARM           S0101O  1
     C           S0101O    IFEQ 'N'
     C                     SETON                     9099
     C                     END
     C*
     C                     ENDSR
@    C*==============================================================*
@    C*          ERRSR ....MOVE DATA TO 'SC02' SCREEN
@    C*==============================================================*
@    C           ERRSR     BEGSR
@    C*
@    C           WCODE1    IFEQ 01021
@    C           WCODE1    OREQ 01218
@    C           WCODE2    OREQ 01021
@    C           WCODE2    OREQ 01218
@    C           WCODE3    OREQ 01021
@    C           WCODE3    OREQ 01218
@    C           WCODE4    OREQ 01021
@    C           WCODE4    OREQ 01218
@    C                     CALL 'P50'
@    C           DMSG      PARM           P5001O 78
@    C                     SETON                     2899
@    C                     MOVEL'*DETC'   WENDSR  6
@    C                     ELSE
@    C                     MOVE *BLANKS   WENDSR
@    C                     END
@    C*
@    C                     MOVE 'SC01'    SCID
@    C                     ENDSRWENDSR
**
新增修改      刪除查詢
