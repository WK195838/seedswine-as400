     H****************************************************************
     H*                                                              *
     H*    PROGRAM-ID   : SOA0311                                    *
     H*    PROGRAM NAME :                                            *
     H*    AUTHOR       :                                            *
     H*    CREATE DATE  : 85/11/11                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :                                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.05 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     H****************************************************************
     FSOSJPF  UF  E           K        DISK                      A
     F*SOSIPF  IF  E           K        DISK
     FMTMAPF  IF  E           K        DISK
     FPA#BPF  IF  E           K        DISK
     FMTMEPF  IF  E           K        DISK
     FSOA0322DCF  E                    WORKSTN      KINFDS DSPFDS
     F                                        RRN2  KSFILE SFLSR1
     E*---------------------------------------------------------------*
     E                    @A01        5  1               作業權利
     E                    @AR1       99 10
     E                    @AR2       99  3 0
     E*---------------------------------------------------------------*
     I*CURAPR APSTITON
     IDSPFDS      DS
     I                                    B 370 3710#CSR
     I                                    B 378 3790#DRRN
     I*TRAN DATE
     IPTDA01      DS
     I                                        1   1 $PFMT
     I                                        2   2 $PTYPE
     I*LOCAL DATA AREA
     I           UDS
     I                                      101 110 $USER
     I                                      125 1270$CPY
     I                                      135 135 $ADD
     I                                      136 136 $UPD
     I                                      137 137 $DLT
     I                                      138 138 $INQ
     I                                      139 139 $RMK01
     I                                      140 140 $RMK02
     I                                      141 141 $RMK03
     I                                      142 147 $DEP
     I                                      152 161 $USERN
     I                                      173 176 $COR
B2MODI                                      201 2080$A8YMD
     I                                      119 1240$A6MDY
B2MODI                                      217 218 $PRTCD
B2MODI                                      601 610 WSI02A
B2MODI                                      602 606 WSI02
     I*
     C*==============================================================*
     C*                    DEFN
     C*==============================================================*
     C* SFLSR1
     C           *LIKE     DEFN RRN2      @DRCD            DSP REC NUM
     C           *LIKE     DEFN RRN2      @PRCD            PAGE REC
     C           *LIKE     DEFN RRN2      @LRRN            LAST REC NUM
     C           *LIKE     DEFN RRN2      @LPAGE           LAST PAGE
     C           *LIKE     DEFN RRN2      @PRRN            PROMPT USE
     C           *LIKE     DEFN RRN2      PRRN2            BOTTOM RRN
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C                     PARM           PSI01   2          COMP ID
     C                     PARM           PSI02  10          CUST ID
     C*                    PARM           PSJ03   9          PROD ID
     C                     PARM           RTNID   2          RETURN ID
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEYSJ     KLIST
     C                     KFLD           PSI01
     C                     KFLD           PSI02
     C                     KFLD           DSJ03
     C           KEYSJ#    KLIST
     C                     KFLD           PSI01
     C                     KFLD           PSI02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INITIAL
     C                     EXSR R1C00                      *SFL HEAD
     C*
  01-C           SCID      DOWEQ'SC03'
    C                     EXSR R2000
  01-C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
01===C           R0100     BEGSR
|    C* FUN AUT
|    C                     MOVE $ADD      @A01,1
|    C                     MOVE $UPD      @A01,2
|    C                     MOVE $DLT      @A01,4
|    C                     MOVE $INQ      @A01,5
|    C*                    Z-ADD$A6MDY    DATE
|    C                     Z-ADD$A6MDY    FDATE   80
|    C                     MOVEL'SC03'    SCID
|    C                     MOVELPSI02     WSI02A
|    C*
01===C                     ENDSR
     C*==============================================================*
     C*          R1C00 ....INIT 'SC03'
     C*==============================================================*
02===C           R1C00     BEGSR
|    C*
|    C* UPD
|    C                     EXSR R1C10                      *INIT SFL
|    C                     EXSR R1C20
|    C                     EXSR R1C30
|    C*SFLDSP
| 01-C           @LRRN     IFGT 0
|   C                     Z-ADD1         DRRN2
|   C                     SETON                     50    *SFLDSP
| 01*C                     ELSE
|   C                     SETON                     50    *NO DTA
|   C*                    MOVEL'UPT2050' ERRID
|   C*                    MOVEL'PTMF'    ERRF
| 01-C                     END
|    C*
02===C                     ENDSR
     C*=============================================================*
     C*          R1C10 ....SUBFILE INIT
     C*=============================================================*
03===C           R1C10     BEGSR
|    C*
|    C                     Z-ADD12        @SFTOP  20       *SFL TOP LIN
|    C                     Z-ADD21        @SFBOT  20       *SFL BOT LIN
|    C* SFL SETTING ++++++++++++++++++++++++++++++++++++++++++++++++++
|    C                     Z-ADD12        @PRCD            *PGE RCD
|    C                     Z-ADD1         @LNUM   20       *明細行數
|    C                     Z-ADD0         DRRN2
|    C                     Z-ADD0         RRN2
|    C                     Z-ADD0         PRRN2
|    C                     Z-ADD0         @LRRN            *LAST RRN
|    C                     Z-ADD0         @DRCD            *DSP RCD
|    C                     SETOF                     51    *SFLDSPCTL
|    C*
|    C                     SETON                     52    *SFLCLR
|    C                     EXSR R1C11
|    C                     WRITESFLCR1
|    C                     SETOF                     52
|    C*
|    C                     SETON                     51    *SFLDSPCTL
|    C                     SETOF                     5053  *SFLDSP,END
|    C*
03===C                     ENDSR
     C*==============================================================*
     C*          R1C11 .... HEADING
     C*==============================================================*
04===C           R1C11     BEGSR
|    C*
|    C           PSI01     CHAIN#B0                  40
|    C                     MOVEL#B03      D#B03
|    C           WSI02     CHAINME0                  40    *客戶代號
| 01-C           *IN40     IFEQ '0'
|   C                     MOVELME04      DME04            *簡稱
| 01*C                     ELSE
|   C                     MOVE *BLANK    DME04
| 01-C                     END
|    C*
04===C                     ENDSR
     C*==============================================================*
     C*          R1C20 ....DETAIL DATA TO SFL
     C*==============================================================*
05===C           R1C20     BEGSR
|    C*
|    C                     SETOF                     54    *54
|    C*
|    C           KEYSJ#    SETLLSJ0
|    C           KEYSJ#    READESJ0                      46
|    C*
| 01-C           *IN46     DOWEQ'0'
|   C                     EXSR R1C21
|   C                     WRITESFLSR1
|   C           KEYSJ#    READESJ0                      46
| 01-C                     END
|    C*
|    C                     Z-ADDRRN2      @LRRN
|    C*
05===C                     ENDSR
     C*==============================================================*
     C*          R1C21 ....DETAIL DATA TO SFL
     C*==============================================================*
06===C           R1C21     BEGSR
|    C*
|    C                     ADD  1         RRN2
|    C                     SETON                     31
|    C                     SETOF                     30
|    C                     MOVEL*BLANK    DOPT2
|    C*
|    C* << OTHER FILE FIELD TO SFLSR >>
|    C                     MOVE SJ03      DSJ03
|    C                     MOVE SJ04      DSJ04
|    C                     MOVE SJ05      DSJ05
|    C                     MOVE SJ07      DSJ07
|    C                     MOVE SJ06      DSJ06
|    C           SJ03      CHAINMA0                  40
|    C                     MOVE MA03      DMA03
|    C* << HIDDEN FIELD TO SFLSR >>
06===C                     ENDSR
     C*==============================================================*
     C*          R1C30 ....SFL DATA INIT
     C*==============================================================*
07===C           R1C30     BEGSR
|    C*
|    C                     SETOF                     54    *54 FOR BLANK LOOP
|    C                     Z-ADD@LRRN     RRN2
|    C*
|    C           @LRRN     DIV  @PRCD     @LPAGE
|    C                     ADD  1         @LPAGE
|    C           @LPAGE    MULT @PRCD     @DRCD
|    C*
|    C                     EXSR R1C31                      *BLANK SFL
|    C*
| 01-C           RRN2      DOWLT@DRCD
|   C                     ADD  1         RRN2
|   C                     WRITESFLSR1
| 01-C                     END
|    C*
|    C                     Z-ADDRRN2      @LRRN
|    C                     Z-ADDRRN2      DRRN2
|    C*
07===C                     ENDSR
     C*==============================================================*
     C*          R1C31 ....SFL INIT DATA ADD ( DUA03 = '1''2''3''4''5')
     C*==============================================================*
08===C           R1C31     BEGSR
|    C*
|    C                     SETOF                     3130
|    C                     MOVEL*BLANK    DOPT2            .DEL OPT
|    C                     MOVEL'Y'       DNEW2            .NEW RECORD
|    C*<<OTHER SFLSR FIELD CLEAR >>
|    C                     MOVE *BLANK    DSJ03
|    C                     MOVE *BLANK    DMA03
|    C                     MOVE *BLANK    DSJ04
|    C                     MOVE *BLANK    DSJ05
|    C                     MOVE *BLANK    DSJ07
|    C                     MOVE *BLANK    DSJ06
|    C* << HIDDEN FIELD TO SFLSR >>
|    C*                    Z-ADD0         HSV02
|    C*
08===C                     ENDSR
     C*==============================================================*
     C*          R2E00 ....欄位查詢 (PF04 : CALL SUB-PROGRAM)
     C*==============================================================*
09===C           R2E00     BEGSR
|    C*
|    C           #CSR      DIV  256       #LIN             *LINE
|    C                     MVR            #COL             *COLUMN
|    C* RETURN CODE CHECK
|    C*
09===C                     ENDSR
     C*==============================================================*
     C*          R2000 .... 'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
10===C           R2000     BEGSR
|    C*
|    C                     WRITEDSPC1
|    C                     EXFMTSFLCR1
|    C                     MOVEA*ALL'0'   *IN,60
|    C                     Z-ADD*ZERO     #LIN
|    C                     Z-ADD*ZERO     #COL
|    C                     Z-ADD#DRRN     DRRN2
|    C*
| 01-C           *IN03     IFEQ '1'                        *END
|   C                     MOVEL'EXIT'    SCID    4
|   C                     MOVEL'03'      RTNID
|   C                     GOTO E2000
| 01-C                     END
|    C*
| 02-C           *IN12     IFEQ '1'                        *PREVIOUS
|   C                     MOVEL'EXIT'    SCID
|   C                     MOVEL'12'      RTNID
|   C                     GOTO E2000
| 02-C                     END
|    C*
| 03-C           *IN08     IFEQ '1'                        *PROMPT
|   C                     EXSR R1C00
|   C                     GOTO E2000
| 03-C                     END
|    C*
| 04-C           *IN25     IFEQ '1'                        *NEXT PAGE
|   C                     EXSR R1C30
|   C                     GOTO E2000
| 04-C                     END
|    C*
| 05-C*          *IN27     IFEQ '1'                        *START KEY
|   C*                    EXSR R1C00
|   C*                    GOTO E2000
| 05-C*                    END
|    C*
|    C                     EXSR R2B00                      *SC02 CHK
|    C*
| 06-C           *IN99     IFEQ '0'                        *CHK OK
|   C           *IN05     ANDEQ'0'
|   C                     EXSR R2D00                      SCR-->FILE
| 06-C                     END
|    C*
| 07-C*          POINT     IFEQ 'Y'
|   C*                    EXSR R1C00
| 07-C*                    END
|    C*
10===C           E2000     ENDSR
     C*==============================================================*
     C*          R2B00 ....'SC02' CHECK  HEADING
     C*==============================================================*
11===C           R2B00     BEGSR
|    C*
|    C* CHECK HEADING (SFLCR)
|    C* CHECK DETAIL (SFLSR)
|    C                     SETON                     54
|    C                     READCSFLSR1                   57
|    C*
| 01-C           *IN57     DOWEQ'0'
|   C           *IN99     ANDEQ'0'
|   C                     EXSR R2B10
|   C                     UPDATSFLSR1
|   C*
| 02-C           *IN99     IFEQ '1'
|   C                     Z-ADDRRN2      DRRN2
| 02*C                     ELSE
|   C  N57                READCSFLSR1                   57
| 02-C                     END
|   C*
| 01-C                     END
|    C   99                GOTO E2B00
|    C*
11===C           E2B00     ENDSR
     C*==============================================================*
     C*          R2B10 ....DETAIL CHECK
     C*==============================================================*
12===C           R2B10     BEGSR
|    C*
| 01-C           DOPT2     IFEQ '4'
|   C                     GOTO E2B20
| 01-C                     END
|    C*
| 02-C           DOPT2     IFEQ '2'
|   C           DNEW2     ANDEQ'Y'
|   C                     MOVEL'CPD6A64' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     9960
|   C   99                GOTO E2B20
| 02-C                     END
|    C*
| 03-C           DOPT2     IFEQ '1'
|   C           DNEW2     ANDNE'Y'
|   C                     MOVEL'CPD6A64' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     996031
|   C   99                GOTO E2B20
| 03-C                     END
|    C*
| 04-C           DOPT2     IFNE *BLANK
|   C*
| 05-C           DOPT2     IFEQ '1'
|   C           DNEW2     ANDEQ'Y'
|   C*檢查產品代號
| 06-C           DSJ03     IFEQ *BLANK
|   C                     MOVEL'UPT1001' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     9961
|   C                     SETOF                     31
| 06*C                     ELSE
|   C           DSJ03     CHAINMA0                  40
| 07-C           *IN40     IFEQ '0'
|   C                     MOVELMA03      DMA03            *產品簡稱
| 07*C                     ELSE
|   C                     MOVEL'UPT4012' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     9961
| 07-C                     END
| 06-C                     END
|   C   99                GOTO E2B20
|   C*
|   C*檢查ＳＯＳＪＰＦ中產品代號是否已存在
|   C           KEYSJ     CHAINSJ0                  40
| 08-C           *IN40     IFEQ '0'
|   C                     MOVEL'UPT3002' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     9961
|   C                     SETOF                     31
|   C   99                GOTO E2B20
| 08-C                     END
|   C*
| 05-C                     END
|   C*
| 09-C           DOPT2     IFEQ '2'
|   C                     SETON                     31
| 09-C                     END
|   C*
| 10-C           DSJ04     IFLT 1
|   C                     MOVEL'UPT1001' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     9962
|   C   99                GOTO E2B20
| 10-C                     END
|   C*
| 11-C           DSJ05     IFLT 1
|   C                     MOVEL'UPT1001' ERRID
|   C                     MOVEL'PTMF'    ERRF
|   C                     SETON                     9963
|   C   99                GOTO E2B20
| 11-C                     END
|   C*
|   C           DSJ04     MULT DSJ05     DSJ06
|   C*
| 04-C                     END
|    C*
12===C           E2B20     ENDSR
     C*==============================================================*
     C*          R2D00 ....FILE PROCESS
     C*==============================================================*
13===C           R2D00     BEGSR
|    C*
|    C* DETAIL
|    C                     READCSFLSR1                   57
|    C*
|    C                     SETOF                     54
| 01-C           *IN57     DOWEQ'0'
|   C*
| 02-C           DOPT2     IFNE '4'
|   C           DOPT2     ANDNE*BLANK
|   C                     EXSR R2D10
|   C                     SETOF                     30
| 02-C                     END
|   C*
| 03-C           DOPT2     IFEQ '4'
|   C                     CALL 'WDS000'
|   C                     PARM 'N'       DLTFLG  1
|   C*
| 04-C           DLTFLG    IFEQ 'Y'
|   C           KEYSJ     CHAINSJ0                  40
|   C                     DELETSJ0
|   C                     SETON                     3031
| 04-C                     END
|   C*
| 03-C                     END
|   C                     UPDATSFLSR1
|   C                     READCSFLSR1                   57
|   C*
| 01-C                     END
|    C*
13===C                     ENDSR
     C*==============================================================*
     C*          R2D10 ....FILE PROCESS
     C*==============================================================*
14===C           R2D10     BEGSR
|    C*
|    C                     SETON                     31
|    C                     SETOF                     30
|    C*
|    C           KEYSJ     CHAINSJ0                  40
|    C                     EXSR R2D11                      * MOVE DATA
|    C*
| 01-C           DOPT2     IFEQ '1'
|   C                     WRITESJ0
| 01-C                     END
|    C*
| 02-C           DOPT2     IFEQ '2'
|   C                     UPDATSJ0
| 02-C                     END
|    C                     MOVEL*BLANK    DOPT2
|    C                     MOVE 'Y'       POINT   1
|    C*
14===C           E2D10     ENDSR
     C*==============================================================*
     C*          R2D11 .... MOVE DATA
     C*==============================================================*
15===C           R2D11     BEGSR
|    C*
|    C                     MOVELPSI01     SJ01
|    C                     MOVELPSI02     SJ02
|    C                     MOVELDSJ03     SJ03
|    C                     MOVELDSJ04     SJ04
|    C                     MOVELDSJ05     SJ05
|    C                     MOVELDSJ06     SJ06
|    C                     MOVELDSJ07     SJ07
|    C                     MOVEL'R'       SJ10
|    C                     MOVELWSI02     SJ11
B2REM ***************************
B2REM ****  CONVERSION START  ***
B2REM ***************************
B2OLDC*                    Z-ADD920331    SJ16
B2VFYC                     Z-ADD19920331  SJ16
B2REM ***************************
B2REM *****  CONVERSION END  ****
B2REM ***************************
B2000C                     Z-ADD$A8YMD    SJXX
|    C*                    MOVEL          SJYY
|    C                     MOVEL$USER     SJZZ
|    C                     MOVEL*BLANK    DNEW2            .HIDE FIELD
|    C*
15===C                     ENDSR
     C*==============================================================*
