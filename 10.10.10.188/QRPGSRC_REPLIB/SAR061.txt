      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : SAR061                                        *
      * AUTHER       : A1084  CHARLES                                *
      * CREATE DATE  : 81/05/04                                      *
      * SYSTEM       :人頭馬匯東                                   *
      * SUBSYSTEM    :銷售分析系統                                 *
      *  REMARK      :產品標準箱數彙總表                           *
      ****************************************************************
     FSAR061D CF  E                    WORKSTN
     F                                              KINFDS DSPFDS
     FSAR061Z O   E             39     PRINTER                        UC
     ILDA        UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$PRTID
     I                                      600 600 COMP
      *產品
     I                                      601 609 DSD03S
     I                                      610 618 DSD03E
      *SELECT起迄日期
      *YY/MM/DD
     I                                      619 6240YMDS
     I                                      625 6300YMDE
     I                                      631 6360DSC39S
     I                                      637 6420DSC39E
     I                                      648 648 DSEL
      *單瓶
     I                                      720 720 DSEL2
      *產品組別
     I                                      797 797 DMA11
     I                                      798 798 DMA11S
     I                                      799 799 DMA11E
M001AI*(YYYY/MM/DD)
M001AI                                      850 857 YMD8S
M001AI                                      858 865 YMD8E
     I* FOR F4 HELP -- GET CURSOR POSITION
     IDSPFDS      DS
     I                                    B 370 3710#CSR
      *==============================================================*
      *          IDENTIFY A PARAMETER LIST
      *==============================================================*
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C           *IN98     PARM *IN98     IN98    1
     C           *IN97     PARM *IN97     IN97    1
      *==============================================================*
      *                    KLIST
      *==============================================================*
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C  N97N98             EXSR RTN010                     .INIT SCREEN
  01-C           *IN03     DOUEQ'1'
    C           *IN99     OREQ '0'
    C                     EXFMTDSPD1
    C                     MOVEA*ALL'0'   *IN,60
  02-C           *IN03     IFEQ '0'
  03-C           *IN04     IFEQ '1'
    C**********************
    C*  F4 PROMPT PROCESS *
    C**********************
    C                     EXSR RTNF4
  03*C                     ELSE
    C                     EXSR RTN100                     .CHECK      ?
  03-C                     END
  02-C                     END
  01-C                     END
     C*
     C                     EXSR OUTLDA
     C*
     C   03                SETON                     LR
     C                     RETRN
      *===============================================================*
      *          RTN010....INIT SCREEN
      *===============================================================*
01===C           RTN010    BEGSR
|    C*
|    C           *NAMVAR   DEFN *LDA      LDA
|    C                     IN   LDA
|    C                     Z-ADD2         $EVR
|    C                     MOVEL$USER     AUT     3
| 04-C           AUT       IFNE 'D91'
|   C           AUT       ANDNE'REP'
|   C                     SETON                     31
| 04-C                     END
|    C                     MOVE *BLANK    COMP
|    C                     MOVE *BLANK    DSD03S
|    C                     MOVE *BLANK    DSD03S
|    C                     Z-ADD0         DSC39S
|    C                     Z-ADD0         DSC39E
|    C                     MOVE *BLANK    DMA11
|    C                     MOVE *BLANK    DMA11E
|    C                     MOVE *BLANK    DMA11S
|    C                     Z-ADD0         YMDS
|    C                     Z-ADD0         YMDE
|    C                     MOVEL'F'       DSEL
|    C                     MOVE 'A'       DSEL2
|    C                     MOVEL'SCR001'  APPSCR  6
|    C*
01===C                     ENDSR
      *===============================================================*
      *          RTN100....CHECK SCREEN
      *===============================================================*
02===C           RTN100    BEGSR
|    C* CHK種類
| 01-C           DMA11     IFEQ *BLANKS
|   C                     MOVE *LOVAL    DMA11S
|   C                     MOVE *HIVAL    DMA11E
| 01*C                     ELSE
|   C                     MOVE DMA11     DMA11S
|   C                     MOVE DMA11     DMA11E
| 01-C                     END
|    C*產品
| 02-C           DSD03S    IFEQ *BLANK
|   C                     MOVE *LOVAL    DSD03S
| 02-C                     END
|    C*
| 03-C           DSD03E    IFEQ *BLANK
|   C                     MOVE *HIVAL    DSD03E
| 03-C                     END
|    C*
| 04-C           DSD03S    IFGT DSD03E
|   C                     SETON                     6061
|   C                     SETON                     9299
| 04-C                     END
|    C   99                GOTO END100
|    C*
|    C*(CHECK DATE)-日期
| 05-C           DSC39S    IFEQ 0
M001DC*                    Z-ADD010101    DSC39S
M001AC                     Z-ADD010192    DSC39S
|   C                     CALL 'P09'
|   C                     PARM DSC39S    P0901I  60
|   C                     PARM           P0901O  1
| 06-C           P0901O    IFEQ 'N'
|   C                     SETON                     629399
| 06-C                     END
| 05-C                     END
|    C   99                GOTO END100
|    C*
| 07-C           DSC39E    IFEQ 0
M001DC*                    Z-ADD123199    DSC39E
M001AC                     Z-ADD123129    DSC39E
|   C                     CALL 'P09'
|   C                     PARM DSC39E    P0901I  60
|   C                     PARM           P0901O  1
| 08-C           P0901O    IFEQ 'N'
|   C                     SETON                     639399
| 08-C                     END
| 07-C                     END
|    C   99                GOTO END100
|    C*日期轉換
|    C                     CALL 'RES001'
|    C                     PARM 'MDY'     S001I1  3
|    C                     PARM DSC39S    S001I2  60
|    C           YMDS      PARM           S001O1  60
M001AC                     MOVE YMDS      HB2@F6  6
M001AC                     EXSR HB@68L
M001AC                     MOVE HB2@T8    YMD8S
|    C*
|    C                     CALL 'RES001'
|    C                     PARM 'MDY'     S001I1  3
|    C                     PARM DSC39E    S001I2  60
|    C           YMDE      PARM           S001O1  60
M001AC                     MOVE YMDE      HB2@F6  6
M001AC                     EXSR HB@68L
M001AC                     MOVE HB2@T8    YMD8E
|    C**
M001DC*          YMDS      IFGT YMDE
M001AC           YMD8S     IFGT YMD8E
|   C                     SETON                     6263
|   C                     SETON                     9299
| 09-C                     END
|    C   99                GOTO END100
      *
      *SCREEN LOG
|    C                     OPEN SAR061Z
|    C                     WRITEPH1
|    C                     CLOSESAR061Z
      *
|    C*
02===C           END100    ENDSR
      *===============================================================*
      *          RTNF4 ....F4 HELP
      *===============================================================*
03===C           RTNF4     BEGSR
|    C*
|    C                     SETON                     99
|    C********************
|    C*  GET ROW,COL     *
|    C********************
|    C           #CSR      DIV  256       D#ROW            *ROW
|    C                     MVR            D#COL            *COL
|    C** 游標位置即為(D#ROW,D#COL)
| 01-C           APPSCR    IFEQ 'SCR001'
|   C*
| 02-C           D#ROW     IFEQ 18
|   C           D#COL     ANDGE39
|   C           D#COL     ANDLE40
|   C                     CALL 'WDS011'
|   C           $PRTID    PARM $PRTID    S011O1  20
|   C                     GOTO ENDF4
| 02-C                     END
|   C*
| 03-C           D#ROW     IFEQ 8
|   C           D#COL     ANDGE38
|   C           D#COL     ANDLE46
|   C                     CALL 'WDS001'
|   C                     PARM           DSD03S
|   C                     GOTO ENDF4
| 03-C                     END
|   C*
| 04-C           D#ROW     IFEQ 8
|   C           D#COL     ANDGE50
|   C           D#COL     ANDLE58
|   C                     CALL 'WDS001'
|   C                     PARM           DSD03E
|   C                     GOTO ENDF4
| 04-C                     END
|   C*
| 01-C                     END
|    C*
03===C           ENDF4     ENDSR
      *===============================================================*
      *          OUTLDA....OUT LDA
      *===============================================================*
04===C           OUTLDA    BEGSR
|    C*
|    C                     OUT  *NAMVAR
|    C*
04===C           ENDLDA    ENDSR
     C/COPY HBP2CVTR
