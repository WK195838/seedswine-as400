      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : IMR0601                                       *
      * AUTHER       : ANDY                                          *
      * CREATE DATE  : 81/08/12                                      *
      * UPDATE DATE  :                                               *
      * SYSTEM       :人頭馬匯東                                   *
      * SUBSYSTEM    :基本資料子系統                               *
      * REMARK       :良壞轉換明細表                               *
     /*=============================================================*/
     /* CODE   AUTHOR.     DATE    DESCRIPTION                   */
     /*-------------------------------------------------------------*/
     /* M001  MICHELLE   99.01.04  Y2K MODIFY                    */
     /*=============================================================*/
      ****************************************************************
     FREWF32  IP  E           K        DISK
     FPA#APF  IF  E           K        DISK
     FMTMAPF  IF  E           K        DISK
     FIMR060P O   E             39     PRINTER
     IWF320
     I                                              IC05  L1
     I                                              ID03  L2
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      128 1280$PRTID
     I                                      601 601 DID01
     I                                      602 606 DIC03
     I                                      611 619 DID03S
     I                                      621 629 DID03E
     I*(MM/DD/YY)
     I                                      631 6360DIC05S
     I                                      641 6460DIC05E
     I                                      650 650 DSEL
     I*(YY/MM/DD)
     I*M001M                                651 6560WIC05S
     I*M001M                                661 6660WIC05E
M001MI                                      651 6580WIC05S
M001MI                                      661 6680WIC05E
     I*
      *==============================================================*
      *                    KEY LIST
      *==============================================================*
     C           KEY#A     KLIST
     C                     KFLD           #A01
     C                     KFLD           #A02
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C  N10                EXSR RTN010                     *INIT
     C*
     C                     EXSR RTN100                     *CHAIN DATA
     C*
     CL1 10                EXSR RTNL1
     CL2 10                EXSR RTNL2
     CLR                   EXSR RTNLR
      *==============================================================*
      *          RTN010....INIT VALUE
      *==============================================================*
     C           RTN010    BEGSR
     C*
     C                     Z-ADD0         ID04D
     C                     Z-ADD0         ID04P
     C                     Z-ADD0         ID04T
     C                     Z-ADD0         T1TOT1
     C                     Z-ADD0         T1TOT2
     C                     Z-ADD0         T1TOT3
     C                     Z-ADD0         T2TOT1
     C                     Z-ADD0         T2TOT2
     C                     Z-ADD0         T2TOT3
     C                     Z-ADD0         T3TOT1
     C                     Z-ADD0         T3TOT2
     C                     Z-ADD0         T3TOT3
     C*
     C           DSEL      IFEQ '1'
     C                     MOVE '良→壞'WDSEL
     C                     ELSE
     C                     MOVE '壞→良'WDSEL
     C                     END
     C*
     C                     MOVE *BLANK    #A01
     C                     MOVE *BLANK    #A02
     C                     MOVEL'STORAGE' #A01
     C                     MOVELDIC03     #A02
     C*
     C           KEY#A     CHAIN#A0                  40
     C*
     C   40                MOVE *BLANK    #A03
     C*
     C                     SETON                     3031
     C*
     C                     EXSR RTN900                     *PROC SKIP
     C*
     C                     ENDSR
      *==============================================================*
      *          RTN100....CHAIN DATA
      *==============================================================*
     C           RTN100    BEGSR
     C*
     C           ID03      CHAINMA0                  40
     C*
     C   40                MOVE *BLANK    MA03
     C*
M001AC                     Z-ADDIC05      W6      60
|    C                     CALL 'RES001'
|    C                     PARM 'YMD'     S001I1  3
|    C*M001M               PARM IC05      S001I2  60
M001MC                     PARM W6        S001I2  60
|    C           PIC05     PARM           S001O1  60
     C*
     C           ID04      MULT MA31      DMA31
     C           ID04      MULT MA32      DMA32
     C           ID04      MULT MA33      DMA33
     C                     ADD  ID04      ID04D
     C                     ADD  ID04      ID04P
     C                     ADD  ID04      ID04T
     C                     ADD  DMA31     T1TOT1
     C                     ADD  DMA32     T1TOT2
     C                     ADD  DMA33     T1TOT3
     C                     ADD  DMA31     T2TOT1
     C                     ADD  DMA32     T2TOT2
     C                     ADD  DMA33     T2TOT3
     C                     ADD  DMA31     T3TOT1
     C                     ADD  DMA32     T3TOT2
     C                     ADD  DMA33     T3TOT3
     C*
     C                     EXSR RTN900                     *PROC SKIP
     C                     WRITEPD1
     C*
     C                     SETOF                     3031
     C*
     C                     ENDSR
      *==============================================================*
      *          RTNL1.....PROC L1
      *==============================================================*
     C           RTNL1     BEGSR
     C*
     C           *INLR     IFEQ '1'
     C           *INL2     OREQ '1'
     C           *IN39     OREQ '1'
     C                     SETON                     99
     C                     ELSE
     C                     SETOF                     99
     C                     END
     C*
     C                     WRITEPD2
     C                     Z-ADD0         ID04D
     C                     Z-ADD0         T1TOT1
     C                     Z-ADD0         T1TOT2
     C                     Z-ADD0         T1TOT3
     C*
     C                     SETON                     31
     C*
     C                     ENDSR
      *==============================================================*
      *          RTNL2.....PROC L2
      *==============================================================*
     C           RTNL2     BEGSR
     C*
     C           *INLR     IFEQ '1'
     C           *IN39     OREQ '1'
     C                     SETON                     99
     C                     ELSE
     C                     SETOF                     99
     C                     END
     C*
     C                     WRITEPD3
     C                     Z-ADD0         ID04P
     C                     Z-ADD0         T2TOT1
     C                     Z-ADD0         T2TOT2
     C                     Z-ADD0         T2TOT3
     C*
     C                     SETON                     30
     C*
     C                     ENDSR
      *==============================================================*
      *          RTNLR.....PROC LR
      *==============================================================*
     C           RTNLR     BEGSR
     C*
     C           *IN10     IFEQ '0'
     C                     WRITEPH1
     C                     WRITEPE3
     C                     ELSE
     C                     WRITEPE2
     C                     END
     C*
     C                     ENDSR
      *==============================================================*
      *          RTN900....PROC SKIP
      *==============================================================*
     C           RTN900    BEGSR
     C*
     C           *IN10     IFEQ '0'
     C                     WRITEPH1
     C                     SETOF                     39
     C                     SETON                     10
     C                     END
     C*
     C           *IN39     IFEQ '1'
     C                     WRITEPE1
     C                     WRITEPH1
     C                     SETOF                     39
     C                     END
     C*
     C                     ENDSR
