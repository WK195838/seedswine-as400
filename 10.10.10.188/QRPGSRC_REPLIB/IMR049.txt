      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : IMR049                                        *
      * AUTHER       : A1139  JANE                                   *
      * CREATE DATE  : 81/04/22                                      *
      * UPDATE DATE  :                                               *
      * SYSTEM       :人頭馬匯東                                   *
      * SUBSYSTEM    :庫存系統                                     *
      * REMARK       :異動明細表                                   *
     /*=============================================================*/
     /* CODE   AUTHOR.     DATE    DESCRIPTION                   */
     /*-------------------------------------------------------------*/
     /* M001  MICHELLE   98.12.23  Y2K MODIFY                    */
     /*=============================================================*/
      ****************************************************************
     FIMR049D CF  E                    WORKSTN
     F                                              KINFDS DSPFDS
     ILDA        UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
 @   I                                      139 139 $RMK01
     I                                      501 505 $WRHUS
     I                                      513 513 $WRHTY
     I                                      598 5990$PRTID
     I                                      603 607 DIR02
     I                                      603 603 DIR02L
     I                                      608 609 DIR05S
     I                                      610 611 DIR05E
     I                                      612 612 CODE
     I                                      613 613 DSEL1
     I*(MM/DD/YY)
     I                                      630 6350DIR04S
     I                                      636 6410DIR04E
     I*(YY/MM/DD)
M001MI*                                     650 6550YIR04S
M001MI*                                     656 6610YIR04E
M001MI                                      650 6570YIR04S
M001MI                                      658 6650YIR04E
     I*
     I                                      671 679 DIR03S
     I                                      681 689 DIR03E
     I* FOR F4 HELP -- GET CURSOR POSITION
     IDSPFDS      DS
     I                                    B 370 3710#CSR
      *==============================================================*
      *          IDENTIFY A PARAMETER LIST
      *==============================================================*
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C           *IN98     PARM *IN98     IN98    1
     C           *IN97     PARM *IN97     IN97    1
      *==============================================================*
      *                    KLIST
      *==============================================================*
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C  N97N98             EXSR RTN010                     .INIT SCREEN
     C                     EXSR RTN020
  01-C           *IN03     DOUEQ'1'
    C           *IN99     OREQ '0'
    C                     EXFMTDSPD1
    C                     MOVEA*ALL'0'   *IN,60
  02-C           *IN03     IFEQ '0'
  03-C           *IN04     IFEQ '1'
    C**********************
    C*  F4 PROMPT PROCESS *
    C**********************
    C                     EXSR RTNF4
  03*C                     ELSE
    C                     EXSR RTN100                     .CHECK      ?
  03-C                     END
  02-C                     END
  01-C                     END
     C*
     C                     EXSR OUTLDA
     C*
     C   03                SETON                     LR
     C                     RETRN
      *===============================================================*
      *          RTN010....INIT SCREEN
      *===============================================================*
01===C           RTN010    BEGSR
|    C*
|    C           *NAMVAR   DEFN *LDA      LDA
|    C                     IN   LDA
|    C                     Z-ADD2         $EVR
|    C                     MOVEL$USER     AUT     3
|    C           AUT       IFNE 'D91'
|    C           AUT       ANDNE'REP'
|    C                     SETON                     31
|    C                     END
|    C                     MOVE *BLANK    DIR02
|    C                     MOVE *BLANK    DIR05S
|    C                     MOVE *BLANK    DIR05E
|    C                     MOVE *BLANK    DIR03S
|    C                     MOVE *BLANK    DIR03E
|    C                     MOVE *BLANK    CODE
|    C                     MOVE *BLANK    DSEL1
|    C                     Z-ADD0         DIR04S
|    C                     Z-ADD0         DIR04E
|    C                     Z-ADD0         YIR04S
|    C                     Z-ADD0         YIR04E
M001AC                     Z-ADD0         W6      60
|    C                     MOVEL'SCR001'  APPSCR  6
|    C*
01===C                     ENDSR
      *===============================================================*
      *          RTN020....INIT SCREEN
      *===============================================================*
02===C           RTN020    BEGSR
|    C*
|    C                     SETON                     38
| 01-C           $WRHUS    IFNE '*ALL '
|   C           $WRHUS    ANDNE*BLANKS
|   C                     SETOF                     38
|   C                     MOVEL$WRHUS    DIR02
| 01-C                     END
|    C*
02===C                     ENDSR
      *===============================================================*
      *          RTN100....CHECK SCREEN
      *===============================================================*
03===C           RTN100    BEGSR
|    C***
|    C*物流點
|    C***
| 01-C           DIR02     IFEQ *BLANK
|   C                     SETON                     619599
| 01-C                     END
|    C   99                GOTO END100
|    C***
| 02-C           $WRHUS    IFNE '*ALL  '
| 03-C           $WRHUS    IFNE *BLANKS
| 04-C           DIR02     IFNE $WRHUS
|   C                     SETON                     619699
| 04-C                     END
| 03*C                     ELSE
| 05-C           DIR02L    IFNE $WRHTY
|   C                     SETON                     619699
| 05-C                     END
| 03-C                     END
| 02-C                     END
|    C   99                GOTO END100
|    C*
|    C*產品代號
|    C***
| 06-C           DIR03S    IFEQ *BLANK
|   C                     MOVE *LOVAL    DIR03S
| 06-C                     END
|    C*
| 07-C           DIR03E    IFEQ *BLANK
|   C                     MOVE *HIVAL    DIR03E
| 07-C                     END
|    C*
| 08-C           DIR03S    IFGT DIR03E
|   C                     SETON                     6768
|   C                     SETON                     9299
| 08-C                     END
|    C   99                GOTO END100
|    C*
|    C*異動代碼
|    C***
| 09-C           DIR05S    IFEQ *BLANK
|   C                     MOVE *LOVAL    DIR05S
| 09-C                     END
|    C*
| 10-C           DIR05E    IFEQ *BLANK
|   C                     MOVE *HIVAL    DIR05E
| 10-C                     END
|    C*
| 11-C           DIR05S    IFGT DIR05E
|   C                     SETON                     6263
|   C                     SETON                     9299
| 11-C                     END
|    C   99                GOTO END100
|    C***
|    C*(CHECK DATE)-異動日期
|    C***
| 12-C           DIR04S    IFEQ 0
|   C                     Z-ADD010101    DIR04S
| 12*C                     ELSE
|   C                     CALL 'P09'
|   C                     PARM DIR04S    P0901I  60
|   C                     PARM           P0911O  1
| 13-C           P0911O    IFEQ 'N'
|   C                     SETON                     649399
| 13-C                     END
|   C   99                GOTO END100
| 12-C                     END
|    C*
| 14-C           DIR04E    IFEQ 0
|   C                     Z-ADD123199    DIR04E
| 14*C                     ELSE
|   C                     CALL 'P09'
|   C                     PARM DIR04E    P0901I  60
|   C                     PARM           P0911O  1
| 15-C           P0911O    IFEQ 'N'
|   C                     SETON                     659399
| 15-C                     END
|   C   99                GOTO END100
| 14-C                     END
|    C*
|    C***
|    C*(CHANGE TO YY/MM/DD)
|    C***
|    C                     CALL 'RES001'
|    C                     PARM 'MDY'     S001I1  3
|    C                     PARM DIR04S    S001I2  60
|    C*M001M     YIR04S    PARM           S001O1  60
M001MC           W6        PARM           S001O1  60
M001AC                     MOVE W6        HB2@F6  6
M001AC                     EXSR HB@68L                     + CENTURY 6->8 LEFT
M001AC                     MOVE HB2@T8    YIR04S
|    C*
|    C                     CALL 'RES001'
|    C                     PARM 'MDY'     S001I1  3
|    C                     PARM DIR04E    S001I2  60
|    C*M001M     YIR04E    PARM           S001O1  60
M001MC           W6        PARM           S001O1  60
M001AC                     MOVE W6        HB2@F6  6
M001AC                     EXSR HB@68L                     + CENTURY 6->8 LEFT
M001AC                     MOVE HB2@T8    YIR04E
|    C***
|    C*(CHENK STRART <= END DATE)
|    C***
| 16-C           YIR04S    IFGT YIR04E
|   C                     SETON                     6465
|   C                     SETON                     9199
| 16-C                     END
|    C   99                GOTO END100
|    C***
|    C*選擇內容---A OR B
|    C***
| 17-C           CODE      IFEQ *BLANK
|   C                     SETON                     669599
| 17-C                     END
|    C   99                GOTO END100
|    C*
03===C           END100    ENDSR
      *===============================================================*
      *          RTNF4 ....F4 HELP
      *===============================================================*
04===C           RTNF4     BEGSR
|    C*
|    C                     SETON                     99
|    C********************
|    C*  GET ROW,COL     *
|    C********************
|    C           #CSR      DIV  256       D#ROW            *ROW
|    C                     MVR            D#COL            *COL
|    C** 游標位置即為(D#ROW,D#COL)
| 01-C           APPSCR    IFEQ 'SCR001'
|   C*
|   C*產品代號
| 02-C           D#ROW     IFEQ 7
| 03-C           D#COL     IFGE 38
|   C           D#COL     ANDLE46
|   C                     CALL 'WDS001'
|   C                     PARM           DIR03S
|   C                     GOTO ENDF4
| 03-C                     END
|   C*
| 04-C           D#COL     IFGE 50
|   C           D#COL     ANDLE58
|   C                     CALL 'WDS001'
|   C                     PARM           DIR03E
|   C                     GOTO ENDF4
| 04-C                     END
|   C*
| 02-C                     END
|   C*
| 02-C           D#ROW     IFEQ 18
|   C           D#COL     ANDGE38
|   C           D#COL     ANDLE39
|   C                     CALL 'WDS011'
|   C           $PRTID    PARM $PRTID    S011O1  20
|   C                     GOTO ENDF4
| 02-C                     END
|   C*
| 01-C                     END
|    C*
04===C           ENDF4     ENDSR
      *===============================================================*
      *          OUTLDA....OUT LDA
      *===============================================================*
05===C           OUTLDA    BEGSR
|    C*
|    C                     OUT  *NAMVAR
|    C*
05===C           ENDLDA    ENDSR
M001AC/COPY HBP2CVTR
