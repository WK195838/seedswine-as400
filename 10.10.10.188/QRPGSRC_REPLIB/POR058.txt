      ****************************************************************
      *   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
      *                                                              *
      * PROGRAM NAME : POR058D                                       *
      * AUTHOR       : AN MING HSIA                                  *
      * CREATE DATE  : 10/26/93                                      *
      * UPDATE DATE  :                                               *
      * SYSTEM       :人頭馬匯東                                   *
      * SUBSYSTEM    :採購資料系統                                 *
      * REMARK       :國內供應商進貨明細表                         *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.12.17 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
      ****************************************************************
     FPOR058D CF  E                    WORKSTN
     F                                              KINFDS DSPFDS
     I*
     ILDA        UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$PRTID
     I*
     I                                      601 605 DPH06S
     I                                      606 610 DPH06E
     I                                      611 619 DPI04S
     I                                      621 629 DPI04E
     I                                      630 634 DPH05S
     I                                      635 639 DPH05E
     I*(MM/YY)
     I                                      650 6530DPH04S
     I                                      650 6510PH04SM
     I                                      652 6530PH04SY
     I                                      654 6570DPH04E
     I                                      654 6550PH04EM
     I                                      656 6570PH04EY
     I*(YY/MM)
     I*M001M                                660 6630YPH04S
     I*M001M                                664 6670YPH04E
     I*M001M                                660 6610YPH4SY
     I*M001M                                662 6630YPH4SM
     I*M001M                                664 6650YPH4EY
     I*M001M                                666 6670YPH4EM
M001MI                                      660 6650YPH04S
M001MI                                      666 6710YPH04E
M001MI                                      660 6630YPH4SY
M001MI                                      664 6650YPH4SM
M001MI                                      666 6690YPH4EY
M001MI                                      670 6710YPH4EM
     I* FOR F4 HELP -- GET CURSOR POSITION
     IDSPFDS      DS
     I                                    B 370 3710#CSR
      *==============================================================*
      *          IDENTIFY A PARAMETER LIST
      *==============================================================*
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C           *IN98     PARM *IN98     IN98    1
     C           *IN97     PARM *IN97     IN97    1
      *==============================================================*
      *                    KLIST
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C*
     C  N97N98             EXSR RTN010                     .INIT SCREEN
  01-C           *IN03     DOUEQ'1'
    C           *IN99     OREQ '0'
    C                     EXFMTDSPD1
    C                     MOVEA*ALL'0'   *IN,60
    C                     Z-ADD0         D#ROW
    C                     Z-ADD0         D#COL
  02-C           *IN03     IFEQ '0'
  03-C           *IN04     IFEQ '1'
    C**********************
    C*  F4 PROMPT PROCESS *
    C**********************
    C                     EXSR RTNF4
  03*C                     ELSE
    C                     EXSR RTN100                     .CHECK      ?
  03-C                     END
  02-C                     END
  01-C                     END
     C*
     C                     EXSR OUTLDA
     C*
     C   03                SETON                     LR
     C                     RETRN
      *===============================================================*
      *          RTN010....INIT SCREEN
      *===============================================================*
01===C           RTN010    BEGSR
|    C*
|    C           *NAMVAR   DEFN *LDA      LDA
|    C                     IN   LDA
|    C                     Z-ADD2         $EVR
|    C                     MOVEL$USER     AUT     3
|    C           AUT       IFNE 'D91'
|    C           AUT       ANDNE'REP'
|    C                     SETON                     31
|    C                     END
|    C                     MOVE *BLANK    DPH06S
|    C                     MOVE *BLANK    DPH06E
|    C                     MOVE *BLANK    DPI04S
|    C                     MOVE *BLANK    DPI04E
|    C                     MOVE *BLANK    DPH05S
|    C                     MOVE *BLANK    DPH05E
|    C                     Z-ADD0         DPH04S
|    C                     Z-ADD0         DPH04E
|    C                     Z-ADD0         YPH04S
|    C                     Z-ADD0         YPH04E
|    C                     MOVEL'SCR001'  APPSCR  6
|    C*
01===C                     ENDSR
      *===============================================================*
      *          RTN100....CHECK SCREEN
      *===============================================================*
02===C           RTN100    BEGSR
|    C*
|    C*供應商代號
| 01-C           DPH06S    IFEQ *BLANKS
|   C                     MOVE *LOVAL    DPH06S
| 01-C                     END
|    C*
| 02-C           DPH06E    IFEQ *BLANKS
|   C                     MOVE *HIVAL    DPH06E
| 02-C                     END
|    C*
| 03-C           DPH06S    IFGT DPH06E
|   C                     SETON                     6061
|   C                     SETON                     9399
| 03-C                     END
|    C   99                GOTO END100
|    C*產品代號
| 04-C           DPI04S    IFEQ *BLANKS
|   C                     MOVE *LOVAL    DPI04S
| 04-C                     END
| 05-C           DPI04E    IFEQ *BLANKS
|   C                     MOVE *HIVAL    DPI04E
| 05-C                     END
|    C*
| 06-C           DPI04S    IFGT DPI04E
|   C                     SETON                     6465
|   C                     SETON                     9399
| 06-C                     END
|    C   99                GOTO END100
|    C*進貨物流點
| 04-C           DPH05S    IFEQ *BLANKS
|   C                     MOVE *LOVAL    DPH05S
| 04-C                     END
| 05-C           DPH05E    IFEQ *BLANKS
|   C                     MOVE *HIVAL    DPH05E
| 05-C                     END
|    C*
| 06-C           DPH05S    IFGT DPH05E
|   C                     SETON                     6263
|   C                     SETON                     9399
| 06-C                     END
|    C   99                GOTO END100
|    C*日期
| 07-C           DPH04S    IFEQ 0
|   C*M001M               Z-ADD0101      DPH04S
M001MC                     Z-ADD0192      DPH04S
| 07*C                     ELSE
|   C           PH04SM    IFGT 12
|   C           PH04SM    ORLT 1
|   C                     SETON                     669499
| 08-C                     END
| 07-C                     END
|    C   99                GOTO END100
|    C*
| 09-C           DPH04E    IFEQ 0
|   C*M001M               Z-ADD1299      DPH04E
M001MC                     Z-ADD1229      DPH04E
| 09*C                     ELSE
|   C           PH04EM    IFGT 12
|   C           PH04EM    ORLT 1
|   C                     SETON                     679499
| 10-C                     END
| 09-C                     END
|    C   99                GOTO END100
|    C*
     C                     Z-ADDPH04SM    YPH4SM
     C*M001M               Z-ADDPH04SY    YPH4SY
M001AC                     MOVE PH04SY    HB2@YY  2
M001AC                     EXSR HB@24Y
M001AC                     MOVE HB2@Y4    YPH4SY
     C                     Z-ADDPH04EM    YPH4EM
     C*M001M               Z-ADDPH04EY    YPH4EY
M001AC                     MOVE PH04EY    HB2@YY  2
M001AC                     EXSR HB@24Y
M001AC                     MOVE HB2@Y4    YPH4EY
|    C*
| 11-C           YPH04S    IFGT YPH04E
|   C                     SETON                     6667
|   C                     SETON                     9399
| 11-C                     END
|    C   99                GOTO END100
|    C*
02===C           END100    ENDSR
      *===============================================================*
      *          RTNF4 ....F4 HELP
      *===============================================================*
03===C           RTNF4     BEGSR
|    C*
|    C                     SETON                     99
|    C********************
|    C*  GET ROW,COL     *
|    C********************
|    C           #CSR      DIV  256       D#ROW            *ROW
|    C                     MVR            D#COL            *COL
|    C** 游標位置即為(D#ROW,D#COL)
| 01-C           APPSCR    IFEQ 'SCR001'
|   C*
|   C**供應商代號
| 02-C           D#ROW     IFEQ 6
|   C           D#COL     ANDGE38
|   C           D#COL     ANDLE42
|   C                     CALL 'WDS002'
|   C                     PARM           DPH06S
|   C                     GOTO ENDF4
| 02-C                     END
|   C*
|   C**
| 03-C           D#ROW     IFEQ 6
|   C           D#COL     ANDGE49
|   C           D#COL     ANDLE53
|   C                     CALL 'WDS002'
|   C                     PARM           DPH06E
|   C                     GOTO ENDF4
| 03-C                     END
|   C*產品代號
| 04-C           D#ROW     IFEQ 8
|   C           D#COL     ANDGE38
|   C           D#COL     ANDLE46
|   C                     CALL 'WDS001'
|   C                     PARM           DPI04S
|   C                     GOTO ENDF4
| 04-C                     END
|   C*
|   C**
| 05-C           D#ROW     IFEQ 8
|   C           D#COL     ANDGE50
|   C           D#COL     ANDLE58
|   C                     CALL 'WDS001'
|   C                     PARM           DPI04E
|   C                     GOTO ENDF4
| 05-C                     END
|   C*
| 06-C           D#ROW     IFEQ 18
|   C           D#COL     ANDGE38
|   C           D#COL     ANDLE39
|   C                     CALL 'WDS011'
|   C           $PRTID    PARM $PRTID    S011O1  20
|   C                     GOTO ENDF4
| 06-C                     END
|   C*
| 01-C                     END
|    C*
03===C           ENDF4     ENDSR
      *===============================================================*
      *          OUTLDA....OUT LDA
      *===============================================================*
04===C           OUTLDA    BEGSR
|    C*
|    C                     OUT  *NAMVAR
|    C*
04===C           ENDLDA    ENDSR
M001AC/COPY HBP2CVTR
