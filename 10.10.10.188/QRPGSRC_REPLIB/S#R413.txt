     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM NAME : S#R4101                                    *
     H*    AUTHOR       : A1062   JEANNY LIN                         *
     H*    CREATE DATE  : 79/07/03                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :程式安全管制系統                          *
     H*    PURPOSE      :依使用者列印管制資料                      *
     H*    SYSTEM       :富大二期                                  *
     H****************************************************************
     FS#SDPF  IP  E           K        DISK
     FS#SUPF  IF  E           K        DISK
     FS#SPPF  IF  E           K        DISK
     FS#R410P O   E             39     PRINTER
     ISD0
     I                                              SD01  L1
     IS#DA01      DS
     I                                        1  10 CENTER
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
     I                                      125 1270@CPY
     I                                      128 1280@PRTID
     I                                      501 510 DSD011
     I                                      511 520 DSD012
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C  N10                EXSR RTN100                     *INIT. RTN.
     C   39                EXSR RTN200                     *OVERFLOW
     C   38                EXSR RTN300                     *L1
     C*
     C           *IN37     IFEQ '1'                        *37:有權用
     C           SD02      CHAINSP0                  40    *NF
     C   40                MOVE *BLANK    SP02
     C                     WRITEPD1
     C                     END
     C*
     CL1 10                SETON                     38    *L1
     CLR 10                EXSR RTN400
     C*==============================================================*
     C*          RTN100....INITIAL ROUTINE
     C*==============================================================*
     C           RTN100    BEGSR
     C           CENTER    IFEQ *BLANK
     C           *NAMVAR   DEFN           S#DA01
     C                     IN   *NAMVAR
     C                     SETOF                     37
     C                     END
     C*
     C                     EXSR RTN300
     C   37                SETON                     10
     C                     ENDSR
     C*==============================================================*
     C*          RTN200....NEXR PAGE
     C*==============================================================*
     CSR         RTN200    BEGSR
     C                     WRITEPE1
     C                     WRITEPH1
     C                     SETOF                     39
     C                     ENDSR
     C*==============================================================*
     C*          RTN300....LEVER BREAK
     C*==============================================================*
     CSR         RTN300    BEGSR
     C           SD01      CHAINSU0                  40    *NF
     C           *IN40     IFEQ '0'                        *40
     C           USER      IFEQ CENTER                     *CTR 01 03
     C           USER      OREQ SU03
     C           USER      OREQ SU01
     C   10                WRITEPE1
     C                     MOVE SU02      SU021
     C                     MOVE SU03      PSU03
     C           PSU03     CHAINSU0                  40    *NF
     C  N40                MOVE SU02      SU031
     C   40                MOVE *BLANK    SU031
     C                     WRITEPH1
     C                     SETON                     37
     C                     END                             *CTR 01 03
     C                     ELSE                            *N40
     C                     SETOF                     37
     C                     END                             *40
     C                     SETOF                     38
     C                     ENDSR
     C*==============================================================*
     C*          RTN400....SET ON LR
     C*==============================================================*
     CSR         RTN400    BEGSR
     C                     WRITEPE2
     C                     MOVEL'S#R410P' PRTF
     C                     Z-ADD@CPY      CPY
     C                     Z-ADD@PRTID    PRTID
     C                     CALL 'P68'
     C                     PARM           PRTF   10
     C                     PARM           CPY     30
     C                     PARM           PRTID   20
     C                     ENDSR
