      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : IMR0621                                       *
      * AUTHER       : ANDY                                          *
      * CREATE DATE  : 81/08/12                                      *
      * UPDATE DATE  :                                               *
      * SYSTEM       :人頭馬匯東                                   *
      * SUBSYSTEM    :基本資料子系統                               *
      * REMARK       :產品報廢明細表                               *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.12.11 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
      ****************************************************************
     FREWF46  IP  E           K        DISK
     FPA#APF  IF  E           K        DISK
     FMTMAPF  IF  E           K        DISK
     FIMR062P O   E             39     PRINTER
     IWF460
     I                                              IF03  L1
     I           UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      128 1280$PRTID
     I                                      601 601 DIE01
     I                                      603 607 DIE03
     I                                      608 616 DIF03S
     I                                      617 625 DIF03E
     I*(MM/DD/YY)
     I                                      631 6360DIE05S
     I                                      641 6460DIE05E
     I*(YY/MM/DD)
M001MI*                                     651 6560WIE05S
M001MI*                                     661 6660WIE05E
M001MI                                      651 6580WIE05S
M001MI                                      661 6680WIE05E
     I*
      *==============================================================*
      *                    KEY LIST
      *==============================================================*
     C           KEY#A     KLIST
     C                     KFLD           #A01
     C                     KFLD           #A02
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C           *IN10     IFEQ '0'
     C                     EXSR RTN010                     *HEADING
     C                     SETON                     10
     C                     END
     C*
     C                     EXSR RTN100                     *CHAIN DATA
     CL1 10                EXSR RTNL1
     CLR                   EXSR RTNLR
      *==============================================================*
      *          RTN010....INIT VALUE
      *==============================================================*
     C           RTN010    BEGSR
     C*
     C                     Z-ADD0         IF04P
     C                     Z-ADD0         IF08P
     C                     Z-ADD0         IF09P
     C                     Z-ADD0         IF10P
     C                     Z-ADD0         IF05P
     C                     Z-ADD0         IF04T
     C                     Z-ADD0         IF08T
     C                     Z-ADD0         IF09T
     C                     Z-ADD0         IF10T
     C                     Z-ADD0         IF05T
     C*
     C                     MOVE *BLANK    #A01
     C                     MOVE *BLANK    #A02
     C                     MOVEL'STORAGE' #A01
     C                     MOVELDIE03     #A02
     C*
     C           KEY#A     CHAIN#A0                  40
     C*
     C   40                MOVE *BLANK    #A03
     C*
     C                     SETON                     30
     C*
     C                     WRITEPH1
     C*
     C                     ENDSR
      *==============================================================*
      *          RTN100....CHAIN DATA
      *==============================================================*
     C           RTN100    BEGSR
     C*
     C           IF03      CHAINMA0                  40
     C*
     C   40                MOVE *BLANK    MA03
     C*
M001AC                     Z-ADDIE05      W6      60
|    C                     CALL 'RES001'
|    C                     PARM 'YMD'     S001I1  3
M001MC*                    PARM IE05      S001I2  60
M001MC                     PARM W6        S001I2  60
|    C           PIE05     PARM           S001O1  60
     C*
     C                     ADD  IF04      IF04P
     C                     ADD  IF08      IF08P
     C                     ADD  IF09      IF09P
     C                     ADD  IF10      IF10P
     C                     ADD  IF05      IF05P
     C                     ADD  IF04      IF04T
     C                     ADD  IF08      IF08T
     C                     ADD  IF09      IF09T
     C                     ADD  IF10      IF10T
     C                     ADD  IF05      IF05T
     C*
     C                     EXSR RTN900                     *PROC SKIP
     C                     WRITEPD1
     C*
     C                     SETOF                     30
     C*
     C                     ENDSR
      *==============================================================*
      *          RTNL1.....PROC L1
      *==============================================================*
     C           RTNL1     BEGSR
     C*
     C           *INLR     IFEQ '1'
     C           *IN39     OREQ '1'
     C                     SETON                     99
     C                     ELSE
     C                     SETOF                     99
     C                     END
     C*
     C                     WRITEPD3
     C                     Z-ADD0         IF04P
     C                     Z-ADD0         IF08P
     C                     Z-ADD0         IF09P
     C                     Z-ADD0         IF10P
     C                     Z-ADD0         IF05P
     C*
     C                     SETON                     30
     C*
     C                     ENDSR
      *==============================================================*
      *          RTNLR.....PROC LR
      *==============================================================*
     C           RTNLR     BEGSR
     C*
     C           *IN10     IFEQ '0'
     C                     EXSR RTN010
     C                     WRITEPE3
     C                     ELSE
     C           *IN39     IFEQ '1'
     C                     SETON                     99
     C                     ELSE
     C                     SETOF                     99
     C                     END
     C                     END
     C*
     C                     WRITEPE2
     C                     Z-ADD0         IF04T
     C                     Z-ADD0         IF08T
     C                     Z-ADD0         IF09T
     C                     Z-ADD0         IF10T
     C                     Z-ADD0         IF05T
     C*
     C                     SETON                     30
     C*
     C                     ENDSR
      *==============================================================*
      *          RTN900....PROC SKIP
      *==============================================================*
     C           RTN900    BEGSR
     C*
     C           *IN39     IFEQ '1'
     C                     WRITEPE1
     C                     WRITEPH1
     C                     SETOF                     39
     C                     END
     C*
     C                     ENDSR
