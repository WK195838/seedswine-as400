      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      * PROGRAM NAME : SAR048                                        *
      * AUTHER       : A1084  CHARLES                                *
      * CREATE DATE  : 81/05/04                                      *
      * SYSTEM       :人頭馬匯東                                   *
      * SUBSYSTEM    :銷售分析系統                                 *
      * REMARK       :產品成長率分析表                             *
     /*=============================================================*/
     /* CODE   AUTHOR.     DATE    DESCRIPTION                   */
     /*-------------------------------------------------------------*/
     /* M001  MICHELLE   99.01.08  Y2K MODIFY                    */
     /*=============================================================*/
      ****************************************************************
     FPA#APF  IF  E           K        DISK
     FPA#CPF  IF  E           K        DISK
     FPA#DPF  IF  E           K        DISK
     FSAR048D CF  E                    WORKSTN
     F                                              KINFDS DSPFDS
     ILDA        UDS
     I                                      101 110 $USER
     I                                      119 1240$EGMDY
     I                                      162 1620$EVR
     I                                      125 1270$CPY
     I                                      598 5990$PRTID
      *通路
     I                                      601 601 DSC33S
     I                                      602 602 DSC33E
      *型態
     I                                      603 604 DSC34S
     I                                      605 606 DSC34E
      *地區
     I                                      607 608 DSC31S
     I                                      609 610 DSC31E
      *YY/MM
     I                                      613 6160WSC391
     I                                      613 6140SC391Y
     I                                      615 6160SC391M
     I                                      617 6200WSC392
     I                                      617 6180SC392Y
     I                                      619 6200SC392M
M001AI                                      711 7160W6S
M001AI                                      721 7260W6E
      *SELECT起迄日期
      *YY/MM
     I                                      621 6240YMS1
     I                                      621 6220YYS1
     I                                      623 6240MMS1
     I                                      625 6280YME1
     I                                      625 6260YYE1
     I                                      627 6280MME1
      *減一年
     I                                      629 6320YMS2
     I                                      629 6300YYS2
     I                                      631 6320MMS2
     I                                      633 6360YME2
     I                                      633 6340YYE2
     I                                      635 6360MME2
      *
     I                                      637 637 DSEL
      *MM/YY
     I                                      638 6410DSC39S
     I                                      638 6390SC39SM
     I                                      640 6410SC39SY
     I                                      642 6450DSC39E
     I                                      642 6430SC39EM
     I                                      644 6450SC39EY
     I                                      646 654 DSD03S
     I                                      655 663 DSD03E
M001AI                                      671 6760Y6S1
M001AI                                      677 6820Y6E1
M001AI                                      685 6900Y6S2
M001AI                                      691 6960Y6E2
     I                                      701 7040DSC08S
     I                                      701 7020SC08SM
     I                                      703 7040SC08SY
     I                                      705 7080DSC08E
     I                                      705 7060SC08EM
     I                                      707 7080SC08EY
     I                                      750 750 IN20
     I* FOR F4 HELP -- GET CURSOR POSITION
     IDSPFDS      DS
     I                                    B 370 3710#CSR
      *==============================================================*
      *          IDENTIFY A PARAMETER LIST
      *==============================================================*
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C           *IN98     PARM *IN98     IN98    1
     C           *IN97     PARM *IN97     IN97    1
      *==============================================================*
      *                    KLIST
      *==============================================================*
     C           KEY#A     KLIST
     C                     KFLD           #A01
     C                     KFLD           #A02
     C           KEY#C     KLIST
     C                     KFLD           #C01
     C                     KFLD           #C02
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C  N97N98             EXSR RTN010                     .INIT SCREEN
  01-C           *IN03     DOUEQ'1'
    C           *IN99     OREQ '0'
    C                     EXFMTDSPD1
    C                     MOVEA*ALL'0'   *IN,60
  02-C           *IN03     IFEQ '0'
  03-C           *IN04     IFEQ '1'
    C**********************
    C*  F4 PROMPT PROCESS *
    C**********************
    C                     EXSR RTNF4
  03*C                     ELSE
    C                     EXSR RTN100                     .CHECK      ?
  03-C                     END
  02-C                     END
  01-C                     END
     C*
     C                     EXSR OUTLDA
     C*
     C   03                SETON                     LR
     C                     RETRN
      *===============================================================*
      *          RTN010....INIT SCREEN
      *===============================================================*
01===C           RTN010    BEGSR
|    C*
|    C           *NAMVAR   DEFN *LDA      LDA
     C                     IN   LDA
|    C                     MOVE *BLANK    DSC33
|    C                     MOVE *BLANK    DSC34
|    C                     MOVE *BLANK    DSC31
|    C                     Z-ADD0         DSC39S
|    C                     Z-ADD0         DSC39E
|    C                     Z-ADD0         DSC08S
|    C                     Z-ADD0         DSC08E
|    C                     Z-ADD0         WSC391
|    C                     Z-ADD0         WSC392
|    C                     MOVE *BLANK    DSD03S
|    C                     MOVE *BLANK    DSD03E
|    C                     Z-ADD0         YMS1
|    C                     Z-ADD0         YME1
|    C                     Z-ADD0         YMS2
|    C                     Z-ADD0         YME2
|    C                     MOVE 'A'       DSEL
|    C                     MOVEL'SCR001'  APPSCR  6
|    C*
01===C                     ENDSR
      *===============================================================*
      *          RTN100....CHECK SCREEN
      *===============================================================*
02===C           RTN100    BEGSR
      *
|    C*通路
| 01-C           DSC33     IFEQ *BLANK
|   C                     MOVE *LOVAL    DSC33S
|   C                     MOVE *HIVAL    DSC33E
     C                     ELSE
|    C                     MOVEL'DISTRUBT'#A01
|    C                     MOVELDSC33     #A02
|    C           KEY#A     SETLL#A0                      40
| 03-C           *IN40     IFEQ '0'
     C                     SETON                     609499
     C                     ELSE
     C                     MOVELDSC33     DSC33S
     C                     MOVELDSC33     DSC33E
| 03-C                     END
| 03-C                     END
|    C   99                GOTO END100
|    C*型態
| 04-C           DSC34     IFEQ *BLANK
|   C                     MOVE *LOVAL    DSC34S
|   C                     MOVE *HIVAL    DSC34E
     C                     ELSE
|    C                     MOVELDSC33     #C01
|    C                     MOVELDSC34     #C02
|    C           KEY#C     SETLL#C0                      40
| 03-C           *IN40     IFEQ '0'
     C                     SETON                     619499
     C                     ELSE
     C                     MOVELDSC34     DSC34S
     C                     MOVELDSC34     DSC34E
| 03-C                     END
| 04-C                     END
|    C   99                GOTO END100
|    C*地區
| 07-C           DSC31     IFEQ *BLANK
|   C                     MOVE *LOVAL    DSC31S
|   C                     MOVE *HIVAL    DSC31E
     C                     ELSE
|    C           DSC31     SETLL#D0                      40
| 03-C           *IN40     IFEQ '0'
     C                     SETON                     629499
     C                     ELSE
     C                     MOVELDSC31     DSC31S
     C                     MOVELDSC31     DSC31E
| 03-C                     END
| 07-C                     END
|    C   99                GOTO END100
|    C*送貨日期或確認日期只輸入一項
| 04-C           DSC08S    IFNE 0
|   C           DSC08E    ORNE 0
| 05-C           DSC39S    IFNE 0
|   C                     SETON                     688999
| 05-C                     END
| 06-C           DSC39E    IFNE 0
|   C                     SETON                     698999
| 06-C                     END
| 04-C                     END
|    C   99                GOTO END100
|    C*
|    C*(CHECK DATE)-日期
| 10-C           DSC39S    IFNE 0
      *
| 11-C           SC39SM    IFLT 1
|   C           SC39SM    ORGT 12
|   C                     SETON                     689399
| 11-C                     END
|    C   99                GOTO END100
|    C*
|    C*
| 13-C           SC39EM    IFLT 1
|   C           SC39EM    ORGT 12
|   C                     SETON                     699399
| 13-C                     END
|    C   99                GOTO END100
|    C*
|    C                     Z-ADDSC39SY    SC391Y
|    C                     Z-ADDSC39SM    SC391M
|    C                     Z-ADDSC39EY    SC392Y
|    C                     Z-ADDSC39EM    SC392M
M001AC                     MOVE '004N7'   HB2@FA
M001AC                     MOVE WSC391    HB2@F
M001AC                     MOVE '006N7'   HB2@TA
M001AC                     EXSR HB@ADD                     ADD CENTURY
M001AC                     MOVE HB2@T     W6S     60
      *
M001AC                     MOVE '004N7'   HB2@FA
M001AC                     MOVE WSC392    HB2@F
M001AC                     MOVE '006N7'   HB2@TA
M001AC                     EXSR HB@ADD                     ADD CENTURY
M001AC                     MOVE HB2@T     W6E     60
     C                     MOVEL'1'       IN20
     C                     SETON                     20
  10*C                     ELSE
| 11-C           SC08SM    IFLT 1
|   C           SC08SM    ORGT 12
|   C                     SETON                     639399
| 11-C                     END
|    C   99                GOTO END100
|    C*
| 13-C           SC08EM    IFLT 1
|   C           SC08EM    ORGT 12
|   C                     SETON                     649399
| 13-C                     END
|    C   99                GOTO END100
|    C*
|    C                     Z-ADDSC08SY    SC391Y
|    C                     Z-ADDSC08SM    SC391M
|    C                     Z-ADDSC08EY    SC392Y
|    C                     Z-ADDSC08EM    SC392M
M001AC                     MOVE '004N7'   HB2@FA
M001AC                     MOVE WSC391    HB2@F
M001AC                     MOVE '006N7'   HB2@TA
M001AC                     EXSR HB@ADD                     ADD CENTURY
M001AC                     MOVE HB2@T     W6S     60
     C*
M001AC                     MOVE '004N7'   HB2@FA
M001AC                     MOVE WSC392    HB2@F
M001AC                     MOVE '006N7'   HB2@TA
M001AC                     EXSR HB@ADD                     ADD CENTURY
M001AC                     MOVE HB2@T     W6E     60
     C                     MOVEL'0'       IN20
     C                     SETOF                     20
     C                     END
| 14-C*M001M     WSC391    IFGT WSC392
M001MC           W6S       IFGT W6E
|   C  N20                SETON                     6364
|   C   20                SETON                     6869
|   C                     SETON                     9299
| 14-C                     END
|    C   99                GOTO END100
      *CHECK日期區間不可跨年度3,4
|    C*M001M     SC392Y    SUB  SC391Y    CHKYY   20
M001AC                     MOVE SC392Y    HB2@YY  2
M001AC                     EXSR HB@24Y
M001AC                     MOVE HB2@Y4    W4S     40
     C*
M001AC                     MOVE SC391Y    HB2@YY  2
M001AC                     EXSR HB@24Y
M001AC                     MOVE HB2@Y4    W4E     40
M001AC           W4S       SUB  W4E       CHKYY   40
| 15-C           CHKYY     IFGT 1
|   C  N20                SETON                     6364
|   C   20                SETON                     6869
|   C                     SETON                     9099
| 15*C                     ELSE
| 16-C           CHKYY     IFEQ 1
| 17-C           SC391M    IFLT 4
|   C           SC392M    ORGT 3
|   C  N20                SETON                     6364
|   C   20                SETON                     6869
|   C                     SETON                     9099
| 17-C                     END
| 16*C                     ELSE
| 18-C           CHKYY     IFEQ 0
| 19-C           SC391M    IFLT 4
|   C           SC392M    ANDGT3
|   C  N20                SETON                     6364
|   C   20                SETON                     6869
|   C                     SETON                     9099
| 19-C                     END
| 18-C                     END
| 16-C                     END
| 15-C                     END
|    C   99                GOTO END100
|    C*產品
| 01-C           DSD03S    IFEQ *BLANK
|   C                     MOVE *LOVAL    DSD03S
| 01-C                     END
|    C*
| 02-C           DSD03E    IFEQ *BLANK
|   C                     MOVE *HIVAL    DSD03E
| 02-C                     END
|    C*
| 03-C           DSD03S    IFGT DSD03E
|   C                     SETON                     6566
|   C                     SETON                     9299
| 03-C                     END
|    C   99                GOTO END100
      *SELECT日期區間
     C           SC391M    IFLT 4
     C*M001M     SC391Y    SUB  1         YYS1
M001AC                     MOVE SC391Y    HB2@YY  2
M001AC                     EXSR HB@24Y
M001AC                     MOVE HB2@Y4    W4Y     40
M001AC           W4Y       SUB  1         W4Y
M001AC                     Z-ADDW4Y       YYS1
     C                     Z-ADD4         MMS1
     C                     Z-ADDWSC392    YME1
     C                     ELSE
     C                     Z-ADDSC391Y    YYS1
     C                     Z-ADD4         MMS1
     C                     Z-ADDWSC392    YME1
     C                     END
M001AC                     MOVE '004N7'   HB2@FA
M001AC                     MOVE YMS1      HB2@F
M001AC                     MOVE '006N7'   HB2@TA
M001AC                     EXSR HB@ADD                     ADD CENTURY
M001AC                     MOVE HB2@T     Y6S1
     C*
M001AC                     MOVE '004N7'   HB2@FA
M001AC                     MOVE YME1      HB2@F
M001AC                     MOVE '006N7'   HB2@TA
M001AC                     EXSR HB@ADD                     ADD CENTURY
M001AC                     MOVE HB2@T     Y6E1
      *減一年
     C           YMS1      SUB  100       YMS2
     C           YME1      SUB  100       YME2
M001AC           Y6S1      SUB  100       Y6S2
M001AC           Y6E1      SUB  100       Y6E2
|    C*
02===C           END100    ENDSR
      *===============================================================*
      *          RTNF4 ....F4 HELP
      *===============================================================*
03===C           RTNF4     BEGSR
|    C*
|    C                     SETON                     99
|    C********************
|    C*  GET ROW,COL     *
|    C********************
|    C           #CSR      DIV  256       D#ROW            *ROW
|    C                     MVR            D#COL            *COL
|    C** 游標位置即為(D#ROW,D#COL)
| 01-C           APPSCR    IFEQ 'SCR001'
|   C*
| 02-C           D#ROW     IFEQ 6
|   C           D#COL     ANDEQ40
|   C                     CALL 'WDS018'
|   C           DSC33     PARM DSC33     PSC33   1
|   C                     GOTO ENDF4
| 02-C                     END
|   C*
| 02-C           D#ROW     IFEQ 7
|   C           D#COL     ANDGE40
|   C           D#COL     ANDLE41
|   C                     CALL 'WDS009'
|   C                     PARM DSC33     PSC33
|   C           DSC34     PARM DSC34     PSC34   2
|   C                     GOTO ENDF4
| 02-C                     END
|   C*
| 02-C           D#ROW     IFEQ 8
|   C           D#COL     ANDGE40
|   C           D#COL     ANDLE41
|   C                     CALL 'WDS007'
|   C           DSC31     PARM DSC31     PSC31   2
|   C                     GOTO ENDF4
| 02-C                     END
|   C*
| 02-C           D#ROW     IFEQ 11
|   C           D#COL     IFGE 40
|   C           D#COL     ANDLE48
|   C                     CALL 'WDS001'
|   C                     PARM           DSD03S
|   C                     GOTO ENDF4
| 02-C                     END
|   C*
|   C           D#COL     IFGE 52
|   C           D#COL     ANDLE60
|   C                     CALL 'WDS001'
|   C                     PARM           DSD03E
|   C                     GOTO ENDF4
| 02-C                     END
|   C*
| 02-C                     END
|   C*
| 02-C           D#ROW     IFEQ 18
|   C           D#COL     ANDGE41
|   C           D#COL     ANDLE42
|   C                     CALL 'WDS011'
|   C           $PRTID    PARM $PRTID    S011O1  20
|   C                     GOTO ENDF4
| 02-C                     END
|   C*
| 01-C                     END
|    C*
03===C           ENDF4     ENDSR
      *===============================================================*
      *          OUTLDA....OUT LDA
      *===============================================================*
04===C           OUTLDA    BEGSR
|    C*
|    C                     OUT  *NAMVAR
|    C*
04===C           ENDLDA    ENDSR
M001AC/COPY HBP2CVTR
