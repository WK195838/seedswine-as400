     F****************************************************************
     F*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     F*                                                              *
     F*    PROGRAM NAME : SHA140                                     *
     F*    AUTHOR       : A1062   JEANNY LIN                         *
     F*    CREATE DATE  : 79/07/03                                   *
     F*    UPDATE DATE  : 80/07/23  TAYLOR                           *
     F*    SYSTEM       :程式安全管制系統                          *
     F*    PURPOSE      :程式基本資料維護                          *
     F****************************************************************
     FS#SPPF  UF  E           K        DISK                      A
     FS#SDLF  IF  E           K        DISK
     FS#SUPF  IF  E           K        DISK
     FS#A140D CF  E                    WORKSTN
     E                    WFUN    5   5  6
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR RTN010                     *INIT RTN.
     C           *IN03     DOWEQ'0'
     C*
     C           SCID      CASEQ'SC01'    RTN100
     C           SCID      CASEQ'SC02'    RTN200
     C                     END
     C*
     C                     END
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE
     C*==============================================================*
     C           RTN010    BEGSR
     C           USER      CHAINSU0                  40    *NF
     C   40                MOVE *BLANK    DUSER
     C  N40                MOVE SU02      DUSER
     C*
     C                     MOVE 'SC01'    SCID    4
     C                     MOVE *BLANK    DSP01
     C                     MOVE '1'       DOPT
     C                     ENDSR
     C*==============================================================*
     C*          RTN100....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           RTN100    BEGSR
     C                     EXFMTDSPC1
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'                        *03 OFF
     C*
     C           *IN18     IFEQ '1'                        *18 OFF
     C                     CALL 'S#S140'
     C           DSP01     PARM DSP01     S1401I 10
     C           *IN03     PARM *IN03     S1401O  1
     C                     END                             *18 END
     C*
     C                     EXSR RTN110                     *SCR CHK
     C  N99                MOVE 'SC02'    SCID
     C*
     C                     END                             *03 END
     C                     ENDSR
     C*==============================================================*
     C*          RTN110....SCREEN CHECK
     C*==============================================================*
     C           RTN110    BEGSR
     C*
     C           DSP01     IFEQ *BLANK
     C                     SETON                     609899
     C                     END
     C   99                GOTO END110
     C*CHECK ADD OPTION='1'  *AND U,D,I OPTION<>'1'
     C           DSP01     SETLLSP0                      42*EQ
     C           DOPT      IFEQ '1'
     C   42                SETON                     609799
     C                     ELSE
     C  N42                SETON                     609699
     C                     END
     C   99                GOTO END110
     C*已建立於程式管制資料中(S#A130)不可刪除
     C           DOPT      IFEQ '4'
     C           DSP01     SETLLSDL1                     42*EQ
     C   42                SETON                     609599
     C                     END
     C*
     C           END110    ENDSR
     C*==============================================================*
     C*          RTN200....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           RTN200    BEGSR
     C*
     C                     MOVE DOPT      WNUM    10
     C                     MOVE WFUN,WNUM DFUN
     C*
     C                     EXSR RTN205
     C           DOPT      CASEQ'1'       RTN210           *ADD
     C           DOPT      CASEQ'2'       RTN220           *UPD
     C           DOPT      CASEQ'4'       RTN230           *DEL
     C           DOPT      CASEQ'5'       RTN240           *INQ
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN205....CLEAR FIELDS
     C*==============================================================*
     C           RTN205    BEGSR
     C                     MOVE *BLANK    DSP02
     C                     MOVE 'P'       DSP03
     C                     MOVE *BLANK    DSP04
     C                     MOVE *BLANK    DSP05
     C                     MOVE *BLANK    DSP06
     C                     ENDSR
     C*==============================================================*
     C*          RTN210....ADD DATA
     C*==============================================================*
     C           RTN210    BEGSR
     C                     SETON                     31    *CAN INPUT
     C*
     C           *IN03     DOWEQ'0'                        *CTL LOOP
     C           SCID      ANDEQ'SC02'
     C*代出PGM OBJ TXT TO DSP02
     C                     CALL 'S#A141'
     C                     PARM DSP01     NAM    10
     C           DSP02     PARM DSP02     TXT    50
     C*
     C                     EXFMTDSPC2
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'                        *03 12 OFF
     C           *IN12     ANDEQ'0'
     C                     EXSR RTN250                     *CHK SCR
     C*
     C           *IN99     IFEQ '0'                        *99 OFF
     C                     EXSR RTN270                     *SRC TO PF
     C                     WRITESP0
     C                     MOVE 'SC01'    SCID
     C                     END                             *99 END
     C*
     C                     END                             *03 12 END
     C*
     C   12                MOVE 'SC01'    SCID
     C                     END                             *LOOP END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN220....UPD DATA
     C*==============================================================*
     C           RTN220    BEGSR
     C                     EXSR RTN260                     *PF TO SCR
     C                     SETON                     31    *CAN INPUT
     C*
     C           *IN03     DOWEQ'0'                        *CTL LOOP
     C           SCID      ANDEQ'SC02'
     C*
     C           DSP02     IFEQ *BLANKS
     C                     CALL 'S#A141'
     C                     PARM DSP01     NAM    10
     C           DSP02     PARM DSP02     TXT    50
     C                     END
     C*
     C                     EXFMTDSPC2
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'                        *03 12 OFF
     C           *IN12     ANDEQ'0'
     C                     EXSR RTN250                     *CHK SCR
     C*
     C           *IN99     IFEQ '0'                        *99 OFF
     C           DSP01     CHAINSP0                  40    *NF
     C                     EXSR RTN270                     *SRC TO PF
     C                     UPDATSP0
     C                     MOVE 'SC01'    SCID
     C                     END                             *99 END
     C*
     C                     END                             *03 12 END
     C*
     C   12                MOVE 'SC01'    SCID
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN230....DEL DATA
     C*==============================================================*
     C           RTN230    BEGSR
     C*
     C                     EXSR RTN260                     *PF TO SCR
     C                     SETOF                     31    *NO INPUT
     C                     EXFMTDSPC2
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     ANDEQ'0'
     C           DSP01     CHAINSP0                  40    *NF
     C  N40                DELETSP0
     C                     END
     C*
     C  N03                MOVE 'SC01'    SCID
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN240....INQ DATA
     C*==============================================================*
     C           RTN240    BEGSR
     C*
     C                     EXSR RTN260                     *PF TO SCR
     C                     SETOF                     31    *NO INPUT
     C                     EXFMTDSPC2
     C*
     C  N03                MOVE 'SC01'    SCID
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN250....CHECK 'SC02' SCREEN
     C*==============================================================*
     C           RTN250    BEGSR
     C*
     C*          DSP02     IFEQ *BLANK
     C*                    SETON                     609899
     C*                    END
     C*
     C           END250    ENDSR
     C*==============================================================*
     C*          RTN260....PF TO SCREEN
     C*==============================================================*
     C           RTN260    BEGSR
     C*
     C           DSP01     CHAINSP0                  40    *NF
     C                     MOVE SP02      DSP02
     C                     MOVE SP03      DSP03
     C                     MOVE SP04      DSP04
     C                     MOVE SP05      DSP05
     C                     MOVE SP06      DSP06
     C           *LOVAL    SETLLSP0
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN270....SCREEN TO PF
     C*==============================================================*
     C           RTN270    BEGSR
     C*
     C                     MOVE DSP01     SP01
     C                     MOVE DSP02     SP02
     C                     MOVE DSP03     SP03
     C                     MOVE DSP04     SP04
     C                     MOVE DSP05     SP05
     C                     MOVE DSP06     SP06
     C                     ENDSR
**
新增修改      刪除查詢
