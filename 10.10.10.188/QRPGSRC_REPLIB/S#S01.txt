     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM NAME : S#S01                                      *
     H*    AUTHOR       : A1034   STEPHANIE                          *
     H*    CREATE DATE  : 79/06/28                                   *
     H*    UPDATE DATE  :                                            *
     F*    SYSTEM       :程式安全管制系統                          *
     F*    PURPOSE      :程式安全管制副程式                        *
     H****************************************************************
     FS#SDPF  IF  E           K        DISK
     FMTMCPF  IF  E           K        DISK
     FS#SUPF  IF  E           K        DISK
     E*
     IS#DA01      DS
     I                                        1  10 $S#01
     I           UDS
     I                                      135 135 $ADD
     I                                      135 135 SD03
     I                                      136 136 $UPD
     I                                      136 136 SD04
     I                                      137 137 $DLT
     I                                      137 137 SD05
     I                                      138 138 $INQ
     I                                      138 138 SD06
     I                                      139 139 $RMK01
     I                                      139 139 SD07
     I                                      140 140 $RMK02
     I                                      140 140 SD08
     I                                      141 141 $RMK03
     I                                      141 141 SD09
     I                                      142 147 $DEP
     I                                      148 151 $STORE
     I                                      152 161 SU02
     I                                      173 176 SU07
     I                                      501 505 MC15
     I                                      501 505 $WRHUS
     I                                      506 506 MC16
     I                                      506 506 $PURID
     I                                      507 507 MC17
     I                                      507 507 $TRNID
     I                                      508 508 MC18
     I                                      509 509 MC19
     I                                      510 510 MC20
     I                                      511 511 MC21
     I                                      512 512 MC22
     I                                      508 512 $APAU1
     I                                      513 513 $WRHTY
     I                                      513 513 MC24
     I                                      522 522 $APAU2
     I                                      522 522 MC25
     I                                      523 5240MC26
     IS0101I      DS
     I*                                       2   7 MC01
     I                                        8  10 X0101I
     C*
     C*==============================================================*
     C*                    LIST PARAMETERS
     C*==============================================================*
     C           *ENTRY    PLIST
     C                     PARM           S0101I 10         USERID
     C                     PARM           S0102I 10         PGMID
     C                     PARM           S0101O  1        執行碼Y/N
     C*==============================================================*
     C*                    KEY  LIST
     C*==============================================================*
     C           KEYSD     KLIST
     C                     KFLD           S0101I            USERID
     C                     KFLD           S0102I            PGMID
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C*
     C           *NAMVAR   DEFN           S#DA01
     C                     IN   *NAMVAR
     C*
     C                     Z-ADD0         MC26
      *USRPRF D9電腦室 OR RE 可使用程式
     C                     MOVELS0101I    USER    2
     C           S0101I    IFEQ $S#01
     C           USER      OREQ 'D9'
     C           USER      OREQ 'RE'
     C           USER      OREQ 'SC'
     C                     EXSR RTN100                        ALL AUT
     C                     ELSE
     C                     EXSR RTN200
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
     C*==============================================================*
     C*          RTN100....MAIN ROUTINE
     C*==============================================================*
     C           RTN100    BEGSR
     C*
     C                     MOVE 'Y'       $ADD
     C                     MOVE 'Y'       $UPD
     C                     MOVE 'Y'       $DLT
     C                     MOVE 'Y'       $INQ
     C                     MOVE 'Y'       $RMK01
     C                     MOVE 'Y'       $RMK02
     C                     MOVE 'Y'       $RMK03
     C                     MOVEL'ALL'     $DEP
     C                     MOVEL'ALL'     $STORE
     C                     MOVE '*ALL '   $WRHUS
     C                     MOVE 'C'       $PURID
     C                     MOVE 'C'       $TRNID
     C                     MOVE 'A'       $WRHTY
      *核准
     C                     MOVE 'YYYYY'   $APAU1
     C                     MOVE 'Y'       $APAU2
     C           S0101I    CHAINSU0                  40
     C                     MOVE 'Y'       S0101O            可執行
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN200....MAIN ROUTINE
     C*==============================================================*
     C           RTN200    BEGSR
     C*
     C                     MOVE 'N'       S0101O            不得執行
     C           KEYSD     CHAINSD0                  40
     C           *IN40     IFEQ '0'
     C           S0101I    CHAINSU0                  40     USERID
     C                     MOVE SU04      $DEP
     C                     MOVE SU05      $STORE
   ! C                     MOVELSU04      MC01
     C           MC01      CHAINMC0                  40     USERID
     C           *IN40     IFEQ '0'
     C                     MOVE 'Y'       S0101O            可執行
     C                     ELSE
     C                     END
     C                     END
     C*
     C                     ENDSR
