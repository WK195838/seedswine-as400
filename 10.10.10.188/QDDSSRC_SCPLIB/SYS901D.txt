     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : SWD001D                                    *
     A*    PROGRAM NAME :網頁人員解除鎖定                          *
     A*    AUTHOR       : NANCY
     A*    CREATE DATE  : 2012.02.10                                 *
     A*    UPDATE DATE  :
      *================================================================
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A*=========================================================================
     A          R SFLSR                     TEXT('SUBFILE RECORD.')
     A                                      SFL
     A  54                                  SFLNXTCHG
     A            RRN            2S 0H
     A            Z1FLD          1A  B  7  3TEXT('登錄狀態')
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      VALUES('N' ' ')
     A            Z1NF01        10A  O  7  7TEXT('程式代號')
     A            Z1NF03        10O  O    +1TEXT('人員姓名')
     A            Z1NF08         1A  O  7 33TEXT('登錄狀態')
     A            Z1@FLD         1A  H
     A*=========================================================================
     A          R SFLCR                     TEXT('SUBFILE CONTROL.')
     A                                      SFLCTL(SFLSR)
     A                                      SFLPAG(15) SFLSIZ(20)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A* COMMAND KEYS............................................................
     A                                      CA03(03 '結束.')
     A                                      CF09(09)
     A*.........................................................................
     A            DRRN           4  0H      SFLRCDNBR(CURSOR)
     A            ZZPGM         10      1  2TEXT('*PROGRAM')
     A                                      COLOR(BLU)
     A                                  1 30'西祺企業有限公司'
     A            ZZYMD          8  0   1 59EDTWRD('    /  /  ')
     A                                  1 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'網頁人員解除鎖定'
     A                                      DSPATR(HI)
     A                                  2 59USER
     A                                  4  2' N -解除鎖定'
     A                                      COLOR(BLU)
     A                                  5  2' /N 人員代號 /人員姓名-
     A                                       /登錄狀態                     '
     A                                      DSPATR(HI UL) COLOR(WHT)
     A            ZHNF01        10A  B  6  7COLOR(WHT)
     A*=========================================================================
     A          R DSPC                      TEXT('COMMAND KEYS')
     A                                      OVERLAY
     A                                 23  2'F3=結束'
     A                                    +1'F9=全選'
     A  99        MSG           60O    24  2COLOR(RED)
     A*=========================================================================
