     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : INT008D                                    *
     A*    AUTHOR       : A1546  VINCENT                             *
     A*    CREATE DATE  : 97/10/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    REMARK       :陳列品退出處理                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  4VALUES(' ' '2' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            TN04      R        O 10  7
     A            DNE05     R        O 10 14REFFLD(NE05)
     A            TN01      R        H
     A            TN02      R        H
     A            TN03      R        H
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'INT008'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'陳列品退出處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 13REFFLD(TN01)
     A                                      DSPATR(HI)
     A            D#B03     R   -4   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(TN02)
     A                                      DSPATR(HI)
     A            DND04     R        O  4 46REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  1'供應廠商:'
     A                                      DSPATR(HI)
     A            DBGN3     R        B  5 13REFFLD(TN03)
     A                                      DSPATR(HI)
     A            DNB03     R        O  5 19REFFLD(NB03)
     A                                      COLOR(BLU)
     A                                  6  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                    +1'2=修改'
     A                                      COLOR(BLU)
     A                                    +1'5=查詢'
     A                                      COLOR(BLU)
     A                                  8  3' A 分店代號／名稱   '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A            DBGN4     R        B  9  7REFFLD(TN04)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +1'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  70                             24  2'單據號碼'
     A                                      DSPATR(HI)
     A  70        DTK05     R             +1REFFLD(TK05)
     A                                      DSPATR(RI)
     A  70                                +1'新增成功'
     A                                      DSPATR(HI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 10  3VALUES(' ' '1' '2' '4')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DTN05     R        O 10  6REFFLD(TN05)
     A            DNC05         16A  O 10 16
     A            TN06      R        O 10 34EDTCDE(1)
     A            TN07      R        O 10 45EDTCDE(1)
     A            DTT       R        O 10 53REFFLD(TN11)
     A                                      EDTCDE(1)
     A            HTL03     R        H      REFFLD(TL03)
     A            DTL03     R        B 10 61REFFLD(TL03)
     A                                      EDTCDE(1)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DCHK           1A  O 10 69
     A            DTN09     R   -4   O 10 71REFFLD(TN09)
     A            HTN09     R        H      REFFLD(TN09)
     A                                 11 20'銷售借貨量:'
     A            DTN13     R             +1REFFLD(TN13)
     A                                      EDTCDE(1)
     A                                 11 47'陳列驗收量:'
     A            DTN14     R             +1REFFLD(TN14)
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0005)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'INT008'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'陳列品退出處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DTN01     R        O  4 13REFFLD(TN01)
     A            D#B03     R   -4   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 33'通路代號:'
     A                                      COLOR(BLU)
     A            DTN02     R        O  4 45REFFLD(TN02)
     A            DND04     R        O  4 48REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  1'供應廠商:'
     A                                      COLOR(BLU)
     A            DTN03     R        O  5 13REFFLD(TN03)
     A            DNB03     R        O  5 19REFFLD(NB03)
     A                                      COLOR(BLU)
     A  33                              5 33'退出日期:'
     A                                      DSPATR(HI)
     A  33        DTK03          6Y 0B  5 45EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A  33                              5 57'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  6  1'分店代號:'
     A                                      COLOR(BLU)
     A            DTN04     R        O  6 13REFFLD(TN04)
     A            DNE05     R        O  6 19REFFLD(NE05)
     A                                      COLOR(BLU)
     A                                  6 33'退出單號:'
     A                                      COLOR(BLU)
     A            DTK05     R        O  6 45REFFLD(TK05)
     A                                  7  1'輸入「退出數量明細」作業選擇:'
     A                                      COLOR(BLU)
     A                                    +1'1=新增'
     A                                      COLOR(BLU)
     A                                    +1'2=修改'
     A                                      COLOR(BLU)
     A                                    +1'4=刪除'
     A                                      COLOR(BLU)
     A                                  8 46'陳列'
     A                                      COLOR(BLU)
     A                                  8 53'己預轉'
     A                                      COLOR(BLU)
     A                                  9  2' A 產品代號 產品名稱        +
     A                                      未稅單價  餘量 進出量+
     A                                      退出量 驗收單號'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3=回主畫面'
     A                                      COLOR(BLU)
     A  33                                +2'F4=視窗查詢'
     A                                      COLOR(BLU)
     A  33                                +2'F9=確認存檔'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
