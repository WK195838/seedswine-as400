     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : PUR050P                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/10/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR050                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :供應商負擔運費總表                        *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<PUR050P>'
     A                                    22
     A                                      MSGCON(030 USC9999 SCMF)
     A                                    65
     A                                      '日期:'
     A            $EGMDY         6  0O    +1
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    27
     A                                      '供應商負擔運費總表'
     A                                    65
     A                                      '時間:'
     A                                    +1
     A                                      TIME
     A                                      SPACEA(001)
     A                                    65
     A                                      '頁次:'
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '公司別:'
     A            DCK01     R        O    13
     A                                      REFFLD(CK01)
     A            D#B03     R        O    16
     A                                      REFFLD(#B03 PA#BPF)
     A                                      SPACEA(001)
     A                                     1
     A                                      '供應商代號:'
     A            DCK08S         5   O    +1
     A                                    +1'-'
     A            DCK08E         5   O    +1
     A                                    29'到貨運日期:'
     A            DCK03S         6  0O    +1EDTCDE(Y)
     A                                    +1'-'
     A            DCK03E         6  0O    +1EDTCDE(Y)
     A                                    63'貨運區碼:'
     A            DCK09S         1   O    +1
     A                                    +1'-'
     A            DCK09E         1   O    +1
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     1'供應商代號／簡稱'
     A                                      SPACEB(001)
     A                                    21'到貨運日期'
     A                                    34'分店所屬貨運區碼'
     A                                    52'箱數'
     A                                    61'單價'
     A                                    70'未稅金額'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        W6101          5A        2
     A  30        W6102         12O       +1
     A  31        W6103O         6S 0     23EDTCDE(Y)
     A  32        W6104          1A       35
     A  32        W6105         12O       +1
     A            W6106          4S 0     52EDTCDE(J)
     A            W6107          4S 0     61EDTCDE(K)
     A            W6108          6S 0     72EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    32'到貨運日期小計:'
     A            Q1             6  0     50EDTCDE(J)
     A            Q2             9  0     68EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    32'    供應商合計:'
     A            Q3             6  0     50EDTCDE(J)
     A            Q4             9  0     68EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD4
     A                                      SPACEB(001)
     A                                    32'          總計:'
     A            Q5             6  0     50EDTCDE(J)
     A            Q6             9  0     68EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<PUR050P>>--->報表結束'
     A                                      SPACEB(001)
     A                                    45
     A                                      '<<單價空白為分攤後的最低運費>>'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
