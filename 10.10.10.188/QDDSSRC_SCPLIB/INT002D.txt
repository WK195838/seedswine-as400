     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : INT002D                                    *
     A*    AUTHOR       : A1546  VINCENT                             *
     A*    CREATE DATE  : 97/09/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INT002                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    REMARK       :進貨退出處理 (A/U/D/I)                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  4VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DTE03          6Y 0O 10  7EDTWRD('  /  /  ')
     A            TE04      R        O 10 17
     A            DNE05     R        O 10 23REFFLD(NE05)
     A            TE05      R        O 10 34
     A            HTE06     R        H      REFFLD(TE06)
     A            HTE03          6Y 0H
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'INT002'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'　進貨退出處理　'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 14REFFLD(TE01)
     A                                      DSPATR(HI)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(TE02)
     A                                      DSPATR(HI)
     A            DND04     R        O  4 46REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 18'2=修改'
     A                                      COLOR(BLU)
     A                                  5 27'4=刪除'
     A                                      COLOR(BLU)
     A                                  5 36'5=查詢'
     A                                      COLOR(BLU)
     A                                  7  3'（月／日／年）'
     A                                      COLOR(BLU)
     A                                  8  3' A 退出日期出貨分店／名稱 +
     A                                       單據編號    '
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN3     R   -2   B  9  7REFFLD(TE03)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            DBGN4     R        B  9 17REFFLD(TE04)
     A                                      DSPATR(HI)
     A            DBGN5     R        B  9 34REFFLD(TE05)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3=離開'
     A                                      COLOR(BLU)
     A                                    +2'F6=新增'
     A                                      COLOR(BLU)
     A  70                             24  2'單據號碼'
     A                                      DSPATR(HI)
     A  70        DTE05     R             +1REFFLD(TE05)
     A                                      DSPATR(RI)
     A  70                                +1'新增成功'
     A                                      DSPATR(HI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A  31                                  CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'INT002'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'　進貨退出處理　'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A                                      COLOR(BLU)
     A            DTE01     R        O  5 14REFFLD(TE01)
     A            D#B03     R   -4   O  5 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  7  2'通路代號:'
     A                                      COLOR(BLU)
     A            DTE02     R        O  7 14REFFLD(TE02)
     A            DND04     R        O  7 17REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  9  2'單據編號:'
     A                                      COLOR(BLU)
     A            DTE05     R        O  9 14REFFLD(TE05)
     A                                 11  2'分店代號:'
     A N31                                  COLOR(BLU)
     A  31                                  DSPATR(HI)
     A            DTE04     R        B 11 14REFFLD(TE04)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DNE05     R        O 11 20REFFLD(NE05)
     A                                      COLOR(BLU)
     A                                 13  2'退出日期:'
     A                                      COLOR(BLU)
     A            DTE03          6Y 0B 13 14EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'(月／日／年)'
     A                                      COLOR(BLU)
     A*
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +1'F3=回主畫面'
     A                                      COLOR(BLU)
     A  31                                +1'F4=視窗查詢'
     A                                      COLOR(BLU)
     A  31                                +1'F5=參考資料'
     A                                      COLOR(BLU)
     A                                    +1'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 11  4VALUES(' ' '1' '2' '4')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DTF03     R        B 11  7REFFLD(TF03)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            NC05      R        O 11 18
     A            NC14      R        O 11 38
     A            HTF04     R        H      REFFLD(TF04)
     A            DTF04     R        B 11 45REFFLD(TF04)
     A                                      EDTCDE(1)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DCHK           1A  H
     A*------------------------------------------------------------
     A*
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A  33                                  CF04(04)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'INT002'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 31'  進貨退出處理  '
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DTE01     R        O  4 14REFFLD(TE01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DTE02     R        O  4 43REFFLD(TE02)
     A            DND04     R        O  4 46REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  2'單據編號:'
     A                                      COLOR(BLU)
     A            DTE05     R        O  5 14REFFLD(TE05)
     A                                  6  2'分店代號:'
     A                                      COLOR(BLU)
     A            DTE04     R        O  6 14REFFLD(TE04)
     A            DNE05     R        O  6 20REFFLD(NE05)
     A                                      COLOR(BLU)
     A                                  6 31'退出日期:'
     A                                      COLOR(BLU)
     A            DTE03          6Y 0O  6 43EDTWRD('  /  /  ')
     A                                  6 52'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  8  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  8 18'1=新增'
     A                                      COLOR(BLU)
     A                                  8 27'2=修改'
     A                                      COLOR(BLU)
     A                                  8 36'4=刪除'
     A                                      COLOR(BLU)
     A                                 10  3' A 產品代號 產品簡稱          +
     A                                        單位　退出數量'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A*
     A          R DSPC3
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +1'F3=回選單'
     A                                      COLOR(BLU)
     A  33                                +1'F4=視窗查詢'
     A                                      COLOR(BLU)
     A  33                                +1'F5=參考資料'
     A                                      COLOR(BLU)
     A  80                                +1'F9=確認存檔'
     A                                      COLOR(BLU)
     A                                    +1'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*
