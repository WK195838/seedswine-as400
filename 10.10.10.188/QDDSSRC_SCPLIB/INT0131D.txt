     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : INT0131D                                   *
     A*    AUTHOR       : A1546  VINCENT                             *
     A*    CREATE DATE  : 97/10/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INT0131                                    *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    REMARK       :現場分店存貨查詢－依分店                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            TB05      R        O 10  3
     A            DNC05     R        O 10 13REFFLD(NC05)
     A            DTA04     R        O 10 32REFFLD(TA04)
     A                                      EDTCDE(J)
     A            TB06      R        O 10 46EDTCDE(J)
     A            TBTT1     R        O 10 55REFFLD(TB06)
     A                                      EDTCDE(J)
     A            TBTT2     R        O 10 64REFFLD(TB06)
     A                                      EDTCDE(J)
     A            TBTT3     R        O 10 73REFFLD(TB06)
     A                                      EDTCDE(J)
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'INT0131'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'現場分店存貨查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        O  4 14REFFLD(TB01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN3     R        O  4 43REFFLD(TB03)
     A            DND04     R        O  4 46REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  2'庫存年月:'
     A                                      COLOR(BLU)
     A            DTB02          4Y 0O  5 14EDTWRD('  /  ')
     A                                  5 20'（月／年）'
     A                                      COLOR(BLU)
     A            DBGN2     R        H      REFFLD(TB02)
     A                                  6  2'分店代號:'
     A                                      COLOR(BLU)
     A            DBGN4     R        O  6 14REFFLD(TB04)
     A            DNE05     R        O  6 20REFFLD(NE05)
     A                                      COLOR(BLU)
     A                                  7 37'上月    期初   現場   帳列'
     A                                      COLOR(BLU)
     A                                    +3'陳列'
     A                                      COLOR(BLU)
     A                                  8  2'產品代號產品名稱          -
     A                                       單位成本 庫存量 庫存量 -
     A                                      庫存量 庫存量'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN5     R        B  9  3REFFLD(TB05)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'F3=回主畫面'
     A                                      COLOR(BLU)
     A                                    +1'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
