     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    DSPF NAME    : MAN010D                                    *
     A*    AUTHOR       : CAROL                                      *
     A*    CREATE DATE  : 2019/10/08                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :基本資料建檔子系統                        *
     A*    REMARK       :缺貨資料維護                              *
     A*==============================================================*
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/INTSPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DNEW           1A  H
     A*
     A            DOPT1          1A  B  9  2VALUES(' ' '1' '4')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DTS03     R        B  9  4REFFLD(TS03)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DNC05     R        O  9 14REFFLD(NC05 MANCPF)
     A            DTS04          6  0B  9 35EDTCDE(Y)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DTS05     R        B  9 46REFFLD(TS05)
     A                                      EDTCDE(1)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A            DTSXX          6  0O  9 58EDTCDE(Y)
     A            DTSVV          6  0O  9 58EDTCDE(Y)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0026)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'MAN010'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'缺貨資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DTS01     R        B  4 13REFFLD(TS01)
     A            S#B02     R   -2   O  4 16REFFLD(#B02 PT#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DTS02     R        B  4 43REFFLD(TS02)
     A            SND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  6  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  6 17'1 =新增 4 =刪除'
     A                                      COLOR(BLU)
     A                                  8  2'A產品代號產品名稱          +
     A                                      預計到貨日   數量  最後修改日'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                 23  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                    +2'F9 =確認存檔'
     A                                      COLOR(BLU)
     A            ERRID          7   H
     A            ERRF          10   H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
