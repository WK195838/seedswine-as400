     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : PUR030P                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/10/30                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR030                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :分店驗收數量差異表                        *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<PUR030>'
     A                                    38MSGCON(030 USC9999 SCMF)
     A                                    99'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    42'分店驗收數量差異表'
     A                                    99'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A                                    99'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司別　:'
     A            DCL01     R        O    13REFFLD(CL01)
     A            D#B03     R        O    16REFFLD(#B03 PA#BPF)
     A                                    33'驗收日期:'
     A            DCL03S         6  0O    45EDTCDE(Y)
     A                                    +1'-'
     A            DCL03E         6  0O    56EDTCDE(Y)
     A*                                     SPACEA(001)
     A                                    +1'通路代號:'
     A            DCL02S         2   O    +1
     A                                    +1'-'
     A            DCL02E         2   O    +1
     A                                    +1'分店代號:'
     A            DCL04S         5   O    +1
     A                                    +1'-'
     A            DCL04E         5   O    +1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============'
     A                                      SPACEB(001)
     A                                     1'驗收日期'
     A                                      SPACEB(001)
     A                                    12'通路別／名稱'
     A                                    27'分店代號／簡稱'
     A                                    45'驗收單號'
     A                                    59'產品代號'
     A                                    70'產品簡稱'
     A                                    90'採購量'
     A                                    +1'驗收量'
     A                                    +1'差異量'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        LCL03          6  0     02EDTWRD('  /  /  ')
     A  31        CL02      R             13
     A  31        ND04      R             +1
     A  32        CL04      R             28
     A  32        NE05      R             +1
     A  33        CM01      R             46
     A            CM03      R             60REFFLD(CM03 POCMPF)
     A            NC05      R             71
     A            CM04      R             90EDTCDE(J)
     A            CM05      R             +1EDTCDE(J)
     A            PCM06     R             +1REFFLD(CM06 POCMPF)
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============'
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============'
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<PUR030P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
