     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : MAN002D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/05/15                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MAN002                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    REMARK       :供應商資料維護 (A/U/D/I)                  *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MANBPF)
     A                                      MSGLOC(24)
     A                                      PRINT
      *----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  5VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            NB01      R        O  9  9
     A            NB03      R        O  9 15
     A            NB09      R        O  9 35
     A            NB10      R        O  9 59
      *----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'MAN002'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'供應商基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5  6'2=修改'
     A                                      COLOR(BLU)
     A                                  5 16'4=刪除'
     A                                      COLOR(BLU)
     A                                  5 26'5=查詢'
     A                                      COLOR(BLU)
     A                                  7  5'A  供應商代號／簡稱         聯+
     A                                      絡人               電話一      -
     A                                        '
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  9REFFLD(NB01)
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                    +2'F6 =新增'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(MSG 0004 SCMF)
     A                                      DSPATR(HI)
     A  98        ERR2          63A  O 24  2MSGID(MSG 0005 SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF09(09)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MAN002'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'供應商基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'供應商代號:'
     A                                      COLOR(BLU)
     A            DNB01     R        B  5 16REFFLD(NB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  5 44'統一編號:'
     A                                      COLOR(BLU)
     A            DNB07     R        B  5 56REFFLD(NB07)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  6  2'供應商全稱:'
     A                                      COLOR(BLU)
     A            DNB02     R        B  6 16REFFLD(NB02)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7  2'供應商簡稱:'
     A                                      COLOR(BLU)
     A            DNB03     R        B  7 16REFFLD(NB03)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7 44'郵遞區號:'
     A                                      COLOR(BLU)
     A            DNB04     R        B  7 56REFFLD(NB04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'供應商地址:'
     A                                      COLOR(BLU)
     A            DNB05     R        B  8 16REFFLD(NB05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DNB06     R        B  8 49REFFLD(NB06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  2'負責人　  :'
     A                                      COLOR(BLU)
     A            DNB08     R        B  9 16REFFLD(NB08)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'聯絡人　  :'
     A                                      COLOR(BLU)
     A            DNB09     R        B 10 16REFFLD(NB09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 44'電話一　:'
     A                                      COLOR(BLU)
     A            DNB10     R        B 10 56REFFLD(NB10)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  2'傳真機　  :'
     A                                      COLOR(BLU)
     A            DNB12     R        B 11 16REFFLD(NB12)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 44'電話二　:'
     A                                      COLOR(BLU)
     A            DNB11     R        B 11 56REFFLD(NB11)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  2'電子信箱  :'
     A                                      COLOR(BLU)
     A            DNB13     R        B 12 16REFFLD(NB13)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 14  2'貨運區號  :'
     A                                      COLOR(BLU)
     A            DNB15     R        B 14 16REFFLD(NB15)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DNA03     R             +1REFFLD(NA03 MANAPF)
     A                                 13  2'付款方式  :'
     A                                      DSPATR(HI)
     A            DNB14     R        B 13 16REFFLD(NB14)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DL203     R  -10        +1REFFLD(L203 UTL2PF)
     A                                      COLOR(BLU)
     A                                 13 44'國內外別:'
     A                                      COLOR(BLU)
     A            DNB16     R        B 13 56REFFLD(NB16)
     A                                      VALUES('F' 'L')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 59'( F-國外 L-國內)'
     A                                      COLOR(BLU)
     A                                 20  2'備註      :'
     A                                      COLOR(BLU)
     A            DNB17     R        B 20 16REFFLD(NB17)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            DNB18     R        B 20 49REFFLD(NB18)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 18'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  71                             22 34'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 50'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 18'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  71                             22 34'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 50'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
