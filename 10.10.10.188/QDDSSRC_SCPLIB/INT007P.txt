     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    AUTHOR       : A1546 VINCENT                              *
     A*    CREATE DATE  : 97/10/15                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INT007P                                    *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    REMARK       :日結單據入庫處理報表列印                  *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    27
     A                                      MSGCON(030 USC9999 SCMF)
     A                                      SPACEA(001)
     A                                    31
     A                                      '日結入庫錯誤記錄表'
     A                                      SPACEA(002)
     A                                    51
     A                                      '日期:'
     A            $EGMDY         6  0O    59
     A                                      EDTCDE(Y)
     A                                    68
     A                                      '頁次:'
     A                                    76
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*                                    1
     A*                                     '處理訖日:'
     A*           PDATE          6  0O    13
     A*                                     EDTCDE(Y)
     A                                    51
     A                                      '時間:'
     A                                    59
     A                                      TIME
     A                                    70
     A                                      '<INT007>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A                                     1
     A                                      '單據號碼'
     A                                    15
     A                                      '產品代號'
     A                                    25
     A                                      '分店'
     A                                    31
     A                                      '數量差'
     A                                    40
     A                                      '錯誤原因'
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEA(001)
     A N20        PDNO          13         2
     A            PPNO           9        16
     A            PDEP           5        26
     A            PCON           5  0     32EDTCDE(M)
     A            PERR          42        38
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A                                     2
     A                                      '***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A                                     2
     A                                      '<<INT007>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE31
     A                                      SPACEA(001)
     A                                     3
     A                                      'もも無錯誤記錄'
