     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SER020P                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/11/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR020                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :銷售系統                                  *
     A*    REMARK       :產品銷貨收入彙總表                        *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* 00A需求單號: 1080949
     A* 00A CAROL  2019/08/02新增篩選條件供應商代號&產品代號
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<SER040>'
     A                                    30MSGCON(030 USC9999 SCMF)
     A                                    70'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A*
     A                                      SPACEA(001)
     A                                    34'產品銷貨收入彙總表'
     A                                    70'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A*
     A                                     1'公司代號:'
     A            DHA01     R        O    +1REFFLD(HA01)
     A            D#B03     R        O    +1REFFLD(#B03 PA#BPF)
     A                                    70'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A                                     1'通路代號:'
     A            DHA02     R        O    +1REFFLD(HA02)
     A            DND04     R        O    +1REFFLD(ND04 MANDPF)
     A                                    70'USER :'
     A            $USER         10A       +1
     A*
     A                                     1'銷售日期:'
     A                                      SPACEB(001)
     A            DHA03S    R   -2   O    +1REFFLD(HA03)
     A                                      EDTCDE(Y)
     A                                    +1'~'
     A            DHA03E    R   -2   O    +1REFFLD(HA03)
     A                                      EDTCDE(Y)
     A*
     A                                     1'產品歸屬:'
     A                                      SPACEB(001)
     A            DNC09S    R        O    +1REFFLD(NC09)
     A                                    +1'~'
     A            DNC09E    R        O    +1REFFLD(NC09)
 00A+A*
 00A+A                                    +1'供應商代號:'
 00A+A            DNB01S         5A  O    +1
 00A+A                                    +1'~'
 00A+A            DNB01E         5A  O    +1
 00A+A*
 00A+A                                    +1'產品代號:'
 00A+A            DHA05S         9A  O    +1
 00A+A                                    +1'~'
 00A+A            DHA05E         9A  O    +1
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =================='
     A                                      SPACEB(001)
     A*
     A                                     1'產品代號'
     A                                      SPACEB(001)
     A                                    +1'產品名稱'
     A                                    33'數量'
     A                                    41'未稅銷售金額'
     A                                    57'含稅銷售金額'
     A                                    77'銷項稅額'
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A            HA05      R              2
     A            NC05      R             +1
     A            PHA06          6S 0     31EDTCDE(J)
     A            PHA07         11S 2     +1EDTCDE(J)
     A            PHA08         11S 2     +1EDTCDE(J)
     A            LHA08         11S 2     +1EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    15'        總計:'
     A            Q1             6  0     31EDTCDE(J)
     A            Q2            11  2     +1EDTCDE(J)
     A            Q3            11  2     +1EDTCDE(J)
     A            Q4            11  2     +1EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------'
     A                                      SPACEB(001)
     A                                     2'<<SER030>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                      SPACEB(001)
     A                                     2'<<SER030>>--->此範圍無資料'
