     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PUC013D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/09/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUC013                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :訂貨單審核                                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/POCDPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  3VALUES(' ' '6' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            CD04      R        O 10  6
     A            SNE05     R        O 10 12REFFLD(NE05 MANEPF)
     A            CD05      R        O 10 24
     A            SCD07     R   -2   O 10 39REFFLD(CD07)
     A                                      EDTWRD('  /  /  ')
     A            SCD08     R   -2   O 10 54REFFLD(CD08)
     A                                      EDTWRD('  /  /  ')
     A            SCD12     R        O 10 72REFFLD(CD12)
     A            CD01      R        H
     A            CD02      R        H
     A            CD03      R        H
     A            CD07      R        H
     A            CD08      R        H
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A*                                     CF06(06)
     A*                                     CF07(07)
     A*                                     CF08(08)
     A                                      CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'PUC013'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'訂貨單審核'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'訂貨日期:'
     A                                      COLOR(BLU)
     A            DBGN3     R   -2   O  4 13REFFLD(CD03)
     A                                      EDTCDE(Y)
     A                                  4 24'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 36REFFLD(CD01)
     A            S#B03     R   -6   O  4 39REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 52'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 64REFFLD(CD02)
     A            SND04     R        O  4 67REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'4=刪除'
     A                                      COLOR(BLU)
     A                                  5 26'5=查詢'
     A                                      COLOR(BLU)
     A                                  5 35'6=審核'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN4     R        B  9  6REFFLD(CD04)
     A            DBGN5     R        B  9 24REFFLD(CD05)
     A                                  7 36'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  7 51'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  8  3'A 分店代號／名稱  訂貨單號   -
     A                                      預計到貨運日 預計到分店日  +
     A                                      審核'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *此燈號控制新增成功以及修改成功專用
     A  95        ERR0          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(MSG 0004 SCMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(MSG 0007 SCMF)
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC013'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 34'訂貨單審核'
     A                                      DSPATR(HI)
     A  15                              3 32'<<陳列訂貨單>>'
     A                                      COLOR(PNK)
     A N15                              3 32'<<一般訂貨單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DCD01     R        O  4 14REFFLD(CD01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5  2'訂貨單號:'
     A                                      COLOR(BLU)
     A            DCD05     R        O  5 14REFFLD(CD05)
     A                                  6  2'通路代號:'
     A                                      COLOR(BLU)
     A            DCD02     R        O  6 14REFFLD(CD02)
     A            DND04     R        O  6 17REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  7  2'分店代號:'
     A                                      COLOR(BLU)
     A            DCD04     R        B  7 14REFFLD(CD04)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DNE05     R        O  7 20REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                 10  2'訂貨日期:'
     A                                      COLOR(BLU)
     A            DCD03     R   -2   O 10 14REFFLD(CD03)
     A                                      EDTCDE(Y)
     A                                 10 23'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 11  2'預計到運貨日期:'
     A                                      COLOR(BLU)
     A            DCD07     R   -2   O 11 20REFFLD(CD07)
     A                                      EDTCDE(Y)
     A                                 11 29'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 12  2'預計到分店日期:'
     A                                      COLOR(BLU)
     A            DCD08     R   -2   B 12 20REFFLD(CD08)
     A                                      EDTCDE(Y)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 29'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 13  2'預計到貨時間:'
     A                                      COLOR(BLU)
     A            DCD09     R        B 13 18REFFLD(CD09)
     A                                      VALUES('A' 'P')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 20'( A-上午10-12點／P-下午02-04-
     A                                      點)'
     A                                      COLOR(BLU)
      *
     A                                 20  2'製表:'
     A                                      COLOR(BLU)
     A            DCD13         10O  O 20 10
     A                                 20 25'審核:'
     A                                      COLOR(BLU)
     A            DCD14         10O  O 20 33
      *
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                    +2'F5 =畫面檢核'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD3
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC013'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 34'訂貨單審核'
     A                                      DSPATR(HI)
     A                                  3 32'<<客戶訂貨單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DCD01     R        O  4 14REFFLD(CD01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5  2'訂貨單號:'
     A                                      COLOR(BLU)
     A            DCD05     R        O  5 14REFFLD(CD05)
     A                                  6  2'通路代號:'
     A                                      COLOR(BLU)
     A            DCD02     R        O  6 14REFFLD(CD02)
     A            DND04     R        O  6 17REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  7  2'分店代號:'
     A                                      COLOR(BLU)
     A            DCD04     R        B  7 14REFFLD(CD04)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DNE05     R        O  7 20REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  9  2'運送方式:'
     A                                      COLOR(BLU)
     A            DCD11     R        B  9 14REFFLD(CD11)
     A                                      VALUES('1' '2' '3')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 16'(供應廠商-１經大榮到客戶，２直+
     A                                      送客戶，３直送分店)'
     A                                      COLOR(BLU)
     A                                 10  2'訂貨日期:'
     A                                      COLOR(BLU)
     A            DCD03     R   -2   O 10 14REFFLD(CD03)
     A                                      EDTCDE(Y)
     A                                 10 23'(月／日／年)'
     A                                      COLOR(BLU)
     A  81                             11  2'預計到運貨日期:'
     A                                      COLOR(BLU)
     A  81        DCD07     R   -2   O 11 20REFFLD(CD07)
     A                                      EDTCDE(Y)
     A  81                             11 29'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 12  2'預計到分店日期:'
     A                                      COLOR(BLU)
     A            DCD08     R   -2   B 12 20REFFLD(CD08)
     A                                      EDTCDE(Y)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 29'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 13  2'預計到貨時間:'
     A                                      COLOR(BLU)
     A            DCD10A    R   -2   B 13 18REFFLD(CD10)
     A                                      EDTCDE(Z)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 21'-'
     A                                      COLOR(BLU)
     A            DCD10B    R   -2   B 13 23REFFLD(CD10)
     A                                      EDTCDE(Z)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 26'時'
     A                                      COLOR(BLU)
     A            DCD09     R        B 13 31REFFLD(CD09)
     A                                      VALUES('A' 'P')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 33'( A-上午／P-下午)'
     A                                      COLOR(BLU)
     A  82                             15  2'客戶姓名:'
     A                                      COLOR(BLU)
     A  82        DCF02     R        B 15 14REFFLD(CF02 POCFPF)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  82                             16  2'聯絡電話:'
     A                                      COLOR(BLU)
     A  82        DCF03     R        B 16 14REFFLD(CF03 POCFPF)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  82                             16 45'行動電話:'
     A                                      COLOR(BLU)
     A  82        DCF04     R        B 16 57REFFLD(CF04 POCFPF)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  82                             17  2'送貨地址:'
     A                                      COLOR(BLU)
     A  82        DCF05     R        B 17 14REFFLD(CF05 POCFPF)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*                                18  2'備註  :'
     A*                                     COLOR(BLU)
     A*           DMARK     R        B 18 14REFFLD(CG03 POCGPF)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
      *
     A                                 20  2'製表:'
     A                                      COLOR(BLU)
     A            DCD13         10O  O 20 10
     A                                 20 25'審核:'
     A                                      COLOR(BLU)
     A            DCD14         10O  O 20 33
      *
      *
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                 22 42'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 11  3VALUES(' ' '1' '2' '4')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A            DCE03     R        B 11  5REFFLD(CE03 POCEPF)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DNC05     R        O 11 15REFFLD(NC05 MANCPF)
     A            DCC09     R        O 11 34REFFLD(CC09 POCCPF)
     A                                      EDTCDE(J)
     A            DTB16     R        O 11 46REFFLD(TB16 INTBPF)
     A                                      EDTCDE(J)
     A  18                                  DSPATR(ND)
     A            DTB07     R        O 11 55REFFLD(TB07 INTBPF)
     A                                      EDTCDE(J)
     A  18                                  DSPATR(ND)
     A            DCE04     R        O 11 64REFFLD(CE04 POCEPF)
     A                                      EDTCDE(J)
     A  19                                  DSPATR(ND)
     A            DCE05     R        B 11 73REFFLD(CE05 POCEPF)
     A                                      EDTCDE(J)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            HCE05     R        H      REFFLD(CE05 POCEPF)
     A            DCHK           1A  H
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A                                      CA12(12)
     A  88                                  CF16(16)
     A  89                                  CF17(17)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC013'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 34'訂貨單審核'
     A                                      DSPATR(HI)
     A  91                              3 32'<<一般訂貨單>>'
     A                                      COLOR(PNK)
     A  92                              3 32'<<客戶訂貨單>>'
     A                                      COLOR(PNK)
     A  93                              3 32'<<陳列訂貨單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DCD01     R        O  4 13REFFLD(CD01)
     A            D#B03     R   -4   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 32'通路代號:'
     A                                      COLOR(BLU)
     A            DCD02     R        O  4 44REFFLD(CD02)
     A            DND04     R        O  4 47REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'分店代號:'
     A                                      COLOR(BLU)
     A            DCD04     R        O  5 13REFFLD(CD04)
     A            DNE05     R        O  5 19REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  5 32'訂貨單號:'
     A                                      COLOR(BLU)
     A            DCD05     R        O  5 44REFFLD(CD05)
     A  81                              5 58'到貨運日期:'
     A                                      COLOR(BLU)
     A  81        DCD07     R   -2   O  5 72REFFLD(CD07)
     A                                      EDTCDE(Y)
     A                                  6  1'訂貨日期:'
     A                                      COLOR(BLU)
     A            DCD03     R   -2   O  6 13REFFLD(CD03)
     A                                      EDTCDE(Y)
     A                                  6 32'到分店日期:'
     A                                      COLOR(BLU)
     A            DCD08     R   -2   O  6 46REFFLD(CD08)
     A                                      EDTCDE(Y)
     A                                  6 58'(月／日／年)'
     A                                      COLOR(BLU)
     A  82                              7  1'客戶名稱:'
     A                                      COLOR(BLU)
     A  82        DCF02     R        O  7 13REFFLD(CF02 POCFPF)
     A  83                              7 32'運送方式:'
     A                                      COLOR(BLU)
     A  83        DCD11     R        O  7 45REFFLD(CD11)
     A  83        CD11N         20O  O  7 47
     A                                  8  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  8 17'1=新增'
     A                                      COLOR(BLU)
     A                                  8 26'2=修改'
     A                                      COLOR(BLU)
     A                                  8 35'4=刪除'
     A                                      COLOR(BLU)
     A                                  9 38'(未稅)'
     A                                      COLOR(BLU)
     A                                  9 48'分店'
     A                                      COLOR(BLU)
     A                                  9 57'分店'
     A                                      COLOR(BLU)
     A                                  9 66'分店'
     A                                      COLOR(BLU)
     A                                  9 74'分店'
     A                                      COLOR(BLU)
     A                                 10  2' A產品代號產品名稱          -
     A                                       單價  陳列量 庫存量 訂貨+
     A                                      量採購量'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 17'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                 22 32'F12=回前畫面'
     A                                      COLOR(BLU)
     A  80                             22 47'F14=審核存檔'
     A                                      COLOR(BLU)
     A  88                             22 62'F16=供應商備註'
     A                                      COLOR(BLU)
     A* 89                             22 62'F17=修改客戶資料'
     A  89                             23  2'F17=修改客戶資料'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPD4
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC013'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 34'訂貨單審核'
     A                                      DSPATR(HI)
     A                                  3 32'<<客戶訂貨單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DCD01     R        O  4 13REFFLD(CD01)
     A            D#B03     R   -4   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DCD02     R        O  4 43REFFLD(CD02)
     A            DND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'分店代號:'
     A                                      COLOR(BLU)
     A            DCD04     R        O  5 13REFFLD(CD04)
     A            DNE05     R        O  5 19REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  5 31'訂貨單號:'
     A                                      COLOR(BLU)
     A            DCD05     R        O  5 43REFFLD(CD05)
     A                                  5 57'到貨運日期:'
     A                                      COLOR(BLU)
     A            DCD07     R   -2   O  5 71REFFLD(CD07)
     A                                      EDTCDE(Y)
     A                                  6  1'訂貨日期:'
     A                                      COLOR(BLU)
     A            DCD03     R   -2   O  6 13REFFLD(CD03)
     A                                      EDTCDE(Y)
     A                                  6 31'到分店日期:'
     A                                      COLOR(BLU)
     A            DCD08     R   -2   O  6 45REFFLD(CD08)
     A                                      EDTCDE(Y)
     A                                  6 58'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  8  1'客戶姓名:'
     A                                      COLOR(BLU)
     A            DCF02     R        B  8 13REFFLD(CF02 POCFPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  1'聯絡電話:'
     A                                      COLOR(BLU)
     A            DCF03     R        B 10 13REFFLD(CF03 POCFPF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 45'行動電話:'
     A                                      COLOR(BLU)
     A            DCF04     R        B 10 57REFFLD(CF04 POCFPF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  1'送貨地址:'
     A                                      COLOR(BLU)
     A            DCF05     R        B 12 13REFFLD(CF05 POCFPF)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*                                14  1'備註  :'
     A*                                     COLOR(BLU)
     A*           DMARK     R        B 14 13REFFLD(CG03 POCGPF)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R SFLSR3                    SFL
     A  54                                  SFLNXTCHG
     A            RRN3           4S 0H
     A            DOPT3          1A  B 10  3VALUES(' ' '1' '2' '4')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A            DCG02     R        B 10  5REFFLD(CG02 POCGPF)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DCG03     R        B 10 18REFFLD(CG03 POCGPF)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            KNB03     R        O 11  5REFFLD(NB03 MANBPF)
     A            DCG04     R        B 11 18REFFLD(CG04 POCGPF)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DCHK3          1A  H
     A*----------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0005)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN3          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC013'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 34'訂貨單審核'
     A                                      DSPATR(HI)
     A*                                 3 32'<<一般訂貨單>>'
     A  21                              3 32'<<一般訂貨單>>'
     A                                      COLOR(PNK)
     A N21                              3 32'<<客戶訂貨單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DCD01     R        O  4 13REFFLD(CD01)
     A            D#B03     R   -4   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DCD02     R        O  4 43REFFLD(CD02)
     A            DND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'分店代號:'
     A                                      COLOR(BLU)
     A            DCD04     R        O  5 13REFFLD(CD04)
     A            DNE05     R        O  5 19REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  5 31'訂貨單號:'
     A                                      COLOR(BLU)
     A            DCD05     R        O  5 43REFFLD(CD05)
     A                                  5 57'到貨運日期:'
     A                                      COLOR(BLU)
     A            DCD07     R   -2   O  5 71REFFLD(CD07)
     A                                      EDTCDE(Y)
     A                                  6  1'訂貨日期:'
     A                                      COLOR(BLU)
     A            DCD03     R   -2   O  6 13REFFLD(CD03)
     A                                      EDTCDE(Y)
     A                                  6 31'到分店日期:'
     A                                      COLOR(BLU)
     A            DCD08     R   -2   O  6 45REFFLD(CD08)
     A                                      EDTCDE(Y)
     A                                  6 58'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  7  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  7 17'1=新增'
     A                                      COLOR(BLU)
     A                                  7 26'2=修改'
     A                                      COLOR(BLU)
     A                                  7 35'4=刪除'
     A                                      COLOR(BLU)
     A                                  9  2' A供應商代號    採購單備註    -
                                                                               -
                                                 '                             -
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC4
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  80                                +2'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  80                                +2'F9 =確認存檔'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
