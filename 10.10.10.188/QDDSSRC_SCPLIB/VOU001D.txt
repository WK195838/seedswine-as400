     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250
     A*
     A*    PROGRAM ID   : VOU001D
     A*    PROGRAM NAME : 銷售傳票批次
     A*    AUTHOR       : A1492 TERRY
     A*    CREATE DATE  : 2009.01.17
     A*    UPDATE DATE  :
     A*    SYSTEM       : 進銷存收TO總帳
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CF04(04 'F4 HELP')
     A                                      CF14(14 '處理作業')
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'VOU001'
     A                                  1 24MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 63'日期:'
     A            $ADMDY         6Y 0O  1 71EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'銷售傳票批次'
     A                                      DSPATR(UL HI)
     A                                  2 63'時間:'
     A                                  2 71TIME
     A                                  3 63' USER :'
     A            $USER         10A  O  3 71DSPATR(UL)
     A                                  8 26'公司別  :'
     A            D01            1A  B  8 39
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                 10 26'年月份  :'
     A            D02            4Y 0B 10 39
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                      EDTWRD('  /  ')
     A                                 10 45'(月/年)'
     A                                 13 26'帳務區間:'
     A            F01            8Y 0O 13 39EDTWRD('    /  /  ')
     A                                    +1'~'
     A            F02            8Y 0O    +1EDTWRD('    /  /  ')
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 39VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 41'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機  :'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備１３２行報表紙***'
     A  28                             21 30'傳票處理中，請稍候'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F14=作業處理'
     A                                 24 67'【泛太資訊】'
     A                                 23 26'F4 =視窗查詢'
