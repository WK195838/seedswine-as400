     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : INR045P                                    *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 100/05/27                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR046                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :現場產品庫存表－依分店                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<INR046P>'
     A                                    26
     A                                      MSGCON(030 USC9999 SCMF)
     A                                    71
     A                                      '日期:'
     A            $EGMDY         6  0O    79
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    28
     A                                      '現場產品庫存表－依分店'
     A                                    71
     A                                      '時間:'
     A                                    79
     A                                      TIME
     A                                      SPACEA(001)
     A                                    71
     A                                      '頁次:'
     A                                    79
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '公司代號:'
     A            DTO01     R        O    13
     A                                      REFFLD(TB01)
     A            D#B03     R   -4   O    16
     A                                      REFFLD(#B03 PA#BPF)
     A                                    33
     A                                      '通路代號:'
     A            DTO02S         2A  O    45
     A                                    48
     A                                      '-'
     A            DTO02E         2A  O    50
     A                                    53
     A                                      '分店代號:'
     A            DTO03S         5   O    65
     A                                    71
     A                                      '-'
     A            DTO03E         5   O    73
     A                                      SPACEA(001)
     A                                     1
     A                                      '產品代號:'
     A            DTO04S         9   O    13
     A                                    23
     A                                      '-'
     A            DTO04E         9   O    25
     A                                    34
     A                                      '本期截止日:'
     A            DTO05          6S 0O    48
     A                                      EDTCDE(Y)
     A                                    57
     A                                      '(月／日／年)'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     1
     A                                      '分店代號'
     A                                      SPACEB(001)
     A                                    11
     A                                      '分店名稱'
     A                                    22
     A                                      '產品代號'
     A                                    35
     A                                      '產品簡稱'
     A                                    48
     A                                      '現場庫存量'
     A                                    60
     A                                      '帳列庫存量'
     A                                    74
     A                                      '陳列餘量'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      --------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        W803           5A        2
     A  30        W804          10O       11
     A            W805           9A       22
     A            W806          18O       32
     A            W810           6S 0     51
     A                                      EDTCDE(J)
     A            W809           6S 0     63
     A                                      EDTCDE(J)
     A            W809A          6S 0     75
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    21
     A                                      '          分店合計:'
     A            QT1            8S 0     48
     A                                      EDTCDE(J)
     A            QT2            8S 0     60
     A                                      EDTCDE(J)
     A            QT3            8S 0     72
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    21
     A                                      '          總計:'
     A            Q1             8S 0     48
     A                                      EDTCDE(J)
     A            Q2             8S 0     60
     A                                      EDTCDE(J)
     A            Q3             8S 0     72
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<INR046P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
