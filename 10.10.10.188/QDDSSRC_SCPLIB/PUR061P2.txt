     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : PUR061P2                                   *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 101/01/09                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR061                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :採購單彙總表                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<PUR061>'
     A                                    56
     A                                      MSGCON(030 USC9999 SCMF)
     A                                   130
     A                                      '日期:'
     A            $EGMDY         6  0O   138
     A                                      EDTCDE(Y)
     A                                    59
     A                                      '採購單彙總表-明細表'
     A                                      SPACEB(001)
     A                                   130
     A                                      '時間:'
     A                                   138
     A                                      TIME
     A                                   130
     A                                      '頁次:'
     A                                      SPACEB(001)
     A                                   138
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     1
     A                                      '公司別  :'
     A                                      SPACEB(001)
     A            DCH01          2   O    13
     A            D#B03     R        O    16
     A                                      REFFLD(#B03 PA#BPF)
     A                                     1
     A                                      '通路代號:'
     A                                      SPACEB(001)
     A            DCH02S         2   O    13
     A                                    16
     A                                      '-'
     A            DCH02E         2   O    18
     A                                    35
     A                                      '分店代號:'
     A            DCH07S         5   O    47
     A                                    53
     A                                      '-'
     A            DCH07E         5   O    55
     A                                    67
     A                                      '供應商代號:'
     A            DCH06S         5   O    81
     A                                    87
     A                                      '-'
     A            DCH06E         5   O    89
     A                                     1
     A                                      '產品代號:'
     A                                      SPACEB(001)
     A            DCI03S         9   O    13
     A                                    23
     A                                      '-'
     A            DCI03E         9   O    25
     A                                    35
     A                                      '訂貨日期:'
     A            DCD03S         6  0O    47
     A                                      EDTCDE(Y)
     A                                    56
     A                                      '-'
     A            DCD03E         6  0O    58
     A                                      EDTCDE(Y)
     A                                    67
     A                                      '到貨運日:'
     A            DCH08S         6  0O    79
     A                                      EDTCDE(Y)
     A                                    88
     A                                      '-'
     A            DCH08E         6  0O    90
     A                                      EDTCDE(Y)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEB(001)
     A                                     1
     A                                      '供應商代號／簡稱'
     A                                      SPACEB(001)
     A                                    21
     A                                      '訂貨日期'
     A                                    32
     A                                      '到貨運日'
     A                                    43
     A                                      '產品代號'
     A                                    54
     A                                      '產品簡稱'
     A                                    71
     A                                      '分店代號／簡稱'
     A                                    88
     A                                      '採購數量'
     A                                   100
     A                                      '未稅進價'
     A                                   113
     A                                      '未稅金額'
     A                                   126
     A                                      '含稅金額'
     A                                   137
     A                                      '採購單號'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        W25202         5         2
     A  30        W25203        12O        8
     A            W25204         8  0     21
     A                                      EDTCDE(W)
     A            W25205         8  0     32
     A                                      EDTCDE(W)
     A            W25206         9        43
     A            W25207        18O       53
     A            W25208         5        72
     A            W25209        10O       78
     A            W25210         6  0     91
     A                                      EDTCDE(L)
     A            W25211         8  2     99
     A                                      EDTCDE(J)
     A            W25212         9  0    111
     A                                      EDTCDE(J)
     A            W25213         9  0    124
     A                                      EDTCDE(J)
     A            W25214        13       137
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    78
     A                                      '產品合計:'
     A            I25210         6  0     91
     A                                      EDTCDE(L)
     A            I25212         9  0    111
     A                                      EDTCDE(J)
     A            I25213         9  0    124
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    78
     A                                      '廠商合計:'
     A            D25210         6  0     91
     A                                      EDTCDE(L)
     A            D25212         9  0    111
     A                                      EDTCDE(J)
     A            D25213         9  0    124
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD4
     A                                      SPACEB(001)
     A                                    82
     A                                      '總計:'
     A            T25210         6  0     91
     A                                      EDTCDE(L)
     A            T25212         9  0    111
     A                                      EDTCDE(J)
     A            T25213         9  0    124
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<PUR061P2>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
