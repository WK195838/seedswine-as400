     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : SER040D                                    *
     A*    PROGRAM NAME :產品銷貨收入彙總表                        *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  : 2008/11/06                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A*---------------------------------------------------------------
     A* 00A需求單號: 1080949
     A* 00A CAROL  2019/08/02新增篩選條件供應商代號&產品代號
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF07(07)
     A*                                     CF14(14)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'SER040'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 63'日期:'
     A            $DATE          6Y 0O  1 71EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'產品銷貨收入彙總表'
     A                                      DSPATR(HI)
     A                                  2 63'時間:'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 63' USER :'
     A            $USER         10A  O  3 71
     A                                  6 25'公司代號  :'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DHA01     R        B    +1REFFLD(HA01 SEHAPF)
     A  61                                  DSPATR(PC RI)
     A            D#B03         16O  O    +1COLOR(BLU)
     A                                  8 25'通路代號  :'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DHA02     R        B    +1REFFLD(HA02 SEHAPF)
     A  62                                  DSPATR(PC RI)
     A            DND04         16O  O    +1COLOR(BLU)
     A                                 10 25'銷售日期  :'
     A                                      COLOR(BLU)
     A            DHA03S         6Y 0B    +1EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC RI)
     A                                    +1'-'
     A            DHA03E         6Y 0B    +1EDTWRD('  /  /  ')
     A  64                                  DSPATR(PC RI)
     A                                    +1'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 12 25'產品歸屬  :'
     A*                                     COLOR(BLU)
     A                                      DSPATR(HI)
     A            DNC09S         2A  B    +1
     A  65                                  DSPATR(RI PC)
     A                                    +1'-'
     A                                      COLOR(BLU)
     A            DNC09E         2A  B    +1
     A  66                                  DSPATR(RI PC)
     A                                    +1'(空白：全部)'
 00A+A                                 14 25'供應商代號:'
 00A+A                                      DSPATR(HI)
 00A+A                                      COLOR(BLU)
 00A+A            DNB01S         5A  B 14 39
 00A+A  67                                  DSPATR(RI)
 00A+A  67                                  DSPATR(PC)
 00A+A                                 14 45'-'
 00A+A                                      COLOR(BLU)
 00A+A            DNB01E         5A  B 14 47
 00A+A  68                                  DSPATR(RI)
 00A+A  68                                  DSPATR(PC)
 00A+A                                 14 53'(空白：全部)'
 00A+A                                      COLOR(BLU)
 00A+A                                 15 25'產品代號  :'
 00A+A                                      DSPATR(HI)
 00A+A                                      COLOR(BLU)
 00A+A            DHA05S         9A  B 15 39
 00A+A  69                                  DSPATR(RI)
 00A+A  69                                  DSPATR(PC)
 00A+A                                 15 49'-'
 00A+A                                      COLOR(BLU)
 00A+A            DHA05E         9A  B 15 51
 00A+A  70                                  DSPATR(RI)
 00A+A  70                                  DSPATR(PC)
 00A+A                                 15 61'(空白：全部)'
 00A+A                                      COLOR(BLU)
     A*
     A                                 17 25'執行環境　:'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 17 39VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 45'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 18 25'列印份數　:'
     A                                      COLOR(BLU)
     A            $CPY           3Y 0B 18 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 25'列表機　　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                    +1'F7 =檔案下載'
     A                                      COLOR(BLU)
     A*                                22 43'F14 =處理作業'
     A*                                     COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
