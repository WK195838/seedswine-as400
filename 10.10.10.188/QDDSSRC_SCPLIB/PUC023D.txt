     A*%%TS  SD  20091116  101433  CWDEV5      REL-V5R1M0  5722-WDS
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PUC023D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/10/14                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :採購管理建檔子系統                        *
     A*    REMARK       :貨運預估箱數維護                          *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/POCKPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '2' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SCK03     R   -2   O  9  6REFFLD(CK03)
     A                                      EDTCDE(Y)
     A            CK05      R        O  9 18
     A            CK08      R        O  9 32
     A            SNB03     R        O  9 38REFFLD(NB03 MANBPF)
     A            STAT           1A  O  9 53
     A*
     A            CK01      R        H
     A            CK02      R        H
     A            CK03      R        H
     A            CK06      R        H
     A            CK10      R        H
     A            CK17      R        H
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF14(14)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'PUC023'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'應到貨箱數表寄出作業'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'採購日期:'
     A                                      COLOR(BLU)
     A*           DBGN8     R   -2   O  4 13REFFLD(CK15)
     A            DBGN8     R   -2   B  4 13REFFLD(CK15)
     A                                      EDTCDE(Y)
     A                                  4 24'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 36REFFLD(CK01)
     A            S#B03     R   -6   O  4 39REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 52'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 64REFFLD(CK02)
     A            SND04     R        O  4 67REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'2=修改'
     A                                      COLOR(BLU)
     A                                  5 26'5=查詢'
     A                                      COLOR(BLU)
     A                                  6  3'(月／日／年)'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  7  3'A 到運貨日  批次號碼    供應+
     A                                      商代號／名稱 狀態'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A            DBGN3     R   -2   B  8  6REFFLD(CK03)
     A                                      EDTCDE(Y)
     A            DBGN4     R        B  8 18REFFLD(CK05)
     A            DBGN5     R        B  8 32REFFLD(CK08)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F14 =處理作業'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *此燈號控制新增成功以及修改成功專用
     A  95        ERR0          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(ERR 0132 SCMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(MSG 0005 SCMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(MSG 0013 SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A*%%TS  SD  20091116  101433  CWDEV5      REL-V5R1M0  5722-WDS
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 10  2VALUES(' ' '2')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DCK06     R        O 10  5REFFLD(CK06)
     A            CK06N          6O  O 10  7
     A            DCK10     R        O 10 17REFFLD(CK10)
     A            DNE05     R        O 10 23REFFLD(NE05 MANEPF)
     A            DCK11     R        B 10 35REFFLD(CK11)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                      EDTCDE(J)
     A            DCF02     R        O 10 43REFFLD(CF02 POCFPF)
     A            DCHK           1A  H
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A  33                                  CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC023'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 29'應到貨箱數表寄出作業'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'採購日期:'
     A                                      COLOR(BLU)
     A            DCK15     R   -2   O  4 13REFFLD(CK15)
     A                                      EDTCDE(Y)
     A                                  4 26'公司代號:'
     A                                      COLOR(BLU)
     A            DCK01     R        O  4 38REFFLD(CK01)
     A            D#B03     R   -6   O  4 41REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 54'通路代號:'
     A                                      COLOR(BLU)
     A            DCK02     R        O  4 66REFFLD(CK02)
     A            DND04     R        O  4 69REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'供應商代號:'
     A                                      COLOR(BLU)
     A            DCK08     R        O  5 15REFFLD(CK08)
     A            DNB03     R   -2   O  5 21REFFLD(NB03 MANBPF)
     A                                      COLOR(BLU)
     A                                  5 33'到運貨日期:'
     A                                      COLOR(BLU)
     A            DCK03     R   -2   O  5 47REFFLD(CK03)
     A                                      EDTCDE(Y)
     A                                  5 56'批次序號:'
     A                                      COLOR(BLU)
     A            DCK05     R        O  5 68REFFLD(CK05)
     A                                  6  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  6 17'2=修改'
     A                                      COLOR(BLU)
     A                                  8  2'A 運送類別  分店代號      預+
     A                                      估箱數  客戶名稱       '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A            DBGN6     R        B  9  5REFFLD(CK06)
     A            DBGN7     R        B  9 17REFFLD(CK10)
      *----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  33                                +2'F9 =確認存檔'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
