     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    DSPF NAME    : INR120D                                    *
     A*    AUTHOR       : DAISY                                      *
     A*    CREATE DATE  : 102/12/27                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR120D                                    *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :盤盈銷售明細表                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'INR120'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'盤盈銷售明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 24'公司代號:'
     A                                      COLOR(BLU)
     A            DTQ01     R        B  6 36REFFLD(TI01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A            D#B03     R        O  6 39REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A*
     A                                  8 24'通路代號:'
     A                                      COLOR(BLU)
     A            DTQ02S    R        B  8 36REFFLD(TI02)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  8 39'-'
     A                                      COLOR(BLU)
     A            DTQ02E    R        B  8 41REFFLD(TI02)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  8 45'(空白：全部)'
     A                                      COLOR(BLU)
     A*
     A                                 10 24'銷售日期:'
     A                                      COLOR(BLU)
     A            DTQ03S         6Y 0B 10 36EDTWRD('  /  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 10 45'-'
     A            DTQ03E         6Y 0B 10 47EDTWRD('  /  /  ')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 10 57'(月／日／年)'
     A                                      COLOR(BLU)
     A*
     A                                 12 24'執行環境:'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 12 36VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 12 39'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
