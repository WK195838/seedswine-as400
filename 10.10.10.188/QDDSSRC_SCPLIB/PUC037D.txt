     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PUC037D                                    *
     A*    AUTHOR       : A1546  VINCENT                             *
     A*    CREATE DATE  : 98/03/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :採購管理子系統                            *
     A*    REMARK       :貨運箱數運費歷史查詢                      *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/POCKPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SCK03     R   -2   O  9  6REFFLD(CK03)
     A                                      EDTCDE(Y)
     A            CK05      R        O    +2
     A            CK08      R        O    +2
     A            SNB03     R        O    +1REFFLD(NB03 MANBPF)
     A            SCK12     R   +1   O    +4REFFLD(CK12)
     A                                      EDTCDE(2)
     A*
     A            CK01      R        H
     A            CK02      R        H
     A            CK03      R        H
     A            CK06      R        H
     A            CK10      R        H
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'PUC037'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'貨運箱數運費歷史查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 13REFFLD(CK01)
     A            S#B03     R   -6   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(CK02)
     A            SND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'5=查詢'
     A                                      COLOR(BLU)
     A                                  6  3'(月／日／年)'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  7  3'A 到運貨日批次號碼    供應+
     A                                      商代號／名稱 實際總箱數'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A            DBGN3     R   -2   B  8  6REFFLD(CK03)
     A                                      EDTCDE(Y)
     A            DBGN4     R        B    +2REFFLD(CK05)
     A            DBGN5     R        B    +2REFFLD(CK08)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DCK06     R        O 10  5REFFLD(CK06)
     A            CK06N          6O  O    +1
     A            DCK10     R        O    +2REFFLD(CK10)
     A            DNE05     R        O    +1REFFLD(NE05 MANEPF)
     A            DCK12     R        O    +3REFFLD(CK12)
     A                                      EDTCDE(2)
     A            DCK13     R        O    +3REFFLD(CK13)
     A                                      EDTCDE(2)
     A            DCK14     R        O    +2REFFLD(CK14)
     A                                      EDTCDE(2)
     A            DCHK           1A  H
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC037'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 29'貨運箱數運費歷史查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DCK01     R        O  4 13REFFLD(CK01)
     A            D#B03     R   -6   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 33'通路代號:'
     A                                      COLOR(BLU)
     A            DCK02     R        O  4 45REFFLD(CK02)
     A            DND04     R        O  4 48REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'供應商代號:'
     A                                      COLOR(BLU)
     A            DCK08     R        O  5 15REFFLD(CK08)
     A            DNB03     R   -2   O  5 21REFFLD(NB03 MANBPF)
     A                                      COLOR(BLU)
     A                                  5 33'到運貨日期:'
     A                                      COLOR(BLU)
     A            DCK03     R   -2   O  5 47REFFLD(CK03)
     A                                      EDTCDE(Y)
     A                                  5 56'批次序號:'
     A                                      COLOR(BLU)
     A            DCK05     R        O  5 68REFFLD(CK05)
     A                                  8  3' 運送類別分店代號／名稱 實+
     A                                      際箱數 單價   金額'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A            DBGN6     R        B  9  5REFFLD(CK06)
     A            DBGN7     R        B  9 15REFFLD(CK10)
      *----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A                                    +2'F14=確認'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
