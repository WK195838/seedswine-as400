     A*%%TS  SD  20091113  134107  CWDEV5      REL-V5R1M0  5722-WDS
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PUC026D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/09/26                                   *
     A*    UPDATE DATE  : 108/08/13                                  *
     A*    PROGRAM NAME : PUC026                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :採購單查詢-採購單號                     *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*00A需求單號: 1080981
     A*00A  108/08/13 CAROL採購單查詢 (依採購單排序)           *
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/POCHPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  3VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
 00A-A*           SCH03     R   -2   O 10  6REFFLD(CH03)
 00A-A*                                     EDTCDE(Y)
 00A-A*           CH04      R        O 10 15
 00A+A            CH04      R        O 10  6
 00A+A            SCH03     R   -2   O 10 20REFFLD(CH03)
 00A+A                                      EDTCDE(Y)
     A            CH06      R        O 10 29
     A            SNB03     R        O 10 35REFFLD(NB03 MANBPF)
     A            CH07      R        O 10 49
     A            SNE05     R        O 10 55REFFLD(NE05 MANEPF)
     A            SCH08     R   -2   O 10 67REFFLD(CH08)
     A                                      EDTWRD('  /  /  ')
     A            CH01      R        H
     A            CH02      R        H
     A            CH03      R        H
     A            CH05      R        H
     A            CH08      R        H
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
 00A-A*                                 1  2'PUC022'
 00A+A                                  1  2'PUC026'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
 00A-A*                                 2 34'採購單查詢'
 00A+A                                  2 28'採購單查詢-採購單號'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 13REFFLD(CH01)
     A            S#B03     R   -6   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(CH02)
     A            SND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'5=查詢'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
 00A-A*           DBGN3     R   -2   B  9  6REFFLD(CH03)
 00A-A*                                     EDTCDE(Y)
 00A-A*           DBGN4     R        B  9 15REFFLD(CH04)
 00A+A            DBGN4     R        B  9  6REFFLD(CH04)
 00A+A            DBGN3     R   -2   B  9 20REFFLD(CH03)
 00A+A                                      EDTCDE(Y)
     A                                  7  3'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  7 64'(月／日／年)'
     A                                      COLOR(BLU)
 00A-A*                                 8  3'A 採購日期採購單號   供應商+
 00A-A*                                     代號／名稱 分店代號／名稱 預+
 00A-A*                                     計到貨運日'
 00A+A                                  8  3'A 採購單號    採購日期供應商+
 00A+A                                      代號／名稱 分店代號／名稱 預+
 00A+A                                      計到貨運日'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *此燈號控制新增成功以及修改成功專用
     A  95        ERR0          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(MSG 0005 SCMF)
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
 00A-A*                                 1  2'PUC022'
 00A+A                                  1  2'PUC026'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
 00A-A*                                 2 34'採購單查詢'
 00A+A                                  2 28'採購單查詢-採購單號'
     A                                      DSPATR(HI)
     A  15                              3 32'<<陳列採購單>>'
     A                                      COLOR(PNK)
     A N15                              3 32'<<一般採購單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DCH01     R        O  4 14REFFLD(CH01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5  2'採購單號:'
     A                                      COLOR(BLU)
     A            DCH04     R        O  5 14REFFLD(CH04)
     A                                  6  2'通路代號:'
     A                                      COLOR(BLU)
     A            DCH02     R        O  6 14REFFLD(CH02)
     A            DND04     R        O  6 17REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  6 40'供應廠商:'
     A                                      COLOR(BLU)
     A            DCH06     R        O  6 52REFFLD(CH06)
     A            DNB03     R        O  6 58REFFLD(NB03 MANBPF)
     A                                      COLOR(BLU)
     A                                  7  2'分店代號:'
     A                                      COLOR(BLU)
     A            DCH07     R        O  7 14REFFLD(CH07)
     A            DNE05     R        O  7 20REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  7 40'貨運站  :'
     A                                      COLOR(BLU)
     A            DNA03     R        O  7 52REFFLD(NA03 MANAPF)
     A                                      COLOR(BLU)
     A                                 10  2'採購單日期:'
     A                                      COLOR(BLU)
     A            DCH03     R   -2   O 10 16REFFLD(CH03)
     A                                      EDTCDE(Y)
     A                                 10 29'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 11  2'預計到運貨日期:'
     A                                      COLOR(BLU)
     A            DCH08     R   -2   O 11 20REFFLD(CH08)
     A                                      EDTCDE(Y)
     A                                 11 29'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 12  2'預計到分店日期:'
     A                                      COLOR(BLU)
     A            DCH09     R   -2   O 12 20REFFLD(CH09)
     A                                      EDTCDE(Y)
     A                                 12 29'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 13  2'預計到貨時間:'
     A                                      COLOR(BLU)
     A            DCH10     R        O 13 18REFFLD(CH10)
     A                                 13 20'( A-上午10-12點／P-下午02-04-
     A                                      點)'
     A                                      COLOR(BLU)
 00A+A                                 15  2'驗收單號:'
 00A+A                                      COLOR(BLU)
 00A+A            DCI05     R        O 15 14REFFLD(CI05 POCIPF)
     A N15                             18  2'備註:'
     A                                      COLOR(BLU)
     A N15        DCH14     R        O 18 10REFFLD(CH14)
     A N15        DCH15     R        O 19 10REFFLD(CH15)
      *
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD3
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
 00A-A*                                 1  2'PUC022'
 00A+A                                  1  2'PUC026'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
 00A-A*                                 2 34'採購單查詢'
 00A+A                                  2 28'採購單查詢-採購單號'
     A                                      DSPATR(HI)
     A                                  3 32'<<客戶採購單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DCH01     R        O  4 14REFFLD(CH01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5  2'採購單號:'
     A                                      COLOR(BLU)
     A            DCH04     R        O  5 14REFFLD(CH04)
     A                                  6  2'通路代號:'
     A                                      COLOR(BLU)
     A            DCH02     R        O  6 14REFFLD(CH02)
     A            DND04     R        O  6 17REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  6 40'供應廠商:'
     A                                      COLOR(BLU)
     A            DCH06     R        O  6 52REFFLD(CH06)
     A            DNB03     R        O  6 58REFFLD(NB03 MANBPF)
     A                                      COLOR(BLU)
     A                                  7  2'分店代號:'
     A                                      COLOR(BLU)
     A            DCH07     R        O  7 14REFFLD(CH07)
     A            DNE05     R        O  7 20REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  7 40'貨運站  :'
     A                                      COLOR(BLU)
     A            DNA03     R        O  7 52REFFLD(NA03 MANAPF)
     A                                      COLOR(BLU)
     A                                  9  2'運送方式:'
     A                                      COLOR(BLU)
     A            DCH12     R        O  9 14REFFLD(CH12)
     A                                  9 16'(供應廠商-１經大榮到客戶，２直+
     A                                      送客戶，３直送分店)'
     A                                      COLOR(BLU)
     A                                 10  2'訂貨日期:'
     A                                      COLOR(BLU)
     A            DCH03     R   -2   O 10 14REFFLD(CH03)
     A                                      EDTCDE(Y)
     A                                 10 23'(月／日／年)'
     A                                      COLOR(BLU)
     A  81                             11  2'預計到運貨日期:'
     A                                      COLOR(BLU)
     A  81        DCH08     R   -2   O 11 20REFFLD(CH08)
     A                                      EDTCDE(Y)
     A  81                             11 29'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 12  2'預計到貨日期:'
     A                                      COLOR(BLU)
     A            DCH09     R   -2   O 12 18REFFLD(CH09)
     A                                      EDTCDE(Y)
     A                                 12 29'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 13  2'預計到貨時間:'
     A                                      COLOR(BLU)
     A            DCH11A    R   -2   O 13 18REFFLD(CH11)
     A                                      EDTCDE(Z)
     A                                 13 21'-'
     A                                      COLOR(BLU)
     A            DCH11B    R   -2   O 13 23REFFLD(CH11)
     A                                      EDTCDE(Z)
     A                                 13 26'時'
     A                                      COLOR(BLU)
     A            DCH10     R        O 13 31REFFLD(CH10)
     A                                 13 33'( A-上午／P-下午)'
     A                                      COLOR(BLU)
     A  82                             15  2'客戶姓名:'
     A                                      COLOR(BLU)
     A  82        DCJ02     R        O 15 14REFFLD(CJ02 POCJPF)
     A  82                             16  2'聯絡電話:'
     A                                      COLOR(BLU)
     A  82        DCJ03     R        O 16 14REFFLD(CJ03 POCJPF)
     A  82                             16 45'行動電話:'
     A                                      COLOR(BLU)
     A  82        DCJ04     R        O 16 57REFFLD(CJ04 POCJPF)
     A  82                             17  2'送貨地址:'
     A                                      COLOR(BLU)
     A  82        DCJ05     R        O 17 14REFFLD(CJ05 POCJPF)
     A                                 18  2'備註  :'
     A                                      COLOR(BLU)
     A            DCH14     R        O 18 14REFFLD(CH14 POCHPF)
     A            DCH15     R        O 19 14REFFLD(CH15 POCHPF)
 00A+A                                 20  2'驗收單號:'
 00A+A                                      COLOR(BLU)
 00A+A            DCI05     R        O 20 14REFFLD(CI05 POCIPF)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DCI03     R        O 11  4REFFLD(CI03 POCIPF)
     A            DNC05     R        O 11 14REFFLD(NC05 MANCPF)
     A            DCC09     R        O 11 33REFFLD(CC09 POCCPF)
     A                                      EDTCDE(J)
     A            DCI04     R        O 11 48REFFLD(CI04 POCIPF)
     A                                      EDTCDE(J)
     A            DCHK           1A  H
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  20091113  134107  CWDEV5      REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
 00A-A*                                 1  2'PUC022'
 00A+A                                  1  2'PUC026'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
 00A-A*                                 2 34'採購單查詢'
 00A+A                                  2 28'採購單查詢-採購單號'
     A                                      DSPATR(HI)
     A  91                              3 32'<<一般採購單>>'
     A                                      COLOR(PNK)
     A  92                              3 32'<<客戶採購單>>'
     A                                      COLOR(PNK)
     A  93                              3 32'<<陳列採購單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DCH01     R        O  4 13REFFLD(CH01)
     A            D#B03     R   -4   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 32'通路代號:'
     A                                      COLOR(BLU)
     A            DCH02     R        O  4 44REFFLD(CH02)
     A            DND04     R        O  4 47REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  4 59'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  5  1'分店代號:'
     A                                      COLOR(BLU)
     A            DCH07     R        O  5 13REFFLD(CH07)
     A            DNE05     R        O  5 19REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  5 32'採購單號:'
     A                                      COLOR(BLU)
     A            DCH04     R        O  5 44REFFLD(CH04)
     A  81                              5 58'到貨運日期:'
     A                                      COLOR(BLU)
     A  81        DCH08     R   -2   O  5 72REFFLD(CH08)
     A                                      EDTCDE(Y)
     A                                  6  1'採購日期:'
     A                                      COLOR(BLU)
     A            DCH03     R   -2   O  6 13REFFLD(CH03)
     A                                      EDTCDE(Y)
     A                                  6 22'到貨日期:'
     A                                      COLOR(BLU)
     A            DCH09     R   -2   O  6 34REFFLD(CH09)
     A                                      EDTCDE(Y)
     A  82                              6 44'客戶名稱:'
     A                                      COLOR(BLU)
     A  82        DCJ02     R        O  6 56REFFLD(CJ02 POCJPF)
     A                                  7  1'供應廠商:'
     A                                      COLOR(BLU)
     A            DCH06     R        O  7 13REFFLD(CH06)
     A            DNB03     R        O  7 19REFFLD(NB03 MANBPF)
     A                                      COLOR(BLU)
     A  81                              7 32'貨運站  :'
     A                                      COLOR(BLU)
     A  81        DNA03     R        O  7 44REFFLD(NA03 MANAPF)
     A                                 10  2' 產品代號產品名稱          -
     A                                      未稅單價    採購量'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 17'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
