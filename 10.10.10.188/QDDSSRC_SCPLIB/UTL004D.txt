     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : UTL004D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/08/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : UTL004                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    REMARK       :休假日期資料維護(A/U/D/I)                 *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/UTL4PF)
     A                                      MSGLOC(24)
     A                                      PRINT
      *----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  5VALUES(' ' '2')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SL401     R   -2   O  9  9EDTWRD('0  /  ')
     A                                      REFFLD(L401)
     A            L401      R        H      REFFLD(L401)
      *----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'UTL004'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'休假日期資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5  6'2=修改'
     A                                      COLOR(BLU)
     A                                  7  5'A  年月份(MM/YY)'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN1     R   -2   B  8  9REFFLD(L401)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  /  ')
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F6 =新增下一全年度'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'UTL004'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'休假日期資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 28'年月份:'
     A                                      COLOR(BLU)
     A            DM        R   -2   O    +1REFFLD(L401)
     A                                      EDTWRD('0  /  ')
     A                                      DSPATR(RI)
     A                                    +1'(MM/YY) '
     A                                      COLOR(BLU)
     A                                  6 25' 日　一　二　三　四　五　六 '
     A                                      COLOR(BLU)
     A            D01            2A  O  7 27DSPATR(HI)
     A            K01            1A  B  8 27VALUES(' ' 'H' 'A' 'B')
     A N78                                  DSPATR(PC)
     A N78                                  DSPATR(UL)
     A  78                                  DSPATR(PR)
     A            D02            2A  O  7 31
     A                                      COLOR(BLU)
     A            K02            1A  B  8 31VALUES(' ' 'H' 'A' 'B')
     A N79                                  DSPATR(PC)
     A N79                                  DSPATR(UL)
     A  79                                  DSPATR(PR)
     A            D03            2A  O  7 35
     A                                      COLOR(BLU)
     A            K03            1A  B  8 35VALUES(' ' 'H' 'A' 'B')
     A N80                                  DSPATR(PC)
     A N80                                  DSPATR(UL)
     A  80                                  DSPATR(PR)
     A            D04            2A  O  7 39
     A                                      COLOR(BLU)
     A            K04            1A  B  8 39VALUES(' ' 'H' 'A' 'B')
     A N81                                  DSPATR(PC)
     A N81                                  DSPATR(UL)
     A  81                                  DSPATR(PR)
     A            D05            2A  O  7 43
     A                                      COLOR(BLU)
     A            K05            1A  B  8 43VALUES(' ' 'H' 'A' 'B')
     A N82                                  DSPATR(PC)
     A N82                                  DSPATR(UL)
     A  82                                  DSPATR(PR)
     A            D06            2A  O  7 47
     A                                      COLOR(BLU)
     A            K06            1A  B  8 47VALUES(' ' 'H' 'A' 'B')
     A N83                                  DSPATR(PC)
     A N83                                  DSPATR(UL)
     A  83                                  DSPATR(PR)
     A            D07            2A  O  7 51DSPATR(HI)
     A            K07            1A  B  8 51VALUES(' ' 'H' 'A' 'B')
     A N84                                  DSPATR(PC)
     A N84                                  DSPATR(UL)
     A  84                                  DSPATR(PR)
     A            D08            2A  O  9 27DSPATR(HI)
     A            K08            1A  B 10 27DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D09            2A  O  9 31
     A                                      COLOR(BLU)
     A            K09            1A  B 10 31DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D10            2A  O  9 35
     A                                      COLOR(BLU)
     A            K10            1A  B 10 35DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D11            2A  O  9 39COLOR(BLU)
     A            K11            1A  B 10 39DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D12            2A  O  9 43COLOR(BLU)
     A            K12            1A  B 10 43DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D13            2A  O  9 47COLOR(BLU)
     A            K13            1A  B 10 47DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D14            2A  O  9 51DSPATR(HI)
     A            K14            1A  B 10 51DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D15            2A  O 11 27DSPATR(HI)
     A            K15            1A  B 12 27DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D16            2A  O 11 31COLOR(BLU)
     A            K16            1A  B 12 31DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D17            2A  O 11 35COLOR(BLU)
     A            K17            1A  B 12 35DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D18            2A  O 11 39COLOR(BLU)
     A            K18            1A  B 12 39DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D19            2A  O 11 43COLOR(BLU)
     A            K19            1A  B 12 43DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D20            2A  O 11 47COLOR(BLU)
     A            K20            1A  B 12 47DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D21            2A  O 11 51DSPATR(HI)
     A            K21            1A  B 12 51DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D22            2A  O 13 27DSPATR(HI)
     A            K22            1A  B 14 27DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D23            2A  O 13 31COLOR(BLU)
     A            K23            1A  B 14 31DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D24            2A  O 13 35COLOR(BLU)
     A            K24            1A  B 14 35DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D25            2A  O 13 39COLOR(BLU)
     A            K25            1A  B 14 39DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D26            2A  O 13 43COLOR(BLU)
     A            K26            1A  B 14 43DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D27            2A  O 13 47COLOR(BLU)
     A            K27            1A  B 14 47DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D28            2A  O 13 51DSPATR(HI)
     A            K28            1A  B 14 51DSPATR(UL)
     A                                      VALUES(' ' 'H' 'A' 'B')
     A            D29            2A  O 15 27DSPATR(HI)
     A            K29            1A  B 16 27
     A                                      VALUES(' ' 'H' 'A' 'B')
     A N76                                  DSPATR(UL)
     A  76                                  DSPATR(PR)
     A            D30            2A  O 15 31COLOR(BLU)
     A            K30            1A  B 16 31
     A                                      VALUES(' ' 'H' 'A' 'B')
     A N77                                  DSPATR(UL)
     A  77                                  DSPATR(PR)
     A            D31            2A  O 15 35COLOR(BLU)
     A            K31            1A  B 16 35VALUES(' ' 'H' 'A' 'B')
     A N85                                  DSPATR(UL)
     A  85                                  DSPATR(PR)
     A            D32            2A  O 15 39COLOR(BLU)
     A            K32            1A  B 16 39VALUES(' ' 'H' 'A' 'B')
     A N86                                  DSPATR(UL)
     A  86                                  DSPATR(PR)
     A            D33            2A  O 15 43COLOR(BLU)
     A            K33            1A  B 16 43VALUES(' ' 'H' 'A' 'B')
     A N87                                  DSPATR(UL)
     A  87                                  DSPATR(PR)
     A            D34            2A  O 15 47COLOR(BLU)
     A            K34            1A  B 16 47VALUES(' ' 'H' 'A' 'B')
     A N88                                  DSPATR(UL)
     A  88                                  DSPATR(PR)
     A            D35            2A  O 15 51DSPATR(HI)
     A            K35            1A  B 16 51VALUES(' ' 'H' 'A' 'B')
     A N89                                  DSPATR(UL)
     A  89                                  DSPATR(PR)
     A            D36            2A  O 17 27DSPATR(HI)
     A            K36            1A  B 18 27VALUES(' ' 'H' 'A' 'B')
     A N90                                  DSPATR(UL)
     A  90                                  DSPATR(PR)
     A            D37            2A  O 17 31COLOR(BLU)
     A            K37            1A  B 18 31VALUES(' ' 'H' 'A' 'B')
     A N91                                  DSPATR(UL)
     A  91                                  DSPATR(PR)
     A            D38            2A  O 17 35COLOR(BLU)
     A            K38            1A  B 18 35VALUES(' ' 'H' 'A' 'B')
     A N92                                  DSPATR(UL)
     A  92                                  DSPATR(PR)
     A            D39            2A  O 17 39COLOR(BLU)
     A            K39            1A  B 18 39VALUES(' ' 'H' 'A' 'B')
     A N93                                  DSPATR(UL)
     A  93                                  DSPATR(PR)
     A            D40            2A  O 17 43COLOR(BLU)
     A            K40            1A  B 18 43VALUES(' ' 'H' 'A' 'B')
     A N94                                  DSPATR(UL)
     A  94                                  DSPATR(PR)
     A            D41            2A  O 17 47COLOR(BLU)
     A            K41            1A  B 18 47VALUES(' ' 'H' 'A' 'B')
     A N95                                  DSPATR(UL)
     A  95                                  DSPATR(PR)
     A            D42            2A  O 17 51DSPATR(HI)
     A            K42            1A  B 18 51VALUES(' ' 'H' 'A' 'B')
     A N96                                  DSPATR(UL)
     A  96                                  DSPATR(PR)
     A*
     A                                 20  4'空白：上班日'
     A                                    +1'H:一般休假日 A:可到分店休假日'
     A                                    +1'B:可到分店及到貨運休假日'
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  71                             22 17'F9 =確定'
     A                                      COLOR(BLU)
     A                                 22 28'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  71                             22 17'F9 =確定'
     A                                      COLOR(BLU)
     A                                 22 28'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
