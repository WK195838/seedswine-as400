     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : SEH001D                                    *
     A*    PROGRAM NAME :通路銷售資料批次處理                      *
     A*    AUTHOR       : A1491 EASON                                *
     A*    CREATE DATE  : 2008/10/21                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<SEH001D>'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 63'日期:'
     A            DDATE          6Y 0O  1 71EDTWRD('0  /  /  ')
     A                                  2  2'SCR001'
     A                                  2 29'通路銷售資料批次處理'
     A                                      DSPATR(RI)
     A                                  2 63'時間:'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 63' USER :'
     A            $USER         10A  O  3 71
     A                                  5 28'公司代號:'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DHA01     R        B  5 40REFFLD(HA01 SEHAPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            HA01N         16O  O  5 43COLOR(BLU)
     A                                  7 28'通路代號:'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DHA02     R        B  7 40REFFLD(HA02 SEHAPF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            HA02N         16O  O  7 43COLOR(BLU)
     A                                  9 15'指定範圍:帳務月份'
     A                                      COLOR(BLU)
     A            DEH02          4Y 0O    +1EDTWRD('  /  ')
     A                                      COLOR(BLU)
     A                                    +1'(月／年)'
     A                                      COLOR(BLU)
     A                                 11 19'1.依全月份:'
     A                                      COLOR(BLU)
     A            HA03N          4Y 0B 11 35EDTWRD('  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 41'(月／年)'
     A                                      COLOR(BLU)
     A                                 11 52'或'
     A                                      COLOR(BLU)
     A                                 13 19'2.依銷售日期:'
     A                                      COLOR(BLU)
     A            DHA03S         6Y 0B 13 37EDTWRD('  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 13 46'-'
     A                                      COLOR(BLU)
     A            DHA03E         6Y 0B 13 48EDTWRD('  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 13 57'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 13 72'及'
     A                                      COLOR(BLU)
     A                                 14 25'分店代號:'
     A                                      COLOR(BLU)
     A            HA041     R        B 14 37REFFLD(HA04 SEHAPF)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A            HA042     R        B 14 43REFFLD(HA04 SEHAPF)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A            HA043     R        B 14 49REFFLD(HA04 SEHAPF)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A            HA044     R        B 14 55REFFLD(HA04 SEHAPF)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A            HA045     R        B 14 61REFFLD(HA04 SEHAPF)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A*
     A                                 17 19'注意:指定範圍內的所有資料將被+
     A                                      清除與取代!'
     A                                      COLOR(PNK)
     A*
     A  28                             21 27'*** 批次處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(BL)
     A  96        ERR1          63A  O 24  2MSGID(ERR 0130 SCMF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                      COLOR(BLU)
     A                                 23 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 23 26'F14 =處理作業'
     A                                      COLOR(BLU)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
