     A*%%TS  SD  20101214  200812  CWDEV5      REL-V5R3M0  5722-WDS
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PUR010D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/10/30                                   *
     A*    UPDATE DATE  : 97/11/17 VINCENT                           *
     A*    PROGRAM NAME : PUR010                                     *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :驗收彙總表                                *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  20101214  200812  CWDEV5      REL-V5R3M0  5722-WDS
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CF07(07 'F7 DOWNLOAD')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUR010'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'驗收彙總表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 24'公司別　　:'
     A                                      COLOR(BLU)
     A            DCL01     R        B  6 38REFFLD(CL01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A            D#B03     R        O  6 41REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  8 24'通路代號　:'
     A                                      COLOR(BLU)
     A            DCL02     R        B  8 38REFFLD(CL02)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A            DND04     R        O  8 41REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                 10 24'供應商代號:'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DCM02S    R        B 10 38REFFLD(CM02 POCMPF)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 10 45'-'
     A                                      COLOR(BLU)
     A            DCM02E    R        B 10 48REFFLD(CM02 POCMPF)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 12 24'到貨運日期:'
     A                                      COLOR(BLU)
     A            DCL07S         6Y 0B 12 38EDTWRD('  /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 12 47'-'
     A                                      COLOR(BLU)
     A            DCL07E         6Y 0B 12 49EDTWRD('  /  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 12 58'(月／日／年)'
     A                                      COLOR(BLU)
     A*
     A                                 16 24'執行環境　:'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 17 24'列印份數　:'
     A                                      COLOR(BLU)
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 24'列表機　　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                 22 42'F7 =檔案下載'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A                                 14 25'資料選擇 :'
     A            DTAOPT         1A  B 14 38
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 14 41'(1 =進貨/退貨 2 =進貨 3 = +
     A                                      退貨)'
