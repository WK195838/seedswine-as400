     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PUR040D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/10/30                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR040                                     *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :運費總表                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CF07(07 'F7下載')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUR040'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 35'運費總表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 24'公司別　　:'
     A                                      COLOR(BLU)
     A            DCK01     R        B  6 38REFFLD(CK01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A            D#B03     R        O  6 41REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  8 24'供應商代號:'
     A                                      DSPATR(HI)
     A            DCK08S    R        B  8 38REFFLD(CK08)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  8 44'-'
     A            DCK08E    R        B  8 46REFFLD(CK08)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 10 24'到貨運日期:'
     A                                      COLOR(BLU)
     A            DCK03S         6Y 0B 10 38EDTWRD('  /  /  ')
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 10 47'-'
     A            DCK03E         6Y 0B 10 49EDTWRD('  /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 10 58'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 12 24'分店地區別:'
     A                                      DSPATR(HI)
     A            DCK09S    R        B 12 38REFFLD(CK09)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 12 40'-'
     A            DCK09E    R        B 12 42REFFLD(CK09)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A*
     A                                 16 24'執行環境　:'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 17 24'列印份數　:'
     A                                      COLOR(BLU)
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 24'列表機　　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                 22 43'F7 =下載'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
