     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : INR090D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 99/03/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR090                                     *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :銷貨成本表                                *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CF07(07 'F7下載')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'INR090'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'銷貨成本表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 24'公司代號  :'
     A                                      COLOR(BLU)
     A            DHA01S    R        B  6 38REFFLD(HA01)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  6 41'-'
     A                                      COLOR(BLU)
     A            DHA01E    R        B  6 43REFFLD(HA01)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A*           D#B03     R        O 06 41REFFLD(#B03 PA#BPF)
     A*                                     COLOR(BLU)
     A*
     A                                  7 24'通路代號  :'
     A                                      COLOR(BLU)
     A            DHA02S    R        B  7 38REFFLD(HA02)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  7 41'-'
     A                                      COLOR(BLU)
     A            DHA02E    R        B  7 43REFFLD(HA02)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  7 60'(空白：全部)'
     A*
     A                                  8 24'分店代號  :'
     A                                      COLOR(BLU)
     A            DHA04S    R        B  8 38REFFLD(HA04)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                  8 44'-'
     A                                      COLOR(BLU)
     A            DHA04E    R        B  8 46REFFLD(HA04)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                  8 60'(空白：全部)'
     A*
     A                                  9 24'產品代號  :'
     A                                      DSPATR(HI)
     A            DHA05S    R        B  9 38REFFLD(HA05)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                  9 48'-'
     A                                      COLOR(BLU)
     A            DHA05E    R        B  9 50REFFLD(HA05)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                  9 60'(空白：全部)'
     A*
     A                                 10 24'銷售日期  :'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DHA03S         6Y 0B 10 38EDTWRD('  /  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                    +1'-'
     A                                      COLOR(BLU)
     A            DHA03E         6Y 0B    +1EDTWRD('  /  /  ')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 10 60'(月／日／年)'
     A*                                     COLOR(BLU)
     A*
     A                                 12 24'報表種類  :'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DOPT1          1A  B 12 38
     A                                      VALUES('1' '2')
     A                                    +2'(1.總表/ 2.明細表)'
     A*
     A                                 14 24'排列格式  :'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DOPT2          1A  B 14 38
     A                                      VALUES('1' '2')
     A                                    +2'(1.分店/ 2.產品)'
     A*
     A                                 17 24'執行環境  :'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 17 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 43'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 18 24'列印份數  :'
     A                                      COLOR(BLU)
     A            $CPY           3Y 0B 18 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 24'列表機　  :'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                 22 43'F7 =檔案下載'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
