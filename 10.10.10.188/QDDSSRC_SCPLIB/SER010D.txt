     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : SER010D                                    *
     A*    PROGRAM NAME :通路無銷售資料異常表                      *
     A*    AUTHOR       : A1491 EASON                                *
     A*    CREATE DATE  : 2008/10/28                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF07(07)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'SER010'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 63'日期:'
     A            DDATE          6Y 0O  1 71EDTWRD('0  /  /  ')
     A                                  2  2'SCR001'
     A                                  2 29'通路無銷售資料異常表'
     A                                      DSPATR(HI)
     A                                  2 63'時間:'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 63' USER :'
     A            $USER         10A  O  3 71
     A                                  6 25'公司代號  :'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DHA01     R        B  6 39REFFLD(HA01 SEHAPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            D#B03         16O  O  6 42COLOR(BLU)
     A                                  8 25'通路代號  :'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DHA02     R        B  8 39REFFLD(HA02 SEHAPF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DND04         16O  O  8 42COLOR(BLU)
     A                                 10 25'銷售日期  :'
     A                                      COLOR(BLU)
     A            DHA03S         6Y 0B 10 39EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 10 48'-'
     A            DHA03E         6Y 0B 10 50EDTWRD('  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 10 59'(月／日／年)'
     A                                      COLOR(BLU)
     A*
     A                                 17 25'執行環境　:'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 17 39VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 45'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 18 25'列印份數　:'
     A                                      COLOR(BLU)
     A            $CPY           3Y 0B 18 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 25'列表機　　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A  28                             21 27'*** 批次處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                      COLOR(BLU)
     A                                 23 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +1'F7 =檔案下載'
     A                                      COLOR(BLU)
     A*                                23 26'F14 =處理作業'
     A*                                     COLOR(BLU)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
