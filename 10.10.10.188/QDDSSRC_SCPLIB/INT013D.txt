     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : INT013D                                    *
     A*    AUTHOR       : A1546  VINCENT                             *
     A*    CREATE DATE  : 97/10/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    REMARK       :現場分店存貨查詢                          *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  4VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            TB04      R        O 10  7
     A            DNE05     R        O 10 13REFFLD(NE05)
     A            TB01      R        H
     A            TB02      R        H
     A            TB03      R        H
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'INT013'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'現場分店存貨查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 14REFFLD(TB01)
     A                                      DSPATR(HI)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN3     R        B  4 43REFFLD(TB03)
     A                                      DSPATR(HI)
     A            DND04     R        O  4 46REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  2'庫存年月:'
     A                                      COLOR(BLU)
     A            DTB02          4Y 0O  5 14EDTWRD('  /  ')
     A                                  5 20'（月／年）'
     A                                      COLOR(BLU)
     A            DBGN2     R        H      REFFLD(TB02)
     A                                  6  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                    +1'5= 存貨明細查詢'
     A                                      COLOR(BLU)
     A                                  8  3' A 分店代號／名稱  '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A            DBGN4     R        B  9  7REFFLD(TB04)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
