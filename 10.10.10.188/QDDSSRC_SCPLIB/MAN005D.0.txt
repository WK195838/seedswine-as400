     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : MAN005D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/09/22                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MAN005                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    REMARK       :通路分店資料維護(A/U/D/I)                 *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MANEPF)
     A                                      MSGLOC(24)
     A                                      PRINT
      *----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B 10  5VALUES(' ' '2' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            NE03      R        O 10  9
     A            NE05      R        O 10 15
     A            NE08      R        O 10 35
     A            NE10      R        O 10 59
      *
     A            NE01      R        H
     A            NE02      R        H
      *----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'MAN005'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'通路分店資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B    +1REFFLD(NE01)
     A            S#B03     R   -6   O    +1REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(NE02)
     A            SND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  6  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                    +1'2=修改'
     A                                      COLOR(BLU)
     A                                    +1'5=查詢'
     A                                      COLOR(BLU)
     A                                  8  5'A  分店代號／簡稱           聯+
     A                                      絡人               電話一      -
     A                                        '
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN3     R        B  9  9REFFLD(NE03)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F6 =新增'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(MSG 0004 SCMF)
     A                                      DSPATR(HI)
     A  98        ERR2          63A  O 24  2MSGID(MSG 0005 SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF09(09)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MAN005'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'通路分店資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號  :'
     A                                      COLOR(BLU)
     A            DNE01     R        O  5 16REFFLD(NE01)
     A                                  6  2'通路代號  :'
     A                                      COLOR(BLU)
     A            DNE02     R        O  6 16REFFLD(NE02)
     A                                  7  2'分店代號  :'
     A                                      COLOR(BLU)
     A            DNE03     R        B  7 16REFFLD(NE03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  7 44'狀態    :'
     A                                      COLOR(BLU)
     A            DNE16     R        B  7 56REFFLD(NE16)
     A                                      VALUES('A' 'I')
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                    +1'(A=ACTIVE I=INACTIVE)'
     A                                      COLOR(BLU)
     A                                  8  2'分店全稱  :'
     A                                      COLOR(BLU)
     A            DNE04     R        B  8 16REFFLD(NE04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  2'分店簡稱  :'
     A                                      COLOR(BLU)
     A            DNE05     R        B  9 16REFFLD(NE05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 44'郵遞區號:'
     A                                      COLOR(BLU)
     A            DNE17     R        B  9 56REFFLD(NE17)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'分店地址  :'
     A                                      COLOR(BLU)
     A            DNE06     R        B 10 16REFFLD(NE06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DNE07     R        B 10 49REFFLD(NE07)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  2'聯絡人　  :'
     A                                      COLOR(BLU)
     A            DNE08     R        B 11 16REFFLD(NE08)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  2'收貨人　:'
     A                                      COLOR(BLU)
     A            DNE09     R        B 12 16REFFLD(NE09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 44'電話一　:'
     A                                      COLOR(BLU)
     A            DNE10     R        B 12 56REFFLD(NE10)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  2'傳真機　  :'
     A                                      COLOR(BLU)
     A            DNE12     R        B 13 16REFFLD(NE12)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 44'電話二　:'
     A                                      COLOR(BLU)
     A            DNE11     R        B 13 56REFFLD(NE11)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  2'電子信箱  :'
     A                                      COLOR(BLU)
     A            DNE13     R        B 14 16REFFLD(NE13)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 15  2'分店等級  :'
     A                                      DSPATR(HI)
     A            DNE14     R        B 15 16REFFLD(NE14)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            NE14D     R             +1REFFLD(L203 UTL2PF)
     A                                      COLOR(BLU)
     A                                 16  2'貨運區碼  :'
     A                                      COLOR(BLU)
     A            DNE15     R        B 16 16REFFLD(NE15)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            NE15D     R             +1REFFLD(L203 UTL2PF)
     A                                      COLOR(BLU)
     A                                 20  2'備註      :'
     A                                      COLOR(BLU)
     A            DNE18     R        B 20 16REFFLD(NE18)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            DNE19     R        B 20 49REFFLD(NE19)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  71                             22 18'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 34'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  71                             22 18'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 34'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
