     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PUR040                                     *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/10/30                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR040                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :運費總表                                  *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<PUR040>'
     A                                    23
     A                                      MSGCON(030 USC9999 SCMF)
     A                                    66
     A                                      '日期:'
     A            $EGMDY         6  0O    74
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    32
     A                                      '運費總表'
     A                                    66
     A                                      '時間:'
     A                                    74
     A                                      TIME
     A                                      SPACEA(001)
     A                                    66
     A                                      '頁次:'
     A                                    74
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '公司別　:'
     A            DCK01     R        O    +1
     A                                      REFFLD(CK01 POCKPF)
     A            D#B03     R        O    +1REFFLD(#B03 PA#BPF)
     A                                      SPACEA(001)
     A                                     1
     A                                      '供應商代號:'
     A            DCK08S    R        O    +1
     A                                      REFFLD(CK08)
     A                                    +1
     A                                      '-'
     A            DCK08E    R        O    +1
     A                                      REFFLD(CK08)
     A                                    +1
     A                                      '到貨運日期:'
     A            DCK03S         6  0O    +1
     A                                      EDTCDE(Y)
     A                                    +1
     A                                      '-'
     A            DCK03E         6  0O    +1
     A                                      EDTCDE(Y)
     A                                    +1
     A                                      '分店地區別:'
     A            DCK09S    R        O    +1
     A                                      REFFLD(CK09)
     A                                    +1
     A                                      '-'
     A            DCK09E    R        O    +1
     A                                      REFFLD(CK09)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     1
     A                                      '供應商代號／簡稱'
     A                                      SPACEB(001)
     A                                    22
     A                                      '未稅金額'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      --------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A            SCK08     R              2REFFLD(CK08 POCKPF)
     A            SNB03     R              8
     A                                      REFFLD(NB03 MANBPF)
     A            SCK14     R             23REFFLD(CK14 POCKPF)
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                     8
     A                                      '總計:'
     A            Q1            11  2     +1
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<PUR040P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
