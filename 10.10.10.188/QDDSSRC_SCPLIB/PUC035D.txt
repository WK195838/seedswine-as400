     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PUC035D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/10/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUC035                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :報價資料維護子系統                        *
     A*    REMARK       :未完成驗收單號查詢                        *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/POCLPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            SCL03     R   -2   O  9  4REFFLD(CL03)
     A                                      EDTCDE(Y)
     A            CL04      R        O  9 14
     A            SNE05     R        O  9 20REFFLD(NE05 MANEPF)
     A            CL05      R        O  9 31
     A            SCL07     R   -2   O  9 47REFFLD(CL07)
     A                                      EDTWRD('  /  /  ')
     A            CL01      R        H
     A            CL02      R        H
     A            CL03      R        H
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'PUC035'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'未完成驗收單查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 13REFFLD(CL01)
     A            S#B03     R   -6   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(CL02)
     A            SND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H
     A            DBGN3     R   -2   B  8  4REFFLD(CL03)
     A                                      EDTCDE(Y)
     A            DBGN4     R        B  8 14REFFLD(CL04)
     A*           DBGN5     R        B  8 31REFFLD(CL05)
     A                                  6  1'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  6 44'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  7  3'到分店日分店代號     驗收+
     A                                      單號      到貨運日'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
