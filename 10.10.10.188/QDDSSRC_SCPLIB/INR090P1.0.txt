     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : INR090P1                                   *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 99/03/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR090                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :銷貨成本表(明細表)                      *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<INR090P1>'
     A                                    56MSGCON(030 USC9999 SCMF)
     A                                   120'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    61'銷貨成本表(明細表)'
     A                                   120'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A                                   120'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司代號:'
     A            DHA01S    R        O    +1REFFLD(HA01)
     A                                    +1'-'
     A            DHA01E    R        O    +1REFFLD(HA01)
     A                                    +5'通路代號:'
     A            DHA02S    R        O    +1REFFLD(HA02)
     A                                    +1'-'
     A            DHA02E    R        O    +1REFFLD(HA02)
     A                                    +5'分店代號:'
     A            DHA04S    R        O    +1REFFLD(HA04)
     A                                    +1'-'
     A            DHA04E    R        O    +1REFFLD(HA04)
     A                                      SPACEA(001)
     A                                     1'產品代號:'
     A            DHA05S    R        O    +1REFFLD(HA05)
     A                                    +1'-'
     A            DHA05E    R        O    +1REFFLD(HA05)
     A                                    +5'銷售日期:'
     A            DHA03S         6S 0O    +1EDTCDE(Y)
     A                                    +1'-'
     A            DHA03E         6S 0O    +1EDTCDE(Y)
     A                                    +1'(月／日／年)'
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A                                     1'分店/產品'
     A                                      SPACEB(001)
     A                                   +18'分店/產品'
     A                                   +18'銷貨數量'
     A                                    +2'銷貨金額(未稅)'
     A                                    +2'銷售佔比'
     A                                    +2'銷貨成本(未稅)'
     A                                    +2'銷貨利潤(未稅)'
     A                                    +6'獲利率'
     A                                    +6'毛利率'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------------------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  71        WK01           9A        2
     A  71        WK02          18O       +1
     A  72        WK01A          9A       +2
     A  72        WK02A         18O       +1
     A            WK03           9S 0     +1EDTCDE(J)
     A            WK04           9S 0     +7EDTCDE(J)
     A            WK05           4S 2     +6EDTCDE(J)
     A            WK06          11S 2     +4EDTCDE(J)
     A            WK07          11S 2     +4EDTCDE(J)
     A            WK08           4S 2    +11EDTCDE(J)
     A            WK09           4S 2     +6EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    53'合計:'
     A            Q1             9S 0     +1EDTCDE(J)
     A            Q2             9S 0     +7EDTCDE(J)
     A            Q3             4S 2     +6EDTCDE(J)
     A            Q4            11S 2     +4EDTCDE(J)
     A            Q5            11S 2     +4EDTCDE(J)
     A            Q6             7S 2     +7EDTCDE(J)
     A            Q7             7S 2     +2EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<INR090P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
