     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PUC021D                                    *
     A*    PROGRAM NAME :採購批次處理                              *
     A*    AUTHOR       : A1491 EASON                                *
     A*    CREATE DATE  : 2008/10/07                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PUC021D>'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 63'日期:'
     A            DDATE          6Y 0O  1 71EDTWRD('0  /  /  ')
     A                                  2  2'SCR001'
     A                                  2 33'採購批次處理'
     A                                      DSPATR(HI)
     A                                  2 63'時間:'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 63' USER :'
     A            $USER         10A  O  3 71
     A                                  6 28'公司代號:'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DCD01     R        B  6 40REFFLD(CD01 POCDPF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            CD01N         16O  O  6 43COLOR(BLU)
     A                                  8 28'通路代號:'
     A                                      COLOR(BLU)
     A            DCD02     R        B  8 40REFFLD(CD02 POCDPF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A            CD02N         16O  O  8 43COLOR(BLU)
     A                                 10 28'批次序號:'
     A                                      COLOR(BLU)
     A            DFC04     R        O 10 40REFFLD(FC04 S@FCPF)
     A                                      EDTCDE(1)
      *
     A  28                             21 27'*** 批次處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                      COLOR(BLU)
     A                                 23 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 23 26'F14 =處理作業'
     A                                      COLOR(BLU)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
