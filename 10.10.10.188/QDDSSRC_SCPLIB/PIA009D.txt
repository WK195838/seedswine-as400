     A*%%TS  SD  20221006  115818  SCPGMR      REL-V5R4M0  5722-WDS
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PIA009D                                    *
     A*    AUTHOR       : RACHEL                                     *
     A*    CREATE DATE  : 2022/09/15                                 *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PIA0091                                    *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :盤點系統                                  *
     A*    REMARK       :計算12/25庫存量                         *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03 '結束執行')
     A                                      CF14(14 '處理作業')
     A                                      KEEP
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PIA009'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $DATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'計算12/25庫存量'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 20'公司代號:'
     A                                      COLOR(BLU)
     A            DPI01     R        B  6 32REFFLD(EH01)
     A  61                                  DSPATR(PC RI)
     A            D#B03     R   -4   O  6 35REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  8 20'通路代號:'
     A                                      COLOR(BLU)
     A            DPI02     R        B  8 32REFFLD(CL02)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A            DND04     R        O  8 35REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                 10 20'盤點年月:'
     A                                      COLOR(BLU)
     A            DPI03          4Y 0B 10 32EDTWRD('  /  ')
     A  63                                  DSPATR(PC RI)
     A                                 10 39'(月／年)'
     A                                      COLOR(BLU)
     A                                 12 20'盤點日期:'
     A                                      COLOR(BLU)
     A            DPI04S         6Y 0B 12 32EDTWRD('  /  /  ')
     A  64                                  DSPATR(PC RI)
     A                                    +1'~'
     A                                      COLOR(BLU)
     A            DPI04E         6Y 0B 12 43EDTWRD('  /  /  ')
     A  65                                  DSPATR(PC RI)
     A                                 12 52'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 14 17'+----------------------------------
     A                                      --------------+'
     A                                 15 17'|'
     A                                 15 65'|'
     A                                 16 17'|請按F14以便處理此作業，否則按-
     A                                      F3結束此作業|'
     A                                 17 17'|'
     A                                 17 65'|'
     A                                 18 17'+----------------------------------
     A                                      --------------+'
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +1'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +1'F14 =作業處理'
     A                                      COLOR(BLU)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
