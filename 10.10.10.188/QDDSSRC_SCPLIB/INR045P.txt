     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : INR040P                                    *
     A*    AUTHOR       : A1010  JOHN                                *
     A*    CREATE DATE  : 97/10/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR045                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :現場產品庫存總表－依產品                  *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<INR045P>'
     A                                    26MSGCON(030 USC9999 SCMF)
     A                                    71'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    32'現場存貨總表            '
     A                                    71'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A                                    71'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司代號:'
     A            DTO01     R        O    +1REFFLD(TB01)
     A            D#B03     R   -4   O    +1REFFLD(#B03 PA#BPF)
     A                                    +5'通路代號:'
     A            DTO02S         2A  O    +1
     A                                    +1'-'
     A            DTO02E         2A  O    +1
     A                                    +1'分店代號:'
     A            DTO03S         5   O    +1
     A                                    +1'-'
     A            DTO03E         5   O    +1
     A                                      SPACEA(001)
     A                                     1'產品代號:'
     A            DTO04S         9   O    +1
     A                                    +1'-'
     A            DTO04E         9   O    +1
     A                                    33'本期截止日:'
     A            DTO05          6S 0O    +1EDTCDE(Y)
     A                                    +1'(月／日／年)'
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     1'產品代號'
     A                                      SPACEB(001)
     A                                    +0'產品簡稱'
     A                                    35'現場庫存量'
     A                                    50'帳列庫存量'
     A                                    65'陳列庫存量'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      --------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        W803           9A        2
     A  30        W804          18O       +1
     A            W811           6S 0     38EDTCDE(J)
     A            W810           6S 0     53EDTCDE(J)
     A            W810A          6S 0     68EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    10'          總計:'
     A            Q1             8S 0     35EDTCDE(J)
     A            Q2             8S 0     50EDTCDE(J)
     A            Q3             8S 0     65EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<INR040P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
