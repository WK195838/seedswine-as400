     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : INT012D                                    *
     A*    AUTHOR       : A1546  VINCENT                             *
     A*    CREATE DATE  : 97/10/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INT012                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    REMARK       :現場產品存貨查詢                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B 10  2VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            TA03      R        O 10  4
     A            DNC05     R        O 10 14REFFLD(NC05)
     A            DTA04     R   -1   O 10 34REFFLD(TA04)
     A                                      EDTCDE(J)
     A            TA06      R        O 10 46EDTCDE(J)
     A            TATT1     R        O 10 55REFFLD(TA06)
     A                                      EDTCDE(J)
     A            TATT2     R        O 10 64REFFLD(TA06)
     A                                      EDTCDE(J)
     A            TATT3     R        O 10 73REFFLD(TA06)
     A                                      EDTCDE(J)
     A*--------------------------------------------------------------*
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'INT012'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'現場產品存貨查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 14REFFLD(TA01)
     A                                      DSPATR(HI)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5  2'庫存年月:'
     A                                      COLOR(BLU)
     A            DTA02          4Y 0O  5 14EDTWRD('  /  ')
     A                                  5 20'（月／年）'
     A                                      COLOR(BLU)
     A            DBGN2     R        H      REFFLD(TA02)
     A                                  6  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                    +1'5=各分店存貨查詢'
     A                                      COLOR(BLU)
     A                                  7 37'上月    期初   現場   帳列'
     A                                      COLOR(BLU)
     A                                    +3'陳列'
     A                                      COLOR(BLU)
     A                                  8  1' A產品代號產品名稱        -
     A                                        單位成本 庫存量 庫存量 -
     A                                      庫存量 庫存量'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN3     R        B  9  4REFFLD(TA03)
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A          R DSPC1
     A                                 22  2'執行'
     A                                    +1'F3 =回主畫面'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
