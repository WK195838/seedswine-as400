     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PUC003D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/09/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :基本資料建檔子系統                        *
     A*    REMARK       :供應商產品報價單核准                      *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/POCAPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '6')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            CA03      R        O  9  6
     A            SNB03     R        O  9 12REFFLD(NB03 MANBPF)
     A            CA04      R        O  9 25
     A            CA05      R        O  9 37
     A            SCA05          6O  O  9 42
     A            SCA07     R   -2   O  9 52REFFLD(CA07)
     A                                      EDTCDE(Y)
     A            SCA08     R   -2   O  9 68REFFLD(CA08)
     A                                      EDTCDE(Y)
     A            CA01      R        H
     A            CA02      R        H
     A            CA06      R        H
     A            CA07      R        H
     A            CA08      R        H
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'PUC003'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'供應商產品報價單核準'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 13REFFLD(CA01)
     A            S#B03     R   -6   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(CA02)
     A            SND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'6=核準'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN3     R        B  8  6REFFLD(CA03)
     A            DBGN4     R        B  8 25REFFLD(CA04)
     A                                  6 49'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  6 65'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  7  3'A 供應商代號       報價單號  -
     A                                      年度單別   生效日期      截+
     A                                      止日期 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A*                                22 26'F6 =新增'
     A*                                     COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *此燈號控制新增成功以及修改成功專用
     A  96        ERR1          63A  O 24  2MSGID(MSG 0004 SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DCB02     R        O  9  5REFFLD(CB02 POCBPF)
     A            DNC05     R        O  9 15REFFLD(NC05 MANCPF)
     A            DNC14     R        O  9 37REFFLD(NC14 MANCPF)
     A            DCB03     R        O  9 46REFFLD(CB03 POCBPF)
     A                                      EDTCDE(1)
     A            DCHK           1A  H
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF14(14)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC003'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'供應商產品報價核準'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號  :'
     A                                      COLOR(BLU)
     A            DCA01     R        O  4 15REFFLD(CA01)
     A            D#B03     R   -6   O  4 18REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 34'通路代號  :'
     A                                      COLOR(BLU)
     A            DCA02     R        O  4 48REFFLD(CA02)
     A            DND04     R        O  4 51REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  4 62'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  5  1'供應商代號:'
     A                                      COLOR(BLU)
     A            DCA03     R        O  5 15REFFLD(CA03)
     A            DNB03     R        O  5 21REFFLD(NB03 MANBPF)
     A                                      COLOR(BLU)
     A                                  5 34'報價單年度:'
     A                                      COLOR(BLU)
     A            DCA05     R        O  5 48REFFLD(CA05)
     A                                  5 56'生效日期:'
     A                                      COLOR(BLU)
     A            DCA07     R   -2   O  5 68REFFLD(CA07)
     A                                      EDTCDE(Y)
     A                                  6  1'報價單號  :'
     A                                      COLOR(BLU)
     A            DCA04     R        O  6 15REFFLD(CA04)
     A                                  6 34'單別:'
     A                                      COLOR(BLU)
     A            DCA06     R        O  6 42REFFLD(CA06)
     A            CA06N         10O  O  6 45COLOR(BLU)
     A                                  6 56'截止日期:'
     A                                      COLOR(BLU)
     A            DCA08     R   -2   O  6 68REFFLD(CA08)
     A                                      EDTCDE(Y)
     A                                  8  2'  產品代號／名稱                -
     A                                      單位     未稅單價'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A                                    +2'F14=核準'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
