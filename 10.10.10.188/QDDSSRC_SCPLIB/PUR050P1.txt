     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : PUR050P                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/10/30                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR050                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :供應商負擔運費明細表                      *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<PUR050P1>'
     A                                    32MSGCON(030 USC9999 SCMF)
     A                                    83'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    35'供應商負擔運費明細表'
     A                                    83'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A                                    83'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司別:'
     A            DCK01     R        O    +1REFFLD(CK01)
     A            D#B03     R        O    +1REFFLD(#B03 PA#BPF)
     A                                    31'供應商代號:'
     A            DCK08S         5   O    +0
     A                                    +0'-'
     A            DCK08E         5   O    +0
     A                                    56'到貨運日期:'
     A            DCK03S         6  0O    +0EDTCDE(Y)
     A                                    +0'-'
     A            DCK03E         6  0O    +0EDTCDE(Y)
     A                                    87'貨運區碼:'
     A            DCK09S         1   O    +0
     A                                    +0'-'
     A            DCK09E         1   O    +0
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A                                     1'供應商代號／簡稱'
     A                                      SPACEB(001)
     A                                    21'到貨運日期'
     A                                    35'分店所屬貨運區碼'
     A                                    +1'分店代號／名稱'
     A                                    73'箱數'
     A                                    82'單價'
     A                                    91'未稅金額'
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------------------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        W6201          5A        2
     A  30        W6202         12O       +1
     A  31        W6203O         6S 0     23EDTCDE(Y)
     A  32        W6204          1A       36
     A  32        W6205         12O       +1
     A            W6206          5A       55
     A            W6207         10O       +1
     A            W6208          4S 0     +2EDTCDE(J)
     A            W6209          4S 0     82EDTCDE(K)
     A            W6210          6S 0     93EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    53'到貨運日期小計:'
     A            Q1             6  0     71EDTCDE(J)
     A            Q2             9  0     89EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    53'    供應商合計:'
     A            Q3             6  0     71EDTCDE(J)
     A            Q4             9  0     89EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD4
     A                                      SPACEB(001)
     A                                    53'          總計:'
     A            Q5             6  0     71EDTCDE(J)
     A            Q6             9  0     89EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<PUR050P1>>--->報表結束'
     A                                      SPACEB(001)
     A                                    45
     A                                      '<<單價空白為分攤後的最低運費>>'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
