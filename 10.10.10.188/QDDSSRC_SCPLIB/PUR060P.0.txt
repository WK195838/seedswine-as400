     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : PUR060P                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/10/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR060                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :驗收月報表                                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<PUR060>'
     A                                    22
     A                                      MSGCON(030 USC9999 SCMF)
     A                                    65
     A                                      '日期:'
     A            $EGMDY         6  0O    +1
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    30
     A                                      '驗收月報表'
     A                                    65
     A                                      '時間:'
     A                                    +1
     A                                      TIME
     A                                      SPACEA(001)
     A                                    65
     A                                      '頁次:'
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '公司別:'
     A            DCL01     R        O    13
     A                                      REFFLD(CK01)
     A            D#B03     R        O    16
     A                                      REFFLD(#B03 PA#BPF)
     A                                    +1'供應商代號:'
     A            DCM02S         5   O    +1
     A                                    +1'-'
     A            DCM02E         5   O    +1
     A                                      SPACEA(001)
     A                                     1'分店代號:'
     A            DCL04S         5   O    +1
     A                                    +1'-'
     A            DCL04E         5   O    +1
     A                                    +1'到貨運日期:'
     A            DCL07S         6  0O    +1EDTCDE(Y)
     A                                    +1'-'
     A            DCL07E         6  0O    +1EDTCDE(Y)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     1'供應商代號／簡稱'
     A                                      SPACEB(001)
     A                                    +2'通路別／名稱'
     A                                    41'未稅金額'
     A                                    55'銷售稅額'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        CM02           5         2
     A  30        NB03      R             +1
     A            CL02      R             +2
     A            ND04      R             +1
     A            PCM08     R             37EDTCDE(J)
     A                                      REFFLD(CM08 POCMPF)
     A            PCM09     R             56EDTCDE(J)
     A                                      REFFLD(CM09 POCMPF)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    20'供應商 合計:'
     A            Q1        R             +1EDTCDE(J)
     A                                      REFFLD(CM08 POCMPF)
     A            Q2        R             56EDTCDE(J)
     A                                      REFFLD(CM09 POCMPF)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    20'         總計:'
     A            Q3        R             +1EDTCDE(J)
     A                                      REFFLD(CM08 POCMPF)
     A            Q4        R             56EDTCDE(J)
     A                                      REFFLD(CM09 POCMPF)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<PUR060P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
