     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SER020P                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/11/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR020                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :銷售系統                                  *
     A*    REMARK       :通路銷售金額表                            *
     A*---------------------------------------------------------------
     A* 00A需求單號: 1080943
     A* 00A CAROL  2019/08/02新增篩選條件供應商代號&產品代號
     A*---------------------------------------------------------------
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<SER030>'
     A                                    30MSGCON(030 USC9999 SCMF)
     A                                    62'日期:'
     A            $EGMDY         6  0O    70EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    34'銷貨收入分店彙總表'
     A                                    62'時間:'
     A                                    70TIME
     A                                      SPACEA(001)
     A                                    62'頁次:'
     A                                    70PAGNBR
     A                                      EDTCDE(Z)
     A*
     A                                      SPACEA(001)
     A                                     1'公司代號:'
     A            DHA01     R        O    +1REFFLD(HA01)
     A            D#B03     R        O    +1REFFLD(#B03 PA#BPF)
     A                                    62'USER :'
     A            $USER         10A       +1
     A                                      SPACEA(001)
     A*
     A                                     1'通路代號:'
     A            DHA02     R        O    +1REFFLD(HA02)
     A            DND04     R        O    +1REFFLD(ND04 MANDPF)
 00A+A*
 00A+A                                    +1'供應商代號:'
 00A+A            DNB01S         5A  O    +1
 00A+A                                    +1'~'
 00A+A            DNB01E         5A  O    +1
     A*
     A                                     1'分店代號:'
     A                                      SPACEB(001)
     A            DHA04S         5A  O    +1
     A                                    +1'~'
     A            DHA04E         5A  O    +1
 00A+A*
 00A+A                                    +1'產品代號:'
 00A+A            DHA05S         9A  O    +1
 00A+A                                    +1'~'
 00A+A            DHA05E         9A  O    +1
     A*
     A                                     1'銷售日期:'
     A                                      SPACEB(001)
     A            DHA03S    R   -2   O    +1REFFLD(HA03)
     A                                      EDTCDE(Y)
     A                                    +1'~'
     A            DHA03E    R   -2   O    +1REFFLD(HA03)
     A                                      EDTCDE(Y)
     A                                    +1'產品歸屬:'
     A            DNC09S    R        O    +1REFFLD(NC09)
     A                                    +1'~'
     A            DNC09E    R        O    +1REFFLD(NC09)
     A*
     A                                     1'品牌:'
     A                                      SPACEB(001)
     A            DNC12S    R        O    +1REFFLD(NC12)
     A                                    +1'~'
     A            DNC12E    R        O    +1REFFLD(NC12)
     A                                    +2'識別:'
     A            DNC10S    R        O    +1REFFLD(NC10)
     A                                    +1'~'
     A            DNC10E    R        O    +1REFFLD(NC10)
     A                                    +2'種類:'
     A            DNC11S    R        O    +1REFFLD(NC11)
     A                                    +1'~'
     A            DNC11E    R        O    +1REFFLD(NC11)
     A                                    +2'原產地:'
     A            DNC18S    R        O    +1REFFLD(NC18)
     A                                    +1'~'
     A            DNC18E    R        O    +1REFFLD(NC18)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A*
     A                                     1'分店代號／名稱'
     A                                      SPACEB(001)
     A                                    20'數量'
     A                                    30'未稅銷售金額'
     A                                    47'含稅銷售金額'
     A                                    68'銷項稅額'
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A            HA04      R              2
     A            NE05      R             +1
     A            PHA06          6S 0     +1EDTCDE(J)
     A            PHA07         11S 2     29EDTCDE(J)
     A            PHA08         11S 2     46EDTCDE(J)
     A            LHA08         11S 2     63EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                     2'        總計:'
     A            Q1             6  0     19EDTCDE(J)
     A            Q2            11  2     29EDTCDE(J)
     A            Q3            11  2     46EDTCDE(J)
     A            Q4            11  2     63EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEB(001)
     A                                     2'<<SER030>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                      SPACEB(001)
     A                                     2'<<SER030>>--->此範圍無資料'
