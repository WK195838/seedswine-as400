     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : INR082P                                    *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 100/06/01                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR082                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :陳列品使用明細表                          *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<INR082P>'
     A                                    56
     A                                      MSGCON(030 USC9999 SCMF)
     A                                   130
     A                                      '日期:'
     A            $EGMDY         6  0O   138
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    61
     A                                      '陳列品使用明細表'
     A                                   130
     A                                      '時間:'
     A                                   138
     A                                      TIME
     A                                      SPACEA(001)
     A                                   130
     A                                      '頁次:'
     A                                   138
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                    58
     A                                      '分店代號  :'
     A            DTO03S         5   O    72
     A                                    78
     A                                      '-'
     A            DTO03E         5   O    80
     A                                      SPACEA(001)
     A                                    58
     A                                      '產品代號  :'
     A            DTO04S         9   O    72
     A                                    82
     A                                      '-'
     A            DTO04E         9   O    84
     A                                      SPACEA(001)
     A                                    58
     A                                      '本期截止日:'
     A            DTO05          6S 0O    72
     A                                      EDTCDE(Y)
     A                                    81
     A                                      '(月／日／年)'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================='
     A                                      SPACEB(001)
     A                                     1
     A                                      '分店代號'
     A                                      SPACEB(001)
     A                                    11
     A                                      '分店名稱'
     A                                    22
     A                                      '產品代號'
     A                                    35
     A                                      '產品簡稱'
     A                                    53
     A                                      '單價'
     A                                    63
     A                                      '驗收單號'
     A                                    76
     A                                      '到店日期'
     A                                    86
     A                                      '驗收數量'
     A                                    98
     A                                      '餘量'
     A                                   107
     A                                      '日期'
     A                                   114
     A                                      '類別'
     A                                   126
     A                                      '分店'
     A                                   135
     A                                      '數量'
     A                                   142'異動單號'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        W24103         5A        2
     A  30        NE05          10O       11
     A  30        W24104         9A       22
     A  30        NC05          18O       32
     A  30        W24105         8S 2     51
     A                                      EDTCDE(J)
     A  30        W24106        13A       63
     A  30        W2107D         6S 0     77
     A                                      EDTCDE(Y)
     A  30        W24108         6S       86
     A                                      EDTCDE(J)
     A  30        W24109         6S       96
     A                                      EDTCDE(J)
     A            W2202D         6S 0    105
     A                                      EDTCDE(Y)
     A            W24203        12O      114
     A            W24204         5A      127
     A            W24205         6S 0    133
     A                                      EDTCDE(K)
     A            W24207        13A      142
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<INR082P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================='
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1'總計'
     A            T24108         7S 0     84EDTCDE(J)
     A            T24109         7S 0     94EDTCDE(J)
     A            T24205         7S 0    131EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
