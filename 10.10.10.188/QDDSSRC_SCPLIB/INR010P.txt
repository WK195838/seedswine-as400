     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : INR010P                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/10/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR010                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :帳列產品庫存金額總表                      *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<INR010P>'
     A                                    22MSGCON(030 USC9999 SCMF)
     A                                    65'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    22'帳列產品庫存金額總表－依分店'
     A                                    65'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A                                    65'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司代號:'
     A            DTO01     R        O    +1REFFLD(TB01)
     A            D#B03     R   -4   O    +1REFFLD(#B03 PA#BPF)
     A                                    +5'通路代號:'
     A            DTO02S         2A  O    +1
     A                                    +0'-'
     A            DTO02E         2A  O    +0
     A                                    +1'分店代號:'
     A            DTO03S         5   O    +1
     A                                    +0'-'
     A            DTO03E         5   O    +0
     A                                      SPACEA(001)
     A                                     1'產品代號:'
     A            DTO04S         9   O    +1
     A                                    +0'-'
     A            DTO04E         9   O    +0
     A  38                                33'月結月份:'
     A  38        DTO05S         4  0O    +1EDTWRD('  /  ')
     A  38                                +1'(月／年)'
     A N38                                33'本期截止日:'
     A N38        DTO05E         6  0O    +1EDTWRD('  /  /  ')
     A N38                                +1'(月／日／年)'
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     1'產品代號'
     A                                      SPACEB(001)
     A                                    +1'產品簡稱'
     A                                    37'單位成本'
     A                                    51'數量'
     A                                    71'金額'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        W8105          9A        2
     A  30        W8106         18O       12
     A            W8107         11S 2     32EDTCDE(J)
     A            W8108          6S 0     49EDTCDE(J)
     A            W8109         13S 2     +2EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    40'總計:'
     A            Q1             6S 0     49EDTCDE(J)
     A            Q2            13S 2     +2EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<INR010P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
