     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<SER010>'
     A                                    30
     A                                      MSGCON(030 USC9999 SCMF)
     A                                    72
     A                                      '日期:'
     A            $EGMDY         6  0O    80
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    33
     A                                      '通路無銷售資料異常表'
     A                                    72
     A                                      '時間:'
     A                                    80
     A                                      TIME
     A                                      SPACEA(001)
     A                                    72
     A                                      '頁次:'
     A                                    80
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     1
     A                                      '公司別:'
     A            DHA01     R        O    +1
     A                                      REFFLD(HA01)
     A            D#B03     R        O    +1
     A                                      REFFLD(#B03 PA#BPF)
     A                                      SPACEA(001)
     A                                     1
     A                                      '通路代號:'
     A            DHA02     R        O    +1
     A                                      REFFLD(HA02)
     A            DND04     R        O    +1
     A                                      REFFLD(ND04 MANDPF)
     A                                    73
     A                                      'USER :'
     A            $USER         10A       +1
     A                                      SPACEA(001)
     A                                     1
     A                                      '銷售日期:'
     A            DHA03S    R   -2   O    +1
     A                                      REFFLD(HA03)
     A                                      EDTCDE(Y)
     A                                    +1
     A                                      '~'
     A            DHA03E    R   -2   O    +1
     A                                      REFFLD(HA03)
     A                                      EDTCDE(Y)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==============================='
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      '銷售日期'
     A                                    13
     A                                      '分店代號／名稱'
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -------------------------------'
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEA(001)
     A  38        SHA03     R   -2   O     2
     A                                      REFFLD(HA03)
     A                                      EDTCDE(Y)
     A            NE03      R             14
     A            DNE05     R             +1
     A                                      REFFLD(NE05 MANEPF)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==============================='
     A                                      SPACEA(001)
     A                                     1'合計筆數:'
     A            TOTAL          3S 0     +1EDTCDE(P)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -------------------------------'
     A                                      SPACEA(001)
     A                                     2
     A                                      '<<SER010>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     2
     A                                      '<<SER010>>--->此範圍無資料'
     A                                      SPACEA(001)
