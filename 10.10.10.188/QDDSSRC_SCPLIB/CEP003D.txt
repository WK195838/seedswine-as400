     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : CEP003D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/09/09                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : CEP003                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    REMARK       :銷售區碼與分店對照維護                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/C#ECPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B 10  5VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            EC03      R        O 10  8
     A            SNG04     R        O 10 11REFFLD(NG04 MANGPF)
     A            EC04      R        O 10 23
     A            SNE05     R        O 10 29REFFLD(NE05 MANEPF)
     A            EC05      R        O 10 41
     A            SNF03     R        O 10 52REFFLD(NF03 MANFPF)
     A            EC01      R        H
     A            EC02      R        H
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'CEP003'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 28'銷售區碼與分店對照維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  5 14REFFLD(EC01)
     A            S#B03     R   -6   O  5 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  5 43REFFLD(EC02)
     A            SND04     R        O  5 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  6  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  6 18'2=修改'
     A                                      COLOR(BLU)
     A                                  6 27'4=刪除'
     A                                      COLOR(BLU)
     A                                  6 36'5=查詢'
     A                                      COLOR(BLU)
     A                                  8  4' A 銷售區碼     分店代號／名稱-
     A                                        駐店人員／姓名       '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A            DBGN3     R        B  9  8REFFLD(EC03)
     A            DBGN4     R        B  9 23REFFLD(EC04)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F6 =新增'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A*                                     CF04(04)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'CEP003'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 28'銷售區碼與分店對照維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6  2'公司代號:'
     A                                      COLOR(BLU)
     A            DEC01     R        O  6 14REFFLD(EC01)
     A            D#B03     R   -6   O  6 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  8  2'通路代號:'
     A                                      COLOR(BLU)
     A            DEC02     R        O  8 14REFFLD(EC02)
     A            DND04     R        O  8 17REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                 10  2'銷售區碼:'
     A                                      COLOR(BLU)
     A            DEC03     R        B 10 14REFFLD(EC03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DNG04     R        O 10 17REFFLD(NG04 MANGPF)
     A                                      COLOR(BLU)
     A                                 12  2'分店代號:'
     A                                      COLOR(BLU)
     A            DEC04     R        B 12 14REFFLD(EC04)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            HEC04     R        H      REFFLD(EC04)
     A            DNE05     R        O 12 20REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                 14  2'駐店人員:'
     A                                      COLOR(BLU)
     A            DEC05     R        B 14 14REFFLD(EC05)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DNF03     R        O 14 25REFFLD(NF03 MANFPF)
     A                                      COLOR(BLU)
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A*                                22 18'F4 =視窗查詢'
     A*                                     COLOR(BLU)
     A  71                             22 34'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 46'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  71                             22 18'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 34'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
