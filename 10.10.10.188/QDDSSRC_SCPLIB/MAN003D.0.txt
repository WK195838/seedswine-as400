     A*%%TS  SD  20090817  135437  CWDEV3      REL-V5R1M0  5722-WDS
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : MAN003D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/09/17                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MAN003                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    REMARK       :產品基本資料 (A/U/D/I)                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MANCPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B 10  5VALUES(' ' '2' '3' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            NC02      R        O 10  8
     A            NC05      R        O 10 19
     A            NC03      R        O 10 39
     A            SNB03     R        O 10 46REFFLD(NB03 MANBPF)
     A            NC15DE        10A  O 10 60
      *配合SFL READCSFLSR
     A            NC01      R        H
      *----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'MAN003'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'產品基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B    +1REFFLD(NC01)
     A*                                     DSPATR(HI)
     A            S#B03     R   -6   O    +1REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  6  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                    +1'2=修改'
     A                                      COLOR(BLU)
     A                                    +1'3=複製'
     A                                      COLOR(BLU)
     A                                    +1'5=查詢'
     A                                      COLOR(BLU)
     A                                  8  5'A 產品代號 產品名稱        +
     A                                      供應商代號／名稱       容量   '
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN2     R        B  9  8REFFLD(NC02)
     A*                                     DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F6 =新增'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(MSG 0004 SCMF)
     A                                      DSPATR(HI)
     A  98        ERR2          63A  O 24  2MSGID(MSG 0005 SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  20090817  135437  CWDEV3      REL-V5R1M0  5722-WDS
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF09(09)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MAN003'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'產品基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A                                      COLOR(BLU)
     A            DNC01     R        O  5 14REFFLD(NC01)
     A            D#B03     R   -6   O  5 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  6  2'產品代號:'
     A                                      COLOR(BLU)
     A            DNC02     R        B  6 14REFFLD(NC02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  7  2'中文全稱:'
     A                                      COLOR(BLU)
     A            DNC04     R        B  7 14REFFLD(NC04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'英文全稱:'
     A                                      COLOR(BLU)
     A            DNC06     R        B  8 14REFFLD(NC06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            DNC07     R        B  8 45REFFLD(NC07)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  9  2'英文簡稱:'
     A                                      COLOR(BLU)
     A            DNC08     R        B  9 14REFFLD(NC08)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  9 33'中文簡稱:'
     A                                      COLOR(BLU)
     A            DNC05     R        B  9 45REFFLD(NC05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'產品識別:'
     A                                      DSPATR(HI)
     A            DNC10     R        B 10 14REFFLD(NC10)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            NC10D     R  -22   O 10 16REFFLD(L203 UTL2PF)
     A                                      COLOR(BLU)
     A                                 10 47'主要供應商:'
     A                                      DSPATR(HI)
     A            DNC03     R        B 10 61REFFLD(NC03)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DNB03     R        O 10 67REFFLD(NB03 MANBPF)
     A                                      COLOR(BLU)
     A                                 11  2'採購單位:'
     A                                      COLOR(BLU)
     A            DNC14     R        B 11 14REFFLD(NC14)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 23'種類:'
     A                                      DSPATR(HI)
     A            DNC11     R        B 11 31REFFLD(NC11)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            NC11D     R  -22   O 11 34REFFLD(L203 UTL2PF)
     A                                      COLOR(BLU)
     A                                 11 47'品牌:'
     A                                      DSPATR(HI)
     A            DNC12     R        B 11 55REFFLD(NC12)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            NC12D     R  -22   O 11 58REFFLD(L203 UTL2PF)
     A                                      COLOR(BLU)
     A                                 12  2'計量單位:'
     A                                      COLOR(BLU)
     A            DNC21     R        B 12 14REFFLD(NC21)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 23'單位容量  :'
     A                                      DSPATR(HI)
     A            DNC15     R        B 12 37REFFLD(NC15)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            NC15D     R  -26   O 12 39REFFLD(L203 UTL2PF)
     A                                      COLOR(BLU)
     A                                 12 47'零售價格:'
     A                                      COLOR(BLU)
     A            DNC19     R        B 12 59REFFLD(NC19)
     A                                      EDTCDE(1)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  2'單位轉換:'
     A                                      COLOR(BLU)
     A            DNC16     R        B 13 14REFFLD(NC16)
     A                                      EDTCDE(3)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 23'包裝單位數:'
     A                                      COLOR(BLU)
     A            DNC17     R        B 13 37REFFLD(NC17)
     A                                      EDTCDE(3)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 47'原產地  :'
     A                                      DSPATR(HI)
     A            DNC18     R        B 13 59REFFLD(NC18)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            NC18D     R  -20   O 13 62REFFLD(L203 UTL2PF)
     A                                      COLOR(BLU)
     A                                 14  2'每瓶長度:'
     A                                      COLOR(BLU)
     A            DNC22     R        B 14 14REFFLD(NC22)
     A                                      EDTCDE(3)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 23'每瓶寬度  :'
     A                                      COLOR(BLU)
     A            DNC23     R        B 14 37REFFLD(NC23)
     A                                      EDTCDE(3)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 47'每瓶高度:'
     A                                      COLOR(BLU)
     A            DNC24     R        B 14 59REFFLD(NC24)
     A                                      EDTCDE(3)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  2'產品歸屬:'
     A                                      DSPATR(HI)
     A            DNC09     R        B 15 14REFFLD(NC09)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            NC09D     R  -22   O 15 17REFFLD(L203 UTL2PF)
     A                                      COLOR(BLU)
     A                                 15 47'國際條碼:'
     A                                      COLOR(BLU)
     A            DNC20     R        B 15 59REFFLD(NC20)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20  2'備註    :'
     A                                      COLOR(BLU)
     A            DNC26     R        B 20 14REFFLD(NC26)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DNC27     R        B 20 47REFFLD(NC26)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  72                             21 14'<<請注意，新增產品會將產品資料寫+
     A                                      到產品庫存檔>>'
     A                                      DSPATR(BL)
     A                                      DSPATR(HI)
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 18'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  71                             22 34'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 50'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 18'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  71                             22 34'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 50'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
