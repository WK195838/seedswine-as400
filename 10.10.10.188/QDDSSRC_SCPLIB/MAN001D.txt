     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : MAN001D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/08/11                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MAN001                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    REMARK       :貨運站基本資料 (A/U/D/I)                  *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MANAPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  5VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            NA01      R        O  9  9
     A            NA03      R        O  9 12
     A            NA06      R        O  9 28
     A            NA07      R        O  9 45
      *----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'MAN001'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'貨運站基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5  6'2=修改'
     A                                      COLOR(BLU)
     A                                  5 16'4=刪除'
     A                                      COLOR(BLU)
     A                                  5 26'5=查詢'
     A                                      COLOR(BLU)
     A                                  7  5'A  區碼／名稱       聯絡人    -
     A                                           電話一        '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A            DBGN1     R        B  8  9REFFLD(NA01)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F6 =新增'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(MSG 0004 SCMF)
     A                                      DSPATR(HI)
     A  98        ERR2          63A  O 24  2MSGID(MSG 0005 SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MAN001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'貨運站基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'貨運站區碼:'
     A                                      COLOR(BLU)
     A            DNA01     R        B  5 16REFFLD(NA01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  6  2'貨運站全稱:'
     A                                      COLOR(BLU)
     A            DNA02     R        B  6 16REFFLD(NA02)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7  2'貨運站簡稱:'
     A                                      COLOR(BLU)
     A            DNA03     R        B  7 16REFFLD(NA03)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'貨運站地址:'
     A                                      COLOR(BLU)
     A            DNA04     R        B  8 16REFFLD(NA04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DNA05     R        B  8 49REFFLD(NA05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'聯絡人　  :'
     A                                      COLOR(BLU)
     A            DNA06     R        B 10 16REFFLD(NA06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 44'電話一　:'
     A                                      COLOR(BLU)
     A            DNA07     R        B 10 56REFFLD(NA07)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  2'有效起日  :'
     A                                      COLOR(BLU)
     A            DNA11     R   -2   B 11 16REFFLD(NA11)
     A                                      EDTCDE(Y)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 11 44'電話二　:'
     A                                      COLOR(BLU)
     A            DNA08     R        B 11 56REFFLD(NA08)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  2'有效迄日  :'
     A                                      COLOR(BLU)
     A            DNA12     R   -2   B 12 16REFFLD(NA12)
     A                                      EDTCDE(Y)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +1'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 12 44'傳真機　:'
     A                                      COLOR(BLU)
     A            DNA09     R        B 12 56REFFLD(NA09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  2'電子信箱  :'
     A                                      COLOR(BLU)
     A            DNA10     R        B 13 16REFFLD(NA10)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 20  2'備註      :'
     A                                      COLOR(BLU)
     A            DNA13     R        B 20 16REFFLD(NA13)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            DNA14     R        B 20 49REFFLD(NA14)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 18'F5 =參考資料'
     A                                      COLOR(BLU)
     A  71                             22 35'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 50'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  71                             22 18'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 34'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
