     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PUR020                                     *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/10/30                                   *
     A*    UPDATE DATE  : 97/11/14 VINCENT                           *
     A*    PROGRAM NAME : PUR020                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :驗收明細表                                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<PUR020>'
     A                                    62
     A                                      MSGCON(030 USC9999 SCMF)
     A                                   152
     A                                      '日期:'
     A            $EGMDY         6  0O   160
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    62
     A                                      '驗收明細表－依供應商／產品／分店'
     A                                   152
     A                                      '時間:'
     A                                   160
     A                                      TIME
     A                                      SPACEA(001)
     A                                   152
     A                                      '頁次:'
     A                                   160
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '公司代號:'
     A            DCL01     R        O    +1
     A                                      REFFLD(CL01)
     A            D#B03     R        O    +1
     A                                      REFFLD(#B03 PA#BPF)
     A                                    +1
     A                                      '通路代號:'
     A            DCL02          2A  O    +1
     A            DND04     R        O    +1
     A                                      REFFLD(ND04 MANDPF)
     A                                    +1
     A                                      '供應商代號:'
     A            DCM02S         5   O    +1
     A                                    +1
     A                                      '-'
     A            DCM02E         5   O    +1
     A                                    +1
     A                                      '到貨運日:'
     A            DCL07S         6  0O    +1
     A                                      EDTCDE(Y)
     A                                    +1
     A                                      '-'
     A            DCL07E         6  0O    +1
     A                                      EDTCDE(Y)
     A                                    +1
     A                                      '產品代號:'
     A            DCM03S         9   O    +1
     A                                    +1
     A                                      '-'
     A            DCM03E         9   O    +1
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============================='
     A*                                     SPACEB(001)
     A                                    85
     A                                      '驗收數量'
     A                                      SPACEB(001)
     A                                    98
     A                                      '未稅進價'
     A                                    67
     A                                      '分店代號／簡稱'
     A                                     1
     A                                      '供應商代號／簡稱'
     A                                    22
     A                                      '到貨運日期'
     A                                    37
     A                                      '產品代號'
     A                                    48
     A                                      '產品簡稱'
     A                                   114
     A                                      '未稅金額'
     A                                   130
     A                                      '含稅金額'
     A                                   145
     A                                      '驗收單號'
     A                                   159
     A                                      '採購單號'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        CM02      R              2
     A                                      REFFLD(CM02 POCMPF)
     A  30        NB03      R              8
     A                                      REFFLD(NB03 MANBPF)
     A  31        LCL07          6  0     24
     A                                      EDTWRD('  /  /  ')
     A  32        CM03      R             38
     A                                      REFFLD(CM03 POCMPF)
     A  32        NC05      R             48
     A            CL04      R             68
     A            NE05      R             74
     A            CM06      R             87
     A                                      EDTCDE(J)
     A            CM07      R             97
     A                                      EDTCDE(J)
     A            CM08      R            110
     A                                      EDTCDE(J)
     A            CM081     R            126
     A                                      REFFLD(CM08)
     A                                      EDTCDE(J)
     A            CM01      R            142
     A            CM10      R            156
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    60
     A                                      '交貨日 合計:'
     A            Q0T1           7  0     85
     A                                      EDTCDE(J)
     A            Q1            11  2    110
     A                                      EDTCDE(J)
     A            Q2            11  2    126
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    60
     A                                      '供應商 合計:'
     A            Q0T2           7  0     85
     A                                      EDTCDE(J)
     A            Q3            11  2    110
     A                                      EDTCDE(J)
     A            Q4            11  2    126
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD4
     A                                      SPACEB(001)
     A                                    60
     A                                      '       總計:'
     A            Q0T3           7  0     85
     A                                      EDTCDE(J)
     A            Q5            11  2    110
     A                                      EDTCDE(J)
     A            Q6            11  2    126
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD5
     A                                      SPACEB(001)
     A                                    60
     A                                      '產品   合計:'
     A            Q0T4           7  0     85
     A                                      EDTCDE(J)
     A            Q7            11  2    110
     A                                      EDTCDE(J)
     A            Q8            11  2    126
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<PUR020P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============================='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
