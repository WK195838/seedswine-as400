     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PUR050D                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/10/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR050                                     *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :分店驗收數量差異表                        *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A*                                     OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CF07(07 'F7下載')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUR050'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'供應商負擔運費表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                 06 24'公司代號  :'
     A                                      COLOR(BLU)
     A            DCK01     R        B 06 38REFFLD(CK01)
     A  60                                  DSPATR(RI PC)
     A            D#B03     R        O 06 41REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A*
     A                                 08 24'供應商代號:'
     A                                      DSPATR(HI)
     A            DCK08S         5A  B    +1
     A  61                                  DSPATR(RI PC)
     A                                    +1'-'
     A                                      COLOR(BLU)
     A            DCK08E         5A  B    +1
     A  62                                  DSPATR(RI PC)
     A*
     A                                 10 24'到貨運日期:'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DCK03S         6Y 0B 10 38EDTWRD('  /  /  ')
     A  63                                  DSPATR(RI PC)
     A                                    +1'-'
     A                                      COLOR(BLU)
     A            DCK03E         6Y 0B    +1EDTWRD('  /  /  ')
     A  64                                  DSPATR(RI PC)
     A                                    +1'(月／日／年)'
     A                                      COLOR(BLU)
     A*
     A                                 12 24'分店地區別:'
     A                                      COLOR(BLU)
     A            DCK09S         1A  B    +1
     A  65                                  DSPATR(RI PC)
     A                                    +1'-'
     A                                      COLOR(BLU)
     A            DCK09E         1A  B    +1
     A  66                                  DSPATR(RI PC)
     A*
     A                                 14 24'報表類別  :'
     A                                      COLOR(BLU)
     A            DATA           1Y 0B    +1VALUES(1 2)
     A                                    +1'(1總表／2明細表)'
     A                                      COLOR(BLU)
     A*
     A                                 17 24'執行環境  :'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 17 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 43'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 18 24'列印份數  :'
     A                                      COLOR(BLU)
     A            $CPY           3Y 0B 18 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 24'列表機　  :'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                 22 43'F7 =下載'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
