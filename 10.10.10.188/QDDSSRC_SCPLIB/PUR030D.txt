     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PUR030D                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/10/30                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR030                                     *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :分店驗收數量差異表                        *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A*                                     OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CF07(07 'F7 DOWNLOAD')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUR030'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'分店驗收數量差異表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 24'公司代號:'
     A                                      COLOR(BLU)
     A            DCL01     R        B  6 36REFFLD(CL01)
     A  60                                  DSPATR(RI PC)
     A            D#B03     R        O  6 39REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A*
     A                                 08 24'到分店日:'
     A                                      COLOR(BLU)
     A            DCL03S         6Y 0B 08 36EDTWRD('  /  /  ')
     A  61                                  DSPATR(RI PC)
     A                                 08 45'-'
     A                                      COLOR(BLU)
     A            DCL03E         6Y 0B 08 47EDTWRD('  /  /  ')
     A  62                                  DSPATR(RI PC)
     A                                 08 56'(月／日／年)'
     A                                      COLOR(BLU)
     A*
     A                                 10 24'通路代號:'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DCL02S    R        B 10 36REFFLD(CL02 POCLPF)
     A  63                                  DSPATR(RI PC)
     A                                 10 45'-'
     A                                      COLOR(BLU)
     A            DCL02E    R        B 10 47REFFLD(CL02 POCLPF)
     A  64                                  DSPATR(RI PC)
     A*
     A                                 12 24'分店代號:'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DCL04S    R        B 12 36REFFLD(CL04 POCLPF)
     A  65                                  DSPATR(RI PC)
     A                                 12 45'-'
     A                                      COLOR(BLU)
     A            DCL04E    R        B 12 47REFFLD(CL04 POCLPF)
     A  66                                  DSPATR(RI PC)
     A*
     A                                 17 24'執行環境:'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 17 36VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 41'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 18 24'列印份數:'
     A                                      COLOR(BLU)
     A            $CPY           3Y 0B 18 36EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 24'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 36CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                 22 42'F7 =檔案下載'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
