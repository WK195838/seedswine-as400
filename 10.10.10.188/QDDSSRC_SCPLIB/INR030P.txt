     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : INR030P                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/10/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR030                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :現場產品庫存表－依分店                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<INR030P>'
     A                                    26MSGCON(030 USC9999 SCMF)
     A                                    71'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    28'現場產品庫存表－依分店'
     A                                    71'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A                                    71'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司代號:'
     A            DTO01     R        O    +1REFFLD(TB01)
     A            D#B03     R   -4   O    +1REFFLD(#B03 PA#BPF)
     A                                    +5'通路代號:'
     A            DTO02S         2A  O    +1
     A                                    +1'-'
     A            DTO02E         2A  O    +1
     A                                    +1'分店代號:'
     A            DTO03S         5   O    +1
     A                                    +1'-'
     A            DTO03E         5   O    +1
     A                                      SPACEA(001)
     A                                     1'產品代號:'
     A            DTO04S         9   O    +1
     A                                    +1'-'
     A            DTO04E         9   O    +1
     A                                    33'本期截止日:'
     A            DTO05          6S 0O    +1EDTCDE(Y)
     A                                    +1'(月／日／年)'
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =================='
     A                                      SPACEB(001)
     A                                     1'分店代號／名稱'
     A                                      SPACEB(001)
     A                                    +1'產品代號'
     A                                    +1'產品簡稱'
     A                                    48'陳列量'
     A                                    57'一般進貨量'
     A                                    70'總庫存量'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        W803           5A        2
     A  30        W804          10O       +1
     A  31        W805           9A       +1
     A  31        W806          18O       +1
     A            W807           6S 0     48EDTCDE(J)
     A            W808           6S 0     60EDTCDE(J)
     A            W809           6S 0     71EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    30'      分店合計:'
     A            Q1             6S 0     48EDTCDE(J)
     A            Q2             6S 0     60EDTCDE(J)
     A            Q3             6S 0     71EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    30'          總計:'
     A            Q4             6S 0     48EDTCDE(J)
     A            Q5             6S 0     60EDTCDE(J)
     A            Q6             6S 0     71EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<INR030P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =================='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
