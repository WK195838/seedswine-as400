     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : INT014D                                    *
     A*    AUTHOR       : A1546  VINCENT                             *
     A*    CREATE DATE  : 97/10/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INT014                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    REMARK       :現場產品異動查詢(I)                       *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  4VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            TO04      R        O 10  7REFFLD(TO04 *LIBL/INTOLF01)
     A            TO03      R        O 10 18REFFLD(TO03 *LIBL/INTOLF01)
     A*           TB06      R        O 10 25EDTCDE(J)
     A            TB06X          6  0O 10 25EDTCDE(J)
     A            TBTT1     R        O 10 35REFFLD(TB06)
     A                                      EDTCDE(J)
     A            TBTT2     R        O 10 45REFFLD(TB06)
     A                                      EDTCDE(J)
     A            TBTT3     R        O 10 55REFFLD(TB17)
     A                                      EDTCDE(J)
     A            NC05      R        H
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'INT014'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'現場產品異動查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 14REFFLD(TO01 *LIBL/INTOLF01)
     A                                      DSPATR(HI)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(TO02 *LIBL/INTOLF01)
     A                                      DSPATR(HI)
     A            DND04     R        O  4 46REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  2'庫存年月:'
     A                                      COLOR(BLU)
     A            DTB02          4Y 0O  5 14EDTWRD('  /  ')
     A                                  5 20'（月／年）'
     A                                      COLOR(BLU)
     A            HTB02     R        H      REFFLD(TB02)
     A                                  6  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                    +1'5=產品異動明細查詢'
     A                                      COLOR(BLU)
     A                                  7 25' 期初    現場    帳列'
     A                                      COLOR(BLU)
     A                                    +4'陳列'
     A                                      COLOR(BLU)
     A                                  8  3' A 產品代號 分店  庫存量  +
     A                                      庫存量  庫存量  庫存量 '
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN3     R        B  9  7REFFLD(TO04 *LIBL/INTOLF01)
     A            DBGN4     R        B  9 18REFFLD(TO03 *LIBL/INTOLF01)
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 23  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  H
     ** 61                                  DSPATR(PR)
     A*N61                                  DSPATR(UL)
     A            DTO05          6Y 0O 11  4EDTWRD('  /  /  ')
     A            TO06      R        O    +2REFFLD(TO06 *LIBL/INTOLF)
     A            DL303         10O  O    +1
     A            TO07      R        O    +1REFFLD(TO07 *LIBL/INTOLF01)
     A            DQTY      R        O    +1REFFLD(TO121 *LIBL/INTOLF01)
     A                                      EDTCDE(J)
     A            DQTY1     R        O    +1REFFLD(TO121 *LIBL/INTOLF01)
     A                                      EDTCDE(K)
     A            DMEMO          6O  O    +1
     A            TTO05          8S 0H
     A*------------------------------------------------------------
     A*
     A**                                    CSRLOC(D#ROW      D#COL)
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'INT014'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'產品異動查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            TO01      R        O  4 14REFFLD(TO01 *LIBL/INTOLF01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 *LIBL/PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            TO02      R        O  4 43REFFLD(TO02 *LIBL/INTOLF01)
     A            DND04     R        O  4 46REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  2'產品代號:'
     A                                      COLOR(BLU)
     A            TO04      R        O  5 14REFFLD(TO04 *LIBL/INTOLF01)
     A            DNC05     R        O  5 24REFFLD(NC05)
     A                                      COLOR(BLU)
     A                                  5 49'庫存年月:'
     A                                      COLOR(BLU)
     A            DTB02          4Y 0O    +1EDTWRD('  /  ')
     A                                    +1'（月／年）'
     A                                      COLOR(BLU)
     A                                  6  2'分店代號:'
     A                                      COLOR(BLU)
     A            TO03      R        O  6 14REFFLD(TO03 *LIBL/INTOLF01)
     A            DNE05     R        O  6 20REFFLD(NE05)
     A                                      COLOR(BLU)
     A                                  6 31'單位:'
     A                                      COLOR(BLU)
     A            DNC14     R        O  6 39REFFLD(NC14)
     A                                  6 49'期初庫存量:'
     A                                      COLOR(BLU)
     A*           TB06      R        O  6 63EDTCDE(J)
     A            TB06X          6  0O  6 63EDTCDE(J)
     A                                  8  1'（月／日／年）'
     A                                      COLOR(BLU)
     A                                  7 49'現場庫存量:'
     A                                      COLOR(BLU)
     A            TBTT1     R        O  7 63REFFLD(TB06)
     A                                      EDTCDE(J)
     A                                  9  3'異動日期單別        +
     A                                      單據編號       數量備註 -
     A                                              '
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN21         6Y 0B 10  4EDTWRD('  /  /  ')
     A*------------------------------------------------------------
     A*
     A          R DSPC2
     A                                 22  2'執行'
     A                                 22 10'F3=回主畫面'
     A                                 22 26'F12=回前畫面'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 23  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPW                      WINDOW(10 51 07 20)
     A                                      CA12(12)
     A                                  2  2'帳列量:'
     A            W01            6  0O    +1EDTCDE(J)
     A                                  3  2'借貨量:'
     A            W02            6  0O    +1EDTCDE(J)
     A                                  4  2'無庫量:'
     A            W03            6  0O    +1EDTCDE(J)
     A*
     A                                  6  3'F12=回前畫面'
