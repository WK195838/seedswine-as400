     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PIA007D                                    *
     A*    AUTHOR       : ROBIN                                      *
     A*    CREATE DATE  : 100/08/02                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PIA0071                                    *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :盤點系統                                  *
     A*    REMARK       :盤點資料上傳                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03 '結束執行')
     A                                      CF14(14 '處理作業')
     A  28                                  FRCDTA
     A  28                                  LOCK
     A                                      KEEP
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PIA007'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $DATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'盤點資料上傳'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'公司代號:'
     A                                      COLOR(BLU)
     A            DPI01     R        B  6 38REFFLD(EH01)
     A  61                                  DSPATR(PC RI)
     A            D#B03     R   -4   O  6 41REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  8 26'盤點年月:'
     A                                      COLOR(BLU)
     A            DPI02          4Y 0B  8 38EDTWRD('  /  ')
     A  62                                  DSPATR(PC RI)
     A                                  8 45'(月／年)'
     A                                      COLOR(BLU)
     A                                 10 17'+----------------------------------
     A                                      --------------+'
     A                                 11 17'|'
     A                                 11 65'|'
     A                                 12 17'|請按F14以便處理此作業，否則按-
     A                                      F3結束此作業|'
     A                                 13 17'|'
     A                                 13 65'|'
     A                                 14 17'+----------------------------------
     A                                      --------------+'
     A*
     A                                 17 19'注意:指定範圍內的所有資料將被+
     A                                      清除與取代!'
     A                                      COLOR(PNK)
     A*
     A  28                             20 27'*** 上傳處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +1'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +1'F14 =作業處理'
     A                                      COLOR(BLU)
     A*
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
