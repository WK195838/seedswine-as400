     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PUR061D                                    *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 101/01/09                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR061                                     *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :採購單彙總表                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CF07(07 'F7 DOWNLOAD')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUR061'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'採購單彙總表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                 06 24'公司代號  :'
     A                                      COLOR(BLU)
     A            DCH01     R        B 06 38REFFLD(CH01)
     A  60                                  DSPATR(RI PC)
     A            D#B03     R        O 06 41REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A*
     A                                 07 24'通路代號  :'
     A                                      COLOR(BLU)
     A            DCH02S         2A  B    +1
     A  61                                  DSPATR(RI PC)
     A                                    +1'-'
     A                                      COLOR(BLU)
     A            DCH02E         2A  B    +1
     A  62                                  DSPATR(RI PC)
     A                                    +1'(空白：全部)'
     A                                      COLOR(BLU)
     A                                 08 24'分店代號  :'
     A                                      COLOR(BLU)
     A            DCH07S         5A  B    +1
     A  63                                  DSPATR(RI PC)
     A                                    +1'-'
     A                                      COLOR(BLU)
     A            DCH07E         5A  B    +1
     A  64                                  DSPATR(RI PC)
     A                                    +1'(空白：全部)'
     A                                      COLOR(BLU)
     A                                 09 24'供應商代號:'
     A                                      DSPATR(HI)
     A            DCH06S         5A  B    +1
     A  65                                  DSPATR(RI PC)
     A                                    +1'-'
     A                                      COLOR(BLU)
     A            DCH06E         5A  B    +1
     A  66                                  DSPATR(RI PC)
     A                                    +1'(空白：全部)'
     A                                      COLOR(BLU)
     A                                 10 24'產品代號  :'
     A                                      DSPATR(HI)
     A            DCI03S         9A  B    +1
     A  67                                  DSPATR(RI PC)
     A                                    +1'-'
     A                                      COLOR(BLU)
     A            DCI03E         9A  B    +1
     A  68                                  DSPATR(RI PC)
     A                                    +1'(空白：全部)'
     A                                      COLOR(BLU)
     A                                 11 24'訂貨日期  :'
     A                                      COLOR(BLU)
     A            DCD03S         6Y 0B 11 38EDTWRD('  /  /  ')
     A  69                                  DSPATR(RI PC)
     A                                    +1'-'
     A                                      COLOR(BLU)
     A            DCD03E         6Y 0B    +1EDTWRD('  /  /  ')
     A  70                                  DSPATR(RI PC)
     A                                    +1'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 12 24'到貨運日  :'
     A                                      COLOR(BLU)
     A            DCH08S         6Y 0B 12 38EDTWRD('  /  /  ')
     A  71                                  DSPATR(RI PC)
     A                                    +1'-'
     A                                      COLOR(BLU)
     A            DCH08E         6Y 0B    +1EDTWRD('  /  /  ')
     A  72                                  DSPATR(RI PC)
     A                                    +1'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 14 24'報表種類  :'
     A                                      COLOR(BLU)
     A            DPRTTP         1A  B    +1VALUES('1' '2')
     A                                    +1'(1 總表／2 明細表)'
     A                                      COLOR(BLU)
     A                                 17 24'執行環境  :'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 17 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 43'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 18 24'列印份數  :'
     A                                      COLOR(BLU)
     A            $CPY           3Y 0B 18 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 24'列表機　  :'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                 22 42'F7 =檔案下載'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
