     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    DSPF NAME    : INR100D                                    *
     A*    AUTHOR       : DEREK                                      *
     A*    CREATE DATE  : 101/12/06                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR100D                                    *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :產品異動報表                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CF07(07 'F7下載')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'INR100'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'產品異動報表              '
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 24'公司代號:'
     A                                      COLOR(BLU)
     A            DTO01     R        B  6 36REFFLD(CM11)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A            D#B03     R        O  6 39REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A*
     A                                  8 24'通路代號:'
     A                                      COLOR(BLU)
     A            DTO02S         2A  B  8 36
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  8 39'-'
     A                                      COLOR(BLU)
     A            DTO02E         2A  B  8 41
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  8 45'(空白：全部)'
     A                                      COLOR(BLU)
     A*
     A                                 10 24'分店代號:'
     A                                      COLOR(BLU)
     A            DTO03S         5A  B 10 36
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 10 42'-'
     A                                      COLOR(BLU)
     A            DTO03E         5A  B 10 44
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 10 51'(空白：全部)'
     A                                      COLOR(BLU)
     A*
     A                                 12 24'產品代號:'
     A                                      DSPATR(HI)
     A            DTO04S         9A  B 12 36
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 12 46'-'
     A                                      COLOR(BLU)
     A            DTO04E         9A  B 12 48
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 12 59'(空白：全部)'
     A                                      COLOR(BLU)
     A*
     A                                 14 24'進出日期:'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DTO05S         6Y 0B 14 36EDTWRD('  /  /  ')
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                 14 45'-'
     A                                      COLOR(BLU)
     A            DTO05E         6Y 0B 14 47EDTWRD('  /  /  ')
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                 14 57'(月／日／年)'
     A                                      COLOR(BLU)
     A*
     A                                 17 24'執行環境:'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 17 36VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 39'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 18 24'列印份數:'
     A                                      COLOR(BLU)
     A            $CPY           3Y 0B 18 36EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 24'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 36CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                 22 43'F7 =檔案下載'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
