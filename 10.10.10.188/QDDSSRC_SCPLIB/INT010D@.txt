     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : INT009D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 99/01/29                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INT009                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    REMARK       :庫存月結還原處理                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03 '結束執行')
     A                                      CF14(14 '處理作業')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'INT010'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'庫存月結還原'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'公司代號:'
     A                                      COLOR(BLU)
     A            DEH01     R        B  6 38REFFLD(EH01)
     A                                      DSPATR(HI)
     A            D#B03     R   -4   O  6 41REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A*                                 6 54'(空白：全部) '
     A*                                     COLOR(BLU)
     A                                  8 26'還原月份:'
     A                                      COLOR(BLU)
     A            DEH02          4Y 0O  8 38EDTWRD('  /  ')
     A                                  8 45'(月／年)'
     A                                      COLOR(BLU)
     A                                 10 17'+----------------------------------
     A                                      --------------+'
     A                                 11 17'|'
     A                                 11 65'|'
     A                                 12 17'|請按F14以便處理此作業，否則按-
     A                                      F3結束此作業|'
     A                                 13 17'|'
     A                                 13 20'注意!請確認所有使用者皆已退出-
     A                                      作業畫面!! '
     A                                      COLOR(RED)
     A                                 13 65'|'
     A                                 14 17'|'
     A                                 14 65'|'
     A                                 15 17'+----------------------------------
     A                                      --------------+'
     A*                                16 26'執行環境:'
     A*                                     COLOR(BLU)
     A*           $EVR           1Y 0B 16 38VALUES(1 2)
     A*                                     EDTCDE(3)
     A*                                     CHECK(RB)
     A*                                16 43'(1-即時 2-批次)'
     A*                                     COLOR(BLU)
     A*                                17 26'列印份數:'
     A*                                     COLOR(BLU)
     A*           $CPY           3Y 0B 17 38EDTCDE(3)
     A*                                     CHECK(RB)
     A*                                18 26'列表機　:'
     A*                                     COLOR(BLU)
     A*           $PRTID         2Y 0B 18 38EDTCDE(3)
     A*                                     CHECK(RB)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +1'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +1'F14 =作業處理'
     A                                      COLOR(BLU)
     A  70                             24  2'通路代號'
     A                                      DSPATR(HI)
     A  70        DND02     R             +1REFFLD(ND02)
     A                                      DSPATR(HI)
     A  70                                +1'銷售資料未完成確認!'
     A                                      DSPATR(HI)
     A*
     A  98        ERR1          60A  O 24  2MSGID(MSG 0014 SCMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(MSG 0014 SCMF)
     A                                      DSPATR(HI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
