     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : INR050P                                    *
     A*    AUTHOR       : A1546  VINCENT                             *
     A*    CREATE DATE  : 97/10/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR050                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :月加權成本計算表                          *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<INR050P>'
     A                                    55
     A                                      MSGCON(030 USC9999 SCMF)
     A                                   135
     A                                      '日期:'
     A            $EGMDY         6  0O    +1
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    60
     A                                      '月加權成本計算表'
     A                                   135
     A                                      '時間:'
     A                                    +1
     A                                      TIME
     A                                      SPACEA(001)
     A                                   135
     A                                      '頁次:'
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '公司代號:'
     A            DTO01     R        O    +1
     A                                      REFFLD(TB01)
     A            D#B03     R   -4   O    +1
     A                                      REFFLD(#B03 PA#BPF)
     A                                    +5
     A                                      '通路代號:'
     A            DTO02S         2A  O    +1
     A                                    +1
     A                                      '-'
     A            DTO02E         2A  O    +1
     A                                    +4
     A                                      '產品代號:'
     A            DTO04S         9   O    +1
     A                                    +1
     A                                      '-'
     A            DTO04E         9   O    +1
     A                                    +4
     A                                      '月結月份:'
     A            DTO05S         4S 0O    +1
     A                                      EDTWRD('  /  ')
     A                                    +1
     A                                      '(月／年)'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                    32
     A                                      '------ 期初帳列 -----'
     A                                      SPACEB(001)
     A                                    +3
     A                                      '---------------------------'
     A                                    +1
     A                                      '本期帳列'
     A                                    +1
     A                                      '----------------------------'
     A                                    +2
     A                                      '------ 期末帳列 ------'
     A                                     1
     A                                      '產品代號'
     A                                      SPACEB(001)
     A                                    +0
     A                                      '產品簡稱'
     A                                    32
     A                                      '庫存量'
     A                                    +6
     A                                      '庫存金額'
     A                                    +1
     A                                      '進貨數量'
     A                                    +5
     A                                      '進貨金額'
     A                                    +1
     A                                      '調整數量'
     A                                    +5
     A                                      '調整金額'
     A                                    +3
     A                                      '成本調整金額'
     A                                    +3
     A                                      '庫存量'
     A                                    +5
     A                                      '庫存金額'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A            W801           9A        2
     A            W802          18O       +1
     A            W803           6S 0     32
     A                                      EDTCDE(J)
     A            W804          11S 2     +1
     A                                      EDTCDE(J)
     A            W805           6S 0     +2
     A                                      EDTCDE(J)
     A            W806          11S 2     +1
     A                                      EDTCDE(J)
     A            W807           6S 0     +2
     A                                      EDTCDE(J)
     A            W808          11S 2     +1
     A                                      EDTCDE(J)
     A            W809          11S 2     +2
     A                                      EDTCDE(J)
     A            W810           6S 0     +3
     A                                      EDTCDE(J)
     A            W811          11S 2     +1
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    20
     A                                      '總 計:'
     A            T03            6S 0     32
     A                                      EDTCDE(J)
     A            T04           11S 2     +1
     A                                      EDTCDE(J)
     A            T05            6S 0     +2
     A                                      EDTCDE(J)
     A            T06           11S 2     +1
     A                                      EDTCDE(J)
     A            T07            6S 0     +2
     A                                      EDTCDE(J)
     A            T08           11S 2     +1
     A                                      EDTCDE(J)
     A            T09           11S 2     +2
     A                                      EDTCDE(J)
     A            T10            6S 0     +3
     A                                      EDTCDE(J)
     A            T11           11S 2     +1
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<INR050P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
