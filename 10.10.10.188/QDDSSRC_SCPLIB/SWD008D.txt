     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : SWD008D                                    *
     A*    PROGRAM NAME :報價檔產品代號                            *
     A*    AUTHOR       : A1491 EASON                                *
     A*    CREATE DATE  : 2008.08.29                                 *
     A*    UPDATE DATE  :
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MANCPF)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPD0                     ASSUME
     A                                  1  2' '
     A*----------------------------------------------------------------
     A          R SFLSR3                    SFL
     A            RRN3           3Y 0H
     A            SEL            1A  B  6  4DSPATR(CS)
     A*                                     DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      VALUES(' ' '/')
     A            SNC02     R        O  6  7REFFLD(NC02)
     A            SNC05     R        O  6 18REFFLD(NC05)
     A*----------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0006)
     A                                      WINDOW(3 11 14 40)
     A                                      CA12(12)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      USRRSTDSP
     A            DRRN3          3S 0H      SFLRCDNBR
     A                                  1 15'報價檔產品代號'
     A                                      DSPATR(HI)
     A                                  1  2' <SWD008>  '
     A                                  4  4'/'
     A                                  4  6'產品代號'
     A                                      DSPATR(UL)
     A                                  4 20'中文簡稱'
     A                                      DSPATR(UL)
     A            KNC02     R        B  5  7REFFLD(NC02)
     A                                      DSPATR(PC)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD1                     WINDOW(SFLCR3)
     A                                 13  2'執行'
     A                                    +2'PF12=回前畫面'
