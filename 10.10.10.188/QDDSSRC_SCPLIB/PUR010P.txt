     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PUR010                                     *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/10/29                                   *
     A*    UPDATE DATE  :101/02/14  NANCY (0214A)                    *
     A*                         當有退貨,報表顯示:退貨&退貨*
     A*                         若無退貨,報表僅顯示:合計       *
     A*    PROGRAM NAME : PUR010                                     *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :驗收單彙總表-依廠商                     *
     A*--------------------------------------------------------------*
     A* 00A 101/03/29 DEREK 增加（產品歸屬）選項                   *
     A*===============================================================
     A                                      REF(SCWK05)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<PUR010>'
     A                                    45
     A                                      MSGCON(030 USC9999 SCMF)
     A                                   111
     A                                      '日期:'
     A            $EGMDY         6  0O   119
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    45
     A                                      '驗收彙總表-依供應商／產品'
     A  71                                75'(進貨/退貨)'
     A  72                                75'(進貨)'
     A  73                                75'(退貨)'
     A                                   111
     A                                      '時間:'
     A                                   119
     A                                      TIME
     A                                      SPACEA(001)
     A                                   111
     A                                      '頁次:'
     A                                   119
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
 00A+A                                     1'產品歸屬:'
 00A+A            DNC09     R        O    13REFFLD(NC09 MANCPF)
 00A+A            NC09D     R  -22   O    16REFFLD(L203 UTL2PF)
 00A+A                                      SPACEA(001)
     A                                     1
     A                                      '公司別　:'
     A            DCL01     R        O    +1
     A                                      REFFLD(CL01 POCLPF)
     A            D#B03     R        O    +2
     A                                      REFFLD(#B03 PA#BPF)
     A                                    +2
     A                                      '通路代號:'
     A            DCL02          2A  O    +1
     A            DND04     R        O    +2
     A                                      REFFLD(ND04 MANDPF)
     A                                    +1
     A                                      '供應商代號:'
     A            DCM02S         5   O    +1
     A                                    +2
     A                                      '-'
     A            DCM02E         5   O    +1
     A                                    +1
     A                                      '到貨運日:'
     A            DCL07S         6  0O    +1
     A                                      EDTCDE(Y)
     A                                    +1
     A                                      '-'
     A            DCL07E         6  0O    +1
     A                                      EDTCDE(Y)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================'
     A                                      SPACEB(001)
     A                                     1
     A                                      '供應商代號／簡稱'
     A                                      SPACEB(001)
     A                                    22
     A                                      '到貨運日期'
     A                                    37
     A                                      '產品代號'
     A                                    48
     A                                      '產品簡稱'
     A                                    67
     A                                      '驗收數量'
     A                                    81
     A                                      '未稅進價'
     A                                    99
     A                                      '未稅金額'
     A                                   117
     A                                      '含稅金額'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        WK04      R              2
     A*                                     REFFLD(CM02 POCMPF)
     A  30        SNB03     R              8
     A                                      REFFLD(NB03 MANBPF)
     A  31        LCL07          6  0     24
     A                                      EDTWRD('  /  /  ')
     A            WK05      R             38
     A*                                     REFFLD(CM03 POCMPF)
     A            SNC05     R             48
     A                                      REFFLD(NC05 MANCPF)
     A            WK08      R             69
     A                                      EDTCDE(J)
     A            WK09      R             80
     A                                      EDTCDE(J)
     A            WK10      R             94
     A                                      EDTCDE(J)
     A            WK101     R            112
     A                                      REFFLD(WK10)
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
0214AA          R PD2I
 +   A                                      SPACEB(001)
 +   A                                    58
 +   A                                      '進貨小計:'
 +   A            QT1I           7  0     67
 +   A                                      EDTCDE(J)
 +   A            Q1I           11  2     94
 +   A                                      EDTCDE(J)
 +   A            Q2I           11  2    112
 +   A                                      EDTCDE(J)
 +   A*----------------------------------------------------------------
 +   A          R PD2R
 +   A                                      SPACEB(001)
 +   A                                    58
 +   A                                      '退貨小計:'
 +   A            QT1R           7  0     67
 +   A                                      EDTCDE(J)
 +   A            Q1R           11  2     94
 +   A                                      EDTCDE(J)
 +   A            Q2R           11  2    112
 +   A                                      EDTCDE(J)
 +---A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    58
     A                                      '小計:'
     A            QT1            7  0     67
     A                                      EDTCDE(J)
     A            Q1            11  2     94
     A                                      EDTCDE(J)
     A            Q2            11  2    112
     A                                      EDTCDE(J)
0214AA*----------------------------------------------------------------
 +   A          R PD3I
 +   A                                      SPACEB(001)
 +   A                                    58
 +   A                                      '進貨合計:'
 +   A            QT2I           7  0     67
 +   A                                      EDTCDE(J)
 +   A            Q3I           11  2     94
 +   A                                      EDTCDE(J)
 +   A            Q4I           11  2    112
 +   A                                      EDTCDE(J)
 +   A*----------------------------------------------------------------
 +   A          R PD3R
 +   A                                      SPACEB(001)
 +   A                                    58
 +   A                                      '退貨合計:'
 +   A            QT2R           7  0     67
 +   A                                      EDTCDE(J)
 +   A            Q3R           11  2     94
 +   A                                      EDTCDE(J)
 +   A            Q4R           11  2    112
 +---A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    58
     A                                      '合計:'
     A            QT2            7  0     67
     A                                      EDTCDE(J)
     A            Q3            11  2     94
     A                                      EDTCDE(J)
     A            Q4            11  2    112
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
0214AA          R PD4I
 +   A                                      SPACEB(001)
 +   A                                    58
 +   A                                      '進貨總計:'
 +   A            QT3I           7  0     67
 +   A                                      EDTCDE(J)
 +   A            Q5I           11  2     94
 +   A                                      EDTCDE(J)
 +   A            Q6I           11  2    112
 +   A                                      EDTCDE(J)
 +   A*----------------------------------------------------------------
 +   A          R PD4R
 +   A                                      SPACEB(001)
 +   A                                    58
 +   A                                      '退貨總計:'
 +   A            QT3R           7  0     67
 +   A                                      EDTCDE(J)
 +   A            Q5R           11  2     94
 +   A                                      EDTCDE(J)
 +   A            Q6R           11  2    112
 +---A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD4
     A                                      SPACEB(001)
     A                                    58
     A                                      '總計:'
     A            QT3            7  0     67
     A                                      EDTCDE(J)
     A            Q5            11  2     94
     A                                      EDTCDE(J)
     A            Q6            11  2    112
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================'
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================'
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<PUR010P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE4
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======================'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
