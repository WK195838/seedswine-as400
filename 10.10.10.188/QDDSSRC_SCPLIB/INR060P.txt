     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : INR060P                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/10/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR060                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :陳列品轉進貨表                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCWK09)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<INR060P>'
     A                                    38MSGCON(030 USC9999 SCMF)
     A                                   103'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    44'陳列品轉進貨表'
     A                                   103'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A                                   103'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司代號:'
     A            DTH06     R        O    +1REFFLD(W901)
     A            D#B03     R        O    +1REFFLD(#B03 PA#BPF)
     A                                    +3'通路代號:'
     A            DTH07S         2A  O    +1
     A                                    +1'-'
     A            DTH07E         2A  O    +1
     A                                    +1'分店代號:'
     A            DTH08S         5   O    +1
     A                                    +1'-'
     A            DTH08E         5   O    +1
     A                                      SPACEA(001)
     A                                     1'產品代號:'
     A            DTH02S         9   O    +1
     A                                    +1'-'
     A            DTH02E         9   O    +1
     A                                    +1'轉進貨日:'
     A            DTH09S         6S 0O    +1EDTCDE(Y)
     A                                    +1'-'
     A            DTH09E         6S 0O    +1EDTCDE(Y)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     1'產品代號／名稱'
     A                                      SPACEB(001)
     A                                   +13'分店代號／名稱'
     A                                    +8'單價'
     A                                    +0'轉進貨量'
     A                                    +7'未稅金額'
     A                                    +1'驗收單號'
     A                                    +5'陳列轉進貨單號'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        W902      R              2
     A  30        W903      R             +1
     A  31        W906      R             +1
     A  31        W907      R             +1
     A            W909      R             +2EDTCDE(J)
     A            W910      R             +2EDTCDE(J)
     A            W911      R             +2EDTCDE(J)
     A            W912      R             +2
     A            W913      R             +2
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    48'分店合計:'
     A            Q1             6S 0     +3EDTCDE(J)
     A            Q2            11S 2     +2EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    48'產品合計:'
     A            Q3             6S 0     +3EDTCDE(J)
     A            Q4            11S 2     +2EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD4
     A                                      SPACEB(001)
     A                                    48'總  計:'
     A            Q5             6S 0     +3EDTCDE(J)
     A            Q6            11S 2     +2EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<INR060P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
