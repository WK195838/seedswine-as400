     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PIA101D                                    *
     A*    AUTHOR       : SHARON                                     *
     A*    CREATE DATE  : 105/05/09                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PIA101                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :每月盤點結案                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PIZAPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  3VALUES(' ' '5' '6')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            ZA01      R        H      REFFLD(ZA01)
     A            ZA02      R        H      REFFLD(ZA02)
     A            SZA03     R   -2   O 10  6REFFLD(ZA03)
     A                                      EDTWRD('  /  /  ')
     A            ZA04      R        O 10 18
     A            SNE05     R        O 10 24REFFLD(NE05 MANEPF)
     A            ZA05      R        O 10 38
     A            SZA08     R   -2   O 10 58REFFLD(ZA08)
     A                                      EDTWRD('  /  /  ')
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'PIA101'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'每月盤點結案'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B    +1REFFLD(ZA01)
     A            S#B03     R   -6   O    +1REFFLD(#B03 PA#BPF)
     A                                    +4'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B    +1REFFLD(ZA02)
     A            SND04     R        O    +1REFFLD(ND04 MANDPF)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'5=查詢'
     A                                      COLOR(BLU)
     A                                  5 26'6=結案'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN3     R   -2   B  9  6REFFLD(ZA03)
     A                                      EDTWRD('  /  /  ')
     A            DBGN4     R        B  9 18REFFLD(ZA04)
     A            DBGN5     R        B  9 38REFFLD(ZA05)
     A                                  7  4'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  7 56'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  8  3'A 盤點日期  分店代號／名稱    +
     A                                      盤點單號          列印日期'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  95        ERR0          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(MSG 0002 SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DZB05     R        O 10  3REFFLD(ZB05 PIZBPF)
     A            DNC05     R        O 10 15REFFLD(NC05 MANCPF)
     A            DZB06     R        O 10 36REFFLD(ZB06 PIZBPF)
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CA03(03)
     A  61                                  CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'PIA101'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 34'每日盤點結案'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DZA01     R        O  4 13REFFLD(ZA01)
     A            D#B03     R   -4   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 32'通路代號:'
     A                                      COLOR(BLU)
     A            DZA02     R        O  4 44REFFLD(ZA02)
     A            DND04     R        O  4 47REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'分店代號:'
     A                                      COLOR(BLU)
     A            DZA04     R        O  5 13REFFLD(ZA04)
     A            DNE05     R        O  5 19REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  5 32'盤點單號:'
     A                                      COLOR(BLU)
     A            DZA05     R        O  5 44REFFLD(ZA05)
     A                                  6  1'盤點日期:'
     A                                      COLOR(BLU)
     A            DZA03     R   -2   O  6 13REFFLD(ZA03)
     A                                      EDTWRD('  /  /  ')
     A                                  6 32'列印日期:'
     A                                      COLOR(BLU)
     A            DZA08     R   -2   O  6 44REFFLD(ZA08)
     A                                      EDTWRD('  /  /  ')
     A                                  9  2'產品代號  產品名稱           +
     A                                      盤點數量 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  61                             22 17'F9 =確認結案'
     A                                      COLOR(BLU)
     A                                 22 32'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
