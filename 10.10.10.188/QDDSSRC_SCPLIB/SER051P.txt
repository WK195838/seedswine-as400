     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    DSPF NAME    : SER051P                                    *
     A*    AUTHOR       : DEREK                                      *
     A*    CREATE DATE  : 2013/06/25                                 *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SER050                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :銷售系統                                  *
     A*    REMARK       :通路銷售明細表-分店                     *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<SER051P>'
     A                                    32MSGCON(030 USC9999 SCMF)
     A                                    70'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A*
     A                                      SPACEA(001)
     A                                    38'通路銷售明細表-分店'
     A                                    70'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A*
     A                                     1'公司代號:'
     A            DHA01     R        O    +1REFFLD(HA01)
     A            D#B03     R        O    +1REFFLD(#B03 PA#BPF)
     A                                    70'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A                                     1'通路代號:'
     A            DHA02     R        O    +1REFFLD(HA02)
     A            DND04     R        O    +1REFFLD(ND04 MANDPF)
     A                                    71'USER :'
     A            $USER         10A       +1
     A*
TERRYA                                    29'產品歸屬:'
TERRYA            DNC09     R        O    41REFFLD(NC09)
     A                                     1'分店代號:'
     A                                      SPACEB(001)
     A            DHA04S    R        O    +1REFFLD(HA04)
     A                                    +1'-'
     A            DHA04E    R        O    +1REFFLD(HA04)
     A                                    +3'產品代號:'
     A            DHA05S    R        O    +1REFFLD(HA05)
     A                                    +1'-'
     A            DHA05E    R        O    +1REFFLD(HA05)
     A                                    +5'品牌:'
     A            DNC12S    R        O    +1REFFLD(NC12)
     A                                    +1'-'
     A            DNC12E    R        O    +1REFFLD(NC12)
     A*
     A                                     1'銷售日期:'
     A                                      SPACEB(001)
     A            DHA03S    R   -2   O    +1REFFLD(HA03)
     A                                      EDTCDE(Y)
     A                                    +1'-'
     A            DHA03E    R   -2   O    +1REFFLD(HA03)
     A                                      EDTCDE(Y)
     A                                    +1'識別:'
     A            DNC10S    R        O    +1REFFLD(NC10)
     A                                    +1'-'
     A            DNC10E    R        O    +1REFFLD(NC10)
     A                                    +2'種類:'
     A            DNC11S    R        O    +1REFFLD(NC11)
     A                                    +1'-'
     A            DNC11E    R        O    +1REFFLD(NC11)
     A                                    +2'原產地:'
     A            DNC18S    R        O    +1REFFLD(NC18)
     A                                    +1'-'
     A            DNC18E    R        O    +1REFFLD(NC18)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================='
     A                                      SPACEB(001)
     A*
     A*                                    1'識別'
     A                                      SPACEA(001)
     A                                     1'分店代號'
     A                                    12'分店簡稱'
     A                                    23'產品代號'
     A                                    34'產品名稱'
     A                                    57'數量'
     A                                    66'含稅銷售金額'
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      --------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A            W705           5A        4
     A            W706          10O       12
     A            W701           9A       24
     A            W702          18O       35
     A            W703           6S 0     55EDTCDE(J)
     A            W704          11S 2     65EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT1
     A                                      SPACEB(001)
     A                                    47'小計:'
     A            W703T1         6  0     55EDTCDE(J)
     A            W704T1        11  2     65EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT2
     A                                      SPACEB(001)
     A                                    47'總計:'
     A            W703T2         6  0     55EDTCDE(J)
     A            W704T2        11  2     65EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      --------------------'
     A                                      SPACEB(001)
     A                                     2'<<SER050P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                      SPACEB(001)
     A                                     2'<<SER050P>>--->此範圍無資料'
