     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PIR001D                                    *
     A*    PROGRAM NAME :盤盈虧報表                                *
     A*    AUTHOR       : A1492 TERRY                                *
     A*    CREATE DATE  : 2010/01/27                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'PIR001'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $DATE          6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'盤盈虧報表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 25'公司代號  :'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DPI01     R        B  6 39REFFLD(PI01 INPIPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            D#B03         16O  O  6 42COLOR(BLU)
     A                                  8 25'盤點年月  :'
     A                                      COLOR(BLU)
     A            DPI02          4Y 0B  8 39EDTWRD('  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 45'(月／年)'
     A                                      COLOR(BLU)
     A*
     A                                 17 25'執行環境　:'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 17 39VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 45'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 18 25'列印份數　:'
     A                                      COLOR(BLU)
     A            $CPY           3Y 0B 18 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 25'列表機　　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A  28                             21 27'*** 批次處理中，請稍候 ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22  9'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
