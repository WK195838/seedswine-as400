     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250
     A*
     A*    PROGRAM ID   : VOU0012
     A*    PROGRAM NAME : 傳票銷貨成本彙總表
     A*    AUTHOR       : CAROL HUANG
     A*    CREATE DATE  : 2010.02.01
     A*    UPDATE DATE  :
     A*    SYSTEM       : 進銷存收TO總帳
     A*===============================================================
     A* CODE |  AUTHOR  |  DATE  | DESCTIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1                       SKIPB(2)
     A                                    35MSGCON(030 USC9999 SCMF)
     A                                      SPACEA(1)
     A                                    39'傳票銷售成本彙總表'
     A                                      SPACEA(2)
     A                                     1'公司別　:'
     A            COM            1A       13
     A            #B03      R             +1REFFLD(#B03  *LIBL/PA#BPF)
     A                                    87'日期:'
     A            $EGMDY         6  0     95EDTCDE(Y)
     A                                   104'頁次:'
     A                                   112PAGNBR
     A                                      SPACEA(1)
     A                                     1'通路別　:'
     A            J1202#    R     A       13REFFLD(HA02)
     A            ND04      R             +1REFFLD(ND04)
     A                                    87'時間:'
     A                                    95TIME
     A                                      EDTWRD('  :  :  ')
     A                                   104'<VOU0012P>'
     A                                      SPACEA(1)
     A                                     1'帳務月份:'
     A            D05            4  0     13EDTWRD('  /  ')
     A                                    87' USER :'
     A            $USER         10A       95
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==========+
     A                                       ========================='
     A                                      SPACEA(1)
     A                                     3'確認日期'
     A                                    +1'分店代號'
     A                                    +1'單別'
     A                                    +1'產品代號/名稱'
     A                                    61'銷售數量'
     A                                    76'銷售成本'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==========+
     A                                       ========================='
     A                                      SPACEA(1)
     A*
     A*----------------------------------------------------------------
     A          R PT1                       SPACEA(1)
     A  20                                 1' '
     A                                      SPACEA(1)
     A            JR1201    R              3REFFLD(JR1201 *LIBL/WFJR12PF)
     A                                      EDTWRD('    /  /  ')
     A            J1203$    R             16REFFLD(HA04)
     A            JR1203    R             27REFFLD(JR1203 *LIBL/WFJR12PF)
     A            JR1204    R             32REFFLD(JR1204 *LIBL/WFJR12PF)
     A            NC05      R            + 1REFFLD(NC05)
     A            T105           6  0     63
     A                                      EDTCDE(J)
     A            T106      R             71REFFLD(JR1206 *LIBL/WFJR12PF)
     A                                      EDTCDE(J)
     A*
     A*----------------------------------------------------------------
     A          R PT2                       SPACEA(1)
     A                                    40'分店小計----------->'
     A            T205           6  0     63
     A                                      EDTCDE(J)
     A            T206      R             71REFFLD(JR1206 *LIBL/WFJR12PF)
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT3                       SPACEA(1)
     A                                    40'單別小計----------->'
     A            T305           6  0     63
     A                                      EDTCDE(J)
     A            T306      R             71REFFLD(JR1206 *LIBL/WFJR12PF)
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT4                       SPACEA(1)
     A                                    40'日期小計----------->'
     A            T405           6  0     63
     A                                      EDTCDE(J)
     A            T406      R             71REFFLD(JR1206 *LIBL/WFJR12PF)
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==========+
     A                                       ========================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==========+
     A                                       ========================='
     A                                      SPACEA(1)
     A                                     2'<<VOU0012P>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE9                       SPACEA(1)
     A                                    16'無符合列印範圍之資料'
