     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SER020P                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/11/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR020                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :銷售系統                                  *
     A*    REMARK       :通路銷售金額表                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<SER020>'
     A                                    30MSGCON(030 USC9999 SCMF)
     A                                    62'日期:'
     A            $EGMDY         6  0O    70EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    36'通路銷售金額表'
     A                                    62'時間:'
     A                                    70TIME
     A                                      SPACEA(001)
     A                                    62'頁次:'
     A                                    70PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司代號:'
     A            DHA01     R        O    +1REFFLD(HA01)
     A            D#B03     R        O    +1REFFLD(#B03 PA#BPF)
     A                                    62'USER :'
     A            $USER         10A       +1
     A                                      SPACEA(001)
     A                                     1'通路代號:'
     A            DHA02     R        O    +1REFFLD(HA02)
     A            DND04     R        O    +1REFFLD(ND04 MANDPF)
     A                                     1'銷售日期:'
     A                                      SPACEB(001)
     A            DHA03S    R   -2   O    +1REFFLD(HA03)
     A                                      EDTCDE(Y)
     A                                    +1'~'
     A            DHA03E    R   -2   O    +1REFFLD(HA03)
     A                                      EDTCDE(Y)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     1'銷售日期'
     A                                      SPACEB(001)
     A                                    12'分店代號／名稱'
     A                                    31'未稅金額'
     A                                    45'含稅金額'
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        SHA03     R   -2   O     2REFFLD(HA03)
     A                                      EDTCDE(Y)
     A            HA04      R             13
     A            NE05      R             +1
     A            PHA07          9  0     +1EDTCDE(J)
     A            PHA08          9  0     +2EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    12'銷售日期合計:'
     A            Q1             9  0     +1EDTCDE(J)
     A            Q2             9  0     +2EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    14'        總計:'
     A            Q3             9  0     +1EDTCDE(J)
     A            Q4             9  0     +2EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEB(001)
     A                                     2'<<SER020>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                      SPACEB(001)
     A                                     2'<<SER020>>--->此範圍無資料'
