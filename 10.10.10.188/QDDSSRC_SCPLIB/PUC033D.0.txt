     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PUC033D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/10/17                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUC033                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :補增驗收單維護                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/POCLPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  3VALUES(' ' '2' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SCL03     R   -2   O 10  6REFFLD(CL03)
     A                                      EDTCDE(Y)
     A            CL04      R        O 10 16
     A            SNE05     R        O 10 22REFFLD(NE05 MANEPF)
     A            CL05      R        O 10 33
     A            CL01      R        H
     A            CL02      R        H
     A            CL03      R        H
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'PUC033'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'補增驗收單維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 13REFFLD(CL01)
     A            S#B03     R   -6   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(CL02)
     A            SND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'2=修改'
     A                                      COLOR(BLU)
     A                                  5 27'5=查詢'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN3     R   -2   B  9  6REFFLD(CL03)
     A                                      EDTCDE(Y)
     A            DBGN4     R        B  9 16REFFLD(CL04)
     A            DBGN5     R        B  9 33REFFLD(CL05)
     A                                  7  3'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  8  3'A 到分店日分店代號／名稱 驗+
     A                                      收單號    '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F6 =新增一般單'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  85                             24  2'驗收單號'
     A                                      DSPATR(HI)
     A  85        DCL05     R             +1REFFLD(CL05)
     A                                      DSPATR(RI)
     A  85                                +1'新增成功'
     A                                      DSPATR(HI)
      *此燈號控制新增成功以及修改成功專用
     A  95        ERR0          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(MSG 0005 SCMF)
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC033'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'補增驗收單維護'
     A                                      DSPATR(HI)
     A                                  3 32'<<一般驗收單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DCL01     R        O  4 14REFFLD(CL01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5  2'驗收單號:'
     A                                      COLOR(BLU)
     A            DCL05     R        O  5 14REFFLD(CL05)
     A                                  6  2'通路代號:'
     A                                      COLOR(BLU)
     A            DCL02     R        O  6 14REFFLD(CL02)
     A            DND04     R        O  6 17REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  7  2'分店代號:'
     A                                      COLOR(BLU)
     A            DCL04     R        B  7 14REFFLD(CL04)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DNE05     R        O  7 20REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                 10  2'驗收單日期:'
     A                                      COLOR(BLU)
     A            DCLVV     R   -2   O 10 16REFFLD(CLVV)
     A                                      EDTCDE(Y)
     A                                 10 25'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 11  2'到運貨日期:'
     A                                      COLOR(BLU)
     A            DCL07     R   -2   O 11 16REFFLD(CL07)
     A                                      EDTCDE(Y)
     A                                 11 25'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 12  2'到分店日期:'
     A                                      COLOR(BLU)
     A            DCL03     R   -2   B 12 16REFFLD(CL03)
     A                                      EDTCDE(Y)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 25'(月／日／年)'
     A                                      COLOR(BLU)
     A*                                20  2'驗收:'
     A*                                     COLOR(BLU)
     A*           DCD13         10O  O 20 10
     A*
     A*
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                 22 42'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 10  3VALUES(' ' '1' '2' '4')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A            DCM03     R        B 10  5REFFLD(CM03 POCMPF)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DNC05     R        O 10 15REFFLD(NC05 MANCPF)
     A            DCC09     R        O 10 34REFFLD(CC09 POCCPF)
     A                                      EDTCDE(J)
     A            DCM04     R        O 10 46REFFLD(CM04 POCMPF)
     A                                      EDTCDE(J)
     A            DCM05     R        O 10 56REFFLD(CM05 POCMPF)
     A                                      EDTCDE(J)
     A            DCM06     R        B 10 66REFFLD(CM06 POCMPF)
     A                                      EDTCDE(J)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DCHK           1A  H
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC033'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'補增驗收單維護'
     A                                      DSPATR(HI)
     A                                  3 32'<<一般訂貨單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DCL01     R        O  4 13REFFLD(CL01)
     A            D#B03     R   -4   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 32'通路代號:'
     A                                      COLOR(BLU)
     A            DCL02     R        O  4 44REFFLD(CL02)
     A            DND04     R        O  4 47REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'分店代號:'
     A                                      COLOR(BLU)
     A            DCL04     R        O  5 13REFFLD(CL04)
     A            DNE05     R        O  5 19REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  5 32'驗收單號:'
     A                                      COLOR(BLU)
     A            DCL05     R        O  5 44REFFLD(CL05)
     A                                  5 58'到貨運日期:'
     A                                      COLOR(BLU)
     A            DCL07     R   -2   O  5 72REFFLD(CL07)
     A                                      EDTCDE(Y)
     A*                                 6  1'訂貨日期:'
     A*                                     COLOR(BLU)
     A*           DCD03     R   -2   O  6 13REFFLD(CD03)
     A*                                     EDTCDE(Y)
     A                                  6  1'到分店日期:'
     A                                      COLOR(BLU)
     A            DCL03     R   -2   O  6 15REFFLD(CL03)
     A                                      EDTCDE(Y)
     A                                  6 24'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  7  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  7 17'1=新增'
     A                                      COLOR(BLU)
     A                                  7 26'2=修改'
     A                                      COLOR(BLU)
     A                                  7 35'4=刪除'
     A                                      COLOR(BLU)
     A                                  9  2' A產品代號產品名稱        +
     A                                      未稅單價  採購量  驗收量  +
     A                                      確認量 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 17'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  80                             22 32'F9 =確認存檔'
     A                                      COLOR(BLU)
     A                                 22 47'F12=回前畫面'
     A                                      COLOR(BLU)
      *
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
