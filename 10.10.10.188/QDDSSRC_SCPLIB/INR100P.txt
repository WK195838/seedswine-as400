     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : INR100P                                    *
     A*    AUTHOR       : DEREK                                      *
     A*    CREATE DATE  : 2012/12/10                                 *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR100P                                    *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :產品異動報表                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCWK26)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<INR100P>'
     A                                    38MSGCON(030 USC9999 SCMF)
     A                                    88'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    45'產品異動報表'
     A                                    88'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A                                    88'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司代號:'
     A            DTO01          2A  O    +1
     A            D#B03     R        O    +1REFFLD(#B03 PA#BPF)
     A                                    +3'通路代號:'
     A            DTO02S         2A  O    +1
     A                                    +1'-'
     A            DTO02E         2A  O    +1
     A                                    +1'分店代號:'
     A            DTO03S         5   O    +1
     A                                    +1'-'
     A            DTO03E         5   O    +1
     A                                      SPACEA(001)
     A                                     1'產品代號:'
     A            DTO04S         9   O    +1
     A                                    +1'-'
     A            DTO04E         9   O    +1
     A                                    +1'進出日期:'
     A            DTO05S         6S 0O    +1EDTCDE(Y)
     A                                    +1'-'
     A            DTO05E         6S 0O    +1EDTCDE(Y)
     A                                      SPACEA(001)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =================================='
     A                                      SPACEA(001)
     A                                     1'分店代號／名稱'
     A                                    20'產品代號／名稱'
     A                                    50'進出日期'
     A                                    62'單別／名稱'
     A                                    77'單據編號'
     A                                    98'數量'
     A                                      SPACEA(001)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =================================='
     A*----------------------------------------------------------------
     A          R PD0
     A                                      SPACEB(001)
     A                                     1'期初數量'
     A            PQTY1     R             92REFFLD(W2611)
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        W2603     R              2
     A  30        W2604     R              8
     A  31        W2605     R             20
     A  31        W2606     R             30
     A            P2607          6S 0     51EDTCDE(Y)
     A            W2608     R             62
     A            W2609     R             65
     A            W2610     R             77
     A            W2611     R             92EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                     1'期末數量'
     A            PQTY2     R             92REFFLD(W2611)
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =================================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =================================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<INR070P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1'==================================-
     A                                      ===================================-
     A                                      =================================='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
