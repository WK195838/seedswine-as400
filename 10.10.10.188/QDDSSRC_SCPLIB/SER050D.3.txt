     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : SER050D                                    *
     A*    PROGRAM NAME :通路銷售明細表                            *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  : 2008/11/10                                 *
     A*    UPDATE DATE  : 2012/01/04 A1492 TERRY 增加產品歸屬      *
     A*    SYSTEM       :西祺進銷存                                *
     A*--------------------------------------------------------------*
     A*MODIFICATION                                                  *
     A*00A 2013/06/25 DEREK增加選項:報表格式                     *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF07(07)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'SER050'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 63'日期:'
     A            $DATE          6Y 0O  1 71EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'通路銷售明細表'
     A                                      DSPATR(HI)
     A                                  2 63'時間:'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 63' USER :'
     A            $USER         10A  O  3 71
     A                                  5 25'公司代號  :'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DHA01     R        B  5 39REFFLD(HA01 SEHAPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            D#B03         16O  O  5 42COLOR(BLU)
     A                                  6 25'通路代號  :'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A            DHA02     R        B  6 39REFFLD(HA02 SEHAPF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DND04         16O  O  6 42COLOR(BLU)
     A                                  8 25'分店代號  :'
     A                                      DSPATR(HI)
     A            DHA04S         5A  B  8 39
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                  8 45'-'
     A                                      COLOR(BLU)
     A            DHA04E         5A  B  8 47
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                  8 53'(空白：全部)'
     A                                      COLOR(BLU)
     A                                 10 25'銷售日期  :'
     A                                      COLOR(BLU)
     A            DHA03S         6Y 0B 10 39EDTWRD('  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 10 49'-'
     A                                      COLOR(BLU)
     A            DHA03E         6Y 0B 10 51EDTWRD('  /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 10 60'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 13 25'品牌    :'
     A                                      DSPATR(HI)
     A            DNC12S    R        B 13 39REFFLD(NC12 MANCPF)
     A                                 13 42'-'
     A                                      COLOR(BLU)
     A            DNC12E    R        B 13 44REFFLD(NC12 MANCPF)
     A                                 13 47'(空白：全部)'
     A                                      COLOR(BLU)
     A                                 11 25'產品代號  :'
     A                                      DSPATR(HI)
     A            DHA05S         9A  B 11 39
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                 11 49'-'
     A                                      COLOR(BLU)
     A            DHA05E         9A  B 11 51
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                 11 61'(空白：全部)'
     A                                      COLOR(BLU)
 00A+A                                 12 25'報表格式  :'
 |   A                                      COLOR(BLU)
 |   A                                      DSPATR(HI)
 |   A            DOPT           1A  B 12 39VALUES('1' '2')
 |   A                                 12 41'(1-彙總2-分店)'
 00A+A                                      COLOR(BLU)
     A                                 14 25'識別    :'
     A                                      DSPATR(HI)
     A            DNC10S         1A  B 14 39
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                 14 42'-'
     A                                      COLOR(BLU)
     A            DNC10E         1A  B 14 44
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                 14 47'(空白：全部)'
     A                                      COLOR(BLU)
     A                                 15 25'種類    :'
     A                                      DSPATR(HI)
     A            DNC11S         2A  B 15 39
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                 15 42'-'
     A                                      COLOR(BLU)
     A            DNC11E         2A  B 15 44
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                 15 47'(空白：全部)'
     A                                      COLOR(BLU)
     A                                 16 25'原產地    :'
     A                                      DSPATR(HI)
     A            DNC18S         2A  B 16 39
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                 16 42'-'
     A                                      COLOR(BLU)
     A            DNC18E         2A  B 16 44
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                 16 47'(空白：全部)'
     A                                      COLOR(BLU)
TERRYA                                 17 25'產品歸屬  :'
|    A                                      DSPATR(HI)
|    A            DNC09          2A  B 17 39
|    A  72                                  DSPATR(RI PC)
|    A                                 17 47'(空白：全部)'
TERRYA                                      COLOR(BLU)
     A*
     A                                 18 25'執行環境　:'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 18 39VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 18 45'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A                                 19 25'列印份數　:'
     A                                      COLOR(BLU)
     A            $CPY           3Y 0B 19 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20 25'列表機　　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 20 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A                                 22 43'F7 =下載'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
