     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SER050P                                    *
     A*    AUTHOR       : A1388  WIND                                *
     A*    CREATE DATE  : 97/11/10                                   *
     A*    UPDATE DATE  : 101/01/04 A1492 TERRY 增加產品歸屬       *
     A*    PROGRAM NAME : SER050                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :銷售系統                                  *
     A*    REMARK       :通路銷售明細表                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<SER050P>'
     A                                    32MSGCON(030 USC9999 SCMF)
     A                                    70'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A*
     A                                      SPACEA(001)
     A                                    38'通路銷售明細表'
     A                                    70'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A*
     A                                     1'公司代號:'
     A            DHA01     R        O    +1REFFLD(HA01)
     A            D#B03     R        O    +1REFFLD(#B03 PA#BPF)
     A                                    70'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A                                     1'通路代號:'
     A            DHA02     R        O    +1REFFLD(HA02)
     A            DND04     R        O    +1REFFLD(ND04 MANDPF)
     A                                    71'USER :'
     A            $USER         10A       +1
     A*
TERRYA                                    29'產品歸屬:'
TERRYA            DNC09     R        O    41REFFLD(NC09)
     A                                     1'分店代號:'
     A                                      SPACEB(001)
     A            DHA04S    R        O    +1REFFLD(HA04)
     A                                    +1'-'
     A            DHA04E    R        O    +1REFFLD(HA04)
     A                                    +3'產品代號:'
     A            DHA05S    R        O    +1REFFLD(HA05)
     A                                    +1'-'
     A            DHA05E    R        O    +1REFFLD(HA05)
     A                                    +5'品牌:'
     A            DNC12S    R        O    +1REFFLD(NC12)
     A                                    +1'-'
     A            DNC12E    R        O    +1REFFLD(NC12)
     A*
     A                                     1'銷售日期:'
     A                                      SPACEB(001)
     A            DHA03S    R   -2   O    +1REFFLD(HA03)
     A                                      EDTCDE(Y)
     A                                    +1'-'
     A            DHA03E    R   -2   O    +1REFFLD(HA03)
     A                                      EDTCDE(Y)
     A                                    +1'識別:'
     A            DNC10S    R        O    +1REFFLD(NC10)
     A                                    +1'-'
     A            DNC10E    R        O    +1REFFLD(NC10)
     A                                    +2'種類:'
     A            DNC11S    R        O    +1REFFLD(NC11)
     A                                    +1'-'
     A            DNC11E    R        O    +1REFFLD(NC11)
     A                                    +2'原產地:'
     A            DNC18S    R        O    +1REFFLD(NC18)
     A                                    +1'-'
     A            DNC18E    R        O    +1REFFLD(NC18)
     A*
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================='
     A                                      SPACEB(001)
     A*
     A*                                    1'識別'
     A                                     1'產品代號'
     A                                      SPACEB(001)
     A                                    12'產品名稱'
     A                                    35'數量'
     A                                    42'含稅銷售金額'
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      --------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A            W701           9A        2
     A            W702          18O       13
     A            W703           6S 0     33EDTCDE(J)
     A            W704          11S 2     41EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    25'總計:'
     A            Q3             6  0     33EDTCDE(J)
     A            Q4            11  2     41EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      --------------------'
     A                                      SPACEB(001)
     A                                     2'<<SER050P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                      SPACEB(001)
     A                                     2'<<SER050P>>--->此範圍無資料'
