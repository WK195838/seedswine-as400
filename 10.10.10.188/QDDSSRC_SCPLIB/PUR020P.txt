     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PUR020                                     *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/10/30                                   *
     A*    UPDATE DATE  :101/02/14  NANCY (0214A)                    *
     A*                供應商:當有退貨,報表顯示:退貨&退貨*
     A*                         若無退貨,報表僅顯示:合計       *
     A*    PROGRAM NAME : PUR020                                     *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :驗收明細表                                *
     A*--------------------------------------------------------------*
     A* 00A 101/03/29 DEREK 增加（產品歸屬）選項                   *
     A* 00B 101/11/26 DEREK 增加（分店代號）選項                   *
     A*===============================================================
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<PUR020>'
     A                                    62
     A                                      MSGCON(030 USC9999 SCMF)
     A                                   152
     A                                      '日期:'
     A            $EGMDY         6  0O   160
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    62
     A                                      '驗收明細表－依供應商／產品／分店'
     A  71                                96'(進貨/退貨)'
     A  72                                96'(進貨)'
     A  73                                96'(退貨)'
     A                                   152
     A                                      '時間:'
     A                                   160
     A                                      TIME
     A                                      SPACEA(001)
     A                                   152
     A                                      '頁次:'
     A                                   160
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
 00A+A                                     1'產品歸屬:'
 00A+A            DNC09     R        O    13REFFLD(NC09 MANCPF)
 00A+A            NC09D     R  -22   O    16REFFLD(L203 UTL2PF)
 00A+A                                      SPACEA(001)
     A                                     1
     A                                      '公司代號:'
     A            DCL01     R        O    +1
     A                                      REFFLD(CL01)
     A            D#B03     R        O    +1
     A                                      REFFLD(#B03 PA#BPF)
     A                                    +1
     A                                      '通路代號:'
     A            DCL02          2A  O    +1
     A            DND04     R        O    +1
     A                                      REFFLD(ND04 MANDPF)
     A                                    +1
     A                                      '供應商代號:'
     A            DCM02S         5   O    +1
     A                                    +1
     A                                      '-'
     A            DCM02E         5   O    +1
 00B+A                                      SPACEA(1)
 00B-A*                                   +1
 00B+A                                     1
     A                                      '到貨運日:'
     A            DCL07S         6  0O    +1
     A                                      EDTCDE(Y)
     A                                    +1
     A                                      '-'
     A            DCL07E         6  0O    +1
     A                                      EDTCDE(Y)
     A                                    +1
     A                                      '產品代號:'
     A            DCM03S         9   O    +1
     A                                    +1
     A                                      '-'
     A            DCM03E         9   O    +1
 00B+A                                    +1
 |   A                                      '分店代號:'
 |   A            DCL04S         5   O    +1
 |   A                                    +1
 |   A                                      '-'
 00B+A            DCL04E         5   O    +1
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============================='
     A*                                     SPACEB(001)
     A                                    85
     A                                      '驗收數量'
     A                                      SPACEB(001)
     A                                    98
     A                                      '未稅進價'
     A                                    67
     A                                      '分店代號／簡稱'
     A                                     1
     A                                      '供應商代號／簡稱'
     A                                    22
     A                                      '到貨運日期'
     A                                    37
     A                                      '產品代號'
     A                                    48
     A                                      '產品簡稱'
     A                                   114
     A                                      '未稅金額'
     A                                   130
     A                                      '含稅金額'
     A                                   145
     A                                      '驗收單號'
     A                                   159
     A                                      '採購單號'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        TX07      R              2
     A                                      REFFLD(TX07 INTXPF)
     A  30        NB03      R              8
     A                                      REFFLD(NB03 MANBPF)
     A  31        LCL07          6  0     24
     A                                      EDTWRD('  /  /  ')
     A  32        TX11      R             38
     A                                      REFFLD(TX11 INTXPF)
     A  32        NC05      R             48
     A            TX12      R             68
     A                                      REFFLD(TX12 INTXPF)
     A            NE05      R             74
     A            PTX15     R             85
     A                                      REFFLD(TX15 INTXPF)
     A                                      EDTCDE(J)
     A            TX18      R             96
     A                                      REFFLD(TX18 INTXPF)
     A                                      EDTCDE(J)
     A            PTX19     R            110
     A                                      REFFLD(TX19 INTXPF)
     A                                      EDTCDE(J)
     A            PTX19X    R            126
     A                                      REFFLD(TX19 INTXPF)
     A                                      EDTCDE(J)
     A            TX05          13       142
     A            TX08          13       156
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    60
     A                                      '交貨日  合計:'
     A            Q0T1           7  0     85
     A                                      EDTCDE(J)
     A            Q1            11  2    110
     A                                      EDTCDE(J)
     A            Q2            11  2    126
     A                                      EDTCDE(J)
0214AA*----------------------------------------------------------------
 +   A          R PD3I
 +   A                                      SPACEB(001)
 +   A                                    60
 +   A                                      '供應商進貨合計:'
 +   A            Q0T2I          7  0     85
 +   A                                      EDTCDE(J)
 +   A            Q3I           11  2    110
 +   A                                      EDTCDE(J)
 +   A            Q4I           11  2    126
 +   A                                      EDTCDE(J)
 +   A*----------------------------------------------------------------
 +   A          R PD3R
 +   A                                      SPACEB(001)
 +   A                                    60
 +   A                                      '供應商退貨合計:'
 +   A            Q0T2R          7  0     85
 +   A                                      EDTCDE(J)
 +   A            Q3R           11  2    110
 +   A                                      EDTCDE(J)
 +   A            Q4R           11  2    126
 +---A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    60
     A                                      '供應商  合計:'
     A            Q0T2           7  0     85
     A                                      EDTCDE(J)
     A            Q3            11  2    110
     A                                      EDTCDE(J)
     A            Q4            11  2    126
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
0214AA          R PD4I
 +   A                                      SPACEB(001)
 +   A                                    60
 +   A                                      '      進貨總計:'
 +   A            Q0T3I          7  0     85
 +   A                                      EDTCDE(J)
 +   A            Q5I           11  2    110
 +   A                                      EDTCDE(J)
 +   A            Q6I           11  2    126
 +   A                                      EDTCDE(J)
 +   A*----------------------------------------------------------------
 +   A          R PD4R
 +   A                                      SPACEB(001)
 +   A                                    60
 +   A                                      '      退貨總計:'
 +   A            Q0T3R          7  0     85
 +   A                                      EDTCDE(J)
 +   A            Q5R           11  2    110
 +   A                                      EDTCDE(J)
 +   A            Q6R           11  2    126
 +   A                                      EDTCDE(J)
 +---A*----------------------------------------------------------------
     A          R PD4
     A                                      SPACEB(001)
     A                                    60
     A                                      '        總計:'
     A            Q0T3           7  0     85
     A                                      EDTCDE(J)
     A            Q5            11  2    110
     A                                      EDTCDE(J)
     A            Q6            11  2    126
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD5
     A                                      SPACEB(001)
     A                                    60
     A                                      '產品    合計:'
     A            Q0T4           7  0     85
     A                                      EDTCDE(J)
     A            Q7            11  2    110
     A                                      EDTCDE(J)
     A            Q8            11  2    126
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<PUR020P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =============================='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
0214AA          R PE4
 +   A                                     1
 +   A                                      '-----------------------------------
 +   A                                      ------------------------------------
 +   A                                      ------------------------------------
 +   A                                      -----------------------'
 +   A                                      SPACEB(001)
 +---A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
