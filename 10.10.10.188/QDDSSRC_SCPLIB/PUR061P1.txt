     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : PUR061P1                                   *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 101/01/09                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUR061                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :採購單彙總表                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<PUR061>'
     A                                    36
     A                                      MSGCON(030 USC9999 SCMF)
     A                                    80
     A                                      '日期:'
     A            $EGMDY         6  0O    88
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    39
     A                                      '採購單彙總表-總表'
     A                                    80
     A                                      '時間:'
     A                                    88
     A                                      TIME
     A                                      SPACEA(001)
     A                                    80
     A                                      '頁次:'
     A                                    88
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '公司別  :'
     A            DCH01          2   O    13
     A            D#B03     R        O    16
     A                                      REFFLD(#B03 PA#BPF)
     A                                      SPACEA(001)
     A                                     1
     A                                      '通路代號:'
     A            DCH02S         2   O    13
     A                                    16
     A                                      '-'
     A            DCH02E         2   O    18
     A                                    35
     A                                      '分店代號:'
     A            DCH07S         5   O    47
     A                                    53
     A                                      '-'
     A            DCH07E         5   O    55
     A                                    67
     A                                      '供應商代號:'
     A            DCH06S         5   O    81
     A                                    87
     A                                      '-'
     A            DCH06E         5   O    89
     A                                      SPACEA(001)
     A                                     1
     A                                      '產品代號:'
     A            DCI03S         9   O    13
     A                                    23
     A                                      '-'
     A            DCI03E         9   O    25
     A                                    35
     A                                      '訂貨日期:'
     A            DCD03S         6  0O    47
     A                                      EDTCDE(Y)
     A                                    56
     A                                      '-'
     A            DCD03E         6  0O    58
     A                                      EDTCDE(Y)
     A                                    67
     A                                      '到貨運日:'
     A            DCH08S         6  0O    79
     A                                      EDTCDE(Y)
     A                                    88
     A                                      '-'
     A            DCH08E         6  0O    90
     A                                      EDTCDE(Y)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==============================='
     A                                      SPACEB(001)
     A                                     1
     A                                      '供應商代號／簡稱'
     A                                      SPACEB(001)
     A                                    21
     A                                      '產品代號'
     A                                    33
     A                                      '產品簡稱'
     A                                    48
     A                                      '採購數量'
     A                                    61
     A                                      '未稅進價'
     A                                    75
     A                                      '未稅金額'
     A                                    89
     A                                      '含稅金額'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -------------------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  30        W25102         5         2
     A  30        W25103        12O        8
     A            W25104         9        21
     A            W25105        18O       31
     A            W25106         6  0     51
     A                                      EDTCDE(L)
     A            W25107         8  2     60
     A                                      EDTCDE(J)
     A            W25108         9  0     73
     A                                      EDTCDE(J)
     A            W25109         9  0     87
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    38
     A                                      '廠商合計:'
     A            D25106         6  0     51
     A                                      EDTCDE(L)
     A            D25108         9  0     73
     A                                      EDTCDE(J)
     A            D25109         9  0     87
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    42
     A                                      '總計:'
     A            T25106         6  0     51
     A                                      EDTCDE(L)
     A            T25108         9  0     73
     A                                      EDTCDE(J)
     A            T25109         9  0     87
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==============================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==============================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<PUR061P1>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==============================='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
