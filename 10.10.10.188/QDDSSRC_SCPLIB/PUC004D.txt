     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PUC004D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/10/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUC004                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :報價資料維護子系統                        *
     A*    REMARK       :供應商產品報價查詢                        *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/POCCPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            CC04      R        O 10  2
     A            SNC05     R        O 10 12REFFLD(NC05 MANCPF)
     A            CC05      R        O 10 31
     A            CC06      R        O 10 36
     A            SCC06          6O  O 10 38
     A            SCC07     R   -2   O 10 45REFFLD(CC07)
     A                                      EDTCDE(Y)
     A            SCC08     R   -2   O 10 55REFFLD(CC08)
     A                                      EDTCDE(Y)
     A            SNC14     R        O 10 65REFFLD(NC14 MANCPF)
     A            CC09      R        O 10 70EDTCDE(J)
     A            CC01      R        H
     A            CC02      R        H
     A            CC03      R        H
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'PUC004'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'供應商產品報價查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號  :'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 16REFFLD(CC01)
     A            S#B03     R   -6   O  4 19REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(CC02)
     A            SND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  2'供應商代號:'
     A                                      COLOR(BLU)
     A            DBGN3     R        B  5 16REFFLD(CC03)
     A                                      DSPATR(HI)
     A            SNB03     R        O  5 22REFFLD(NB03 MANBPF)
     A                                      COLOR(BLU)
     A                                  6  2'年  度  :'
     A                                      COLOR(BLU)
     A            DBGN4     R        B  6 16REFFLD(CC05)
     A                                  6 31'類  別:'
     A                                      COLOR(BLU)
     A            DBGN5     R        B  6 43REFFLD(CC06)
     A                                      VALUES('1' '2' ' ')
     A                                  6 46'(空白-全部／１年度／２活動)'
     A                                      COLOR(BLU)
     A                                  7 47'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  7 65'單'
     A                                      COLOR(BLU)
     A                                  8  2'產品代號產品名稱        年度-
     A                                       類別 生效起日生效迄日 位  -
     A                                      未稅單價'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *
     A            DBGN6     R        B  9  2REFFLD(CC04)
     A                                      DSPATR(HI)
     A            DBGN7     R   -2   B  9 45REFFLD(CC07)
     A                                      EDTCDE(Y)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
