     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : INT005D                                    *
     A*    AUTHOR       : A1546  VINCENT                             *
     A*    CREATE DATE  : 97/10/01                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INT005                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                            *
     A*    REMARK       :產品成本調整處理(A/U/D/I)                 *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
      *----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B 10  4VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DTM02          6Y 0O 10  7EDTWRD('  /  /  ')
     A            TM03      R        O 10 17
     A            NC05      R        O 10 28
     A            TM08      R        O 10 48
     A            HTM07     R        H      REFFLD(TM07)
      *----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'INT005'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'產品成本調整處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B    +1REFFLD(TM01)
     A            D#B03     R   -4   O    +1REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  6  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                    +1'2=修改'
     A                                      COLOR(BLU)
     A                                    +1'4=刪除'
     A                                      COLOR(BLU)
     A                                    +1'5=查詢'
     A                                      COLOR(BLU)
     A                                  7  4'（月／日／年）'
     A                                      COLOR(BLU)
     A                                  8  3' A 調整日期產品代號 產品名稱-
     A                                                單據編號     '
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN2          6Y 0B  9  7EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            DBGN3     R        B  9 17REFFLD(TM03)
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F6 =新增'
     A                                      COLOR(BLU)
     A  70                             24  2'產品代號'
     A                                      DSPATR(HI)
     A  70        DTM03     R             +1REFFLD(TM03)
     A                                      DSPATR(RI)
     A  70                                +1'新增成功'
     A                                      DSPATR(HI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'INT005'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'產品成本調整處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A                                      COLOR(BLU)
     A            DTM01     R        O  5 14REFFLD(TM01)
     A            D#B03     R   -4   O  5 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5 50'單據編號:'
     A                                      COLOR(BLU)
     A            DTM08     R        O  5 62REFFLD(TM08)
     A                                  7  2'產品代號:'
     A N31                                  COLOR(BLU)
     A  31                                  DSPATR(HI)
     A            DTM03     R        B  7 14REFFLD(TM03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DNC05     R        O  7 25REFFLD(NC05)
     A                                      COLOR(BLU)
     A                                  9  2'總庫存量:'
     A                                      COLOR(BLU)
     A            DTATQ     R        O  9 14REFFLD(TA10)
     A                                      EDTCDE(J)
     A                                 11  2'調整日期:'
     A                                      COLOR(BLU)
     A            DTM02          6Y 0B 11 14EDTWRD('  /  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 24'(月／日／年)'
     A                                      COLOR(BLU)
     A*                                13 17'總庫存金額'
     A*                                     COLOR(BLU)
     A*                                14  2'現況  :'
     A*                                     COLOR(BLU)
     A*           DTATA     R        O 14 14REFFLD(TA08)
     A*                                     EDTCDE(J)
     A                                 15  2'調整金額:'
     A                                      COLOR(BLU)
     A            DTM05     R        B 15 14REFFLD(TM05)
     A                                      EDTCDE(J)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*                                16  2'調整後:'
     A*                                     COLOR(BLU)
     A*           DAMT      R        O 16 14REFFLD(TM05)
     A*                                     EDTCDE(J)
     A                                 18  2'調整原因:'
     A N31                                  COLOR(BLU)
     A  31                                  DSPATR(HI)
     A            DTM06     R        B 18 14REFFLD(TM06)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DL203         22A  O 18 17COLOR(BLU)
     A                                 20  2'備註:'
     A                                      COLOR(BLU)
     A            DTM09     R        B 20 10REFFLD(TM09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            HTM02          6Y 0H
     A*
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  31                                +1'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  31                                +1'F5=參考資料'
     A                                      COLOR(BLU)
     A  80                                +1'F9 =確認存檔'
     A                                      COLOR(BLU)
     A                                    +1'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
