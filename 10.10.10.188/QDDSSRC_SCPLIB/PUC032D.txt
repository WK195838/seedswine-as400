     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PUC032D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/10/16                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUC032                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :異常驗收單維護                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/POCLPF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  3VALUES(' ' '6' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SCL03     R   -2   O 10  6REFFLD(CL03)
     A                                      EDTCDE(Y)
     A            CL04      R        O 10 16
     A            SNE05     R        O 10 22REFFLD(NE05 MANEPF)
     A            CL05      R        O 10 33
     A            SCL07     R   -2   O 10 48REFFLD(CL07)
     A                                      EDTWRD('  /  /  ')
     A            SCL13A    R        O 10 62REFFLD(CL13)
     A            SCL13B    R        O 10 69REFFLD(CL13)
     A            SCL14     R        O 10 76REFFLD(CL14)
     A            CL01      R        H
     A            CL02      R        H
     A            CL03      R        H
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF07(07)
     A                                      CF08(08)
     A                                      CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'PUC032'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'異常驗收單審核'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 13REFFLD(CL01)
     A            S#B03     R   -6   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(CL02)
     A            SND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 36'6=審核'
     A                                      COLOR(BLU)
     A                                  5 27'5=查詢'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN3     R   -2   B  9  6REFFLD(CL03)
     A                                      EDTCDE(Y)
     A            DBGN4     R        B  9 16REFFLD(CL04)
     A            DBGN5     R        B  9 33REFFLD(CL05)
     A                                  7  3'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  7 45'(月／日／年)數量 品項'
     A                                      COLOR(BLU)
     A                                  8  2' A 到貨日期分店代號     驗+
     A                                      收單號     到貨運日  差異-
                                             多收 確認'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *此燈號控制新增成功以及修改成功專用
     A  95        ERR0          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A* 96        ERR1          63A  O 24  2MSGID(MSG 0004 SCMF)
     A*                                     DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(MSG 0005 SCMF)
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC032'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'異常驗收單審核'
     A                                      DSPATR(HI)
     A  15                              3 32'<<陳列驗收單>>'
     A                                      COLOR(PNK)
     A N15                              3 32'<<一般驗收單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DCL01     R        O  4 14REFFLD(CL01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5  2'驗收單號:'
     A                                      COLOR(BLU)
     A            DCL05     R        O  5 14REFFLD(CL05)
     A                                  6  2'通路代號:'
     A                                      COLOR(BLU)
     A            DCL02     R        O  6 14REFFLD(CL02)
     A            DND04     R        O  6 17REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  7  2'分店代號:'
     A                                      COLOR(BLU)
     A            DCL04     R        O  7 14REFFLD(CL04)
     A            DNE05     R        O  7 20REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                 10  2'驗收單日期:'
     A                                      COLOR(BLU)
     A            DCLVV     R   -2   O 10 16REFFLD(CLVV)
     A                                      EDTCDE(Y)
     A                                 10 25'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 11  2'到運貨日期:'
     A                                      COLOR(BLU)
     A            DCL07     R   -2   O 11 16REFFLD(CL07)
     A                                      EDTCDE(Y)
     A                                 11 25'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 12  2'到分店日期:'
     A                                      COLOR(BLU)
     A            DCL03     R   -2   O 12 16REFFLD(CL03)
     A                                      EDTCDE(Y)
     A                                 12 25'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 13  2'到貨時間:'
     A                                      COLOR(BLU)
     A            DCL08     R        O 13 14REFFLD(CL08)
     A                                 13 16'( A-上午10-12點／P-下午02-04-
     A                                      點)'
     A                                      COLOR(BLU)
     A                                 20  2'驗收:'
     A                                      COLOR(BLU)
     A                                 20 20'審核:'
     A                                      COLOR(BLU)
     A*
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD3
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC032'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'異常驗收單審核'
     A                                      DSPATR(HI)
     A                                  3 32'<<客戶驗收單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DCL01     R        O  4 14REFFLD(CL01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5  2'驗收單號:'
     A                                      COLOR(BLU)
     A            DCL05     R        O  5 14REFFLD(CL05)
     A                                  6  2'通路代號:'
     A                                      COLOR(BLU)
     A            DCL02     R        O  6 14REFFLD(CL02)
     A            DND04     R        O  6 17REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  7  2'分店代號:'
     A                                      COLOR(BLU)
     A            DCL04     R        O  7 14REFFLD(CL04)
     A            DNE05     R        O  7 20REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  9  2'運送方式:'
     A                                      COLOR(BLU)
     A            DCL10     R        O  9 14REFFLD(CL10)
     A                                  9 16'(供應廠商-１經大榮到客戶，２直+
     A                                      送客戶，３直送分店)'
     A                                      COLOR(BLU)
     A                                 10  2'驗收單日期:'
     A                                      COLOR(BLU)
     A            DCLVV     R   -2   O 10 16REFFLD(CLVV)
     A                                      EDTCDE(Y)
     A                                 10 25'(月／日／年)'
     A                                      COLOR(BLU)
     A  81                             11  2'到運貨日期:'
     A                                      COLOR(BLU)
     A  81        DCL07     R   -2   O 11 16REFFLD(CL07)
     A                                      EDTCDE(Y)
     A  81                             11 25'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 12  2'到分店日期:'
     A                                      COLOR(BLU)
     A            DCL03     R   -2   O 12 16REFFLD(CL03)
     A                                      EDTCDE(Y)
     A                                 12 25'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 13  2'到貨時間:'
     A                                      COLOR(BLU)
     A            DCL09A    R   -2   O 13 14REFFLD(CL09)
     A                                      EDTCDE(Z)
     A                                 13 17'-'
     A                                      COLOR(BLU)
     A            DCL09B    R   -2   O 13 19REFFLD(CL09)
     A                                      EDTCDE(Z)
     A                                 13 22'時'
     A                                      COLOR(BLU)
     A            DCL08     R        O 13 27REFFLD(CL08)
     A                                 13 29'( A-上午／P-下午)'
     A                                      COLOR(BLU)
     A  82                             15  2'客戶姓名:'
     A                                      COLOR(BLU)
     A  82        DCN02     R        O 15 14REFFLD(CN02 POCNPF)
     A  82                             16  2'聯絡電話:'
     A                                      COLOR(BLU)
     A  82        DCN03     R        O 16 14REFFLD(CN03 POCNPF)
     A  82                             16 45'行動電話:'
     A                                      COLOR(BLU)
     A  82        DCN04     R        O 16 57REFFLD(CN04 POCNPF)
     A  82                             17  2'送貨地址:'
     A                                      COLOR(BLU)
     A  82        DCN05     R        O 17 14REFFLD(CN05 POCNPF)
     A*                                18  2'備註  :'
     A*                                     COLOR(BLU)
     A*           DMARK     R        O 18 14REFFLD(CO03 POCOPF)
     A                                 20  2'驗收:'
     A                                      COLOR(BLU)
     A                                 20 20'審核:'
     A                                      COLOR(BLU)
     A*
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 10  3VALUES(' ' '2' '4')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A            DCM03     R        O 10  5REFFLD(CM03 POCMPF)
     A            DNC05     R        O 10 15REFFLD(NC05 MANCPF)
     A            DCC09     R        O 10 34REFFLD(CC09 POCCPF)
     A                                      EDTCDE(L)
     A            DCM04     R        O 10 45REFFLD(CM04 POCMPF)
     A                                      EDTCDE(L)
     A            DCM05     R        O 10 53REFFLD(CM05 POCMPF)
     A                                      EDTCDE(L)
     A            DCM06     R        B 10 61REFFLD(CM06 POCMPF)
     A                                      EDTCDE(L)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A*           DCM10     R        O 10 66REFFLD(CM10 POCMPF)
     A            DCM10     R   -2   O 10 69REFFLD(CM10 POCMPF)
     A            HCM06     R        H      REFFLD(CM06 POCMPF)
     A            DCHK           1A  H
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF09(09)
     A                                      CA12(12)
     A  88                                  CF16(16)
     A  89                                  CF17(17)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC032'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'異常驗收單審核'
     A                                      DSPATR(HI)
     A  91                              3 32'<<一般驗收單>>'
     A                                      COLOR(PNK)
     A  92                              3 32'<<客戶驗收單>>'
     A                                      COLOR(PNK)
     A  93                              3 32'<<陳列驗收單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DCL01     R        O  4 13REFFLD(CL01)
     A            D#B03     R   -4   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 32'通路代號:'
     A                                      COLOR(BLU)
     A            DCL02     R        O  4 44REFFLD(CL02)
     A            DND04     R        O  4 47REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'分店代號:'
     A                                      COLOR(BLU)
     A            DCL04     R        O  5 13REFFLD(CL04)
     A            DNE05     R        O  5 19REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  5 32'驗收單號:'
     A                                      COLOR(BLU)
     A            DCL05     R        O  5 44REFFLD(CL05)
     A  81                              5 58'到貨運日期:'
     A                                      COLOR(BLU)
     A  81        DCL07     R   -2   O  5 72REFFLD(CL07)
     A                                      EDTCDE(Y)
     A                                  6  1'訂貨日期:'
     A                                      COLOR(BLU)
     A            DCLVV     R   -2   O  6 13REFFLD(CLVV)
     A                                      EDTCDE(Y)
     A                                  6 32'到分店日期:'
     A                                      COLOR(BLU)
     A            DCL03     R   -2   O  6 46REFFLD(CL03)
     A                                      EDTCDE(Y)
     A                                  6 58'(月／日／年)'
     A                                      COLOR(BLU)
     A  82                              7  1'客戶名稱:'
     A                                      COLOR(BLU)
     A  82        DCN02     R        O  7 13REFFLD(CN02 POCNPF)
     A  83                              7 32'運送方式:'
     A                                      COLOR(BLU)
     A  83        DCL10     R        O  7 44REFFLD(CL10)
     A  83        CD10N         20O  O  7 46
     A                                      COLOR(BLU)
     A                                  8  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  8 17'2=修改'
     A                                      COLOR(BLU)
     A                                  8 26'4=刪除'
     A                                      COLOR(BLU)
     A                                  9  2' A產品代號產品名稱         -
     A                                      未稅單價 採購量驗收量確認+
     A                                      量採購單號  '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 17'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  80                             22 32'F9 =確認存檔'
     A                                      COLOR(BLU)
     A                                 22 47'F12=回前畫面'
     A                                      COLOR(BLU)
     A  88                             22 62'F16=供應商備註'
     A                                      COLOR(BLU)
     A  89                             23  2'F17=查詢客戶資料'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPD4
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC032'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'異常驗收單審核'
     A                                      DSPATR(HI)
     A                                  3 32'<<客戶驗收單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DCL01     R        O  4 13REFFLD(CL01)
     A            D#B03     R   -4   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DCL02     R        O  4 43REFFLD(CL02)
     A            DND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'分店代號:'
     A                                      COLOR(BLU)
     A            DCL04     R        O  5 13REFFLD(CL04)
     A            DNE05     R        O  5 19REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  5 31'驗收單號:'
     A                                      COLOR(BLU)
     A            DCL05     R        O  5 43REFFLD(CL05)
     A                                  5 57'到貨運日期:'
     A                                      COLOR(BLU)
     A            DCL07     R   -2   O  5 71REFFLD(CL07)
     A                                      EDTCDE(Y)
     A                                  6  1'訂貨日期:'
     A                                      COLOR(BLU)
     A            DCLVV     R   -2   O  6 13REFFLD(CLVV)
     A                                      EDTCDE(Y)
     A                                  6 31'到分店日期:'
     A                                      COLOR(BLU)
     A            DCL03     R   -2   O  6 45REFFLD(CL03)
     A                                      EDTCDE(Y)
     A                                  6 58'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  8  1'客戶姓名:'
     A                                      COLOR(BLU)
     A            DCN02     R        B  8 13REFFLD(CN02 POCNPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  1'聯絡電話:'
     A                                      COLOR(BLU)
     A            DCN03     R        B 10 13REFFLD(CN03 POCNPF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 45'行動電話:'
     A                                      COLOR(BLU)
     A            DCN04     R        B 10 57REFFLD(CN04 POCNPF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  1'送貨地址:'
     A                                      COLOR(BLU)
     A            DCN05     R        B 12 13REFFLD(CN05 POCNPF)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*                                14  1'備註  :'
     A*                                     COLOR(BLU)
     A*           DMARK     R        B 14 13REFFLD(CO03 POCOPF)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R SFLSR3                    SFL
     A  54                                  SFLNXTCHG
     A            RRN3           4S 0H
     A            DOPT3          1A  B 10  3VALUES(' ' '1' '2' '4')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A            DCO02     R        B 10  5REFFLD(CO02 POCOPF)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DCO03     R        B 10 18REFFLD(CO03 POCOPF)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A            KNB03     R        O 11  5REFFLD(NB03 MANBPF)
     A            DCO04     R        B 11 18REFFLD(CO04 POCOPF)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A            DCHK3          1A  H
     A*----------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0005)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  55                                  SFLDSP
     A  56                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN3          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC032'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'異常驗收單審核'
     A                                      DSPATR(HI)
     A*                                 3 32'<<一般驗收單>>'
     A  21                              3 32'<<一般驗收單>>'
     A                                      COLOR(PNK)
     A N21                              3 32'<<客戶驗收單>>'
     A                                      COLOR(PNK)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A                                      COLOR(BLU)
     A            DCL01     R        O  4 13REFFLD(CL01)
     A            D#B03     R   -4   O  4 16REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DCL02     R        O  4 43REFFLD(CL02)
     A            DND04     R        O  4 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  5  1'分店代號:'
     A                                      COLOR(BLU)
     A            DCL04     R        O  5 13REFFLD(CL04)
     A            DNE05     R        O  5 19REFFLD(NE05 MANEPF)
     A                                      COLOR(BLU)
     A                                  5 31'驗收單號:'
     A                                      COLOR(BLU)
     A            DCL05     R        O  5 43REFFLD(CL05)
     A                                  5 57'到貨運日期:'
     A                                      COLOR(BLU)
     A            DCL07     R   -2   O  5 71REFFLD(CL07)
     A                                      EDTCDE(Y)
     A                                  6  1'訂貨日期:'
     A                                      COLOR(BLU)
     A            DCLVV     R   -2   O  6 13REFFLD(CLVV)
     A                                      EDTCDE(Y)
     A                                  6 31'到分店日期:'
     A                                      COLOR(BLU)
     A            DCL03     R   -2   O  6 45REFFLD(CL03)
     A                                      EDTCDE(Y)
     A                                  6 58'(月／日／年)'
     A                                      COLOR(BLU)
     A*                                 7  1'輸入作業選擇:'
     A*                                     COLOR(BLU)
     A*                                 7 17'1=新增'
     A*                                     COLOR(BLU)
     A*                                 7 26'2=修改'
     A*                                     COLOR(BLU)
     A*                                 7 35'4=刪除'
     A*                                     COLOR(BLU)
     A                                  9  2' A供應商代號    驗收單備註    -
                                                                               -
                                                 '                             -
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC4
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  80                                +2'F9 =確認存檔'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
