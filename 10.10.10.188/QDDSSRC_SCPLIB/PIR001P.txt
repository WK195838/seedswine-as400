     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<PIR001>'
     A                                    52
     A                                      MSGCON(030 USC9999 SCMF)
     A                                   113
     A                                      '日期:'
     A            $EGMDY         6  0O   121
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    60
     A                                      '盤盈虧報表'
     A                                   113
     A                                      '時間:'
     A                                   121
     A                                      TIME
     A                                      SPACEA(001)
     A                                   113
     A                                      '頁次:'
     A                                   121
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     1
     A                                      '公司別  :'
     A            DPI01     R        O    13
     A                                      REFFLD(PI01 INPIPF)
     A            D#B03     R        O    16
     A                                      REFFLD(#B03 PA#BPF)
     A                                      SPACEA(001)
     A                                     1
     A                                      '盤點年月:'
     A            DPI02          4  0O    13
     A                                      EDTCDE(Y)
     A                                    19
     A                                      '(月／年)'
     A                                   114
     A                                      'USER :'
     A            $USER         10A      121
     A                                      SPACEA(001)
     A                                     1
     A                                      '分店代號:'
     A            PI03      R             13
     A                                      REFFLD(PI03 INPIPF)
     A            NE05      R             19
     A                                      REFFLD(NE05)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =='
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      '產品代號／名稱'
     A                                    31
     A                                      '庫存量'
     A                                    41
     A                                      '陳列量'
     A                                    50
     A                                      '盤點量'
     A                                    59
     A*                                     '差異量+'
     A                                      '盤盈量 '
     A                                    69
     A*                                     '差異量-'
     A                                      '盤損量-'
     A                                    79
     A*                                     '陳列差異量'
     A                                      '陳列盤盈損'
     A                                    92
     A                                      '單位成本'
     A                                   104
     A*                                     '差異金額+'
     A                                      '盤盈金額 '
     A                                   117
     A*                                     '差異金額-'
     A                                      '盤損金額 '
     A                                   128
     A                                      '陳列差異金額'
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --'
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEA(001)
     A            PI04      R              2
     A                                      REFFLD(PI04 INPIPF)
     A            NC05      R             12
     A                                      REFFLD(NC05 MANCPF)
     A            PI05      R             33
     A                                      REFFLD(PI05 INPIPF)
     A                                      EDTCDE(P)
     A            PI06      R             42
     A                                      REFFLD(PI06 INPIPF)
     A                                      EDTCDE(P)
     A            PI07      R             50
     A                                      REFFLD(PI07 INPIPF)
     A                                      EDTCDE(P)
     A            PI08P     R             61
     A                                      REFFLD(PI08 INPIPF)
     A                                      EDTCDE(P)
     A            PI08D     R             71
     A                                      REFFLD(PI08 INPIPF)
     A                                      EDTCDE(P)
     A            PI08A     R             84
     A                                      REFFLD(PI08 INPIPF)
     A                                      EDTCDE(P)
     A            PI09      R             92
     A                                      REFFLD(PI09 INPIPF)
     A                                      EDTCDE(P)
     A            PI10P     R            104
     A                                      REFFLD(PI10 INPIPF)
     A                                      EDTCDE(P)
     A            PI10D     R            117
     A                                      REFFLD(PI10 INPIPF)
     A                                      EDTCDE(P)
     A            PI10A     R            131
     A                                      REFFLD(PI10A INPIPF)
     A                                      EDTCDE(P)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =='
     A                                      SPACEA(001)
     A                                     1
     A                                      '合計:'
     A            TSPI05         7S 0     32
     A                                      EDTCDE(P)
     A            TSPI06         7S 0     41
     A                                      EDTCDE(P)
     A            TSPI07         7S 0     49
     A                                      EDTCDE(P)
     A            TSP08P         7S 0     60
     A                                      EDTCDE(P)
     A            TSP08D         7S 0     70
     A                                      EDTCDE(P)
     A            TSP08A         7S 0     83
     A                                      EDTCDE(P)
     A            TSP10P        10S 2    103
     A                                      EDTCDE(P)
     A            TSP10D        10S 2    116
     A                                      EDTCDE(P)
     A            TSP10A        10S 2    130
     A                                      EDTCDE(P)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PE4
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =='
     A                                      SPACEA(001)
     A                                     1
     A                                      '總計:'
     A            TTPI05         7S 0     32
     A                                      EDTCDE(P)
     A            TTPI06         7S 0     41
     A                                      EDTCDE(P)
     A            TTPI07         7S 0     49
     A                                      EDTCDE(P)
     A            TTP08P         7S 0     60
     A                                      EDTCDE(P)
     A            TTP08D         7S 0     70
     A                                      EDTCDE(P)
     A            TTP08A         7S 0     83
     A                                      EDTCDE(P)
     A            TTP10P        10S 2    103
     A                                      EDTCDE(P)
     A            TTP10D        10S 2    116
     A                                      EDTCDE(P)
     A            TTP10A        10S 2    130
     A                                      EDTCDE(P)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --'
     A                                      SPACEA(001)
     A                                     2
     A                                      '<<PIR001>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     2
     A                                      '<<PIR001>>--->此範圍無資料'
     A                                      SPACEA(001)
