     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : SWD009D                                    *
     A*    PROGRAM NAME :陳列退出日期(陳列品退出主檔)            *
     A*    AUTHOR       : A1546 VINCENT                              *
     A*    CREATE DATE  : 2008.10.21                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :西祺進銷存系統                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPD0                     ASSUME
     A                                  1  2' '
     A*----------------------------------------------------------------
     A          R SFLSR3                    SFL
     A            RRN3           3Y 0H
     A            SEL            1A  B  6  4DSPATR(CS)
     A*                                     DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      VALUES(' ' '/')
     A            QTK03     R   -2   O  6  7REFFLD(TK03)
     A                                      EDTWRD('0  /  /  ')
     A            TK05      R        O  6 18
     A            TK03      R        H
     A*----------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0006)
     A                                      WINDOW(3 11 14 35)
     A                                      CA12(12)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      USRRSTDSP
     A            DRRN3          3S 0H      SFLRCDNBR
     A                                  1 15'陳列退出日期查詢'
     A                                      DSPATR(HI)
     A                                  1  2' <SWD009>  '
     A                                  4  4'/'
     A                                  4  6'退出日期'
     A                                      DSPATR(UL)
     A                                  4 20'單據號碼'
     A                                      DSPATR(UL)
     A            KTK03     R   -2   B  5  7REFFLD(TK03)
     A                                      EDTWRD('0  /  /  ')
     A                                      DSPATR(PC)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD1                     WINDOW(SFLCR3)
     A                                 13  2'執行'
     A                                    +2'PF12=回前畫面'
