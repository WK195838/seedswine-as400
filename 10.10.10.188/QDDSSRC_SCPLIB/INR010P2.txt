     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : INT010P2                                   *
     A*    AUTHOR       : DEREK                                      *
     A*    CREATE DATE  : 101/11/28                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INT010                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :帳列產品庫存金額總表－依分店              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1'<INR010P2>'
     A                                    39MSGCON(030 USC9999 SCMF)
     A                                    88'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    38'帳列產品庫存金額總表－依分店'
     A                                    88'時間:'
     A                                    +1TIME
     A                                      SPACEA(001)
     A                                    88'頁次:'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司代號:'
     A            DTO01     R        O    +1REFFLD(TB01)
     A            D#B03     R        O    +1REFFLD(#B03 PA#BPF)
     A                                    +1'通路代號:'
     A            DTO02S         2   O    +1
     A                                    +0'-'
     A            DTO02E         2   O    +0
     A                                    +1'分店代號:'
     A            DTO03S         5   O    +1
     A                                    +0'-'
     A            DTO03E         5   O    +0
     A                                      SPACEA(001)
     A                                     1'產品代號:'
     A            DTO04S         9   O    +1
     A                                    +0'-'
     A            DTO04E         9   O    +0
     A  38                                33'月結月份:'
     A  38        DTO05S         4  0O    +1EDTWRD('  /  ')
     A  38                                +1'(月／年)'
     A N38                                33'本期截止日:'
     A N38        DTO05E         6  0O    +1EDTWRD('  /  /  ')
     A N38                                +1'(月／日／年)'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A                                     1'分店代號／名稱'
     A                                      SPACEB(001)
     A                                    22'帳列庫存量'
     A                                    46'帳列存貨金額'
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------------------------------'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A            W8603          5A        2
     A            W8604         10O       +1
     A            W8608          6S 0     26EDTCDE(J)
     A            W8609         13S 2     42EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                     1'合計:'
     A            Q1             6S 0     26EDTCDE(J)
     A            Q2            13S 2     42EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<INR010P1>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
