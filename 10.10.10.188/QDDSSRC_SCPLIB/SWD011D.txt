     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : SWD011D                                    *
     A*    PROGRAM NAME :銷售區碼資料視窗查詢                      *
     A*    AUTHOR       : A1492 TERRY                                *
     A*    CREATE DATE  : 2009.09.07                                 *
     A*    UPDATE DATE  :
     A*    SYSTEM       :                                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MANGPF)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPD0                     ASSUME
     A                                  1  2' '
     A*----------------------------------------------------------------
     A          R SFLSR3                    SFL
     A            RRN3           3Y 0H
     A            SEL            1A  B  6  4DSPATR(CS)
     A                                      DSPATR(HI)
     A                                      VALUES(' ' '/')
     A            NG03      R        O  6  8
     A            NG04      R        O  6 13
     A*----------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0006)
     A                                      WINDOW(3 11 14 40)
     A                                      CA12(12)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      USRRSTDSP
     A            DRRN3          3S 0H      SFLRCDNBR
     A                                  1 15'銷售區碼資料查詢'
     A                                      DSPATR(HI)
     A                                  1  2' <SWD011>  '
     A                                  4  4'/'
     A                                  4  6'區碼'
     A                                      DSPATR(UL)
     A                                  4 13'  簡稱  '
     A                                      DSPATR(UL)
     A            KNG03     R        B  5  8REFFLD(NG03)
     A                                      DSPATR(PC)
     A                                      DSPATR(CS)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD1                     WINDOW(SFLCR3)
     A                                 13  2'執行'
     A                                    +2'PF12=回前畫面'
