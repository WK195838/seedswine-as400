     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    DSPF NAME    : INR110D                                    *
     A*    AUTHOR       : DEREK                                      *
     A*    CREATE DATE  : 102/09/12                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : INR110D                                    *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :存貨調整明細表                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CF07(07 'F7下載')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'INR110'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'存貨調整明細表            '
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 24'公司代號:'
     A                                      COLOR(BLU)
     A            DTI01     R        B  6 36REFFLD(TI01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A            D#B03     R        O  6 39REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A*
     A                                  8 24'通路代號:'
     A                                      COLOR(BLU)
     A            DTI02S    R        B  8 36REFFLD(TI02)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  8 39'-'
     A                                      COLOR(BLU)
     A            DTI02E    R        B  8 41REFFLD(TI02)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  8 45'(空白：全部)'
     A                                      COLOR(BLU)
     A*
     A                                 10 24'分店代號:'
     A                                      COLOR(BLU)
     A            DTI04S    R        B 10 36REFFLD(TI04)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 10 42'-'
     A                                      COLOR(BLU)
     A            DTI04E    R        B 10 44REFFLD(TI04)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 10 51'(空白：全部)'
     A                                      COLOR(BLU)
     A*
     A                                 12 24'調整日期:'
     A                                      COLOR(BLU)
     A            DTI03S         6Y 0B 12 36EDTWRD('  /  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 12 45'-'
     A            DTI03E         6Y 0B 12 47EDTWRD('  /  /  ')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 12 57'(月／日／年)'
     A                                      COLOR(BLU)
     A                                 14 24'原因代碼:'
     A                                      COLOR(BLU)
     A            DTI06S    R        B 14 36REFFLD(TI06)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                 14 39'-'
     A            DTI06E    R        B 14 41REFFLD(TI06)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                 14 45'(空白：全部)'
     A                                      COLOR(BLU)
     A*
     A                                 16 24'產品代號:'
     A                                      DSPATR(HI)
     A            DTJ02S    R        B 16 36REFFLD(TJ02)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                 16 46'-'
     A                                      COLOR(BLU)
     A            DTJ02E    R        B 16 48REFFLD(TJ02)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                 16 59'(空白：全部)'
     A                                      COLOR(BLU)
     A*
     A                                 18 24'執行環境:'
     A                                      COLOR(BLU)
     A            $EVR           1Y 0B 18 36VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 18 39'(1-即時 2-批次)'
     A                                      COLOR(BLU)
     A***                              19 24'列印份數:'
     A***                                   COLOR(BLU)
     A***         $CPY           3Y 0B 19 36EDTCDE(3)
     A***                                   CHECK(RB)
     A***                              20 24'列表機　:'
     A***                                   DSPATR(HI)
     A***         $PRTID         2Y 0B 20 36CHECK(RB)
     A***                                   DSPATR(HI)
     A***                                   EDTCDE(3)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A***                              22 43'F7 =檔案下載'
     A***                                   COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
