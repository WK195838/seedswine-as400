     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : UTL001D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/09/23                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : UTL001                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :                        *
     A*    REMARK       :代碼資料設定   (A/U/D/I)                  *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/UTL1PF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '2' '4' '5' '6' '7')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            L101      R        O  9  6
     A            L102      R        O  9 15
     A            L105      R        O  9 41EDTCDE(N)
     A            L106      R        O  9 48EDTCDE(N)
     A            L107      R        O  9 58
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'UTL001'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'　代碼資料維護　'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  4 17'2=修改'
     A                                      COLOR(BLU)
     A                                  4 26'4=刪除'
     A                                      COLOR(BLU)
     A                                  4 35'5=查詢'
     A                                      COLOR(BLU)
     A                                  4 44'6=明細維護'
     A                                      COLOR(BLU)
     A                                  6 40'代碼'
     A                                      COLOR(BLU)
     A                                  6 47'說明'
     A                                      COLOR(BLU)
     A                                  6 54'維護否'
     A                                      COLOR(BLU)
     A                                  7  3'A 代碼   簡稱               -
     A                                       　長度 長度   (Y/N)   '
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  6REFFLD(L101)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F6 =新增'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *此燈號控制新增成功以及修改成功專用
     A  96        ERR1          63A  O 23  2MSGID(MSG 0004 SCMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 23  2MSGID(MSG 0005 SCMF)
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'UTL001'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'　代碼資料維護　'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'代  碼:'
     A                                      COLOR(BLU)
     A            DL101     R        B  5 14REFFLD(L101)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  7  2'簡  稱:'
     A                                      COLOR(BLU)
     A            DL102     R        B  7 14REFFLD(L102)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A                                  9  2'內容長度:'
     A                                      COLOR(BLU)
     A            DL105     R        B  9 14REFFLD(L105)
     A                                      EDTCDE(N)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  2'說明長度:'
     A                                      COLOR(BLU)
     A            DL106     R        B 11 14REFFLD(L106)
     A                                      EDTCDE(N)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  2'可維護否:'
     A                                      COLOR(BLU)
     A            DL107     R        B 13 14REFFLD(L107)
     A                                      VALUES('Y' 'N')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A          R DSPC3
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  80                                +2'F9 =確認'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 10  3VALUES(' ' '1' '2' '4')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A            DL202     R        B 10  6REFFLD(L202 UTL2PF)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DCHK           1A  H
     A            DL203     R        B 10 18REFFLD(L203 UTL2PF)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MANA01'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 32'代碼資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'代  碼:'
     A                                      COLOR(BLU)
     A            DL101     R        O  5 14REFFLD(L101)
     A            DL102     R        O  5 23REFFLD(L102)
     A                                  6  2'內容長度:'
     A                                      COLOR(BLU)
     A            DL105     R        O  6 14REFFLD(L105)
     A                                      EDTCDE(N)
     A                                  6 22'說明長度:'
     A                                      COLOR(BLU)
     A            DL106     R        O  6 34REFFLD(L106)
     A                                      EDTCDE(N)
     A                                  7  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  7 19'1=新增'
     A                                      COLOR(BLU)
     A                                  7 29'2=修改'
     A                                      COLOR(BLU)
     A                                  7 39'4=刪除'
     A                                      COLOR(BLU)
     A                                  9  3'A 內容代號  說明            -
     A                                       　         '
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
