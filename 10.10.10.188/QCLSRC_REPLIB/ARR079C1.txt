     /***************************************************************/
     /*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266   */
     /*                                                             */
     /*    AUTHOR       :                                           */
     /*    CREATE DATE  :                                           */
     /*    UPDATE DATE  :                                           */
     /*    PROGRAM NAME : ARR079C1                                  */
     /*    SYSTEM       :人頭馬匯東                               */
     /*    SUBSYSTEM    :應收系統                                 */
     /*    REMARK       :票據兌現明細表                           */
     /***************************************************************/
             PGM
             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)

             DCL        VAR(&DAC01S) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DAC01E) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DSEL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&DAC23S) TYPE(*CHAR) LEN(9)
             DCL        VAR(&DAC23E) TYPE(*CHAR) LEN(9)
             DCL        VAR(&DAC07S) TYPE(*CHAR) LEN(8) /*M001M*/
             DCL        VAR(&DAC07E) TYPE(*CHAR) LEN(8) /*M001M*/

             CHGVAR     VAR(&DAC01S)  VALUE(%SST(*LDA 601 2))
             CHGVAR     VAR(&DAC01E)  VALUE(%SST(*LDA 603 2))
             CHGVAR     VAR(&DAC23S) VALUE(%SST(*LDA 605 9))
             CHGVAR     VAR(&DAC23E) VALUE(%SST(*LDA 614 9))
             CHGVAR     VAR(&DAC07S) VALUE(%SST(*LDA 671 8)) /*M001M*/
             CHGVAR     VAR(&DAC07E) VALUE(%SST(*LDA 681 8)) /*M001M*/
             CHGVAR     VAR(&DSEL) VALUE(%SST(*LDA 635 1))


             OVRPRTF    FILE(ARR079P) USRDTA('預計兌現')
             OVRDBF     FILE(ARACLF05) SHARE(*YES)

             IF         COND(&DSEL *EQ '1') THEN(DO)
             OPNQRYF    FILE((ARACLF05)) QRYSLT(' (AC01 *EQ +
                          %RANGE("' *CAT &DAC01S *CAT '" "' *CAT +
                          &DAC01E *CAT '") ) & (AC23 *EQ %RANGE("' +
                          *CAT &DAC23S *CAT '" "' *CAT &DAC23E *CAT +
                          '") ) & (AC07C *EQ %RANGE("' *CAT &DAC07S +
                          *CAT '" "' *CAT &DAC07E *CAT '") ) ') +
                          KEYFLD((AC01) (AC07) (AC02) (AC03)) +
                          MAPFLD((AC23 'AC02 || AC03' *CHAR 10) +
                          (AC07C AC07 *CHAR 8)) /*M001M*/
             CALL       ARR0791
             CHGVAR     VAR(&P6601I) VALUE('ARR079P')
                        ENDDO
             ELSE       CMD(DO)
             OVRPRTF    FILE(ARR079P1) USRDTA('預計兌現')
             OPNQRYF    FILE((ARACLF05)) QRYSLT(' (AC01 *EQ +
                          %RANGE("' *CAT &DAC01S *CAT '" "' *CAT +
                          &DAC01E *CAT '") ) & (AC23 *EQ %RANGE("' +
                          *CAT &DAC23S *CAT '" "' *CAT &DAC23E *CAT +
                          '") ) & (AC07C *EQ %RANGE("' *CAT &DAC07S +
                          *CAT '" "' *CAT &DAC07E *CAT '") ) ') +
                          KEYFLD((AC01) (AC02) (AC03) (AC07)) +
                          MAPFLD((AC23 'AC02 || AC03' *CHAR 10) +
                          (AC07C AC07 *CHAR 8)) /*M001M*/
             CALL       ARR0792
             CHGVAR     VAR(&P6601I) VALUE('ARR079P1')
                        ENDDO


             CLOF       OPNID(ARACLF05)
             DLTOVR     FILE(*ALL)

             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)

             ENDPGM
