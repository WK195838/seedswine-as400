     /***************************************************************/
     /*   人頭馬匯東洋酒份有限公司－版權所有        TEL:5042266   */
     /*                                                             */
     /*    AUTHOR       : AN MING HSIA                              */
     /*    CREATE DATE  : 82/01/18                                  */
     /*    UPDATE DATE  :                                           */
     /*    PROGRAM NAME : ARR067C1                                  */
     /*    SYSTEM       :人頭馬匯東                               */
     /*    SUBSYSTEM    :應收帳款系統                             */
     /*    REMARK       :收款分配明細表                           */
     /***************************************************************/
     /* 00A需求單號: 1050388                                       */
     /* 00A 2016/08/17 SHARON產生為EXCEL格式                     */
     /****************************************************************/
             PGM
             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)

             DCL        VAR(&DAD10S) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DAD10E) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DAD02S) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DAD02E) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DAD05S) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DAD05E) TYPE(*CHAR) LEN(2)
             DCL        VAR(&YMDS)   TYPE(*CHAR) LEN(6)
             DCL        VAR(&YMDE)   TYPE(*CHAR) LEN(6)
/*M001A*/    DCL        VAR(&YMD8S)  TYPE(*CHAR) LEN(8)
/*M001A*/    DCL        VAR(&YMD8E)  TYPE(*CHAR) LEN(8)
/*00A*/      DCL        VAR(&LIB)   TYPE(*CHAR) LEN(10)
/*00A*/      DCL        VAR(&FILE)  TYPE(*CHAR) LEN(10)

             CPYF       FROMFILE(REFLIB/REWF81) TOFILE(QTEMP/REWF81) +
                          CRTFILE(*YES)

             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/REWF81))
             OVRDBF     FILE(REWF81) TOFILE(QTEMP/REWF81) SHARE(*YES)

             CHGVAR     VAR(&DAD10S) VALUE(%SST(*LDA 601 2))
             CHGVAR     VAR(&DAD10E) VALUE(%SST(*LDA 603 2))
             CHGVAR     VAR(&DAD05S) VALUE(%SST(*LDA 605 2))
             CHGVAR     VAR(&DAD05E) VALUE(%SST(*LDA 607 2))
             CHGVAR     VAR(&DAD02S) VALUE(%SST(*LDA 611 5))
             CHGVAR     VAR(&DAD02E) VALUE(%SST(*LDA 616 5))
             CHGVAR     VAR(&YMDS)   VALUE(%SST(*LDA 630 6))
             CHGVAR     VAR(&YMDE)   VALUE(%SST(*LDA 636 6))
/*M001A*/    CHGVAR     VAR(&YMD8S)  VALUE(%SST(*LDA 850 8))
/*M001A*/    CHGVAR     VAR(&YMD8E)  VALUE(%SST(*LDA 858 8))
/*00A*/      CHGVAR     VAR(&LIB)   VALUE('QTEMP')
/*00A*/      CHGVAR     VAR(&FILE)  VALUE('ARR067PF')

/*00A*/      CPYF       FROMFILE(ARR067PF) TOFILE(QTEMP/ARR067PF) +
                          CRTFILE(*YES)
/*00A*/      MONMSG     MSGID(CPF2817) EXEC(CLRPFM +
                          FILE(QTEMP/ARR067PF))
/*00A*/      CLRPFM     FILE(QTEMP/ARR067PF)

/*00A*/      OVRDBF     FILE(ARR067PF) TOFILE(QTEMP/ARR067PF) +
                          SHARE(*YES)

             OVRPRTF    FILE(ARR067P) PAGESIZE(*N 198) CPI(15) +
                        USRDTA('收款分配')
             OVRDBF     FILE(ARADPF) SHARE(*YES)

             OPNQRYF    FILE((ARADPF)) QRYSLT('AD10 *EQ %RANGE("' +
                          *CAT &DAD10S *CAT '" "' *CAT &DAD10E *CAT +
                          '") & AD02 *EQ %RANGE("' *CAT &DAD02S +
                          *CAT '" "' *CAT &DAD02E *CAT '") & AD05 +
                          *EQ %RANGE("' *CAT &DAD05S *CAT '" "' +
                          *CAT &DAD05E *CAT '") &  AD12A *EQ +
                          %RANGE("' *CAT &YMD8S *CAT '" "' *CAT +
                          &YMD8E *CAT '") ') KEYFLD((AD10) (AD02) +
                          (AD12)) MAPFLD((AD12A 'AD12' *CHAR 8))
/*M001D                   %RANGE("' *CAT &YMDS *CAT '" "' *CAT +
                          &YMDE *CAT '") ') KEYFLD((AD10) (AD02) + */

             OVRDBF     FILE(ARAFPF) SHARE(*YES)

             OPNQRYF    FILE((ARAFPF)) QRYSLT('AF01 *EQ %RANGE("' +
                          *CAT &DAD10S *CAT '" "' *CAT &DAD10E *CAT +
                          '") & AF03 *EQ %RANGE("' *CAT &DAD02S +
                          *CAT '" "' *CAT &DAD02E *CAT '") &  AF05A +
                          *EQ %RANGE("' *CAT &YMD8S *CAT '" "' *CAT +
                          &YMD8E *CAT '") ') MAPFLD((AF05A 'AF05' +
                          *CHAR 8))
/*M001D                   *EQ %RANGE("' *CAT &YMDS *CAT '" "' *CAT +
                          &YMDE *CAT '") ') MAPFLD((AF05A 'AF05' +  */

             CALL       ARR0671
             CALL       ARR0672

             CLOF       OPNID(ARADPF)
             CLOF       OPNID(ARAFPF)
             DLTOVR     FILE(*ALL)

             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)
             CHGVAR     VAR(&P6601I) VALUE('ARR067P')
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)

/*00A*/      /*將ARR063PF轉換成CSV FILE AND FTP TO PC */
/*00A*/         CALL       PGM(ECL100C) PARM(&LIB &FILE)

             ENDPGM
