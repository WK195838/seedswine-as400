     /***************************************************************/
     /*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266   */
     /*                                                             */
     /*    AUTHOR       : AN MING HSIA                              */
     /*    CREATE DATE  : 05/24/93                                  */
     /*    UPDATE DATE  :                                           */
     /*    PROGRAM NAME : SOR073C1                                  */
     /*    SYSTEM       :人頭馬匯東                               */
     /*    SUBSYSTEM    :銷售訂單系統                             */
     /*    REMARK       :組別產品銷售週報表                       */
     /***************************************************************/
             PGM
             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)

             DCL        VAR(&COMPS)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&COMPE)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&DSC04S) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DSC04E) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DSC27S) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DSC27E) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DSC28S) TYPE(*CHAR) LEN(4)
             DCL        VAR(&DSC28E) TYPE(*CHAR) LEN(4)
             DCL        VAR(&YSC39S) TYPE(*CHAR) LEN(6)
             DCL        VAR(&YSC39E) TYPE(*CHAR) LEN(6)
             DCL        VAR(&IN20)   TYPE(*CHAR) LEN(1)
/*M001A*/    DCL        VAR(&YMD8S)  TYPE(*CHAR) LEN(8)
/*M001A*/    DCL        VAR(&YMD8E)  TYPE(*CHAR) LEN(8)

             CPYF       FROMFILE(REFLIB/REWF74) TOFILE(QTEMP/REWF74) +
                          CRTFILE(*YES)

             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/REWF74))
             OVRDBF     FILE(REWF74) TOFILE(QTEMP/REWF74) SHARE(*YES)

/*M001A*/    CHGVAR     VAR(&YMD8S)  VALUE(%SST(*LDA 850 8))
/*M001A*/    CHGVAR     VAR(&YMD8E)  VALUE(%SST(*LDA 858 8))
             CHGVAR     VAR(&DSC28S) VALUE(%SST(*LDA 601 4))
             CHGVAR     VAR(&DSC28E) VALUE(%SST(*LDA 605 4))
             CHGVAR     VAR(&DSC04S) VALUE(%SST(*LDA 675 5))
             CHGVAR     VAR(&DSC04E) VALUE(%SST(*LDA 680 5))
             CHGVAR     VAR(&YSC39S) VALUE(%SST(*LDA 621 6))
             CHGVAR     VAR(&YSC39E) VALUE(%SST(*LDA 627 6))
             CHGVAR     VAR(&DSC27S) VALUE(%SST(*LDA 651 6))
             CHGVAR     VAR(&DSC27E) VALUE(%SST(*LDA 657 6))
             CHGVAR     VAR(&COMPS)  VALUE(%SST(*LDA 672 1))
             CHGVAR     VAR(&COMPE)  VALUE(%SST(*LDA 673 1))
             CHGVAR     VAR(&IN20)   VALUE(%SST(*LDA 750 1))

             OVRPRTF    FILE(SOR073P) PAGESIZE(*N 106) CPI(12) +
                        USRDTA('組別銷售')
             OVRDBF     FILE(SOSCPF) SHARE(*YES)
             OVRDBF     FILE(SOSGPF) SHARE(*YES)

             IF         COND(&IN20 = '0') THEN(DO)
             OPNQRYF    FILE((SOSCPF)) QRYSLT(' SC01A *EQ %RANGE("' +
                          *CAT &COMPS *CAT '" "' *CAT &COMPE *CAT +
                          '") & SC27 *EQ %RANGE("' *CAT &DSC27S +
                          *CAT '"  "' *CAT &DSC27E *CAT '") & SC28 +
                          *EQ %RANGE("' *CAT &DSC28S *CAT '" "' +
                          *CAT &DSC28E *CAT '" ) & SC08 *EQ +
                          %RANGE(' *CAT &YMD8S  *CAT ' ' *CAT +
                          &YMD8E  *CAT ') & SC04 *EQ %RANGE("' *CAT +
                          &DSC04S *CAT '" "' *CAT &DSC04E *CAT '" ) +
                          & SC13 *NE "*" & SC35 *NE "T" & SC03 *NE +
                          "S4"  & SC01B *NE "K" ') KEYFLD((SC28) +
                          (SC27) (SC04) (SC05) (SC08) (SC02)) +
                          MAPFLD((SC01A '%SST(SC01 2 1)' *CHAR 1) +
                          (SC01B '%SST(SC01 1 1)' *CHAR 1))
/*M001D                   %RANGE(' *CAT &YSC39S *CAT ' ' *CAT +
                        &YSC39E *CAT ') & SC04 *EQ %RANGE("' *CAT + */

             OPNQRYF    FILE((SOSGPF)) QRYSLT('SG01A *EQ %RANGE("' +
                          *CAT &COMPS *CAT '" "' *CAT &COMPE *CAT +
                          '") & SG11 *EQ %RANGE("' *CAT &DSC27S +
                          *CAT '" "' *CAT &DSC27E *CAT '") & SG12 +
                          *EQ %RANGE("' *CAT &DSC28S *CAT '" "' +
                          *CAT &DSC28E *CAT '") & SG06 *EQ %RANGE(' +
                          *CAT &YMD8S  *CAT ' ' *CAT &YMD8E  *CAT +
                          ') & SG04 *EQ %RANGE("' *CAT &DSC04S *CAT +
                          '" "' *CAT &DSC04E *CAT '" ) & SG17 *EQ +
                          "            "  +
                          & SG01B *NE "K" ') KEYFLD(*FILE) +
                          MAPFLD((SG01A '%SST(SG01 2 1)' *CHAR 1) +
                          (SG01B '%SST(SG01 1 1)' *CHAR 1))
/*M001D                   *CAT &YSC39S *CAT ' ' *CAT &YSC39E *CAT + */
             ENDDO
             ELSE       CMD(DO)
             OPNQRYF    FILE((SOSCPF)) QRYSLT('SC01A *EQ %RANGE("' +
                          *CAT &COMPS *CAT '" "' *CAT &COMPE *CAT +
                          '") & SC27 *EQ %RANGE("' *CAT &DSC27S +
                          *CAT '" "' *CAT &DSC27E *CAT '") & SC28 +
                          *EQ %RANGE("' *CAT &DSC28S *CAT '" "' +
                          *CAT &DSC28E *CAT '") & SC04 *EQ +
                          %RANGE("' *CAT &DSC04S *CAT '" "' *CAT +
                          &DSC04E *CAT '") & SC39 *EQ %RANGE(' *CAT +
                          &YMD8S  *CAT ' ' *CAT &YMD8E  *CAT ') & +
                          SC13 *EQ "V" & SC35 *NE "T" & SC03 *NE +
                          "S4" & SC01B *NE "K" ') KEYFLD((SC28) +
                          (SC27) (SC04) (SC05) (SC39) (SC02)) +
                          MAPFLD((SC01A '%SST(SC01 2 1)' *CHAR 1) +
                          (SC01B '%SST(SC01 1 1)' *CHAR 1))
/*M001D                   &YSC39S *CAT ' ' *CAT &YSC39E *CAT ') & + */

             OPNQRYF    FILE((SOSGPF)) QRYSLT('SG01A *EQ %RANGE("' +
                          *CAT &COMPS *CAT '" "' *CAT &COMPE *CAT +
                          '") & SG11 *EQ %RANGE("' *CAT &DSC27S +
                          *CAT '" "' *CAT &DSC27E *CAT '") & SG12 +
                          *EQ %RANGE("' *CAT &DSC28S *CAT '" "' +
                          *CAT &DSC28E *CAT '" ) & SG04 *EQ +
                          %RANGE("' *CAT &DSC04S *CAT '" "' *CAT +
                          &DSC04E *CAT '" ) & SG26 *EQ %RANGE(' +
                          *CAT &YMD8S  *CAT ' ' *CAT &YMD8E  *CAT +
                          ') & SG17 *EQ "            " & SG01B *NE +
                          "K" ') KEYFLD(*FILE) MAPFLD((SG01A +
                          '%SST(SG01 2 1)' *CHAR 1) (SG01B +
                          '%SST(SG01 1 1)' *CHAR 1))
/*M001D                   *CAT &YSC39S *CAT ' ' *CAT &YSC39E *CAT + */
             ENDDO

             CALL       SOR0731
             CALL       SOR0732

             CLOF       OPNID(SOSCPF)
             CLOF       OPNID(SOSGPF)
             DLTOVR     FILE(*ALL)

             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)

             CHGVAR     VAR(&P6601I) VALUE('SOR073P')
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)
             ENDPGM
