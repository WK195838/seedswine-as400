     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    AUTHOR       : A1034   STEPHANIE                         */
     /*    CREATE DATE  : 81/06/03                                  */
     /*    UPDATE DATE  :                                           */
     /*    PROGRAM NAME : SOA010 SOA0101                            */
     /*    SYSTEM       :人頭馬匯東                               */
     /*    SUBSYSTEM    :銷售訂單系統                             */
     /*    REMARK       :訂單批次處理                             */
     /***************************************************************/
             PGM

             DCL        VAR(&DSC01) TYPE(*CHAR) LEN(2)
             DCL        VAR(&WSC07S) TYPE(*CHAR) LEN(8)
             DCL        VAR(&WSC07E) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DSC04S) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DSC04E) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DSC02S) TYPE(*CHAR) LEN(12)
             DCL        VAR(&DSC02E) TYPE(*CHAR) LEN(12)
             DCL        VAR(&IN03)   TYPE(*LGL)  LEN(1)
             DCL        VAR(&IN14)   TYPE(*LGL)  LEN(1)
             DCL        VAR(&IN97)   TYPE(*LGL)  LEN(1)
             DCL        VAR(&IN96)   TYPE(*LGL)  LEN(1)

 START:      CALL       PGM(SOA010) PARM(&IN03 &IN14 &IN97 &IN96)
             IF         COND(&IN03) THEN(DO)
             CHGVAR     VAR(%SST(*LDA 601 100)) VALUE(' ')
             RETURN
             ENDDO

             IF         COND(&IN14) THEN(DO)
             CHGVAR     VAR(&DSC01) VALUE(%SST(*LDA 601 2))
             CHGVAR     VAR(&WSC07S) VALUE(%SST(*LDA 603 8))
             CHGVAR     VAR(&WSC07E) VALUE(%SST(*LDA 611 8))
             CHGVAR     VAR(&DSC04S) VALUE(%SST(*LDA 625 5))
             CHGVAR     VAR(&DSC04E) VALUE(%SST(*LDA 635 5))
             CHGVAR     VAR(&DSC02S) VALUE(%SST(*LDA 645 12))
             CHGVAR     VAR(&DSC02E) VALUE(%SST(*LDA 665 12))

             OVRDBF     FILE(SOSCPF) SHARE(*YES)
             OVRPRTF    FILE(SOA010P) USRDTA('寄賣批次')
             OVRPRTF    FILE(SOA010BP) USRDTA('寄賣批次')
             OPNQRYF    FILE((SOSCPF)) OPTION(*ALL) QRYSLT(' +
                          SC01  *EQ       ("' *CAT &DSC01 *CAT '") +
                        & SC07  *EQ %RANGE('  *CAT &WSC07S *CAT ' +
                                           '  *CAT &WSC07E *CAT ') +
                        & SC02  *EQ %RANGE("' *CAT &DSC02S *CAT '" +
                                           "' *CAT &DSC02E *CAT '") +
                        & SC04  *EQ %RANGE("' *CAT &DSC04S *CAT '" +
                                           "' *CAT &DSC04E *CAT '") +
                        & (SC03 *EQ "S3") +
                        & SC11 *EQ "     " +
                        & SC30 *EQ " " +
                        & (SC13 *EQ "C" | SC13 *EQ " ") +
                        & SC38 *EQ " " ') +
                          KEYFLD((SC02))

                        CALL       PGM(SOA0101) PARM(&IN96)

             IF         COND(&IN96 = '0') THEN(DO)
                        CHGVAR     VAR(&IN97)  VALUE('1')
             ENDDO
             CLOF       OPNID(SOSCPF)
             DLTOVR     FILE(*ALL)

             ENDDO

             GOTO       START
             ENDPGM
