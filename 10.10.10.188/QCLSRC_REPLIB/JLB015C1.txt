     /**************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250  */
     /*                                                            */
     /*    PROGRAM ID   : JLB015                                   */
     /*    PROGRAM NAME : 人工作廢發票傳票批次                   */
     /*    AUTHOR       : D910074 TINA                             */
     /*    CREATE DATE  : 1993.09.13                               */
     /*    UPDATE DATE  :                                          */
     /*    SYSTEM       : 進銷存收TO總帳                       */
     /**************************************************************/
             PGM

             DCL        VAR(&D01)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&D02)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&D03)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&D04)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&F01)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&F02)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)

             CHGVAR     VAR(&D01)   VALUE(%SST(*LDA 625 1))
             CHGVAR     VAR(&D02)   VALUE(%SST(*LDA 626 1))
             CHGVAR     VAR(&D03)   VALUE(%SST(*LDA 627 1))
             CHGVAR     VAR(&D04)   VALUE(%SST(*LDA 628 1))
             CHGVAR     VAR(&F01)   VALUE(%SST(*LDA 601 8))
             CHGVAR     VAR(&F02)   VALUE(%SST(*LDA 609 8))
     /**** WF#GPF   ****/
             CPYF       FROMFILE(WF#GPF) TOFILE(QTEMP/WF#GPF) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/WF#GPF))
             OVRDBF     FILE(WF#GPF) TOFILE(QTEMP/WF#GPF) SHARE(*YES)

     /**** WFJL10PF ****/
             CPYF       FROMFILE(WFJL10PF) TOFILE(QTEMP/WFJL10PF) +
                          CRTFILE(*YES)
             MONMSG   MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/WFJL10PF))
             OVRDBF   FILE(WFJL10PF) TOFILE(QTEMP/WFJL10PF) SHARE(*YES)

     /**** WFJL10LF ****/
             CRTDUPOBJ  OBJ(WFJL10LF) FROMLIB(REFLIB) OBJTYPE(*ALL) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPD2104 CPF2130)
             OVRDBF   FILE(WFJL10LF) TOFILE(QTEMP/WFJL10LF) SHARE(*YES)
     /**** WFJL22PF ****/
             CPYF       FROMFILE(WFJL22PF) TOFILE(QTEMP/WFJL22PF) +
                          CRTFILE(*YES)
             MONMSG   MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/WFJL22PF))
             OVRDBF   FILE(WFJL22PF) TOFILE(QTEMP/WFJL22PF) SHARE(*YES)

             OVRDBF     FILE(SOSSPF) SHARE(*YES)
             OPNQRYF    FILE((SOSSPF)) OPTION(*ALL) QRYSLT('SS01L +
                          *EQ %RANGE("' *CAT &D01 *CAT '" "' *CAT +
                          &D02 *CAT '") & SS01R *EQ %RANGE("' *CAT +
                          &D03 *CAT '" "' *CAT &D04 *CAT '") & SS35 +
                          *EQ %RANGE(' *CAT &F01 *CAT ' ' *CAT &F02 +
                          *CAT ') & (SS16 *EQ "S01") *AND SS36 *NE +
                          "V" ') KEYFLD(*FILE) MAPFLD((SS01L +
                          '%SST(SS01 1 1)' *CHAR 1) (SS01R +
                          '%SST(SS01 2 1)' *CHAR 1))


   /*產生暫存檔及傳票*/
             CALL       JLB0152

             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)
             CALL       LOG
             CHGVAR     VAR(&P6601I) VALUE('LOGP')
             CHGVAR     VAR(&P6602I) VALUE('2')
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE('00')
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)

             CLOF       OPNID(SOSSPF)
   /*試飲訂單明細報表*/
             OVRPRTF    FILE(JLB0153P) PAGESIZE(*N 198) CPI(15) +
                        USRDTA('人工作廢')
             CALL       JLB0153
             CHGVAR     VAR(&P6601I) VALUE('JLB0153P')
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)

             DLTOVR     FILE(*ALL)

             ENDPGM
