     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    AUTHOR       : ANDY                                      */
     /*    CREATE DATE  : 81/08/27                                  */
     /*    UPDATE DATE  :                                           */
     /*    PROGRAM NAME : SOR057C1                                  */
     /*    SYSTEM       :人頭馬匯東                               */
     /*    SUBSYSTEM    :主檔                                     */
     /*    REMARK       :客戶折扣報價明細表                       */
     /***************************************************************/
             PGM
             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)

             DCL        VAR(&DSA02S) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DSA02E) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DSA03S) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DSA03E) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DSB01S) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DSB01E) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DSA01S) TYPE(*CHAR) LEN(9)
             DCL        VAR(&DSA01E) TYPE(*CHAR) LEN(9)
             DCL        VAR(&WDATE)  TYPE(*CHAR) LEN(6)
/*M001A*/    DCL        VAR(&WDAT8)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&DSEL)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&WMD07)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&WMD08)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&NO)     TYPE(*CHAR) LEN(6)
             DCL        VAR(&MBR)    TYPE(*CHAR) LEN(10)

             CHGVAR     VAR(&DSA02S) VALUE(%SST(*LDA 601 1))
             CHGVAR     VAR(&DSA02E) VALUE(%SST(*LDA 602 1))
             CHGVAR     VAR(&DSA03S) VALUE(%SST(*LDA 611 2))
             CHGVAR     VAR(&DSA03E) VALUE(%SST(*LDA 613 2))
             CHGVAR     VAR(&DSB01S) VALUE(%SST(*LDA 621 5))
             CHGVAR     VAR(&DSB01E) VALUE(%SST(*LDA 626 5))
             CHGVAR     VAR(&DSA01S) VALUE(%SST(*LDA 631 9))
             CHGVAR     VAR(&DSA01E) VALUE(%SST(*LDA 641 9))
             CHGVAR     VAR(&WDATE)  VALUE(%SST(*LDA 661 6))
/*M001A*/    CHGVAR     VAR(&WDAT8)  VALUE(%SST(*LDA 850 8))
             CHGVAR     VAR(&DSEL)   VALUE(%SST(*LDA 671 1))
             CHGVAR     VAR(&WMD07)  VALUE(%SST(*LDA 672 1))
             CHGVAR     VAR(&WMD08)  VALUE(%SST(*LDA 673 2))

             OVRPRTF    FILE(SOR057P) PAGESIZE(*N 198) CPI(15) +
                        USRDTA('客搭贈明')
             OVRDBF     FILE(SOSAPF)  SHARE(*YES)
             OVRDBF     FILE(SOSBLF1) SHARE(*YES)
             OVRDBF     FILE(SOSALF09) SHARE(*YES)

             OPNQRYF    FILE((SOSAPF)) QRYSLT('( SA01 *EQ %RANGE("' +
                          *CAT &DSA01S *CAT '" "' *CAT &DSA01E *CAT +
                          '")) &  (SA02  *EQ %RANGE("' *CAT &DSA02S +
                          *CAT '" "' *CAT &DSA02E *CAT '")) & (SA03 +
                          *EQ %RANGE("' *CAT &DSA03S *CAT '" "' +
                          *CAT &DSA03E *CAT '")) & SA05 *LE ' *CAT +
                          &WDAT8 *CAT ' & SA13 *GE ' *CAT &WDAT8 +
                          *CAT ' & SA04 *EQ "A" ') KEYFLD((SA01) +
                          (SA02) (SA03) (SA04) (SA05 *DESCEND))

             OPNQRYF    FILE((SOSALF09)) QRYSLT('( SA01 *EQ +
                          %RANGE("' *CAT &DSA01S *CAT '" "' *CAT +
                          &DSA01E *CAT '")) &  (SA02  *EQ ("' *CAT +
                          &WMD07 *CAT '")) & (SA03 *EQ ("' *CAT +
                          &WMD08 *CAT '")) & SA05 *LE ' *CAT &WDAT8 +
                          *CAT ' & SA13 *GE ' *CAT &WDAT8 *CAT ' & +
                          SA04 *EQ "A" ') KEYFLD((SA01) (SA02) +
                          (SA03) (SA04) (SA05 *DESCEND))

             OPNQRYF    FILE((SOSBLF1)) QRYSLT('( SB01 *EQ %RANGE("' +
                          *CAT &DSB01S *CAT '" "' *CAT &DSB01E *CAT +
                          '")) & ( SB02 *EQ %RANGE("' *CAT &DSA01S +
                          *CAT '" "' *CAT &DSA01E *CAT '")) & (SB05 +
                          *LE ' *CAT &WDAT8 *CAT ') & (SB13 *GE ' +
                          *CAT &WDAT8 *CAT ') & SB04 *EQ "A" ') +
                          KEYFLD((SB02) (SB01) (SB04) (SB05 +
                          *DESCEND))

             CPYF       FROMFILE(REWF35) TOFILE(QTEMP/REWF35) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/REWF35))

             OVRDBF     FILE(REWF35) TOFILE(QTEMP/REWF35) SHARE(*YES)

             CALL       SOR0571
             CALL       SOR0572

             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)
             CHGVAR     VAR(&P6601I) VALUE('SOR057P')
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)

             CLOF       OPNID(SOSAPF)
             MONMSG CPF0000
             CLOF       OPNID(SOSALF09)
             MONMSG CPF0000
             CLOF       OPNID(SOSBLF1)
             MONMSG CPF0000
             DLTOVR     FILE(*ALL)

             ENDPGM
