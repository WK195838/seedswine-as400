     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    AUTHOR       : A1139  JANE                               */
     /*    CREATE DATE  : 81/01/20                                  */
     /*    UPDATE DATE  : 81/01/21                                  */
     /*    PROGRAM NAME : POR041C1                                  */
     /*    SYSTEM       :人頭馬匯東                               */
     /*    SUBSYSTEM    :採購系統                                 */
     /*    REMARK       :供應商報價明細表列印                     */
     /***************************************************************/
             PGM
             DCL        VAR(&DPA01)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&DPA01S) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DPA01E) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DDATE)  TYPE(*CHAR)  LEN(6)
             DCL        VAR(&DDETAL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)
/*M001A*/    DCL        VAR(&YMD8S)  TYPE(*CHAR) LEN(8)


/*M001A*/    CHGVAR     VAR(&YMD8S)  VALUE(%SST(*LDA 850 8))
             CHGVAR     VAR(&DPA01)  VALUE(%SST(*LDA 601 1))
             CHGVAR     VAR(&DPA01S) VALUE(%SST(*LDA 602 5))
             CHGVAR     VAR(&DPA01E) VALUE(%SST(*LDA 607 5))
             CHGVAR     VAR(&DDATE)  VALUE(%SST(*LDA 616 +
                          2) || %SST(*LDA 612 4))
             CHGVAR     VAR(&DDETAL) VALUE(%SST(*LDA 618 1))

             OVRPRTF    FILE(POR041P) PAGESIZE(*N 158) CPI(12) +
                        USRDTA('供報價明')
             OVRDBF     FILE(POPAPF) SHARE(*YES)

             IF         COND(&DDETAL *EQ 'C') THEN(DO)

             OPNQRYF    FILE((POPAPF)) QRYSLT(' +
                        PA011 *EQ "' *CAT &DPA01 *CAT '" +
                      & PA01 *EQ %RANGE("' *CAT &DPA01S *CAT '" +
                                        "' *CAT &DPA01E *CAT '") +
                      & PA04 *LE ' *CAT &YMD8S *CAT ' +
                      & PA06 *GE ' *CAT &YMD8S *CAT ' ')  +
                        KEYFLD((PA01)(PA03)) +
                        MAPFLD((PA011 '%SST(PA01 1 1)'))
/*M001D               & PA04 *LE ' *CAT &DDATE *CAT ' +
                      & PA06 *GE ' *CAT &DDATE *CAT ' ')  +   */

             ENDDO
             ELSE       CMD(DO)

             OPNQRYF    FILE((POPAPF)) QRYSLT(' +
                        PA011 *EQ "' *CAT &DPA01 *CAT '" +
                      & PA01 *EQ %RANGE("' *CAT &DPA01S *CAT '" +
                                        "' *CAT &DPA01E *CAT '")') +
                        KEYFLD((PA01)(PA03)) +
                        MAPFLD((PA011 '%SST(PA01 1 1)'))
             ENDDO

             CALL       POR0411

             CLOF       OPNID(POPAPF)
             DLTOVR     FILE(POPAPF)
             DLTOVR     FILE(POR041P)

             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)
             CHGVAR     VAR(&P6601I) VALUE('POR041P')
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)
             ENDPGM
