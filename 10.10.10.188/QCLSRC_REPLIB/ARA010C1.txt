     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    AUTHOR       : A1118   PHILIP TSAI                       */
     /*    CREATE DATE  : 81/04/27                                  */
     /*    UPDATE DATE  :                                           */
     /*    PROGRAM NAME : ARA0101                                   */
     /*    SYSTEM       :人頭馬匯東                               */
     /*    SUBSYSTEM    :應收帳款子系統                           */
     /*    REMARK       :統一發票申報表                           */
     /***************************************************************/
             PGM
             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)

             DCL        VAR(&DSI01)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&WSI21S) TYPE(*CHAR) LEN(6)
             DCL        VAR(&WSI21E) TYPE(*CHAR) LEN(6)

             CHGVAR     VAR(&DSI01)  VALUE(%SST(*LDA 601 2))
             CHGVAR     VAR(&WSI21S) VALUE(%SST(*LDA 621 6))
             CHGVAR     VAR(&WSI21E) VALUE(%SST(*LDA 627 6))

             OVRPRTF    FILE(ARA010AP) PAGESIZE(*N 132) CPI(12) +
                        USRDTA('發票申報')
             OVRPRTF    FILE(ARA010BP) USRDTA('發票申報')
             OVRPRTF    FILE(ARA010P) USRDTA('發票申報')
             OVRDBF     FILE(SOSIPF) SHARE(*YES)

             OPNQRYF    FILE((SOSIPF) (PA#BPF)) OPTION(*INP) +
                          FORMAT(SOSIPF) QRYSLT(' #B17 *EQ ("' *CAT +
                          &DSI01 *CAT '") & PSI21 *EQ %RANGE("' +
                          *CAT &WSI21S *CAT '" "' *CAT &WSI21E *CAT +
                          '") & (SI16="N") & (SI20=" ") & (PSI02 +
                          *NE "**") & (SI18 *NE "*")') +
                          KEYFLD((SI01) (SI21) (SI02)) JFLD((#B01 +
                          SI01 *EQ)) MAPFLD((CSI21 SI21 *CHAR 8) +
                          (PSI21 '%SST(CSI21 1 6)' *CHAR 6) (PSI02 +
                          '%SST(SI02 1 2)'))

             CALL       PGM(ARA0101)
             CALL       PGM(ARA0102)

             CLOF       OPNID(SOSIPF)
             DLTOVR     FILE(*ALL)

             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)
             CHGVAR     VAR(&P6601I) VALUE('ARA010AP')
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)

             CHGVAR     VAR(&P6601I) VALUE('ARA010BP')
             CHGVAR     VAR(&P6602I) VALUE(1)
             CHGVAR     VAR(&P6603I) VALUE(00)
             CHGVAR     VAR(&P6604I) VALUE(000)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)

             ENDPGM
