     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    AUTHOR       : A1139  JANE                               */
     /*    CREATE DATE  : 81/04/08                                  */
     /*    UPDATE DATE  :                                           */
     /*    PROGRAM NAME : ARR042C1                                  */
     /*    SYSTEM       :人頭馬匯東                               */
     /*    SUBSYSTEM    :應收系統                                 */
     /*    REMARK       :帳齡分析表                               */
     /***************************************************************/
             PGM
             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)

             DCL        VAR(&DSI01S) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DSI01E) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DSI03S) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DSI03E) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DSI04S) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DSI04E) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DSI12S) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DSI12E) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DSI31S) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DSI31E) TYPE(*CHAR) LEN(6)
             DCL        VAR(&YDATES) TYPE(*CHAR) LEN(6)
/*M001A*/    DCL        VAR(&YMD8)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&DSEL) TYPE(*CHAR) LEN(1)

             CHGVAR     VAR(&DSI01S) VALUE(%SST(*LDA 601 2))
             CHGVAR     VAR(&DSI01E) VALUE(%SST(*LDA 603 2))
             CHGVAR     VAR(&DSI03S) VALUE(%SST(*LDA 605 1))
             CHGVAR     VAR(&DSI03E) VALUE(%SST(*LDA 606 1))
             CHGVAR     VAR(&DSI04S) VALUE(%SST(*LDA 607 2))
             CHGVAR     VAR(&DSI04E) VALUE(%SST(*LDA 609 2))
             CHGVAR     VAR(&DSI12S) VALUE(%SST(*LDA 650 5))
             CHGVAR     VAR(&DSI12E) VALUE(%SST(*LDA 655 5))
             CHGVAR     VAR(&DSI31S) VALUE(%SST(*LDA 631 6))
             CHGVAR     VAR(&DSI31E) VALUE(%SST(*LDA 637 6))
             CHGVAR     VAR(&DSEL) VALUE(%SST(*LDA 643 1))
             CHGVAR     VAR(&YDATES) VALUE(%SST(*LDA 617 6))
             CHGVAR     VAR(&YMD8)   VALUE(%SST(*LDA 850 8))

             OVRDBF     FILE(SOSILF07) SHARE(*YES)

             IF         COND(&DSEL *EQ 'A') THEN(DO)
             OVRPRTF    FILE(ARR042AP) PAGESIZE(*N 198) CPI(15) +
                        USRDTA('帳齡分析')
             OPNQRYF    FILE((SOSILF07)) QRYSLT(' SI01 *EQ %RANGE("' +
                          *CAT &DSI01S *CAT '" "' *CAT &DSI01E *CAT +
                          '") & SI01A *NE "K" & SI12 *EQ %RANGE("' +
                          *CAT &DSI12S *CAT '" "' *CAT &DSI12E *CAT +
                          '") & SI35 *NE 0 &  SI35 *LE ' *CAT &YMD8 +
                          *CAT ' & SI03 *EQ %RANGE("' *CAT &DSI03S +
                          *CAT '" "' *CAT &DSI03E *CAT '") & SI04 +
                          *EQ %RANGE("' *CAT &DSI04S *CAT '" "' +
                          *CAT &DSI04E *CAT '")') +
                          KEYFLD((SI01) (SI12)) MAPFLD((SI01A +
                          '%sst(si01 1 1)' *CHAR 1)) /*M001A*/
/*M001D                   &YDATES *CAT ' & SI03 *EQ %RANGE("' *CAT + */

             CALL       ARR0421
             DLTOVR     FILE(ARR042AP)
             CHGVAR     VAR(&P6601I) VALUE('ARR042AP')
             ENDDO
             ELSE
             IF         COND(&DSEL *EQ 'B') THEN(DO)
             OVRPRTF    FILE(ARR042BP) PAGESIZE(*N 198) CPI(15)  +
                        USRDTA('帳齡分析')
             CPYF       FROMFILE(REWF77) TOFILE(QTEMP/REWF77) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/REWF77))
             OVRDBF     FILE(REWF77) TOFILE(QTEMP/REWF77) SHARE(*YES)

             OPNQRYF    FILE((SOSILF07)) QRYSLT(' SI01 *EQ %RANGE("' +
                          *CAT &DSI01S *CAT '" "' *CAT &DSI01E *CAT +
                          '") & SI01A *NE "K"  & SI12 *EQ %RANGE("' +
                          *CAT &DSI12S *CAT '" "' *CAT &DSI12E *CAT +
                          '") & SI35 *NE 0 & SI35 *LE ' *CAT +
                          &YMD8   *CAT ' & SI03 *EQ %RANGE("' *CAT +
                          &DSI03S *CAT '" "' *CAT &DSI03E *CAT '") +
                          & SI04 *EQ %RANGE("' *CAT &DSI04S *CAT '" +
                          "' *CAT &DSI04E *CAT '")  ') +
                          KEYFLD((SI12)) MAPFLD((SI01A '%SST(si01 1 +
                          1)' *CHAR 1))

/*M001D                   &YDATES *CAT ' & SI03 *EQ %RANGE("' *CAT + */
             CALL       ARR0422
             CALL       ARR0423
             DLTOVR     FILE(ARR042BP)
             CHGVAR     VAR(&P6601I) VALUE('ARR042BP')
             ENDDO

             CLOF       OPNID(SOSILF07)
             DLTOVR     FILE(SOSILF07)

             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)


             ENDPGM
