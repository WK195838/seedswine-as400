     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    AUTHOR       : A1084  CHARLES                            */
     /*    CREATE DATE  : 81/05/04                                  */
     /*    UPDATE DATE  :                                           */
     /*    PROGRAM NAME : SAR048C1                                  */
     /*    SYSTEM       :人頭馬匯東                               */
     /*    SUBSYSTEM    :銷售分析系統                             */
     /*    REMARK       :產品成長率分析表                         */
     /***************************************************************/
             PGM
             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)

             DCL        VAR(&DSC33S) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DSC33E) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DSC34S) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DSC34E) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DSC31S) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DSC31E) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DSD03S) TYPE(*CHAR) LEN(9)
             DCL        VAR(&DSD03E) TYPE(*CHAR) LEN(9)
             DCL        VAR(&YMS1) TYPE(*CHAR) LEN(4)
             DCL        VAR(&YME1) TYPE(*CHAR) LEN(4)
             DCL        VAR(&YMS2) TYPE(*CHAR) LEN(4)
             DCL        VAR(&YME2) TYPE(*CHAR) LEN(4)
/*M001A*/    DCL        VAR(&Y6S1) TYPE(*CHAR) LEN(6)
/*M001A*/    DCL        VAR(&Y6E1) TYPE(*CHAR) LEN(6)
/*M001A*/    DCL        VAR(&Y6S2) TYPE(*CHAR) LEN(6)
/*M001A*/    DCL        VAR(&Y6E2) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DSEL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN20) TYPE(*CHAR) LEN(1)

             CPYF       FROMFILE(REWF28) TOFILE(QTEMP/REWF28) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/REWF28))
             OVRDBF     FILE(REWF28) TOFILE(QTEMP/REWF28) SHARE(*YES)
             OVRPRTF    FILE(SAR048P) USRDTA('產成長分')
     /*  禮盒拆單瓶 */
             CPYF       FROMFILE(RES18WF) TOFILE(QTEMP/RES18WF) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/RES18WF))
             OVRDBF     FILE(RES18WF) TOFILE(QTEMP/RES18WF) SHARE(*YES)


             CHGVAR     VAR(&DSC33S) VALUE(%SST(*LDA 601 1))
             CHGVAR     VAR(&DSC33E) VALUE(%SST(*LDA 602 1))
             CHGVAR     VAR(&DSC34S) VALUE(%SST(*LDA 603 2))
             CHGVAR     VAR(&DSC34E) VALUE(%SST(*LDA 605 2))
             CHGVAR     VAR(&DSC31S) VALUE(%SST(*LDA 607 2))
             CHGVAR     VAR(&DSC31E) VALUE(%SST(*LDA 609 2))
             CHGVAR     VAR(&YMS1) VALUE(%SST(*LDA 621 4))
             CHGVAR     VAR(&YME1) VALUE(%SST(*LDA 625 4))
             CHGVAR     VAR(&YMS2) VALUE(%SST(*LDA 629 4))
             CHGVAR     VAR(&YME2) VALUE(%SST(*LDA 633 4))
/*M001A*/    CHGVAR     VAR(&Y6S1) VALUE(%SST(*LDA 671 6))
/*M001A*/    CHGVAR     VAR(&Y6E1) VALUE(%SST(*LDA 677 6))
/*M001A*/    CHGVAR     VAR(&Y6S2) VALUE(%SST(*LDA 685 6))
/*M001A*/    CHGVAR     VAR(&Y6E2) VALUE(%SST(*LDA 691 6))
             CHGVAR     VAR(&DSEL) VALUE(%SST(*LDA 637 1))
             CHGVAR     VAR(&DSD03S) VALUE(%SST(*LDA 646 9))
             CHGVAR     VAR(&DSD03E) VALUE(%SST(*LDA 655 9))
             CHGVAR     VAR(&IN20) VALUE(%SST(*LDA 750 1))

             OVRDBF     FILE(SOSCPF) SHARE(*YES)
             IF         COND(&IN20 = '0') THEN(DO)
             OPNQRYF    FILE((SOSCPF)) QRYSLT('SC33 *EQ %RANGE("' +
                          *CAT &DSC33S *CAT '" "' *CAT &DSC33E *CAT +
                          '") & SC34 *EQ %RANGE("' *CAT &DSC34S +
                          *CAT '" "' *CAT &DSC34E *CAT '") & SC31 +
                          *EQ %RANGE("' *CAT &DSC31S *CAT '" "' +
                          *CAT &DSC31E *CAT '") & (MSC08 *EQ +
                          %RANGE("' *CAT &Y6S1 *CAT '" "' *CAT +
                          &Y6E1 *CAT '") *OR MSC08 *EQ %RANGE("' +
                          *CAT &Y6S2 *CAT '"  "' *CAT &Y6E2 *CAT '" +
                          )) & SC13 *NE "*" & SC35 *NE "T" ') +
                          KEYFLD((SC31) (SC02)) MAPFLD((CSC08 SC08 +
                          *CHAR 8) (MSC08 '%SST(CSC08 1 6)' *CHAR 6))
             ENDDO
             ELSE       CMD(DO)

             OPNQRYF    FILE((SOSCPF)) QRYSLT('SC33 *EQ %RANGE("' +
                          *CAT &DSC33S *CAT '" "' *CAT &DSC33E *CAT +
                          '") & SC34 *EQ %RANGE("' *CAT &DSC34S +
                          *CAT '" "' *CAT &DSC34E *CAT '") & SC31 +
                          *EQ %RANGE("' *CAT &DSC31S *CAT '" "' +
                          *CAT &DSC31E *CAT '") & (MSC39 *EQ +
                          %RANGE("' *CAT &Y6S1 *CAT '" "' *CAT +
                          &Y6E1 *CAT '") *OR MSC39 *EQ %RANGE("' +
                          *CAT &Y6S2 *CAT '"  "' *CAT &Y6E2 *CAT '" +
                          )) & SC13 *EQ "V" & SC35 *NE "T" ') +
                          KEYFLD((SC31) (SC02)) MAPFLD((CSC39 SC39 +
                          *CHAR 8) (MSC39 '%SST(CSC39 1 6)' *CHAR 6))
             ENDDO

             OVRDBF     FILE(SOSDPF) SHARE(*YES)
             OPNQRYF    FILE((SOSDPF)) QRYSLT('SD03 *EQ %RANGE("' +
                          *CAT &DSD03S *CAT '" "' *CAT &DSD03E *CAT +
                          '")') KEYFLD(*FILE)

             CALL       SAR0481
             CALL       SAR0482

             CLOF       OPNID(SOSCPF)
             CLOF       OPNID(SOSDPF)
             DLTOVR     FILE(*ALL)



             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)
             CHGVAR     VAR(&P6601I) VALUE('SAR048P')
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)


             ENDPGM
