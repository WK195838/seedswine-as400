     A*%%TS  SD  19940307  114607  D910121     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*                                                              *
     A*    DSPF NAME    : VTA017D                                    *
     A*    AUTHOR       : MICHELLE                                   *
     A*    CREATE DATE  : 04/16/02                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : VTA017                                     *
     A*    SYSTEM       :寰盛洋酒                                  *
     A*    SUBSYSTEM    :媒體申報                                  *
     A*    REMARK       :合併手工輸入／進銷項憑證檔－製作磁片      *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF14(14 '處理作業')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'VTA017'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'合併進銷項資料'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'公司別　　:'
     A            DVP91          1   B  6 40VALUES('W' 'R' 'K' 'S')
     A                                    +2'(W/R/K/S)'
     A                                  8 26'歸屬年月　:'
     A            DVP04S         4  0B  8 40
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                      EDTWRD('  /  ')
     A                                    +2'-'
     A            DVP04E         4  0B    +2
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                      EDTWRD('  /  ')
     A                                    +2'(MM/YY)'
     A                                 12 21'+----------------------------------
     A                                      --------------+'
     A                                 13 21'|  　此作業將重新合併進銷項資料'
     A                                 13 69'|'
     A                                 14 21'|　　請按F3放棄此作業或F14執+
     A                                      行此作業'
     A                                 14 69'|'
     A                                 15 21'+----------------------------------
     A                                      --------------+'
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F14=處理作業'
      *
     A  98                             17  2'資料檔製作完成，請將檔案下載至 +
                                             PC並另命名為 '
     A  98                                  DSPATR(HI)
     A  98        FNAME          8A       +1
     A  98                                  DSPATR(HI)
     A  98                                +1'.TXT'
     A  98                                  DSPATR(HI)
     A  98                             19  2'檔案名稱：REFLIB/VAT001'
     A  98                                  DSPATR(HI)
      *
     A  98        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A                                 24 64MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
