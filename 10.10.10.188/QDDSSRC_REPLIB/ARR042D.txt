     A*%%TS  SD  19930625  161947  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : ARR042D                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/04/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR042                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :帳齡分析表                                *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930625  161947  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR042'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'帳齡分析表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 26'公司代號:'
     A            DSI01S    R        B  4 38REFFLD(SI01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  4 41'-'
     A            DSI01E    R        B  4 43REFFLD(SI01)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  6 26'通　　路:'
     A                                      DSPATR(HI)
     A            DSI03S    R        B  6 38REFFLD(SI03)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  6 41'-'
     A            DSI03E    R        B  6 43REFFLD(SI03)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                  7 26'型　　態:'
     A                                      DSPATR(HI)
     A            DSI04S    R        B  7 38REFFLD(SI04)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A                                  7 41'-'
     A            DSI04E    R        B  7 43REFFLD(SI04)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A N65                                  DSPATR(HI)
     A                                  8 26'業務員　:'
     A                                      DSPATR(HI)
     A            DSI31S    R        B  8 38REFFLD(SI31)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(HI)
     A                                  8 45'-'
     A            DSI31E    R        B  8 47REFFLD(SI31)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A N68                                  DSPATR(HI)
     A                                  9 26'客戶代號:'
     A                                      DSPATR(HI)
     A            DSI12S    R        B  9 38REFFLD(SI12)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A N69                                  DSPATR(HI)
     A                                  9 45'-'
     A            DSI12E    R        B  9 47REFFLD(SI12)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A N70                                  DSPATR(HI)
     A                                 10 26'確認日期:'
     A            DSI35          6Y 0B 10 38EDTWRD('  /  /  ')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 10 48'(MM/DD/YY)'
     A                                 12 26'選　　擇:'
     A            DSEL           1   B 12 38VALUES('A' 'B')
     A                                 12 42'(A-依公司)'
     A                                 13 42'(B-依業務員)'
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 42'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR9          50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
