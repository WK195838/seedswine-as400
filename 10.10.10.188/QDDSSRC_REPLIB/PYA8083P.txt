     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR8083P                                   *
     A*    PROGRAM NAME :產品別銷售分析明細表                      *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/01/24                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      REF(PYRF)
      *----------------------------------------------------------------
     A          R PH1
      *----------------------------------------------------------------
     A                                      SKIPB(002)
     A            CM03      R             20
     A                                      SPACEA(001)
     A                                    25
     A                                      '產品別銷售分析明細表'
     A                                      SPACEA(002)
     A                                    51
     A                                      '日期:'
     A            DDATE          6  0     59
     A                                      EDTWRD('0  /  /  ')
     A                                    68
     A                                      '頁次:'
     A                                    76
     A                                      PAGNBR
     A                                      SPACEA(002)
     A                                     1
     A                                      '部門別:'
     A            PMG03          2A       12
     A            PMG03N         8O       15
      *
     A                                    25
     A                                      '銷售年月:'
     A            PSU02          4  0     37
     A                                      EDTWRD('0  /  ')
     A                                    51
     A                                      '時間:'
     A                                    59
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    69
     A                                      '<PYR8083P>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '分店代號'
     A                                    13
     A                                      '分店名稱'
     A                                    35
     A                                      '銷售量'
     A                                    50
     A                                      '售價'
     A                                    63
     A                                      '銷售金額'
     A                                      SPACEA(001)
      *----------------------------------------------------------------
     A          R PH2
      *----------------------------------------------------------------
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEA(001)
     A                                     1
     A                                      '產品代號'
     A            PSU04     R             12
     A                                      REFFLD(SU04 SOSUPF)
     A                                    23
     A                                      '產品名稱'
     A            PMA03     R             34
     A                                      REFFLD(MA03 MTMAPF)
     A                                    63
     A                                      '單位'
     A            PMA13     R             70
     A                                      REFFLD(MA13 MTMAPF)
      *
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEA(001)
      *----------------------------------------------------------------
     A          R PD1
      *----------------------------------------------------------------
     A            PSU01     R              3
     A                                      REFFLD(SU01 SOSUPF)
     A            PME04     R             12
     A                                      REFFLD(ME04 MTMEPF)
     A            PSU06     R             33
     A                                      REFFLD(SU06 SOSUPF)
     A                                      EDTCDE(1)
     A            PSU07     R             47
     A                                      REFFLD(SU07 SOSUPF)
     A                                      EDTCDE(1)
     A            PTOT          11S 2     61
     A                                      EDTCDE(1)
     A                                      SPACEA(002)
      *----------------------------------------------------------------
     A          R PT1
      *----------------------------------------------------------------
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEA(001)
     A                                    23
     A                                      '小計'
     A            PTO01         10  2     32
     A                                      EDTCDE(1)
     A            PTO02         10  2     46
     A                                      EDTCDE(1)
     A            PTO03         12  2     60
     A                                      EDTCDE(1)
     A                                      SPACEA(001)
     A*
      *----------------------------------------------------------------
     A          R PE2
      *----------------------------------------------------------------
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A                                     1
     A                                      'PYR8083P--->報表結束'
      *----------------------------------------------------------------
     A          R PE9
      *----------------------------------------------------------------
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
