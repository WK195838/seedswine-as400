     A*%%TS  SD  19920822  084119  REPGMRP     REL-V2R1M0  5738-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : IMI026D                                    *
     A*    AUTHOR       : A1184  CHARLES                             *
     A*    CREATE DATE  : 81/04/20                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMI026                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :產品保留量查詢 (I)                        *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920807  134142  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  2VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            IA03      R        O 10  5
     A            IA02      R        O 10 16
     A            IA01      R        O 10 28
     A            IA06      R        O 10 34EDTCDE(J)
     A            IA07      R        O 10 43EDTCDE(J)
     A            IA08      R        O 10 52EDTCDE(J)
     A            IA09      R        O 10 61EDTCDE(J)
     A            IA10      R        O 10 70EDTCDE(J)
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920807  134142  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'IMI026'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'產品保留量查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'輸入作業選擇'
     A                                  6  6'5=產品保留量明細查詢'
     A                                  8  2'A'
     A                                  8  4'產品'
     A                                  8 15'物流點'
     A                                  8 26'公司'
     A                                  8 33'庫存量'
     A                                  8 42'良品量'
     A                                  8 51'壞品量'
     A                                  8 60'保留量'
     A                                  8 69'可用量'
     A            DBGN1     R        B  9  5REFFLD(IA03)
     A            DBGN2     R        B  9 16REFFLD(IA02)
     A            DBGN3     R        B  9 28REFFLD(IA01)
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A*%%TS  SD  19920821  102538  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DIR04          6Y 0O 11  3EDTWRD('  /  /  ')
     A            IR05      R        O 11 14REFFLD(IR05 *LIBL/IMIRLF09)
     A            D#203         10O  O 11 17
     A            IR06      R        O 11 28REFFLD(IR06 *LIBL/IMIRLF09)
     A            IS071     R        O 11 43REFFLD(SC04)
     A            IS072     R        O 11 50REFFLD(SC05)
     A            DQTY           5Y 0O 11 60EDTCDE(J)
     A            MA13      R        O 11 71
     A*------------------------------------------------------------
     A*
     A**                                    CSRLOC(D#ROW      D#COL)
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19920822  084119  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'IMI026'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'產品保留量明細查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  3  2'產品代號:'
     A            IA03      R        O  3 14
     A            MA031     R        O  3 24REFFLD(MA03)
     A                                  4  2'物流點　:'
     A            IA02      R        O  4 14
     A            IA02C         22O  O  4 24
     A                                  5  2'公　司　:'
     A            IA01      R        O  5 14
     A            IA01C         12O  O  5 24
     A                                  5 40'庫存量　:'
     A            IA06      R        O  5 52EDTCDE(J)
     A                                  6  2'良品量　:'
     A            IA07      R        O  6 14EDTCDE(J)
     A                                  6 40'壞品量　:'
     A            IA08      R        O  6 52EDTCDE(J)
     A                                  7  2'保留量　:'
     A            IA09      R        O  7 14EDTCDE(J)
     A                                  7 40'可用量　:'
     A            IA10      R        O  7 52EDTCDE(J)
     A                                  8  2'單據日期'
     A                                  9  3'MM/DD/YY  單別        單據編號-
     A                                           收貨物流點       數量    -
     A                                      單位'
     A                                      DSPATR(UL)
     A            DBGN21         6Y 0B 10  3EDTWRD('  /  /  ')
     A            TQTY           6Y 0O  8 59EDTCDE(J)
     A*------------------------------------------------------------
     A*
     A          R DSPC2
     A*%%TS  SD  19920420  105249  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
