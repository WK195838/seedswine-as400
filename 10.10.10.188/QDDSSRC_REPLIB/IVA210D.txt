     A*  91/02/12  11:40:42    SSPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : IVA210D                                    *
     A*    AUTHOR       : A1092  TAYLOR                              *
     A*    CREATE DATE  : 79/08/22                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IVA210                                     *
     A*    SYSTEM       :銷售系統                                  *
     A*    SUBSYSTEM    :車輛採購                                  *
     A*    REMARK       :車型進貨單建立（庫存）                    *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/IVRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A*  91/02/12  11:40:42    SSPGMR      REL-R02M00  5728-PW1
     A                                  1  2'<IVA210D>'
     A            CONAME        40A  O  1 20MSGID(US#9001 S#MF)
     A                                  1 64'日期:'
     A            $CHYMD         6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 26'車型進貨單建立（倉庫）'
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A          R DSPC3
     A*  91/02/12  11:40:42    SSPGMR      REL-R02M00  5728-PW1
     A                                      OVERLAY
     A  98        ERR1          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR3          50A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR4          50A  O 22  2MSGID(UPO 0010 POMF)
     A                                      DSPATR(HI)
     A  88        ERR5          50A  O 22  2MSGID(UPO 0009 POMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 38'PF19=進貨單列印'
     A                                 23 58'PF20=廠商明細'
     A                                 24 67'【泛太資訊】'
     A          R DSPC4
     A*  91/02/12  11:40:42    SSPGMR      REL-R02M00  5728-PW1
     A*
     A                                      OVERLAY
     A  96        ERR1          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          50A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          50A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          50A  O 22  2MSGID(UPO 0007 POMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR6          50A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          50A  O 22  2MSGID(UPO 0009 POMF)
     A                                      DSPATR(HI)
     A  88        ERR9          50A  O 22  2MSGID(UPO 0010 POMF)
     A                                      DSPATR(HI)
     A  87        ERR10         50A  O 22  2MSGID(UIV 0001 IVMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF1 =明細顯示'
     A                                 23 20'PF3 =回主畫面'
     A                                 23 36'PF5 =參考資料'
     A                                 23 53'PF12=回前畫面'
     A                                 24  2'PF19=進貨單列印'
     A                                 24 20'PF20=廠商明細'
     A                                 24 36'PA1 =下一頁'
     A                                 24 53'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
     A          R DSPC5
     A*  91/02/12  11:40:42    SSPGMR      REL-R02M00  5728-PW1
     A*
     A                                      OVERLAY
     A  96        ERR1          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          50A  O 22  2MSGID(UPT 2031 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          50A  O 22  2MSGID(UPT 2041 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          50A  O 22  2MSGID(UPO 0007 POMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR6          50A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          50A  O 22  2MSGID(UPO 0009 POMF)
     A                                      DSPATR(HI)
     A  88        ERR9          50A  O 22  2MSGID(UPO 0010 POMF)
     A                                      DSPATR(HI)
     A  87        ERR10         50A  O 22  2MSGID(UIV 0001 IVMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF1 =明細顯示'
     A                                 23 20'PF3 =回主畫面'
     A                                 23 38'PF7 =上一筆'
     A                                 23 54'PF8 =下一筆'
     A                                 24  2'PF12=回前畫面'
     A                                 24 20'PF19=進貨單列印'
     A                                 24 38'PF20=廠商明細'
     A                                 24 67'【泛太資訊】'
     A          R DSPD1
     A*  91/02/12  11:40:42    SSPGMR      REL-R02M00  5728-PW1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<IVA210D>'
     A            CONAME        40A  O  1 20MSGID(US#9001 S#MF)
     A                                  1 64'日期:'
     A            $CHYMD         6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 26'車型進貨單建立（倉庫）'
     A                                  4  2'進貨單號:'
     A            DYB01     R        B  4 14REFFLD(YB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5  2'進貨日期:'
     A            DYB021         2Y 0B  5 14
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      CHECK(RB)
     A                                      EDTWRD('  ')
     A                                  5 17'/'
     A            DYB022         2Y 0B  5 19
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      CHECK(RB)
     A                                      EDTWRD('  ')
     A                                  5 22'/'
     A            DYB023         2Y 0B  5 24
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      CHECK(RB)
     A                                      EDTWRD('  ')
     A                                  7 25'作業選擇:'
     A            DOPT           1A  B  7 37VALUES('1' '2' '4' '5' '6')
     A                                  7 43'1.新增'
     A                                  8 43'2.修改'
     A                                  9 43'4.刪除'
     A                                 10 43'5.查詢'
     A                                 11 43'6.確認'
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR6          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR5          50A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 20'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A          R DSPD2
     A*  91/02/12  11:40:42    SSPGMR      REL-R02M00  5728-PW1
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF19(19)
     A                                      CF20(20)
     A                                  4  2'進貨單號:'
     A            DYB01     R        O  4 14REFFLD(YB01)
     A                                  4 48'供應廠商:'
     A            DYB03     R        B  4 60REFFLD(YB03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DQB02         10A  O  4 69
     A                                  5  2'進貨日期:'
     A            DYB021         2Y 0O  5 14EDTWRD('  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 17'/'
     A            DYB022         2Y 0O  5 19
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      EDTWRD('  ')
     A                                  5 22'/'
     A            DYB023         2Y 0B  5 24CHECK(RB)
     A                                      EDTWRD('  ')
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  5 48'存放位置:'
     A            DYB04     R        B  5 60REFFLD(YB04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A            DQA04         10A  O  5 69
     A                                  6 20'車型編號'
     A                                  6 34'車'
     A                                  6 39'-------配　備--------預進　進-
     A                                      貨　確'
     A                                  7  5'NO採購單號'
     A                                  7 20'車型名稱'
     A                                  7 34'色　一　二　三　四　五　六　貨量-
     A                                      　數量　認'
     A                                  8  5'-- ---------- --------------- --- -
     A                                      --- --- --- --- --- --- ----  ---- -
     A                                       --'
     A          R DSPD3
     A*  91/02/12  11:40:42    SSPGMR      REL-R02M00  5728-PW1
     A                                      OVERLAY
     A                                      CA03(03)
     A  36                                  CF07(07)
     A  36                                  CF08(08)
     A                                      CF12(12)
     A                                      CF19(19)
     A                                      CF20(20)
     A                                 17  5'-- ---------- --------------- --- -
     A                                      --- --- --- --- --- --- ----  ---- -
     A                                       ----'
     A                                 18  2'備註:'
     A            DYB13     R        B 18 10REFFLD(YB13)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  65                                  DSPATR(UL)
     A N65                                  DSPATR(PR)
     A                                 18 55'合計:'
     A            DYB14          4Y 0O 18 63EDTCDE(3)
     A            DYB15          4Y 0O 18 69EDTCDE(3)
     A            DYB16          4Y 0O 18 75EDTCDE(3)
     A  32                             21 43'採購明細:'
     A  32        DOP1           2Y 0B 21 55CHECK(RB)
     A                                      EDTCDE(4)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A  33                             21 61'車身明細:'
     A  33        DYD02          2Y 0B 21 73CHECK(RB)
     A                                      EDTCDE(4)
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A          R DSPD4
     A*  91/02/12  11:40:42    SSPGMR      REL-R02M00  5728-PW1
     A                                      CA03(03 '結束執行')
     A                                  8 27'執行環境　　:'
     A            $EVR           1Y 0B  8 43EDTCDE(3)
     A                                      VALUES(1 2)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8 46'(1-即時2-批次)'
     A                                  9 27'列印份數　　:'
     A            $CPY           3Y 0B  9 43EDTCDE(3)
     A                                      CHECK(RB)
     A                                 10 27'列表機　　　:'
     A            $PRTID         1Y 0B 10 43EDTCDE(3)
     A            PRINTMARK     40A  O 12 28MSGID(UPT 0001 PTMF)
     A                                 17 27'***請準備１３２行報表紙***'
     A                                 23  2'PF3 =回主畫面'
     A                                 24 67'【泛太資訊】'
     A*--------------------------------------------------------------
     A          R DSPD5
     A*  91/02/12  11:40:42    SSPGMR      REL-R02M00  5728-PW1
     A                                      CLRL(*NO)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF19(19)
     A                                      CF20(20)
     A                                  4  2'進貨單號:'
     A            DYB01     R        O  4 14REFFLD(YB01)
     A                                  4 48'供應廠商:'
     A            DYB03     R        B  4 60REFFLD(YB03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DQB02         10A  O  4 69
     A                                  5  2'進貨日期:'
     A            DYB021         2Y 0O  5 14EDTWRD('  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 17'/'
     A            DYB022         2Y 0O  5 19
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                      EDTWRD('  ')
     A                                  5 22'/'
     A            DYB023         2Y 0B  5 24CHECK(RB)
     A                                      EDTWRD('  ')
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  5 48'存放位置:'
     A            DYB04     R        B  5 60REFFLD(YB04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A            DQA04         10A  O  5 69
     A                                  6 20'車型編號'
     A                                  6 34'車'
     A                                  6 39'-------配　備--------預進　進-
     A                                      貨　確'
     A                                  7  5'NO採購單號'
     A                                  7 20'車型名稱'
     A                                  7 34'色　一　二　三　四　五　六　貨量-
     A                                      　數量　認'
     A                                  8  5'-- ---------- --------------- --- -
     A                                      --- --- --- --- --- --- ----  ---- -
     A                                       --'
     A                                 17  5'-- ---------- --------------- --- -
     A                                      --- --- --- --- --- --- ----  ---- -
     A                                       ----'
     A                                 18  2'備註:'
     A            DYB13     R        B 18 10REFFLD(YB13)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  65                                  DSPATR(UL)
     A N65                                  DSPATR(PR)
     A                                 18 55'合計:'
     A            DYB14          4Y 0O 18 63EDTCDE(3)
     A            DYB15          4Y 0O 18 69EDTCDE(3)
     A            DYB16          4Y 0O 18 75EDTCDE(3)
     A                                 20  2'                                  -
     A                                                                       '
     A*--------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4  0H
     A            DYC02S         2Y 0O  9  5EDTCDE(4)
     A            DYC03S    R        O  9  8REFFLD(YC03)
     A            DYC04S         2Y 0O    +1EDTCDE(4)
     A            ZK05      R        O    +1REFFLD(ZK05 *LIBL/PORF)
     A            ZK06      R        O    +1REFFLD(ZK06 *LIBL/PORF)
     A            ZK10      R        O    +1REFFLD(ZK10 *LIBL/PORF)
     A            ZK11      R        O    +1REFFLD(ZK11 *LIBL/PORF)
     A            ZK12      R        O    +1REFFLD(ZK12 *LIBL/PORF)
     A            ZK13      R        O    +1REFFLD(ZK13 *LIBL/PORF)
     A            ZK14      R        O    +1REFFLD(ZK14 *LIBL/PORF)
     A            ZK15      R        O    +1REFFLD(ZK15 *LIBL/PORF)
     A            DYC08S         4  0O    +1
     A                                      EDTCDE(3)
     A            DYC09S         4  0O    +2
     A                                      EDTCDE(3)
     A            YC24      R        O    +2
     A            DZC02S        22   O 10 16
     A            ZK16      R        O    +1REFFLD(ZK16 *LIBL/PORF)
     A            ZK17      R        O    +1REFFLD(ZK17 *LIBL/PORF)
     A            ZK18      R        O    +1REFFLD(ZK18 *LIBL/PORF)
     A            ZK19      R        O    +1REFFLD(ZK19 *LIBL/PORF)
     A            ZK20      R        O    +1REFFLD(ZK20 *LIBL/PORF)
     A            ZK21      R        O    +1REFFLD(ZK21 *LIBL/PORF)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0004)
     A                                      SFLDROP(CF01)
     A                                      CA03(03)
     A N36                                  CF05(05)
     A  36                                  CF07(07)
     A  36                                  CF08(08)
     A                                      CF12(12)
     A                                      CF19(19)
     A                                      CF20(20)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                 17  5'-- ---------- --------------- --- -
     A                                      --- --- --- --- --- --- ----  ---- -
     A                                       ----'
     A            DYC02          2Y 0B 18  5EDTCDE(4)
     A                                      CHECK(RB)
     A  81                                  DSPATR(PC RI)
     A            DYC03     R        B    +1REFFLD(YC03)
     A  82                                  DSPATR(PC RI)
     A            DYC04          2Y 0B    +1EDTCDE(4)
     A                                      CHECK(RB)
     A  83                                  DSPATR(PC RI)
     A            DZK05     R        O    +1REFFLD(ZK05 *LIBL/PORF)
     A            DZK06     R        O    +1REFFLD(ZK06 *LIBL/PORF)
     A            DZK10     R        O    +1REFFLD(ZK10 *LIBL/PORF)
     A            DZK11     R        O    +1REFFLD(ZK11 *LIBL/PORF)
     A            DZK12     R        O    +1REFFLD(ZK12 *LIBL/PORF)
     A            DZK13     R        O    +1REFFLD(ZK13 *LIBL/PORF)
     A            DZK14     R        O    +1REFFLD(ZK14 *LIBL/PORF)
     A            DZK15     R        O    +1REFFLD(ZK15 *LIBL/PORF)
     A            DYC08          4  0B    +1
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A  84                                  DSPATR(PC RI)
     A            DYC09          4  0O    +2
     A                                      EDTCDE(3)
     A            DYC24     R        O    +2REFFLD(YC24)
     A            DZC02         22   O 19 16
     A            DZK16     R        O    +1REFFLD(ZK16 *LIBL/PORF)
     A            DZK17     R        O    +1REFFLD(ZK17 *LIBL/PORF)
     A            DZK18     R        O    +1REFFLD(ZK18 *LIBL/PORF)
     A            DZK19     R        O    +1REFFLD(ZK19 *LIBL/PORF)
     A            DZK20     R        O    +1REFFLD(ZK20 *LIBL/PORF)
     A            DZK21     R        O    +1REFFLD(ZK21 *LIBL/PORF)
     A                                 20  2'功能:'
     A            DOP            1   B 20 10VALUES('0' '1' '2' '4')
     A                                    +2'(0.結束1.新增2.修改4.刪除)'
     A  32                             21 43'採購明細:'
     A  32        DOP1           2  0B    +1
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A  79                                  DSPATR(PC RI)
     A  32                                  DSPATR(UL)
     A  33                             21 61'車身明細:'
     A  33        DYD02          2  0B 21 73
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A  78                                  DSPATR(PC RI)
     A  33                                  DSPATR(UL)
