     A*%%TS  SD  20020917  175610  REPGMR      REL-V4R4M0  5769-PW1
     A*  81/03/09  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : SOA007D                                    *
     A*    AUTHOR       : A1118  PHILIP                              *
     A*    CREATE DATE  : 81/03/09                                   *
     A*    UPDATE DATE  : 81/04/17 PHILIP                            *
     A*    PROGRAM NAME : SOA007                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售系統                                  *
     A*    REMARK       :產品收回處理 (A/U/D/I)                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.11 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M004      | 00.11.23 |開放修正業務員代號
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920608  165730  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            HSN09          1A  H
     A            SN11      R        H
     A            DOPT1          1A  B  9  2VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
M001MA            DSN05          6Y 0O  9  5
     A                                      EDTWRD('  /  /  ')
     A            SN02      R        O  9 16
     A            SN03      R        O  9 30
     A            ME0411        18A  O  9 36
     A            SN04      R        O  9 56
M001MA            DSN06          6Y 0O  9 70
     A                                      EDTWRD('  /  /  ')
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920608  165730  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'SOA007'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'產品收回處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  6'2=修改'
     A                                  5 16'4=刪除'
     A                                  5 26'5=查詢'
     A                                  6  4'收回日期'
     A                                  7  2'A  MM/DD/YY  單據號碼    出貨+
     A                                      物流點              收貨物流點 -
     A                                      實際入庫日'
     A                                      DSPATR(UL)
M001MA*           DBGN1     R        B  8  5REFFLD(SN05)
M001MA            DBGN1          6Y 0B  8  5
     A                                      EDTWRD('  /  /  ')
     A            DBGN2     R        B  8 16REFFLD(SN02)
     A*------------------------------------------------------------
     A*
     A          R DSPC1
     A*%%TS  SD  19920929  115247  D910095     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F6 =新增'
     A  70                             24  2'單據號碼'
     A                                      DSPATR(HI)
     A  70        DSN02     R        O 24 13REFFLD(SN02)
     A                                      DSPATR(RI)
     A  70                             24 26'新增成功'
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0018 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0063 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 22 38'F9 =列印'
     A*------------------------------------------------------------
     A*
     A          R DSPD2
     A*%%TS  SD  19920520  104015  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A            HSN03     R        H      REFFLD(SN03)
     A                                  1  2'SOA007'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'產品收回處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6  2'公司代號　:'
     A            DSN01     R        B  6 16REFFLD(SN01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            #B03      R        O  6 19
     A                                  8  2'單據號碼　:'
     A            DSN02     R        O  8 16REFFLD(SN02)
     A                                 10  2'出貨物流點:'
     A                                      DSPATR(HI)
     A            DSN03     R        B 10 16REFFLD(SN03)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSN11     R        B 10 22REFFLD(SN11)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            ME0412        22A  O 10 27
     A                                 12 48'業務員:'
     A                                      DSPATR(HI)
M004MA*           DSN07     R        O 12 58REFFLD(SN07)
M004MA            DSN07     R        B 12 58REFFLD(SN07)
M004AA N67                                  DSPATR(HI)
M004AA  67                                  DSPATR(PC)
M004AA  67                                  DSPATR(RI)
M004AA  31                                  DSPATR(UL)
M004AA N31                                  DSPATR(PR)
     A            DMC02     R        O 12 65REFFLD(MC02)
     A                                 12  2'收貨物流點:'
     A                                      DSPATR(HI)
     A            DSN04     R        B 12 16REFFLD(SN04)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            #A0322        22A  O 12 22
     A            DSN08     R        H      REFFLD(SN08)
     A                                 14  2'收回日期　:'
M001MA            DSN05          6Y 0B 14 16
     A                                      EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 26'(MM/DD/YY)'
     A                                 16  2'實際入庫日:'
M001MA            DSN06          6Y 0O 16 16
     A                                      EDTWRD('  /  /  ')
     A                                 16 26'(MM/DD/YY)'
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A                                 22 58'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0015 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0016 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  84        ERR13         63A  O 24  2MSGID(URE 0064 REMF)
     A                                      DSPATR(HI)
     A  83        ERR14         63A  O 24  2MSGID(URE 0111 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPC3
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F4 =視窗查詢'
     A                                    +2'F5 =參考資料'
     A                                    +2'F12=回前畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR9          63A  O 24  2MSGID(UPT 0080 PTMF)
     A                                      DSPATR(HI)
     A  86        ERR10         63A  O 24  2MSGID(URE 0090 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  84        ERR13         63A  O 24  2MSGID(URE 0113 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A*%%TS  SD  20020917  175458  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            HMA07          1A  H
     A            DOPT2          1A  B 12  2VALUES(' ' '1' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DSO03     R        B 12  5REFFLD(SO03)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            MA03      R        O 12 15
     A            MA13      R        O 12 34
     A            HSO04     R        H      REFFLD(SO04)
     A            DSO06     R        B 12 43REFFLD(SO06)
     A                                      EDTCDE(1)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DSO07     R        B 12 74REFFLD(SO07)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A            DCHK           1A  H
     A            DSO12     R        O 12 51REFFLD(RERFR/SO12 *LIBL/RERF)
     A                                      EDTCDE(3)
     A            DAMT      R        O 12 62REFFLD(RERFR/SE37 *LIBL/RERF)
     A                                      EDTCDE(3)
     A*                                     'Y' -已存檔中--> UPD
     A*                                     ' ' -不存檔中--> ADD
     A*------------------------------------------------------------
     A*
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19920706  115253  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0160)
     A                                      SFLPAG(0008)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOA007'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 33'產品收回處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號　:'
     A            DSN01     R        O  4 16REFFLD(SN01)
     A            #B03      R        O  4 19
     A                                  5  2'單據號碼　:'
     A            DSN02     R        O  5 16REFFLD(SN02)
     A                                  5 50'業務員　:'
M004AA                                      DSPATR(HI)
     A            DSN07     R        O  5 62REFFLD(SN07)
M004AA                                      DSPATR(HI)
     A            DMC02     R        O  5 69REFFLD(MC02)
     A                                  6  2'出貨物流點:'
     A            DSN03     R        O  6 16REFFLD(SN03)
     A            DSN11     R        O  6 22REFFLD(SN11)
     A            ME0412        22A  O  6 27
     A                                  6 50'收回日期:'
M001MA            DSN05          6Y 0O  6 62
     A                                      EDTWRD('  /  /  ')
     A                                  6 71'(MM/DD/YY)'
     A                                  7  2'收貨物流點:'
     A            DSN04     R        O  7 16REFFLD(SN04)
     A            #A0322        22A  O  7 22
     A                                  7 50'實際入庫:'
M001MA            DSN06          6Y 0O  7 62
     A                                      EDTWRD('  /  /  ')
     A                                  7 71'(MM/DD/YY)'
     A                                  9  4'1=新增'
     A                                  9 16'2=修改'
     A                                  9 28'4=刪除'
     A                                  9 40'5=庫存查詢'
     A                                 11  2'A 產品代號／名稱              +
     A                                      單位　訂購數量  單價   　金　+
     A                                      額'
     A                                      DSPATR(UL)
     A                                  8 48'收回總金額:'
     A            DSUM           9Y 0O  8 62EDTCDE(1)
