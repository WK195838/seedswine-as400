     A*%%TS  SD  20021018  150504  REPGMR      REL-V4R4M0  5769-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : SOA003D                                    *
     A*    AUTHOR       : A1092  TAYLOR                              *
     A*    CREATE DATE  : 81/02/10                                   *
     A*    UPDATE DATE  : 81/04/17 PHILIP                            *
     A*    PROGRAM NAME : SOA003                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :銷售訂單處理 (A/U/D/I)                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.11 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M002      | 00.04.25 | FOR DUTY菸酒稅法實施
     A*---------------------------------------------------------------
     A* M004      | 00.11.23 |開放修正業務員代號
     A*---------------------------------------------------------------
     A* M005 |MICHELLE| 09.17.02 | SC06變更為活動代碼
     A*---------------------------------------------------------------
     A* M007 | 03.05.06 |葡萄酒部門訂單，只能由該部門修正或刪除
     A*---------------------------------------------------------------
     A* M008 | 05.05.04 |企業客戶不得開立二聯式發票
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920921  100330  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            HSC08     R        H      REFFLD(SC08)
     A            HSC38          1A  H
     A            DOPT1          1A  B  9  2VALUES(' ' '2' '4' '5' '6' '7')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
M001MA*           SC08      R        O  9  5EDTWRD('  /  /  ')
M001MA            LSC08          6Y 0O  9  5EDTWRD('  /  /  ')
     A            SC02      R        O  9 15
     A            SC03      R        O  9 28
     A            D#203         10A  O  9 31
     A            SC31      R        O  9 42
     A            SC04      R        O  9 46
     A            SC05      R        O  9 52
     A            ME04      R        O  9 57
     A            SC13      R        O  9 76
     A            DSTA           1   O  9 79
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920921  100330  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'SOA003'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'銷售訂單處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'輸入作業選擇:'
     A                                  5  4'2=修改'
     A                                  5 14'4=刪除'
     A                                  5 24'5=查詢'
     A                                  5 34'6=訂單明細'
     A                                  6 41'地'
     A                                  7  2'A 送貨日期訂單號碼   單別   -
     A                                           區客戶代號  客戶簡稱    -
     A                                         態 D'
     A                                      DSPATR(UL)
     A                                  6 74'狀'
M001MA*           DBGN1     R        B  8  5REFFLD(SC07)
M001MA            DBGN1          6Y 0B  8  5
     A                                      EDTWRD('  /  /  ')
     A            DBGN2     R        B  8 15REFFLD(SC02)
     A                                  5 48'7=核準後修改'
     A*
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R DSPC1
     A*%%TS  SD  19920908  162205  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F6 =新增'
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0021 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0022 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0023 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0026 REMF)
     A                                      DSPATR(HI)
     A  84        ERR13         63A  O 24  2MSGID(URE 0063 REMF)
     A                                      DSPATR(HI)
     A  83        ERR14         63A  O 24  2MSGID(URE 0140 REMF)
     A                                      DSPATR(HI)
M007AA  82        ERR15         63A  O 24  2MSGID(URE 0166 REMF)
     A                                      DSPATR(HI)
     A  90        ERR8          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A  80                             24  2'訂單號碼'
     A                                      DSPATR(HI)
     A  80        DSC02     R        O 24 13REFFLD(SC02)
     A  80                                  DSPATR(RI)
     A  80                             24 26'新增成功'
     A                                      DSPATR(HI)
     A                                 22 40'F9 =訂單明細列印'
     A          R DSPD2
     A*%%TS  SD  20021008  111710  REPGMR      REL-V4R4M0  5769-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A            HSC11     R        H      REFFLD(SC11)
     A            DSC36     R        H      REFFLD(SC36)
     A            HSC36     R        H      REFFLD(SC36)
     A            HSC33     R        H      REFFLD(SC0/SC33 *LIBL/SOSCPF)
     A            HSC34     R        H      REFFLD(SC0/SC34 *LIBL/SOSCPF)
     A                                  1  2'SOA003'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'銷售訂單處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號  :'
     A            DSC01     R        B  5 16REFFLD(SC01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            D#B031    R        O  5 19REFFLD(#B03)
     A                                  5 43'地區代號:'
     A            DSC31     R        O  5 55REFFLD(SC0/SC31 *LIBL/SOSCPF)
     A            #D02      R        O  5 58
     A                                  6  2'客戶代號  :'
     A N61                                  DSPATR(HI)
     A            DSC04     R        B  6 16REFFLD(SC0/SC04 *LIBL/SOSCPF)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSC05     R        B  6 22REFFLD(SC0/SC05 *LIBL/SOSCPF)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DME04     R        O  6 27REFFLD(ME04)
     A                                  6 47'單別:'
     A N62                                  DSPATR(HI)
     A            DSC03     R        B  6 55REFFLD(SC0/SC03 *LIBL/SOSCPF)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            #203      R        O  6 58
     A                                  7  2'訂單號碼  :'
     A            DSC02     R        O  7 16REFFLD(SC0/SC02 *LIBL/SOSCPF)
M005MA*                                 7 39'客戶訂單號碼:'
M005MA                                  7 39'促銷活動代碼:'
     A            DSC06     R        B  7 55REFFLD(SC0/SC06 *LIBL/SOSCPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'訂單日期  :'
     A            DSC07          6Y 0B  8 16EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 43'送貨日期:'
     A            DSC08          6Y 0B  8 55EDTWRD('  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  9  2'發票聯數  :'
     A            DSC09     R        B  9 16REFFLD(SC0/SC09 *LIBL/SOSCPF)
     A                                      EDTCDE(Z)
     A                                      VALUES(0 2 3)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 19'(2.二聯 3.三聯)'
     A                                  9 43'試飲原因:'
     A N65                                  DSPATR(HI)
     A            DSC10     R        B  9 55REFFLD(SC0/SC10 *LIBL/SOSCPF)
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A03         22A  O  9 58
     A                                 10  2'出貨物流點:'
     A N66                                  DSPATR(HI)
     A            DSC11     R        B 10 16REFFLD(SC0/SC11 *LIBL/SOSCPF)
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSC11C        22O  O 10 22
     A                                 10 45'業務員:'
     A                                      DSPATR(HI)
     A            DSC27     R        B 10 55REFFLD(SC27)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            MC02      R        O 10 62REFFLD(MC0/MC02 *LIBL/MTMCPF)
     A                                 11  2'訂單狀態  :'
     A            DSC13     R        O 11 16REFFLD(SC0/SC13 *LIBL/SOSCPF)
     A            DSC13C        10O  O 11 18
     A                                 11 45'總金額:'
     A            DSC12     R        O 11 57REFFLD(SC0/SC12 *LIBL/SOSCPF)
     A                                      EDTCDE(2)
     A                                 12 45'總稅額:'
     A            DTAX          10Y 0O 12 55EDTCDE(2)
     A                                 13 41'含稅總金額:'
     A            DTOT          10Y 0O 13 55EDTCDE(2)
     A                                 12  2'課稅別    :'
     A            SC37      R        O 12 16REFFLD(SC37)
     A                                 12 18'(D-應稅B-免稅Z-零稅)'
     A                                 13  2'統一編號  :'
     A            DMD17     R        O 13 16REFFLD(MD17)
     A                                 14  2'發票地址  :'
     A            DSC14     R        B 14 16REFFLD(SC0/SC14 *LIBL/SOSCPF)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A N68                                  DSPATR(HI)
     A            DSC15     R        B 14 22REFFLD(SC0/SC15 *LIBL/SOSCPF)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A N68                                  DSPATR(HI)
     A            ME031     R        O 14 27REFFLD(ME03)
     A            ME051     R        O 15 16REFFLD(ME05)
     A            ME061     R        O 15 49REFFLD(ME06)
     A                                 16  2'送貨地址  :'
     A            DSC16     R        B 16 16REFFLD(SC0/SC16 *LIBL/SOSCPF)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A N69                                  DSPATR(HI)
     A            DSC17     R        B 16 22REFFLD(SC0/SC17 *LIBL/SOSCPF)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A N69                                  DSPATR(HI)
     A            ME032     R        O 16 27REFFLD(ME03)
     A            ME052     R        O 17 16REFFLD(ME05)
     A            ME062     R        O 17 49REFFLD(ME06)
     A                                 18  2'訂單附註: 1.'
     A            DSC18     R        B 18 17REFFLD(SC0/SC18 *LIBL/SOSCPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19 14'2.'
     A            DSC19     R        B 19 17REFFLD(SC0/SC19 *LIBL/SOSCPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20 14'3.'
     A            DSC20     R        B 20 17REFFLD(SC0/SC20 *LIBL/SOSCPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 21  2'核準備註  :'
     A            DSC21     R        B 21 16REFFLD(SC0/SC21 *LIBL/SOSCPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 22  2'送貨單號  :'
     A            SC29      R        O 22 16REFFLD(SC0/SC29 *LIBL/SOSCPF)
     A                                 21 45'確認日期:'
     A            DSC39          6Y 0O 21 57EDTWRD('  /  /  ')
     A                                 22 45'發票號碼:'
     A            SC30      R        O 22 57REFFLD(SC0/SC30 *LIBL/SOSCPF)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =視窗查詢'
     A                                 23 42'F5 =參考資料'
     A                                 23 58'F12=回前畫面'
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR8          63A  O 24  2MSGID(URE 0024 REMF)
     A                                      DSPATR(HI)
     A  87        ERR9          63A  O 24  2MSGID(URE 0027 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0016 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  84        ERR13         63A  O 24  2MSGID(URE 0029 REMF)
     A                                      DSPATR(HI)
     A  83        ERR14         63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  82        ERR15         63A  O 24  2MSGID(URE 0015 REMF)
     A                                      DSPATR(HI)
     A  81        ERR16         63A  O 24  2MSGID(URE 0042 REMF)
     A                                      DSPATR(HI)
     A  80        ERR17         63A  O 24  2MSGID(URE 0064 REMF)
     A                                      DSPATR(HI)
     A  79        ERR18         63A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  78        ERR19         63A  O 24  2MSGID(URE 0082 REMF)
     A                                      DSPATR(HI)
     A  77        ERR20         63A  O 24  2MSGID(URE 0092 REMF)
     A                                      DSPATR(HI)
     A  76        ERR21         63A  O 24  2MSGID(URE 0105 REMF)
     A                                      DSPATR(HI)
     A  75        ERR22         63A  O 24  2MSGID(URE 0111 REMF)
     A                                      DSPATR(HI)
     A* 74        ERR23         63A  O 24  2MSGID(URE 0134 REMF)
M008MA  74        ERR23         63A  O 24  2MSGID(URE 0167 REMF)
     A                                      DSPATR(HI)
M008MA  73        ERR24         63A  O 24  2MSGID(UPT 5018 PTMF)
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPC3
     A*%%TS  SD  19920127  162226  REPGMR      REL-V2R1M0  5738-PW1
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =視窗查詢'
     A                                 23 42'F5 =參考資料'
     A                                 23 58'F12=回前畫面'
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR8          63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  87        ERR9          63A  O 24  2MSGID(URE 0029 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0028 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0059 REMF)
     A                                      DSPATR(HI)
     A  84        ERR13         63A  O 24  2MSGID(URE 0065 REMF)
     A                                      DSPATR(HI)
     A  83        ERR14         63A  O 24  2MSGID(URE 0112 REMF)
     A                                      DSPATR(HI)
     A  82        ERR15         63A  O 24  2MSGID(URE 0127 REMF)
     A                                      DSPATR(HI)
M008MA  73        ERR24         63A  O 24  2MSGID(UPT 5018 PTMF)
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R SFLSR2                    SFL
     A*%%TS  SD  20021018  150504  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 13  3
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                      VALUES(' ' '0' '1' '2' '4' '5' '6')
     A            DSD03     R        B 13  6REFFLD(SD03)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            MA03      R        O 13 16REFFLD(MA03)
     A            MA13      R        O 13 35REFFLD(MA13)
     A            DSD04     R        B 13 42REFFLD(SD04)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                      EDTCDE(2)
     A            HSD04     R        H      REFFLD(SD04)
     A            DSD05     R        B 13 51REFFLD(SD05)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A            HSD05     R        H      REFFLD(SD05)
     A            DSD06     R        B 13 57REFFLD(SD06)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A            DSD07          9Y 0O 13 68EDTCDE(3)
     A            DSD071        11S 2H
     A            HSD07     R        H      REFFLD(SD07)
     A            HSD08     R        H      REFFLD(SD08)
     A            HSD09     R        H      REFFLD(SD09)
     A            HSD10     R        H      REFFLD(SD10)
     A            HSD11     R        H      REFFLD(SD11)
     A            HSD12     R        H      REFFLD(SD12)
     A            HSD13     R        H      REFFLD(SD13)
     A            HSD15     R        H      REFFLD(SD15)
     A            HSD16     R        H      REFFLD(SD16)
     A            DCHK           1A  H
     A*                                     'Y' -已存檔中--> UPD
     A*                                     ' ' -不存檔中--> ADD
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19940216  174226  D910121     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOA003'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 33'銷售訂單資料'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A            DSC01     R        O  5 14REFFLD(SC01)
     A            D#B03         12O  O  5 17
     A                                  5 30'地區'
     A            DSC31     R        O  5 37REFFLD(SC0/SC31 *LIBL/SOSCPF)
     A            D#D02         12O  O  5 40
     A                                  5 53'客戶代號:'
     A            DSC04     R        O  5 65REFFLD(SC0/SC04 *LIBL/SOSCPF)
     A            DSC05     R        O  5 72REFFLD(SC0/SC05 *LIBL/SOSCPF)
     A            DME04     R        O  6 62REFFLD(ME04)
     A                                  6 28'單別:'
     A            DSC03     R        O  6 36REFFLD(SC0/SC03 *LIBL/SOSCPF)
     A            #203      R        O  6 39
     A                                  6  2'訂單號碼:'
     A            DSC02     R        O  6 14REFFLD(SC0/SC02 *LIBL/SOSCPF)
     A                                  7  2'訂單日期:'
M001MA            DSC07          6Y 0O  7 14
     A                                      EDTWRD('  /  /  ')
     A                                  7 26'交貨日期:'
M001MA            DSC08          6Y 0O  7 38
     A                                      EDTWRD('  /  /  ')
     A                                  7 51'出貨物流點:'
     A            DSC11     R        O  7 65REFFLD(SC0/SC11 *LIBL/SOSCPF)
     A                                  8 55'總金額:'
     A            DSC12     R        O  8 67REFFLD(SC0/SC12 *LIBL/SOSCPF)
     A                                      EDTCDE(2)
M002AA                                  9 51'含稅總金額:'
M002AA            DTOT          10  0O  9 65
M002AA                                      EDTCDE(2)
     A                                  8  4'課稅別:'
     A            SC37      R        O  8 14REFFLD(SC37)
     A                                  8 18'(D-應稅  B-免稅  Z-零稅)'
     A                                  9  4'1=新增'
     A                                  9 14'2=修改'
     A                                  9 24'4=刪除'
     A                                  9 34'5=庫存查詢'
     A                                 11 43'訂貨 搭贈'
     A                                 12  3'A 產品代號／名稱              +
     A                                      單位  數量 數量  單價     -
     A                                      金　額'
     A                                      DSPATR(UL)
M002MA*                                 9 49'客戶型態:'
M002MA*           ASC34     R        O  9 61REFFLD(RERFR/MD08 *LIBL/RERF)
M002MA*           #C031         16A  O  9 64
M002MA                                 10 49'客戶型態:'
M002MA            ASC34     R        O 10 61REFFLD(RERFR/MD08 *LIBL/RERF)
M002MA            #C031         16A  O 10 64
     A                                 10  4'0=搭贈＝０'
     A                                 10 34'6=年份查詢'
     A          R DSPD4
     A*%%TS  SD  19920610  170135  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOA003'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR004'
     A                                  2 33'銷售訂單處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號  :'
     A            DSC01     R        O  5 16REFFLD(SC01)
     A            D#B031    R        O  5 19REFFLD(#B03)
     A                                  5 43'地區代號:'
     A            DSC31     R        O  5 55REFFLD(SC0/SC31 *LIBL/SOSCPF)
     A            #D02      R        O  5 58
     A                                  6  2'客戶代號  :'
     A N61                                  DSPATR(HI)
     A            DSC04     R        O  6 16REFFLD(SC0/SC04 *LIBL/SOSCPF)
     A            DSC05     R        B  6 22REFFLD(SC0/SC05 *LIBL/SOSCPF)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DME04     R        O  6 27REFFLD(ME04)
     A                                  6 47'單別:'
     A N62                                  DSPATR(HI)
     A            DSC03     R        O  6 55REFFLD(SC0/SC03 *LIBL/SOSCPF)
     A            #203      R        O  6 58
     A                                  7  2'訂單號碼  :'
     A            DSC02     R        O  7 16REFFLD(SC0/SC02 *LIBL/SOSCPF)
     A*M005M                            7 39'客戶訂單號碼:'
M005MA                                  7 39'促銷活動代碼:'
     A            DSC06     R        B  7 55REFFLD(SC0/SC06 *LIBL/SOSCPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'訂單日期  :'
M001MA            DSC07          6Y 0O  8 16
     A                                      EDTWRD('  /  /  ')
     A                                  8 43'交貨日期:'
M001MA            DSC08          6Y 0B  8 55
     A                                      EDTWRD('  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                  9  2'發票聯數  :'
     A            DSC09     R        B  9 16REFFLD(SC0/SC09 *LIBL/SOSCPF)
     A                                      EDTCDE(Z)
     A                                      VALUES(0 2 3)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 19'(2.二聯 3.三聯)'
     A                                  9 43'試飲原因:'
     A N65                                  DSPATR(HI)
     A            DSC10     R        O  9 55REFFLD(SC0/SC10 *LIBL/SOSCPF)
     A            D#A03         22A  O  9 58
     A                                 10  2'出貨物流點:'
     A N66                                  DSPATR(HI)
     A            DSC11     R        O 10 16REFFLD(SC0/SC11 *LIBL/SOSCPF)
     A            DSC11C        22O  O 10 22
     A            HSC11     R        H      REFFLD(SC11)
     A            DSC36     R        H      REFFLD(SC36)
     A            HSC36     R        H      REFFLD(SC36)
     A                                 10 45'業務員:'
M004AA                                      DSPATR(HI)
     A            DSC27     R        O 10 55REFFLD(SC27)
M004AA                                      DSPATR(HI)
     A            MC02      R        O 10 62REFFLD(MC0/MC02 *LIBL/MTMCPF)
     A                                 11  2'訂單狀態  :'
     A            DSC13     R        O 11 16REFFLD(SC0/SC13 *LIBL/SOSCPF)
     A            DSC13C        10O  O 11 18
     A                                 11 45'總金額:'
     A            DSC12     R        O 11 57REFFLD(SC0/SC12 *LIBL/SOSCPF)
     A                                      EDTCDE(2)
M002AA                                 12 45'總稅額:'
M002AA*           DTAX           7  0O 12 59
M002AA            DTAX          10  0O 12 59
M002AA                                      EDTCDE(2)
M002AA                                 13 41'含稅總金額:'
M002AA            DTOT          10  0O 13 55
M002AA                                      EDTCDE(2)
     A                                 12  2'課稅別    :'
     A            SC37      R        O 12 16REFFLD(SC37)
     A                                 12 18'(D-應稅B-免稅Z-零稅)'
M002MA*                                12 45'統一編號:'
M002MA*           DMD17     R        O 12 57REFFLD(MD17)
M002MA                                 13  2'統一編號  :'
M002MA            DMD17     R        O 13 16REFFLD(MD17)
     A                                 14  2'發票地址  :'
     A            DSC14     R        O 14 16REFFLD(SC0/SC14 *LIBL/SOSCPF)
     A            DSC15     R        O 14 22REFFLD(SC0/SC15 *LIBL/SOSCPF)
     A            ME031     R        O 14 27REFFLD(ME03)
     A            ME051     R        O 15 16REFFLD(ME05)
     A            ME061     R        O 15 49REFFLD(ME06)
     A                                 16  2'送貨地址  :'
     A            DSC16     R        O 16 16REFFLD(SC0/SC16 *LIBL/SOSCPF)
     A            DSC17     R        B 16 22REFFLD(SC0/SC17 *LIBL/SOSCPF)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            ME032     R        O 16 27REFFLD(ME03)
     A            ME052     R        O 17 16REFFLD(ME05)
     A            ME062     R        O 17 49REFFLD(ME06)
     A                                 18  2'訂單附註: 1.'
     A            DSC18     R        B 18 17REFFLD(SC0/SC18 *LIBL/SOSCPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19 14'2.'
     A            DSC19     R        B 19 17REFFLD(SC0/SC19 *LIBL/SOSCPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20 14'3.'
     A            DSC20     R        B 20 17REFFLD(SC0/SC20 *LIBL/SOSCPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 21  2'核準備註  :'
     A            DSC21     R        B 21 16REFFLD(SC0/SC21 *LIBL/SOSCPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            HSC33     R        H      REFFLD(SC0/SC33 *LIBL/SOSCPF)
     A            HSC34     R        H      REFFLD(SC0/SC34 *LIBL/SOSCPF)
     A                                 21 45'確認日期:'
M001MA            DSC39          6Y 0O    +1
     A                                      EDTWRD('  /  /  ')
     A                                 22  2'送貨單號  :'
     A            SC29      R        O 22 16REFFLD(SC0/SC29 *LIBL/SOSCPF)
     A                                 22 41'發票號碼  :'
     A            SC30      R        O 22 55REFFLD(SC0/SC30 *LIBL/SOSCPF)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =視窗查詢'
     A                                 23 42'F5 =參考資料'
     A                                 23 58'F12=回前畫面'
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR8          63A  O 24  2MSGID(URE 0024 REMF)
     A                                      DSPATR(HI)
     A  87        ERR9          63A  O 24  2MSGID(URE 0027 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0016 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  84        ERR13         63A  O 24  2MSGID(URE 0029 REMF)
     A                                      DSPATR(HI)
     A  83        ERR14         63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  82        ERR15         63A  O 24  2MSGID(URE 0015 REMF)
     A                                      DSPATR(HI)
     A  81        ERR16         63A  O 24  2MSGID(URE 0042 REMF)
     A                                      DSPATR(HI)
     A  80        ERR17         63A  O 24  2MSGID(URE 0064 REMF)
     A                                      DSPATR(HI)
     A  79        ERR18         63A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  78        ERR19         63A  O 24  2MSGID(URE 0082 REMF)
     A                                      DSPATR(HI)
     A  75        ERR22         63A  O 24  2MSGID(URE 0111 REMF)
     A                                      DSPATR(HI)
     A* 74        ERR23         63A  O 24  2MSGID(URE 0134 REMF)
M008MA  74        ERR23         63A  O 24  2MSGID(URE 0167 REMF)
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
