     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    30
     A                                      MSGCON(030 URE9999 REMF)
     A                                      SPACEA(001)
     A                                    34
     A                                      DFT('倉庫預訂送貨明細表      ')
     A                                      SPACEA(001)
     A                                    38
     A                                      DFT('依客戶明細')
     A                                      SPACEA(002)
     A                                     1
     A                                      DFT('通　　路:')
     A            DSEL1          1   O    +1
     A  20                                15'不含酒店'
     A  21                                15'只含酒店'
     A  22                                15'全部通路'
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('物流點　:')
     A            DSC11          5   O    +1
     A                                    58
     A                                      DFT('日期:')
     A            $EGMDY         6  0O    65
     A                                      EDTCDE(Y)
     A                                    75
     A                                      DFT('頁次:')
     A                                    83
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('送貨日期:')
M001MA*           DSC08     R        O    +1
M001MA            DSC08          6  0O    +1
M001DA*                                     REFFLD(SC08)
     A                                      EDTCDE(Y)
     A                                    58
     A                                      DFT('時間:')
     A                                    65
     A                                      TIME
     A                                    76
     A                                      DFT('<IMR058BP>')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('含未核准:')
     A            DSC13          1   O    +1
     A                                    58' USER :'
     A            $USER         10   O    65
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ========================')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('客戶代號')
     A                                    13
     A                                      DFT('客戶簡稱')
     A                                    34
     A                                      DFT('產品代號')
     A                                    45
     A                                      DFT('產品簡稱')
     A                                    65
     A                                      DFT('單位')
     A                                    73
     A                                      DFT('數量')
     A                                    80
     A                                      DFT('核准狀態')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('-------------------------------
     A                                      ------------------------------------
     A                                      ------------------------')
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEA(001)
     A  29                                 1
     A                                      DFT(' ')
     A                                      SPACEA(001)
     A N32        WF5301    R        O     1
     A                                      REFFLD(SC04)
     A N32                                 6
     A                                      DFT('-')
     A N32        WF5302    R        O     7
     A                                      REFFLD(SC05)
     A N32        ME04      R        O    13
     A                                      REFFLD(ME04)
     A            WF5303    R        O    34
     A                                      REFFLD(SD03)
     A            MA03      R        O    45
     A                                      REFFLD(MA03)
     A            MA13      R        O    65
     A                                      REFFLD(MA13)
     A            QTY1           5  0O    73
     A                                      EDTCDE(J)
     A            WF5304         1   O    84
     A*----------------------------------------------------------------
     A          R PT1
     A                                      SPACEA(001)
     A                                    50
     A                                      DFT('客戶小計------------>')
     A            T1QTY1         5  0O    73
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT2
     A                                      SPACEA(001)
     A                                    50
     A                                      DFT('合　　計------------>')
     A            T2QTY1         5  0O    73
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      =======================')
     A                                      SPACEA(001)
     A                                     2
     A                                      DFT('***續下頁***')
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ========================')
     A                                      SPACEA(001)
     A                                     2
     A                                      DFT('<<IMR058BP>>--->報表結束')
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ========================')
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PE31                      SPACEA(1)
     A                                     3'もも此範圍無資料もも'
