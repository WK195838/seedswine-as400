     A*%%TS  SD  19961204  155630  D910454     REL-V3R1M0  5763-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : SOA032D                                    *
     A*    AUTHOR       : A1092  TAYLOR                              *
     A*    CREATE DATE  : 11/26/96                                   *
     A*    UPDATE DATE  :   /  /   PHILIP                            *
     A*    PROGRAM NAME : SOA032                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.05 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR1                    SFL
     A*%%TS  SD  19961126  103534  D910454     REL-V3R1M0  5763-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  2VALUES(' ' '2' '4' '5' '6')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SI01      R        O  9  7
     A            SI02      R        O  9 14
     A            SI08      R        O  9 29
     A            SI23      R        O  9 41EDTCDE(J)
     A            SI25      R        O  9 55EDTCDE(J)
M001MA*           SI29      R        O  9 72EDTCDE(Y)
M001MA            DSI29          6  0O  9 72EDTCDE(Y)
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19961126  104630  D910454     REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(1200)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA04(04)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                      CSRLOC(#LIN       #COL)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'SOA032'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'退貨無發票資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'輸入作業選擇:'
     A                                  5  4'2=修改'
     A                                  5 18'4=刪除'
     A                                  5 32'5=查詢'
     A                                  5 46'6=明細作業'
     A                                  7  2'A  公司　　發票號碼　　　客戶代+
     A                                      號　　　　銷售金額　　　發票金額+
     A                                      　　　應收日期'
     A                                      DSPATR(UL)
     A            DSI01     R        B  8  7REFFLD(SI01)
     A                                      CHANGE(27)
     A            DSI02     R        B  8 14REFFLD(SI02)
     A                                      CHANGE(27)
     A          R DSPC1
     A*%%TS  SD  19961204  155630  D910454     REL-V3R1M0  5763-PW1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24  2'執行'
     A                                 24 10'F3 =回主畫面'
     A                                 24 24'F6 =新增'
     A                                 24 27'F6= 執行'
