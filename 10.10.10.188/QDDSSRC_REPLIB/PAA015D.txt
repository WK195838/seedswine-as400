     A*%%TS  SD  19920602  125712  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PAA015D                                    *
     A*    AUTHOR       : MICHELLE                                   *
     A*    CREATE DATE  : 89/12/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PAA015                                     *
     A*    SYSTEM       :人頭馬寰盛                                *
     A*    SUBSYSTEM    :基本資料建檔子系統                        *
     A*    REMARK       :銷售大區域地區資料建立 (A/U/D/I)          *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*           |          |
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19920528  082329  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  4VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            #M01      R        O  9  8REFFLD(#D01)
     A            #L02      R        O  9 13REFFLD(#D02)
     A            #M02      R        O  9 39REFFLD(#D01)
     A            #M03      R        O  9 45REFFLD(#D02)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920602  125712  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'PAA015'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 28'銷售大區域地區碼資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  4'2=修改'
     A                                  5 14'4=刪除'
     A                                  5 24'5=查詢'
     A                                  7  4'A 大區大區域說明              -
     A                                      地區 地區說明   '
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  8REFFLD(#E01)
     A            DBGN2     R        B  8 39REFFLD(#E02)
     A          R DSPC11
     A*%%TS  SD  19920114  134429  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A                                    +2'F9 =列印'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R DSPC2
     A*%%TS  SD  19920127  161312  REPGMR      REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CF03(03 '結束執行')
     A                                      CF12(12 '回前畫面')
     A                                      CF04(04 'F4 HELP')
     A                                      CF05(05 '參考資料')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PAA015'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 28'銷售大區域地區碼資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'大區域代號:'
     A            D#M01     R        B    +2REFFLD(#D01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            #L02      R        O    +2REFFLD(#D02)
     A                                  5  4'地區代號:'
     A                                      DSPATR(HI)
     A            D#M02     R        B    +2REFFLD(#D01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*M001M                            7  4'地區說明:'
     A            D#M03     R        O    +2REFFLD(#D02)
     A                                 19  2'異動日期:'
     A            D#MXX          6  0O 19 14
     A                                      EDTCDE(Y)
     A                                 19 23'(MM/DD/YY)'
     A                                 20  2'異動時間:'
     A            D#MYY     R        O 20 14REFFLD(#EYY)
     A                                      EDTWRD('  :  :  ')
     A                                 21  2'使用者　:'
     A            D#MZZ     R        O 21 14REFFLD(#EZZ)
     A                                 22  2'執行'
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F12=回前畫面'
     A  27                                +4'F4 =視窗查詢'
     A  27                                +4'F5 =參考資料'
     A  98        ERR1          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
