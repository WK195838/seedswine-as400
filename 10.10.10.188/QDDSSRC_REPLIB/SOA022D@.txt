     A*%%TS  SD  20020917  191247  REPGMR      REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : SOA022D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 09/13/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOA022                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料建檔子系統                        *
     A*    REMARK       :專櫃五日資料建立 (A/U/D/I)                *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          2A  B 11  4DSPATR(UL)
     A                                      VALUES('20' '21' '22' '23' '24' '25+
     A                                      ' '26' '31' '32' '33' '34' '35' '36+
     A                                      ' '5 ' ' ' '30')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DST02          4  0O 11  8EDTWRD('  /  ')
     A            ST01      R        O 11 16
     A            ME04      R        O 11 22
     A            DST14          2   O 11 41
     A            #A03      R        O 11 44
     A            ST13      R        H
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'SOA022'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'專櫃五日資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'輸入作業選擇:'
     A                                  5  4'20=全月數量修改 21=01-05  +
     A                                       22=06-10  23=11-15  24=16-20  +
     A                                       25=21-25  26=26-31'
     A                                  6  4'30=全月售價修改 31=01-05  +
     A                                       32=06-10  33=11-15  34=16-20  +
     A                                       35=21-25  36=26-31'
     A                                  7  5'5=查詢'
     A            DRRN1          4S 0H      SFLRCDNBR
M001MA            DBGN1          4Y 0B 10  8
     A                                      EDTWRD('  /  ')
     A            DBGN2     R        B 10 16REFFLD(ST01)
     A            DBGN3     R        B 10 41REFFLD(ST14)
     A                                  9  4'A  年月　專櫃代號／簡稱　+
     A                                       　　　　　部門／說明　　+
     A                                       　　　　　　　　　　　'
     A                                      DSPATR(UL)
     A          R DSPC1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F6 =產生專櫃新月資料'
     A  97        ERR1          63A  O 24  2MSGID(UPT 5016 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          63A  O 24  2MSGID(URE 0063 REMF)
     A                                      DSPATR(HI)
     A  95        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          63A  O 24  2MSGID(URE 0028 REMF)
     A                                      DSPATR(HI)
     A  91        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPC2
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F5 =參考資料'
     A                                    +2'F12=回前畫面'
     A  97        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          63A  O 24  2MSGID(URE 0028 REMF)
     A                                      DSPATR(HI)
     A  91        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0140 PTMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R SFLSR2                    SFL
     A*%%TS  SD  20020917  191247  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            HST02          6S 0H
     A            DOPT2          1A  B 11  2VALUES(' ' '1' '2')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DST03     R        B 11  4REFFLD(ST03)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DST04     R        O 11  8REFFLD(ST04)
     A            MA03      R        O 12  8
     A            DST05     R        O 11 20REFFLD(ST05)
     A                                      EDTCDE(L)
     A            DST06     R        B 11 27REFFLD(ST06)
     A                                      EDTCDE(L)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A            DST6$     R        B 12 27REFFLD(ST6$)
     A                                      EDTCDE(L)
     A  62                                  DSPATR(UL)
     A N62                                  DSPATR(PR)
     A            DST07     R        B 11 34REFFLD(ST07)
     A                                      EDTCDE(L)
     A  63                                  DSPATR(UL)
     A N63                                  DSPATR(PR)
     A            DST7$     R        B 12 34REFFLD(ST6$)
     A                                      EDTCDE(L)
     A  64                                  DSPATR(UL)
     A N64                                  DSPATR(PR)
     A            DST08     R        B 11 41REFFLD(ST08)
     A                                      EDTCDE(L)
     A  65                                  DSPATR(UL)
     A N65                                  DSPATR(PR)
     A            DST8$     R        B 12 41REFFLD(ST6$)
     A                                      EDTCDE(L)
     A  66                                  DSPATR(UL)
     A N66                                  DSPATR(PR)
     A            DST09     R        B 11 48REFFLD(ST09)
     A                                      EDTCDE(L)
     A  67                                  DSPATR(UL)
     A N67                                  DSPATR(PR)
     A            DST9$     R        B 12 48REFFLD(ST6$)
     A                                      EDTCDE(L)
     A  68                                  DSPATR(UL)
     A N68                                  DSPATR(PR)
     A            DST10     R        B 11 55REFFLD(ST10)
     A                                      EDTCDE(L)
     A  69                                  DSPATR(UL)
     A N69                                  DSPATR(PR)
     A            DST0$     R        B 12 55REFFLD(ST6$)
     A                                      EDTCDE(L)
     A  70                                  DSPATR(UL)
     A N70                                  DSPATR(PR)
     A            DST11     R        B 11 60REFFLD(ST11)
     A                                      EDTCDE(L)
     A  71                                  DSPATR(UL)
     A N71                                  DSPATR(PR)
     A            DST1$     R        B 12 62REFFLD(ST6$)
     A                                      EDTCDE(L)
     A  72                                  DSPATR(UL)
     A N72                                  DSPATR(PR)
     A            SUM       R        O 11 71REFFLD(ST11)
     A                                      EDTCDE(J)
     A            SUM1           8Y 0O 12 69EDTCDE(J)
     A            HST12     R        H      REFFLD(ST12)
     A            HST15     R        H      REFFLD(ST15)
     A            HST17     R        H      REFFLD(ST17)
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  20020917  191247  REPGMR      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0005)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOA022'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'專櫃五日資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'專櫃代號:'
     A            DST01     R        O  5 14REFFLD(ST01)
     A                                  5 22'專櫃簡稱:'
     A            ME04      R        O  5 34REFFLD(ME04)
     A                                  5 54'銷售年月:'
     A            DST02          4Y 0O  5 66EDTWRD('  /  ')
     A                                  6  2'專櫃部門:'
     A            DST14     R        O  6 14REFFLD(ST14)
     A                                  6 22'部門說明:'
     A            #A03      R        O  6 34
     A                                  7  3'1=新增'
     A                                  7 11'2=修改'
     A                                  8 27'01-05  06-10  11-15  16-20  21-25 -
     A                                       26-31    合計'
     A                                  9  2'A PLU產品代號  正常  數量 +
     A                                      數量 數量 數量 數量 數量 -
     A                                      銷售數量'
     A                                 10  2'     產品簡稱  售價  售價 +
     A                                      售價 售價 售價 售價 售價 -
     A                                      銷售金額 '
     A                                      DSPATR(UL)
     A          R DSPD3
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF14(14 '處理作業')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOA022'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 31'專櫃五日資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  8 23'專櫃代號:'
     A            DST01S    R        B  8 35REFFLD(ST01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  8 41'-'
     A            DST01E    R        B  8 43REFFLD(ST01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                 10 23'銷售年月:'
M001MA            DST02          4Y 0B 10 35
     A                                      EDTWRD('  /  ')
     A  61                                  DSPATR(RI)
     A                                 10 42'(MM/YY)'
     A                                 14 17'+----------------------------------
     A                                      --------------+'
     A                                 15 17'|'
     A                                 15 65'|'
     A                                 16 17'|請按F14以便處理此作業，否則按-
     A                                      F3結束此作業|'
     A                                 17 17'|'
     A                                 17 65'|'
     A                                 18 17'+----------------------------------
     A                                      --------------+'
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F14=處理作業'
     A  98        ERR1          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(URE 0082 REMF)
     A                                      DSPATR(HI)
     A  96        ERR3          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          63A  O 24  2MSGID(URE 0028 REMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR12         50A  O 24  2MSGID(UPT 2072 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR13         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
