     A*%%TS  SD  20020916  181726  REPGMR      REL-V4R4M0  5769-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARI023D                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/04/10                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARI023                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :應收帳款查詢－帳齡分析查詢 (I)            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  20020916  181726  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  2VALUES(' ' '5' '6')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            MD01      R        O 10  4
     A            MD31      R        O 10 11EDTCDE(J)
     A            MD32      R        O 10 26EDTCDE(J)
     A            DMD22          8Y 0O 10 56EDTCDE(1)
     A            USEM          10Y 0O 10 67EDTCDE(J)
     A            ME04      R        O 11  3
     A            MD30      R        O 10 41REFFLD(RERFR/MD30 *LIBL/RERF)
     A                                      EDTCDE(J)
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920824  100539  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0005)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      SFLDROP(CF01)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'ARI023'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'應收帳款查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'輸入作業選擇'
     A                                  6  6'5=應收明細查詢'
     A            DBGN1     R        B  9  4REFFLD(MD01)
     A                                  6 26'6=帳齡分析'
     A                                  8  2'A客戶代號  應收帳款   未兌現+
     A                                      票額 計劃使用額度  信用額度  -
     A                                        可用額度'
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A*
     A          R DSPC1
     A*%%TS  SD  19920820  140343  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 10'F1 =明細顯示'
     A                                 22 26'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0055 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A*%%TS  SD  20020916  181726  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B  9  2VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            WSI12     R        O  9  4REFFLD(SI12)
     A            WSI13     R        O  9 10REFFLD(SI13)
     A            DSI30A         9Y 0O  9 15EDTCDE(J)
     A            DSI30B         9Y 0O  9 28EDTCDE(J)
     A            DSI30C         9Y 0O  9 41EDTCDE(J)
     A            DSI30D         9Y 0O  9 54EDTCDE(J)
     A            DSI30E         9Y 0O  9 67EDTCDE(J)
     A*------------------------------------------------------------
     A*
     A****                                  CSRLOC(D#ROW      D#COL)
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19920611  101801  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'ARI023'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'帳齡分析查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  5'5=應收明細查詢'
     A                                  7  2'A客戶代號    <----- 30    31 -->-
     A                                       60    61 --> 90   91 --> 180    18-
     A                                      0 ---->'
     A                                      DSPATR(UL)
     A            DBGN21    R        B  8  4REFFLD(SI12)
     A            DBGN22    R        B  8 10REFFLD(SI13)
     A*------------------------------------------------------------
     A          R DSPC2
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0037 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
