     A*%%TS  SD  20021018  150237  REPGMR      REL-V4R4M0  5769-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARA0081D                                   *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/03/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARA008                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款作業                              *
     A*    REMARK       :銷貨折讓處理－發票分配                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.10 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M002      | 00.04.13 | FOR DUTY菸酒稅法實施
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  20021017  165056  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 13  2VALUES(' ' '2' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DAG04     R        O 13  4REFFLD(AG04)
     A            DSJ04A    R        O 13 15REFFLD(SJ04)
     A                                      EDTCDE(1)
     A            SJ13      R        O 13 22EDTCDE(1)
     A            DSJ06A    R        O 13 28REFFLD(SJ05)
     A                                      EDTCDE(3)
     A            DSJ06B         9Y 0O 13 39EDTCDE(3)
     A            DAG12     R        B 13 49REFFLD(AG12)
     A                                      EDTCDE(3)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            HAG12     R        H      REFFLD(AG12)
     A            DAG13     R        B 13 55REFFLD(AG13)
     A                                      EDTCDE(3)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A            HAG13     R        H      REFFLD(AG13)
     A            DAG07          9Y 0O 13 71EDTCDE(3)
     A            HAG05     R        H      REFFLD(AG05)
     A            HAG07     R        H      REFFLD(AG07)
     A            HAG21          9S 0H
     A            HAG08     R        H      REFFLD(AG08)
     A            HAG14     R        H      REFFLD(AG14)
     A            HAG15     R        H      REFFLD(AG15)
     A            HAG16     R        H      REFFLD(AG16)
     A            HRCDN          4A  H
     A            HSJ08     R        H      REFFLD(SJ08)
     A            HSJ09     R        H      REFFLD(SJ09)
     A            HSZ09     R        H      REFFLD(SJ09)
     A            HSJ14     R        H      REFFLD(SJ14)
     A            HSJ15     R        H      REFFLD(SJ15)
     A            HSJ16     R        H      REFFLD(SJ16)
     A            DAG06     R        B 13 60REFFLD(AG06)
     A                                      EDTCDE(3)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DSI21          6Y 0O 14  4EDTCDE(Y)
     A            QTY1      R        O 14 15REFFLD(SJ04)
     A                                      EDTCDE(1)
     A            QTY2      R        O 14 22REFFLD(SJ13)
     A                                      EDTCDE(1)
     A            DAG21          9Y 0B 14 55EDTCDE(1)
     A  64
     AO 66                                  DSPATR(PC)
     A  64
     AO 66                                  DSPATR(RI)
     A            DSI24          9Y 0O 14 67EDTCDE(J)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 14 28'＜－發票已分配數量'
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  20021018  150237  REPGMR      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0004)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DATA2          6Y 0H
     A                                  1  2'ARA008'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 33'銷貨折讓處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司　　:'
     A            SH01      R        O  4 14
     A            #B03      R        O  4 17
     A                                  4 36'客戶代號　:'
     A            SG04      R        O  4 50
     A            SG05      R        O  4 56
     A                                  5  2'產品代號:'
     A            SH03      R        O  5 14
     A            MA03      R        O  5 24
     A            ME04      R        O  4 61
     A                                  6  2'折讓單號:'
     A            SG09      R        O  6 14
     A                                  5 43'退貨日期　:'
     A            DSG06          6Y 0O  5 57EDTWRD('  /  /  ')
     A                                  5 67'(MM/DD/YY)'
     A                                  6 43'折讓日期　:'
     A            DSG26          6Y 0O  6 57EDTWRD('  /  /  ')
     A                                  6 67'(MM/DD/YY)'
     A                                  7  2'退貨單號:'
     A            SH02      R        O  7 14
     A                                  7 39'含稅折讓金額　:'
     A            DSH07     R        O  7 57REFFLD(SH07)
     A                                      EDTCDE(J)
     A                                  8  2'訂購數量:'
     A            SH04      R        O  8 14EDTCDE(J)
     A                                  8 39'未分配訂購數量:'
     A            DSH041    R        O  8 57REFFLD(SH04)
     A                                      EDTCDE(J)
     A                                  9  2'搭贈數量:'
     A            SH05      R        O  9 14EDTCDE(J)
     A                                  9 39'未分配搭贈數量:'
     A            DSH051    R        O  9 57REFFLD(SH05)
     A                                      EDTCDE(J)
     A                                 10  4'2=發票分配'
     A                                 10 18'5=折讓狀況查詢'
     A            DSG25          1A  B 10 58VALUES(' ' '*')
     A                                 10 41'跨月折讓註記:'
     A                                 10 61'*=註記'
     A                                 11 15'-發票數量-'
     A                                 11 47'-分配數量- 單價　　銷退金額'
     A                                 12  2'A發票號碼   訂購搭贈 售價 -
     A                                       發票金額 訂購搭贈稅額　　+
     A                                      未沖稅額'
     A                                      DSPATR(UL)
     A*
     A          R DSPC1
     A*%%TS  SD  19930201  164920  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 31'F3 =回主畫面'
     A                                 22 47'F5 =參考資料'
     A                                 22 63'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0056 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0057 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0062 REMF)
     A                                      DSPATR(HI)
     A  84        ERR13         63A  O 24  2MSGID(URE 0100 REMF)
     A                                      DSPATR(HI)
     A  83        ERR14         63A  O 24  2MSGID(URE 0123 REMF)
     A                                      DSPATR(HI)
M002AA  82        ERR15         63A  O 24  2MSGID(URE 0158 REMF)
M002AA                                      DSPATR(HI)
M002AA  81        ERR16         63A  O 24  2MSGID(URE 0159 REMF)
M002AA                                      DSPATR(HI)
M002AA  80        ERR17         63A  O 24  2MSGID(URE 0160 REMF)
M002AA                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
