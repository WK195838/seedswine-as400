     A*%%TS  SD  19920813  112239  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PAR001D                                    *
     A*    AUTHOR       : A1005  ROGER CHIEN                         *
     A*    CREATE DATE  : 80/11/20                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PAR001                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :公司別資料列印                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19920813  112239  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'POR052'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備　８０格報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行  F3 =回主畫面F4 =視窗查+
     A                                      詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                  6 26'公司別　:'
     A                                  8 26'供應商　:'
     A                                 10 26'採購日期:'
     A            DPC01S    R        B  6 38REFFLD(RERFR/PB01 *LIBL/RERF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  6 47'-'
     A            DPC01E    R        B  6 49REFFLD(RERFR/PB01 *LIBL/RERF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DPC05S    R        B  8 38REFFLD(RERFR/PB05 *LIBL/RERF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 47'-'
     A            DPC05E    R        B  8 49REFFLD(RERFR/PB05 *LIBL/RERF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
M001MA*           DPC04S    R        B 10 38REFFLD(RERFR/PB04 *LIBL/RERF)
M001MA            DPC04S         6  0B 10 38
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      EDTCDE(Y)
     A                                 10 47'-'
M001MA*           DPC04E    R        B 10 49REFFLD(RERFR/PB04 *LIBL/RERF)
M001MA            DPC04E         6  0B 10 49
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                      EDTCDE(Y)
     A                                 10 59'(MM/DD/YY)'
     A                                  2 31'供應商採購明細表'
     A                                      DSPATR(HI)
