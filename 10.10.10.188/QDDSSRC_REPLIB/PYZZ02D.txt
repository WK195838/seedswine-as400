     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PYZZ02D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 96/09/15                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PYZZ02                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :專櫃試飲會計科目對應                      *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYW1PF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  2VALUES(' ' '2' '4')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            W101      R        O  9  4
     A            W101X          6O  O  9  7
     A            W102      R        O  9 14
     A            W102X         15O  O  9 20
     A            W103X          4O  O  9 36
     A            W104      R        O  9 41
     A            W104X         30O  O  9 50
     A            W103      R        H
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'PYZZ02'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'專櫃試飲會計科目對應'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  5'2=修改'
     A                                  5 15'4=刪除'
     A                                  7  2'A對應系統專櫃代號             -
     A                                        會計科目                  '
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  4REFFLD(W101)
     A            DBGN2     R        B  8 14REFFLD(W102)
     A            DBGN3     R        H      REFFLD(W103)
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PYZZ02'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'專櫃試飲會計科目對應'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  8  2'對應系統:'
     A            DW101     R        B  8 14REFFLD(W101)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DW101X         6O  O    +2
     A                                 10  2'專櫃代號:'
     A            DW102     R        B 10 14REFFLD(W102)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DW102X        18O  O    +2
     A                                 12  2'借/貸 :'
     A            DW103     R        B 12 14REFFLD(W103)
     A                                      VALUES('1' '2')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                    +2'(1.借 / 2.貸)'
     A                                 14  2'會計科目:'
     A            DW104     R        B 14 14REFFLD(W104)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DW104X        32O  O    +2
     A*                                22  2'執行'
     A                                 22  2'F3 =回主畫面'
     A  71                             22 18'F9 =確定存檔'
     A                                 22 34'F12=回前畫面'
     A  98        ERR1          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR3          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPC2
     A*                                     OVERLAY
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  71                             22 18'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 34'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
