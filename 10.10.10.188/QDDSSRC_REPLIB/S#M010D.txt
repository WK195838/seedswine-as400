     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : S#M010D                                    *
     A*    AUTHOR       : A1034   STEPHANIE                          *
     A*    CREATE DATE  : 79/06/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : S#M010                                     *
     A*    SYSTEM       :程式安全管制系統                          *
     A*    REMARK       :程式安全管制系統主畫面(SUBFILE)           *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                  1  2'<S#M010D>'
     A                                  1 20MSGCON(40 US#9001 S#MF)
     A                                  1 64'日期:'
     A            $CHYMD         6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A            RRN            2  0O  4 19
     A            SP02      R        O  4 23
     A            DSP01         12A  O  4 57
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0089)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN           2S 0H      SFLRCDNBR
     A                                 18 19'90'
     A                                 18 23'結束'
     A                                 20 27'請輸入選擇項目:'
     A            DOP            2Y 0B 20 45
     A                                      CHECK(RB)
     A  60                                  DSPATR(PC RI)
     A  95        ERR2          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
