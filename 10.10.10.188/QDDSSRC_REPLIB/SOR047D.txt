     A*%%TS  SD  19920929  155927  D910091     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR047D                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/02/14                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR047                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :訂單系統                                  *
     A*    REMARK       :送貨未確認明細表                          *
     A*===============================================================
     A* CODE | AUTHOR |  DATE  | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001 |MICHELLE|11.03.04|增加客戶代號選項
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19920929  155927  D910091     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR047'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'送貨未確認明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  7 26'物流點　:'
     A            DSE07S    R        B  7 38REFFLD(RERFR/SE07 *LIBL/RERF)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  7 47'-'
     A            DSE07E    R        B  7 49REFFLD(RERFR/SE07 *LIBL/RERF)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
M001AA                                  9 26'客戶代號:'
 "   A                                      DSPATR(HI)
 "   A            DSE04S    R        B  9 38REFFLD(RERFR/SE04 *LIBL/RERF)
 "   A                                      DSPATR(HI)
 "   A  62                                  DSPATR(RI)
 "   A  62                                  DSPATR(PC)
 "   A                                  9 47'-'
 "   A                                      DSPATR(HI)
 "   A            DSE04E    R        B  9 49REFFLD(RERFR/SE04 *LIBL/RERF)
 "   A                                      DSPATR(HI)
 "   A  62                                  DSPATR(RI)
M001AA  62                                  DSPATR(PC)
     A                                 11 26'送貨日期:'
M001DA*           DSE06S    R        B  9 38REFFLD(SE06)
M001AA            DSE06S         6  0B 11 38
     A                                      EDTWRD('  /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 11 47'-'
M001DA*           DSE06E    R        B  9 49REFFLD(SE06)
M001AA            DSE06E         6  0B 11 49
     A                                      EDTWRD('  /  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 16 26'執行環境'
     A                                 16 38':'
     A            $EVR           1Y 0B 16 40VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 46'(1-即時   2-批次)'
     A                                 17 26'列印份數'
     A                                 17 38':'
     A            $CPY           3Y 0B 17 40EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機'
     A                                      DSPATR(HI)
     A                                 18 38':'
     A            $PRTID         2Y 0B 18 40CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
