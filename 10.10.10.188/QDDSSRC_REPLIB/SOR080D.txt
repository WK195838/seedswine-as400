     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : SOR080D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 12/31/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR080                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :專櫃銷售系統                              *
     A*    REMARK       :專櫃結帳表                                *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR080'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'專櫃結帳表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 28'專櫃名稱:'
     A                                      DSPATR(HI)
     A            DST01S    R        B  6 40REFFLD(ST01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N60                                  DSPATR(HI)
     A                                  6 46'-'
     A            DST01E    R        B  6 48REFFLD(ST01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A                                  8 28'結帳年月:'
     A            DST02          4Y 0B  8 40
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      EDTWRD('  /  ')
     A                                  8 46'(MM/YY)'
     A                                 16 28'執行環境:'
     A            $EVR           1Y 0B 16 41VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 28'列印份數'
     A                                 17 39':'
     A            $CPY           3Y 0B 17 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 28'列表機'
     A                                      DSPATR(HI)
     A                                 18 39':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 41CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 29'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0082 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
