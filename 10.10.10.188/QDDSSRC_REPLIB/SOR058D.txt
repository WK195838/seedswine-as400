     A*%%TS  SD  19960415  114510  D910074     REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR058D                                    *
     A*    AUTHOR       : A1087  JOYCE TSAI                          *
     A*    CREATE DATE  : 81/02/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR058                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :訂單系統                                  *
     A*    REMARK       :訂單明細表                                *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19960415  114510  D910074     REL-V3R1M0  5763-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR058'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'銷售明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 26'辦事處　:'
     A            COMP           1A  B  5 38VALUES(' ' '1' '2' '3')
     A                                  5 40'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  6 26'轉帳　　:'
     A            DSEL           1A  B  6 38VALUES('Y' 'N')
     A                                  6 40'( Y=轉帳N=非轉帳)'
     A                                  7 26'內容　　:'
     A            DSEL1          1A  B  7 38VALUES('A' 'B')
     A                                  7 40'( A=不含康齡B=含康齡)'
     A                                  9 26'送貨日期:'
M001DA*           DSC08S    R        B  9 38REFFLD(SC39)
M001AA            DSC08S         6  0B  9 38
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  9 47'-'
M001DA*           DSC08E    R        B  9 49REFFLD(SC39)
M001AA            DSC08E         6  0B  9 49
     A                                      EDTWRD('  /  /  ')
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                  9 60'(MM/DD/YY)'
     A                                 10 26'確認日期:'
M001DA*           DSC39S    R        B 10 38REFFLD(SC39)
M001AA            DSC39S         6  0B 10 38
     A                                      EDTWRD('  /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 10 47'-'
M001DA*           DSC39E    R        B 10 49REFFLD(SC39)
M001AA            DSC39E         6  0B 10 49
     A                                      EDTWRD('  /  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 10 60'(MM/DD/YY)'
     A                                 12 26'客戶代號:'
     A                                      DSPATR(HI)
     A            DSC04S         9A  B 12 38
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                 12 48'-'
     A            DSC04E         9A  B 12 50
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                 13 26'產品代號:'
     A                                      DSPATR(HI)
     A            DSD03S    R        B 13 38REFFLD(MA01)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                 13 48'-'
     A            DSD03E    R        B 13 50REFFLD(MA01)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A                                 14 26'單別　　:'
     A                                      DSPATR(HI)
     A            DSC03S         2A  B 14 38
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 14 47'-'
     A            DSC03E         2A  B 14 49
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 42'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          63A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          63A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 0042 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR9          63A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
