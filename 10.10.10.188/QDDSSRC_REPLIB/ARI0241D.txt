     A*%%TS  SD  20021220  102248  REPGMR      REL-V4R4M0  5769-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARI0241D                                   *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/03/31                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARI0241                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :銷貨折讓查詢－對應票查詢 (I)              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M004      | 00.05.18 |菸酒稅法實施
     A*================================================================
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR                     SFL
     A*%%TS  SD  20021220  102248  REPGMR      REL-V4R4M0  5769-PW1
     A            RRN            4S 0H
     A            AG04      R        O 14  4
     A            D#A       R        O 14 15REFFLD(SJ04)
     A                                      EDTCDE(1)
     A            SJ13           4Y 0O 14 22EDTCDE(1)
     A            DATA1          9Y 2O 14 28EDTCDE(3)
     A            DATA2          9Y 0O 14 39EDTCDE(3)
     A            AG12      R        O 14 49REFFLD(RERFR/AG12 *LIBL/RERF)
     A                                      EDTCDE(3)
     A            AG13      R        O 14 55REFFLD(RERFR/AG13 *LIBL/RERF)
     A                                      EDTCDE(3)
     A            AG06      R        O 14 60REFFLD(RERFR/AG06 *LIBL/RERF)
     A                                      EDTCDE(3)
     A            AG07      R        O 14 71REFFLD(RERFR/AG07 *LIBL/RERF)
     A                                      EDTCDE(3)
     A            DATA3          9Y 0O 15 39EDTCDE(3)
     A            AG21           9Y 0O 15 71EDTCDE(3)
     A*------------------------------------------------------------
     A*
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920514  115613  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0008)
M004MA*                                     SFLPAG(0008)
M004MA                                      SFLPAG(0004)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPC1
     A*%%TS  SD  19920731  155333  REPGMRP     REL-V2R1M0  5738-PW1
     A                                  1  2'ARI024'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 33'銷貨折讓查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司　　:'
     A            SG01      R        O  4 15
     A            D#B03         16A  O  4 18
     A                                  5  2'客戶代號:'
     A            SG04      R        O  5 15
     A            SG05      R        O  5 21
     A            ME04      R        O  5 26
     A                                  6  2'產品代號:'
     A            PRDN      R        O  6 15REFFLD(AG03)
     A            MA03      R        O  6 25
     A                                  7  2'折讓單號:'
     A            SG09      R        O  7 15
     A                                  7 39'折讓日期:'
M001DA*           DSG26     R        O  7 51REFFLD(SG06)
M001AA            DSG26          6  0O  7 51
     A                                      EDTWRD('  /  /  ')
     A                                  7 61'(MM/DD/YY)'
     A                                  8  2'退貨單號:'
     A            SG02      R        O  8 15
     A                                  8 39'退貨日期:'
M001DA*           DSG06     R        O  8 51REFFLD(RERFR/SG06 *LIBL/RERF)
M001AA            DSG06          6  0O  8 51
     A                                      EDTCDE(Y)
     A                                  8 61'(MM/DD/YY)'
     A                                  9  2'退貨數量:'
     A            QTY            9Y 0O  9 15EDTCDE(1)
M004MA*                                 9 39'折讓金額:'
M004MA                                  9 35'未稅折讓金額:'
     A            SH07      R        O  9 51EDTCDE(1)
M004AA                                 10 39'折讓稅額:'
M004AA            S207           9  0O 10 51EDTCDE(1)
M004AA                                 11 35'含稅折讓稅額:'
M004AA            W207           9  0O 11 51EDTCDE(1)
M004MA*                                12 16'訂購 搭贈              發票 -
     A                                 12 16'訂購 搭贈      未稅發票金額 -
M004MA*                                      分配數量  銷退      銷退'
M004MA                                       分配數量  銷退  銷退金額'
M004MA*                                13  3'發票號碼   數量 數量  售價-
M004MA                                 13  3'發票號碼   數量 數量  售價-
M004MA*                                           金額 訂購搭贈 單價-
M004MA                                        發票金額 訂購搭贈-
M004MA*                                        金額'
M004MA                                       單價  銷退稅額'
     A                                      DSPATR(UL)
