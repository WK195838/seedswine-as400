     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR043D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/04/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR043                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售訂單系統                              *
     A*    REMARK       :客戶報價單                                *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR043'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'客戶報價單'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 28'地區:'
     A                                      DSPATR(HI)
     A            DME07S    R        B  4 36REFFLD(ME07)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N60                                  DSPATR(HI)
     A                                  4 45'-'
     A            DME07E    R        B  4 47REFFLD(ME07)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  5 28'通路:'
     A                                      DSPATR(HI)
     A            DMD07S    R        B  5 36REFFLD(MD07)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  5 45'-'
     A            DMD07E    R        B  5 47REFFLD(MD07)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                  6 28'型態:'
     A                                      DSPATR(HI)
     A            DMD08S    R        B  6 36REFFLD(MD08)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A                                  6 45'-'
     A            DMD08E    R        B  6 47REFFLD(MD08)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A N65                                  DSPATR(HI)
     A                                  7 24'客戶代號:'
     A                                      DSPATR(HI)
     A            DMD01S    R        B  7 36REFFLD(MD01)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A N66                                  DSPATR(HI)
     A                                  7 45'-'
     A            DMD01E    R        B  7 47REFFLD(MD01)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(HI)
     A                                  9 28'日期:'
     A            DATE           6Y 0B  9 36EDTWRD('  /  /  ')
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                  9 49'(MM/DD/YY)'
     A                                 10 24'產品代號:'
     A                                      DSPATR(HI)
     A            DSA01S         9A  B 10 36
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A N69                                  DSPATR(HI)
     A                                 10 46'-'
     A            DSA01E         9A  B 10 48
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A N70                                  DSPATR(HI)
     A                                 12 22'列印抬頭否:'
     A            DSEL           1A  B 12 36VALUES('Y' 'N')
     A                                 12 38'(Y-列印 N-不列印) '
     A                                 13 22'報價折扣率:'
     A            DSEL1          1A  B 13 36VALUES('A' 'B')
     A                                 13 38'A-客戶報價'
     A                                 14 38'B-自定報價'
     A            DSEL2          5  2B 14 51EDTCDE(J)
     A                                 14 59'%'
     A                                 16 24'執行環境:'
     A            $EVR           1Y 0O 16 36EDTCDE(3)
     A                                 16 41'(1-即時 2-批次)'
     A                                 17 24'列印份數:'
     A            $CPY           3Y 0B 17 36EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 24'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 36CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 30'***請準備套版紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
