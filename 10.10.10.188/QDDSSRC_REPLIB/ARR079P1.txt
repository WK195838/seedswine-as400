     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : ARR079P                                    *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  :                                            *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR079                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :票據兌現明細表依客戶                      *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    32MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    32'票據兌現明細表（客戶）'
     A                                      SPACEA(1)
     A                                     1'公司別　　:'
     A            AC01      R        O    15REFFLD(AC01)
     A            D#B03         16   O    19
     A                                    51'日期:'
     A            $EGMDY         6  0O    59EDTCDE(Y)
     A                                    68'頁次:'
     A                                    76PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'客戶代號　:'
     A            DAC23S         9   O    15
     A                                    25'-'
     A            DAC23E         9   O    27
     A                                    51'時間:'
     A                                    59TIME
     A                                    70'<ARR079>'
     A                                      SPACEA(1)
     A                                     1'預兌現日:'
     A            DAC07S         6  0O    15EDTWRD('  /  /  ')
     A                                    25'-'
     A            DAC07E         6  0O    27EDTWRD('  /  /  ')
     A                                    51' USER :'
     A            $USER         10A  O    59
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ==================='
     A                                      SPACEA(1)
     A                                     3'客戶代號／名稱'
     A                                    24'預計兌現日'
     A                                    37'票據號碼'
     A                                    59'金額'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       -------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            AC02      R              3
     A            DAC07          6  0     28EDTWRD('  /  /  ')
     A            ME04      R             19
     A            AC06      R             38
     A            AC08      R             54EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT1
     A                                     3'客戶別合計 +
     A                                       ---------------------------->'
     A            TOT1           9  0     54EDTCDE(J)
     A                                      SPACEA(2)
     A*----------------------------------------------------------------
     A          R PT2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ==================='
     A                                      SPACEA(1)
     A                                     3'公司別合計 +
     A                                       ---------------------------->'
     A            TOT2           9  0     54EDTCDE(J)
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ==================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ==================='
     A                                      SPACEA(1)
     A                                     2'<<ARR079>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
