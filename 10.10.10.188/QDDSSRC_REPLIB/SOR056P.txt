     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR056P                                   *
     A*    AUTHOR       : ANDY                                       *
     A*    CREATE DATE  : 81/08/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR0561                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :主檔                                      *
     A*    REMARK       :客戶折扣報價明細表                        *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    41MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    45'客戶折扣報價明細表'
     A                                      SPACEA(1)
     A                                     1'通　路'
     A                                    11':'
     A            DSA02S    R             +1REFFLD(SA02)
     A                                    22'∼'
     A            DSA02E    R             +0REFFLD(SA02)
     A                                      SPACEA(1)
     A                                     1'型　態'
     A                                    11':'
     A            DSA03S    R             +1REFFLD(SA03)
     A                                    22'∼'
     A            DSA03E    R             +0REFFLD(SA03)
     A                                      SPACEA(1)
     A                                     1'客　戶'
     A                                    11':'
     A            DSB01S    R        O    +1REFFLD(SB01)
     A                                    22'∼'
     A            DSB01E    R        O    +0REFFLD(SB01)
     A                                    79'日期:'
     A            $EGMDY         6  0O    86EDTCDE(Y)
     A                                    96'頁次:'
     A                                   103PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'產　品'
     A                                    11':'
     A            DSA01S    R        O    +1REFFLD(SA01)
     A                                    22'∼'
     A            DSA01E    R        O    +0REFFLD(SA01)
     A                                    79'時間:'
     A                                    87TIME
     A                                    97'<SOR056P>'
     A                                      SPACEA(1)
     A                                     1'有效日期'
     A                                    11':'
M001DA*           DDATE     R        O    +1REFFLD(SB05)
M001AA            DDATE          6  0O    +1
     A                                      EDTCDE(Y)
     A                                    80'USER :'
     A            $USER         10A  O    +1
     A                                      SPACEA(1)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEA(1)
     A                                     1'產品品牌／名稱'
     A                                    29'通路型態／說明'
     A                                      SPACEA(1)
     A                                     1'產品代號／名稱'
     A                                    29'客戶代號／簡稱'
     A                                    52'折扣別'
     A                                    60'折扣率'
     A                                    73'售　價'
     A                                    84'生效日'
     A                                    93'截止日'
     A                                   102'確認'
     A                                      SPACEA(1)
     A                                     1'----------------------------------+
     A                                      -----------------------------------+
     A                                      -----------------------------------+
     A                                      -----------------------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            WF3401    R              2REFFLD(SA01)
     A            DMA03     R             +0REFFLD(MA03)
     A            CUS            5        29
     A            NAME          18O       +0
     A            DSCNT          8O       52
     A            D3404          6  4     60
     A                                      EDTCDE(J)
     A                                    +0'%'
     A            PRICE$         9  2     70EDTCDE(J)
M001DA*           D3405     R             84REFFLD(SA05)
M001AA            D3405          6  0     84
     A                                      EDTCDE(Y)
M001DA*           D3406     R             93REFFLD(SA13)
M001AA            D3406          6  0     93
     A                                      EDTCDE(Y)
     A            COMF           8O      102
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==================================='
     A                                      SPACEA(1)
     A                                     2'<<SOR056P>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE9
     A                                     3'もも此範圍無資料もも'
     A                                      SPACEA(1)
