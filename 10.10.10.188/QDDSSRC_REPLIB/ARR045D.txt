     A*%%TS  SD  19930507  140730  D910074     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : ARR045D                                    *
     A*    AUTHOR       : A1118  PHILP                               *
     A*    CREATE DATE  : 81/04/02                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR045                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款子系統                            *
     A*    REMARK       :銷貨收入明細表                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.12.10 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930507  140730  D910074     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR045'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'銷貨收入明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 26'公司別　:'
     A            DSI01          1A  B  5 38VALUES('R' 'W' 'K')
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  5 42'(W=匯齡R=匯東K=康齡)'
     A                                  7 26'辦事處　:'
     A            COMP           1A  B  7 38DSPATR(HI)
     A                                      VALUES(' ' '1' '2' '3')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                  7 42'(1=台北2=台中3=高雄 " " =+
     A                                      全部)'
     A                                  9 26'轉　　帳:'
     A            DSI10          1A  B  9 38VALUES('1' '2' '3')
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  9 42'(1=非轉帳2=轉帳3=全部)'
     A                                 11 26'確認日期:'
M001MA*           DSI35S    R        B 11 38REFFLD(SI35)
M001MA            DSI35S         6Y 0B 11 38
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 11 47'-'
M001MA*           DSI35E    R        B 11 49REFFLD(SI35)
M001MA            DSI35E         6Y 0B 11 49
     A                                      EDTWRD('  /  /  ')
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 11 59'(MM/DD/YY)'
     A                                 13 26'選　　項:'
     A            DSEL           1A  B 13 38VALUES('A' 'B')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 13 42'(A=不含專櫃B=僅含專櫃)'
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備１３２格報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
