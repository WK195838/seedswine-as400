     A*%%TS  SD  20020911  100335  REPGMR      REL-V4R4M0  5769-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARA0041D                                   *
     A*    AUTHOR       : A1118  PHILIP TSAI                         *
     A*    CREATE DATE  : 81/03/23                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARA0041                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款作業系統                          *
     A*    REMARK       :發票作廢重開－重開 (A/U/D/I)              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920812  145717  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            HSI02     R        H      REFFLD(SI02)
     A            DOPT1          1A  B 10  3VALUES(' ' '2' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SI01      R        O 10  6
     A            SI12      R        O 10 21
     A            SI13      R        O 10 27
     A            SI02      R        O 10 33
     A            SI11      R        O 10 47
     A            SI25      R        O 10 61EDTCDE(1)
     A*           SI17      R        O 10 75
M001MA*           DSC08C    R        O 10 11REFFLD(SC07)
M001MA            DSC08C         6  0O 10 73
     A                                      EDTCDE(Y)
     A            SI10      R        H
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920617  112242  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'ARA0041'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 33'發票重開處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'輸入作業選擇:'
     A                                  5  6'2=重開'
     A                                  5 16'5=明細查詢'
     A            DBGN1     R        B  9  6REFFLD(SI01)
     A            DBGN2     R        B  9 21REFFLD(SI12)
     A            DBGN3     R        B  9 27REFFLD(SI13)
     A            DBGN0          6Y 0B  9 11EDTCDE(Y)
     A                                  8  3'A公司　送貨日期客戶代號  發+
     A                                      票號碼    送貨單號           +
     A                                      金額　聯數'
     A                                      DSPATR(UL)
     A                                  7 10'(MM/DD/YY)'
     A*  81/01/21  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R DSPC1
     A*%%TS  SD  19920324  134224  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A  80                             24  2'發票號碼'
     A                                      DSPATR(HI)
     A  80        XSI02     R        O 24 13REFFLD(SI02)
     A  80                                  DSPATR(RI)
     A  80                             24 24'重開成功'
     A                                      DSPATR(HI)
     A          R DSPD2
     A*%%TS  SD  19920801  093439  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARA0041'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'發票資料更改'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A            SI01      R        O  4 14
     A            #B03      R        O  4 17
     A                                  4 34'客戶編號:'
     A            SI08      R        O  4 46
     A                                  4 52'-'
     A            SI09      R        O  4 54
     A                                  5  2'送貨單號:'
     A            SI11      R        O  5 14
     A            DME04     R        O  5 46REFFLD(ME04)
     A                                  6  2'發票月份:'
     A            DSIMM          4Y 0O  6 14EDTWRD('  /  ')
     A                                  6 34'發票日期:'
M001MA*           DSI21C    R        B  6 46REFFLD(SI21)
M001MA            DSI21C         6  0B  6 46
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                      EDTWRD('  /  /  ')
     A                                  7  2'發票號碼:'
     A            SI02      R        O  7 14
     A                                  7 34'發票別'
     A            DSI17     R        B  7 46REFFLD(SI17)
     A                                      EDTCDE(1)
     A                                      VALUES(2 3)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  7 49'(2-二聯，3-三聯)'
     A                                  9 34'課稅別'
     A            DSI22     R        B  9 46REFFLD(SI22)
     A                                      VALUES('D' 'B' 'Z')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  9 49'(D-應稅，B-免稅，Z-零稅)'
     A                                 11  2'客戶訂單:'
     A            SC06      R        O 11 14
     A                                 11 34'訂單號碼:'
     A            SI10      R        O 11 46
     A                                 13  2'發票地址:'
     A            DSI12     R        O 13 14REFFLD(SI12)
     A            DSI13     R        B 13 20REFFLD(SI13)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            ME031     R        O 13 25REFFLD(ME03)
     A            ME051     R        O 14 15REFFLD(ME05)
     A            ME061     R        O 14 48REFFLD(ME06)
     A                                 16  2'送貨地址:'
     A            DSI14     R        B 16 14REFFLD(SI14)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DSI15     R        B 16 20REFFLD(SI15)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            ME032     R        O 16 25REFFLD(ME03)
     A            ME052     R        O 17 15REFFLD(ME05)
     A            ME062     R        O 17 48REFFLD(ME06)
     A                                 19  2'銷售金額:'
     A            DSI23     R        O 19 14REFFLD(SI23)
     A                                      EDTCDE(1)
     A                                 19 31'營業稅:'
     A            DSI24     R        O 19 41REFFLD(SI24)
     A                                      EDTCDE(1)
     A                                 19 56'發票金額:'
     A            DSI25     R        O 19 68REFFLD(SI25)
     A                                      EDTCDE(1)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0107 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
