     A*%%TS  SD  19930129  112051  D910121     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PAR001D                                    *
     A*    AUTHOR       : A1005  ROGER CHIEN                         *
     A*    CREATE DATE  : 80/11/20                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PAR001                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :公司別資料列印                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930129  112051  D910121     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'IMR060'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備　８０格報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行  F3 =回主畫面F4 =視窗查+
     A                                      詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                  2 32'良壞轉換明細表'
     A                                      DSPATR(HI)
     A                                  5 27'公　司　:'
     A                                  7 27'物流點　:'
     A                                  9 27'產品代號:'
     A                                 11 27'轉換日期:'
     A                                 13 27'型　態　:'
     A            DSEL           1A  B 13 39VALUES(' ' '1' '2')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 13 42'( 1 :良->壞  2 :壞->良)'
     A            DID01          1A  B  5 39
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      VALUES(' ' 'W' 'C' 'R')
     A            DIC03     R        B  7 39REFFLD(RERFR/IC03 *LIBL/RERF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DID03S    R        B  9 39REFFLD(RERFR/ID03 *LIBL/RERF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 49'-'
     A            DID03E    R        B  9 51REFFLD(RERFR/ID03 *LIBL/RERF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
M001MA*           DIC05S    R        B 11 39REFFLD(RERFR/IC05 *LIBL/RERF)
M001MA            DIC05S         6  0B 11 39
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      EDTCDE(Y)
     A                                 11 49'-'
M001MA*           DIC05E    R        B 11 51REFFLD(RERFR/IC05 *LIBL/RERF)
M001MA            DIC05E         6  0B 11 51
     A                                      EDTCDE(Y)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 11 61'(MM/DD/YY)'
     A                                  5 43'('
     A                                  5 45'R'
     A                                  5 47' '
     A                                  5 49'C'
     A                                  5 51' '
     A                                  5 53'W'
     A                                  5 55')'
