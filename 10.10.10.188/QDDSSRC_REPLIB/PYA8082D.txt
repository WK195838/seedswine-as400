     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:2731-3250  *
     A*                                                              *
     A*    PROGRAM-ID   : PYA8082D                                   *
     A*    PROGRAM NAME :產品別銷售分析明細                        *
     A*    AUTHOR       : A1491 EASON                                *
     A*    CREATE DATE  : 97/01/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :豪門                                      *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            3Y 0H
     A            SSEL           1A  B  7  7VALUES('/' ' ')
     A            SMA01          9A  O  7 14
     A            SMA03         18O  O  7 29
     A            SMA06         15O  O  7 53
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0999)
     A                                      SFLPAG(0012)
     A                                      VLDCMDKEY(20)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PYA8082>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A            DATE           7Y 0O  1 70EDTWRD('0   /  /  ')
     A                                  2 33'產品別銷售分析'
     A                                      DSPATR(RI)
     A                                  2 70TIME
     A                                  4  2'廠商別:'
     A                                      DSPATR(HI)
     A            DMA12          5A  B  4 12CHANGE(90)
     A                                      DSPATR(PC)
     A                                  4 50'全選:'
     A                                      COLOR(BLU)
     A            ALL            1A  B  4 58VALUES('/' ' ')
     A                                  6  7'/'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  6 14'產品代碼'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  6 30'中文簡稱'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  6 54'英文簡稱'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                      OVERLAY
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03=回主畫面'
     A                                    +2'PF04=查詢'
     A                                 24 63'【泛太資訊】'
