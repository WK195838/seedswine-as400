     A*%%TS  SD  19930724  123618  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250
     A*
     A*    DSPF NAME    : POR056D
     A*    AUTHOR       : 920197 MERCURY
     A*    CREATE DATE  : 82/07/15
     A*    UPDATE DATE  :
     A*    PROGRAM NAME : POR056
     A*    SYSTEM       :人頭馬匯東
     A*    SUBSYSTEM    :採購系統
     A*    REMARK       :國內驗收金額月彙總表
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930724  123618  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'POR056'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'國內驗收金額月彙總表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 27'公　司:'
     A            DPH01S         1A  B  6 39
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  6 45'-'
     A            DPH01E         1A  B  6 47
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  8 27'供應商:'
     A            DPH06S    R        B  8 39REFFLD(PH06)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  8 45'-'
     A            DPH06E    R        B  8 47REFFLD(PH06)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                 10 27'專　櫃:'
     A            DPH05S    R        B 10 39REFFLD(PH05)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 10 45'-'
     A            DPH05E    R        B 10 47REFFLD(PH05)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 12 27'月　份:'
     A            DDATES         4Y 0B 12 39EDTWRD('  /  ')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 12 45'-'
     A            DDATEE         4Y 0B 12 47EDTWRD('  /  ')
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                 12 54'(MM/YY)'
     A                                 16 22'執行環境'
     A                                 16 35':'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 42'(1-即時   2-批次)'
     A                                 17 22'列印份數'
     A                                 17 35':'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 22'列表機'
     A                                      DSPATR(HI)
     A                                 18 35':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 22'***請準備８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
