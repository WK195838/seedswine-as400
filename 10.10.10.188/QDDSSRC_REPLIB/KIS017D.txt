     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : KIS017D                                    *
     A*    PROGRAM NAME :物流點庫存報表　                          *
     A*    AUTHOR       : A1491 EASON                                *
     A*    CREATE DATE  : 2009/06/15                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :專櫃系統                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF07(07)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<KIS017D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 31'物流點庫存報表'
     A                                      DSPATR(RI)
     A                                  7 23'物流點  　起:'
     A*                                     DSPATR(HI)
     A            DIA02S    R        B  7 41REFFLD(IA02 IMIAPF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 23'　　　　　　迄:'
     A*                                     DSPATR(HI)
     A            DIA02E    R    5   B  8 41REFFLD(IA02 IMIAPF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 10 23'產品代號　　起:'
     A                                      DSPATR(HI)
     A            DIA03S    R        B 10 41REFFLD(IA03 IMIAPF)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 11 23'　　　　　　迄:'
     A                                      DSPATR(HI)
     A            DIA03E    R        B 11 41REFFLD(IA03 IMIAPF)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 18  2'執行環境:'
     A            $PENV          1A  B 18 14VALUES('0' '1' '2')
     A                                 18 18'(0-查詢 1-即時 2-批次)'
     A*                                19  2'列印份數:'
     A*           $CPY           3Y 0B 19 14EDTCDE(3)
     A*                                     CHECK(RB)
     A*                                20  2'列表機　:'
     A*                                     DSPATR(HI)
     A*           $PRTCD         2A  B 20 14
     A  27                             21 27'    請輸入挑選區間      '
     A                                      DSPATR(RI)
     A  28                             21 27'    報表處理中，請稍候    '
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03 =回主畫面'
     A                                 23 28'PF04 =欄位查詢'
     A                                 23 46'PF07 =檔案下載'
     A                                 24 67'【泛太資訊】'
