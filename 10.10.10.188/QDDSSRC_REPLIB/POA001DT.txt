     A*%%TS  SD  19920113  142128  REPGMR      REL-V2R1M0  5738-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PAA001D                                    *
     A*    AUTHOR       : A1005  ROGER CHIEN                         *
     A*    CREATE DATE  : 80/11/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PAA001                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料建檔子系統                        *
     A*    REMARK       :公司別資料建立 (A/U/D/I)                  *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.10 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920110  133930  REPGMR      REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  8  3VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            PA01      R        O  8  6
     A            MB02      R        O  8 12
     A            PA02      R        O  8 55
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920110  133930  REPGMR      REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                 20  1'起始值　　'
     A            DBGN1     R        B 20 15REFFLD(PA01)
     A            DBGN2     R        B 20 21REFFLD(PA02)
     A                                 22  1'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 25'F6 =新增'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R DSPC1
     A*%%TS  SD  19920113  142128  REPGMR      REL-V2R1M0  5738-PW1
     A                                  1  2'POA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'供應商產品報價設定'
     A                                  2 32'供應商產品報價設定'
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'輸入作業選擇:'
     A                                  5  4'2=修改'
     A                                  5 16'4=刪除'
     A                                  5 28'5=查詢'
     A                                  7  3'A 供應商代號／名稱              -
     A                                                       報價單號碼'
     A                                      DSPATR(UL)
     A          R DSPD2                     CSRLOC(D#ROW D#COL)
     A*%%TS  SD  19920110  145017  REPGMR      REL-V2R1M0  5738-PW1
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            D#ROW          3  0H
     A            D#COL          3  0H
     A                                  1  2'POA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'供應商產品報價設定'
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'供應商代號:'
     A            DPA01     R        B  4 18REFFLD(PA01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            MB02      R        O  4 24
     A                                  6  2'報價單號:'
     A            DPA02     R        B  6 18REFFLD(PA02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  9  2'生效日期:'
M001MA            DPA04          6  0B  9 18
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  2'幣別:'
     A                                      DSPATR(HI)
     A            DPA08     R        B 11 18REFFLD(PA08)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A031        22A  O 11 22
     A                                 13  2'付款條件:'
     A                                      DSPATR(HI)
     A            DPA07     R        B 13 18REFFLD(PA07)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A032        22A  O 13 22
     A                                 15  2'報價價格條件:'
     A            DPA10     R        B 15 18REFFLD(PA10)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A033        22A  O 15 22
     A                                 17  2'交易價格條件:'
     A            DPA11     R        B 17 18REFFLD(PA11)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A034        22A  O 17 22
     A                                 19  2'截止日期:'
M001MA            DPA06          6  0B 19 18
     A                                      EDTWRD('  /  /  ')
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 22  1'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 25'F4 =視窗查詢'
     A                                 22 40'F5 =參考資料'
     A                                 22 55'F12=回前畫面'
     A*
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPC3
     A*%%TS  SD  19920110  145017  REPGMR      REL-V2R1M0  5738-PW1
     A                                  1  2'POA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 32'供應商產品報價設定'
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  2 30'供應商產品報價設定'
     A                                  4  2'供應商代號:'
     A            DPA01     R        O  4 16REFFLD(PA01)
     A            MB02      R        O  4 22
     A                                  5  2'報價單號:'
     A            DPA02     R        O  5 16REFFLD(PA02)
     A                                  5 43'生效日期:'
M001MA            DPA04          6  0O  5 55
     A                                      EDTWRD('  /  /  ')
     A                                  6 43'付款條件:'
     A            DPA07     R        O  6 55REFFLD(PA07)
     A                                  6  2'幣別:'
     A            DPA08     R        O  6 16REFFLD(PA08)
     A            D#A031        22A  O  6 20
     A            D#A032        22A  O  6 58
     A                                  8  4'1=新增'
     A                                  8 16'2=修改'
     A                                  8 28'4=刪除'
     A                                  9 64'---價格條件---'
     A                                 10  2'A 產品代號／名稱    10     供+
     A                                      應商產品 單位    單價   報+
     A                                      價　　交易'
     A                                      DSPATR(UL)
     A          R SFLSR2                    SFL
     A*%%TS  SD  19920110  133930  REPGMR      REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 11  2VALUES(' ' '1' '2' '4')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DPA03     R        B 11  5REFFLD(PA03)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            MA06      R        O 11 15
     A            DPA05     R        B 11 32REFFLD(PA05)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            MA14      R        O 11 45REFFLD(MA14)
     A            DPA09     R        B 11 52REFFLD(PA09)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A*                                     EDTCDE(1)
     A            DPA101    R        B 11 64REFFLD(PA10)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            D#A035         4   O 11 67
     A            DPA111    R        B 11 72REFFLD(PA11)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            D#A036         4   O 11 75
     A            DCHK           1   H
     A*                                     'Y' -已存檔中--> UPD
     A*                                     ' ' -不存檔中--> ADD
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19920110  145017  REPGMR      REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                 22  1'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 25'F4 =視窗查詢'
     A                                 22 40'F5 =參考資料'
     A                                 22 55'F12=回前畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
