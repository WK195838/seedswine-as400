     A*%%TS  SD  19950819  085731  D910074     REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR161D                                    *
     A*    AUTHOR       : AN MAIN HSIA                               *
     A*    CREATE DATE  : 81/11/18                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR161                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :訂單系統                                  *
     A*    REMARK       :客戶銷售明細表                            *
     /*=============================================================*/
     /*  CODE       AUTHOR.     DATE    DESCRIPTION
     /*-------------------------------------------------------------
     /*  M001      MICHELLE   98.12.19  Y2K MODIFY
     F*----------------------------------------------------------------
     F*  M002     | MICHELLE  | 99.11.08 | ADD CUSGRP OPTION
     F*----------------------------------------------------------------
     F*  M003     | 00.11.08 |增加大區域選項
     F*----------------------------------------------------------------
     /*=============================================================*/
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19950819  085731  D910074     REL-V3R1M0  5763-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CF07(07)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR161'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'客戶銷售明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 22'辦事處　:'
     A            COMP           1A  B  5 38VALUES(' ' '1' '2' '3')
     A                                  5 40'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  6 22'產品組別:'
     A            DMA11          1A  B  6 38VALUES('1' '2' '3' ' ')
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A                                  6 40'(1-BRANDY 2-WHISKY 3-WINE " "-ALL)'
M003AA                                  7 22'大區域別:'
M003AA                                      DSPATR(HI)
M003AA            D#L01S         2   B  7 38
M003AA N59                                  DSPATR(HI)
M003AA  59                                  DSPATR(PC)
M003AA  59                                  DSPATR(RI)
M003AA                                  7 47'-'
M003AA            D#L01E         2   B  7 49
M003AA N58                                  DSPATR(HI)
M003AA  58                                  DSPATR(PC)
M003AA  58                                  DSPATR(RI)
     A                                  8 22'通　　路:'
     A                                      DSPATR(HI)
     A                                  8 47'-'
     A            DSC33S    R        B  8 38REFFLD(SC33)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N60                                  DSPATR(HI)
     A            DSC33E    R        B  8 49REFFLD(SC33)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  9 22'型　　態:'
     A                                      DSPATR(HI)
     A            DSC34S    R        B  9 38REFFLD(SC34)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  9 47'-'
     A            DSC34E    R        B  9 49REFFLD(SC34)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                 10 22'客戶代號:'
     A                                      DSPATR(HI)
     A            DSC04S    R        B 10 38REFFLD(SC04)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A                                 10 47'-'
     A            DSC04E    R        B 10 49REFFLD(SC04)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A N65                                  DSPATR(HI)
     A                                 11 22'產品代號:'
     A                                      DSPATR(HI)
     A            DSD03S    R        B 11 38REFFLD(SD03)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A N66                                  DSPATR(HI)
     A                                 11 47'-'
     A            DSD03E    R        B 11 49REFFLD(SD03)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(HI)
     A                                 15 22'送／退貨日期:'
M001MA*           DSC08S    R        B 14 38REFFLD(SC39)
M001MA            DSC08S         6  0B 15 38
     A                                      EDTWRD('  /  /  ')
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A            YSC39S    R        H      REFFLD(SC39)
     A                                 15 47'-'
M001MA*           DSC08E    R        B 14 49REFFLD(SC39)
M001MA            DSC08E         6  0B 15 49
     A                                      EDTWRD('  /  /  ')
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A            YSC39E    R        H      REFFLD(SC39)
     A                                 15 60'(MM/DD/YY)'
     A                                 16 22'確認日期:'
M001MA*           DSC39S    R        B 15 38REFFLD(SC39)
M001MA            DSC39S         6  0B 16 38
     A                                      EDTWRD('  /  /  ')
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                 16 47'-'
M001MA*           DSC39E    R        B 15 49REFFLD(SC39)
M001MA            DSC39E         6  0B 16 49
     A                                      EDTWRD('  /  /  ')
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                 16 60'(MM/DD/YY)'
     A                                 17 22'執行環境:'
     A            $EVR           1Y 0B 17 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 42'(1-即時   2-批次)'
     A                                 18 22'列印份數:'
     A            $CPY           3Y 0B 18 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 22'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F7 =檔案下載'
     A  98        ERR1          63A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          63A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR9          63A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 12 22'業務員　:'
     A                                      DSPATR(HI)
     A            DSC27S    R        B 12 38REFFLD(SC27)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A N72                                  DSPATR(HI)
     A                                 12 47'-'
     A            DSC27E    R        B 12 49REFFLD(SC27)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A N73                                  DSPATR(HI)
M002AA                                 13 22'集　　團:'
M002AA                                      DSPATR(HI)
M002AA            DMD06S         2   B 13 38
M002AA  74                                  DSPATR(RI)
M002AA  74                                  DSPATR(PC)
M002AA N74                                  DSPATR(HI)
M002AA                                 13 47'-'
M002AA            DMD06E         2   B 13 49
M002AA  75                                  DSPATR(RI)
M002AA  75                                  DSPATR(PC)
M002AA N75                                  DSPATR(HI)
     A                                 14 22'禮盒碼　:'
     A            DMA54          1   B 14 38VALUES('A' 'B' 'C')
     A                                 14 42'(A=全部 B=非禮盒 C=禮盒)'
