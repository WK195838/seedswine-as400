     A*%%TS  SD  19921002  110347  D910121     REL-V2R1M0  5738-PW1
     A*  81/02/13  15:20:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : SOA001D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/02/13                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOA001                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售訂單作業                              *
     A*    REMARK       :通路型態產品報價設定 (A/U/D/I)            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.11 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR                     SFL
     A*%%TS  SD  19921002  110347  D910121     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  3
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      VALUES(' ' '2' '3' '4' '5' '6')
     A            SA01      R        O  9  6
     A            SA02      R        O  9 16
     A            D#A03         12A  O  9 18
     A            SA03      R        O  9 31
     A            D#C03         12A  O  9 34
     A            DSALE          6A  O  9 47
     A            DSA06          4Y 2O  9 54EDTCDE(1)
M001MA            DSA05          6Y 0O  9 60
     A                                      EDTWRD('  /  /  ')
M001MA            DSA13          6Y 0O  9 69
     A                                      EDTWRD('  /  /  ')
     A            SA14      R        O  9 78
     A            SA04      R        H      REFFLD(SA04)
     A*------------------------------------------------------------
     A*
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19921002  110347  D910121     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A* 53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'SOA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'通路型態產品報價設定'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  4'輸入作業選擇:'
     A                                  5  5'2=修改'
     A                                  5 15'3=拷貝'
     A                                  5 35'5=查詢'
     A                                  5 45'6=確認'
     A                                  6 47'折扣　折扣'
     A                                  7  3'A 產品代號　通　路       型　+
     A                                      態          別　　率　　生效日 -
     A                                      截止日 F'
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  6REFFLD(SA01)
     A            DBGN2     R        B  8 16REFFLD(SA02)
     A            DBGN3     R        B  8 31REFFLD(SA03)
     A                                  5 25'4=刪除'
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A  97        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR9          63A  O 24  2MSGID(URE 0018 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPD2
     A*
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CF03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 29'通路型態產品報價設定'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6  3'產品代號:'
     A                                      DSPATR(HI)
     A            DSA01     R        B  6 16REFFLD(SA01)
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DMA03         22O  O  6 27
     A                                  6 50'單位:'
     A            MA13      R        O  6 58
     A                                  8  3'通路　　:'
     A                                      DSPATR(HI)
     A            DSA02     R        B  8 16REFFLD(SA02)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A031        22A  O  8 19
     A                                  8 50'確認:'
     A            DSA14     R        O  8 58REFFLD(SA14)
     A                                 10  3'型態　　:'
     A                                      DSPATR(HI)
     A            DSA03     R        B 10 16REFFLD(SA03)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            #C03      R        O 10 19
     A                                 12  3'折扣別　:'
     A            DSA04     R        B 12 16REFFLD(SA04)
     A                                      VALUES('N' 'S' 'A')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 28'N :普通折扣'
     A                                 12 44'S :特別折扣'
     A                                 12 60'A :搭贈'
     A                                 13  3'折扣率:'
     A            DSA06          4  2B 13 13EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 19'%'
     A                                 13 21'售價:'
     A                                      DSPATR(RI)
     A            PRICE     R        O 13 29REFFLD(MA40)
     A                                      EDTCDE(1)
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                 13 39'經銷價:'
     A            DMA40     R        O 13 49REFFLD(MA40)
     A                                      EDTCDE(1)
     A                                 13 59'零售價:'
     A            DMA41     R        O 13 69REFFLD(MA41)
     A                                      EDTCDE(1)
     A                                 15  3'搭贈比率:'
     A            DSA07     R        B 15 16REFFLD(SA07)
     A                                      EDTCDE(Z)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 20'/'
     A            DSA08     R        B 15 22REFFLD(SA08)
     A                                      EDTCDE(Z)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 28'(搭贈數量/訂購數量)'
     A            DSA09     R        B 16 16REFFLD(SA09)
     A                                      EDTCDE(Z)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16 20'/'
     A            DSA10     R        B 16 22REFFLD(SA10)
     A                                      EDTCDE(Z)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSA11     R        B 17 16REFFLD(SA11)
     A                                      EDTCDE(Z)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 20'/'
     A            DSA12     R        B 17 22REFFLD(SA12)
     A                                      EDTCDE(Z)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19  3'生效日期:'
M001MA            DSA05          6Y 0B 19 16
     A                                      EDTWRD('  /  /  ')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19 28'(MM/DD/YY)'
     A                                 20  3'截止日期:'
M001MA            DSA13          6Y 0B 20 16
     A                                      EDTWRD('  /  /  ')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20 28'(MM/DD/YY)'
     A                                 22  1'執行'
     A                                 22  9'F3 =回主畫面'
     A                                 22 25'F4 =視窗查詢'
     A                                 22 41'F5 =參考資料'
     A                                 22 57'F12=回前畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR8          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR10         63A  O 24  2MSGID(URE 0020 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
