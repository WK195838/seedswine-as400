     A*%%TS  SD  20021023  110253  REPGMR      REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : MTA001D                                    *
     A*    AUTHOR       : A1034  STEPHAIE                            *
     A*    CREATE DATE  : 81/01/13                                   *
     A*    UPDATE DATE  : 81/03/20 PHILIP                            *
     A*    PROGRAM NAME : MTA001                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    REMARK       :產品資料維護 (A/U/D/I)                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.04 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M002      | 01.03.20 |組拆碼：建立禮盒結構，另設ON-PACK CODE
     A*           |          |禮盒碼：原裝禮盒
     A*---------------------------------------------------------------
     A* M003      | 01.05.25 | NEW INPUT
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19920528  083105  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  5VALUES(' ' '3' '2' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            MA01      R        O  9  9
     A            MA03      R        O  9 19
     A            MA06      R        O  9 38
     A            MA12      R        O  9 58
     A            MA15      R        O  9 66EDTCDE(2)
     A            MA54      R        O  9 75
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920618  173359  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'MTA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'產品基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  6'2=修改'
     A                                  5 16'3=拷備'
     A                                  7  5'A  產品代號 中文簡稱        +
     A                                      英文簡稱      供應商代號 容積-
M002MA*                                      禮盒'
M002MA                                       組拆'
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  9REFFLD(MA01)
     A                                  5 27'5=查詢'
     A          R DSPC1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F6 =新增'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPD2
     A*%%TS  SD  20021023  110253  REPGMR      REL-V4R4M0  5769-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MTA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'產品基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'產品代號:'
     A            DMA01     R        B  4 14REFFLD(MA01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  5  2'中文全稱:'
     A            DMA02     R        B  5 14REFFLD(MA02)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  5 49'中文簡稱:'
     A            DMA03     R        B  5 61REFFLD(MA03)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  6  2'英文全稱:'
     A            DMA04     R        B  6 14REFFLD(MA04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMA05     R        B  6 46REFFLD(MA05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7  2'英文簡稱:'
     A            DMA06     R        B  7 14REFFLD(MA06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7 42'產品轉帳:'
     A                                      DSPATR(HI)
     A            DMA07     R        O  7 54REFFLD(MA07)
     A            DCMP           6A  O  7 57
     A                                  8  2'識別碼　:'
     A                                      DSPATR(HI)
     A            DMA08     R        O  8 14REFFLD(MA08)
     A            DID           22A  O  8 18
     A                                  8 42'種類　　:'
     A                                      DSPATR(HI)
     A            DMA09     R        B  8 54REFFLD(MA09)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DGROUP        22A  O  8 57
     A                                  9  2'品牌　　:'
     A                                      DSPATR(HI)
     A            DMA10     R        O  9 14REFFLD(MA10)
     A            DBRAND        22A  O  9 18
     A                                  9 42'Ａ＆Ｐ　:'
     A            DMA11     R        B  9 54REFFLD(MA11)
     A N68                                  DSPATR(HI)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'課稅別  :'
     A            DMA59     R        B 10 14REFFLD(MA59)
     A                                      VALUES('D' 'B' 'Z')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 22'(D-應稅, B-免稅, Z-零稅)'
     A                                 11  2'庫存單位:'
     A            DMA13     R        B 11 14REFFLD(MA13)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 24'主要供應商   :'
     A                                      DSPATR(HI)
     A            DMA12     R        B 11 41REFFLD(MA12)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMB03         12A  O 11 47
     A                                 11 60'F.O.B.:'
     A            DMA31     R        O 11 68REFFLD(MA31)
     A                                      EDTCDE(2)
     A                                 12  2'採購單位:'
     A            DMA14     R        B 12 14REFFLD(MA14)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 24'前置時間     :'
     A            DMA18     R        B 12 41REFFLD(MA18)
     A                                      EDTCDE(2)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 46'日'
     A                                 12 60'FHI :'
     A            DMA32     R        O 12 68REFFLD(MA32)
     A                                      EDTCDE(2)
     A                                 13  2'單位容積:'
     A            DMA15     R        B 13 14REFFLD(MA15)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(2)
     A                                 13 20'l'
     A                                 13 24'採購人員     :'
     A                                      DSPATR(HI)
     A            DMA19     R        B 13 41REFFLD(MA19)
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMC02         10A  O 13 49
     A                                 13 60'DUTY :'
     A            DMA33     R        O 13 68REFFLD(MA33)
     A                                      EDTCDE(2)
     A                                 14  2'單位轉換:'
     A            DMA16     R        B 14 14REFFLD(MA16)
     A                                      EDTCDE(2)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 24'原產地       :'
     A            DMA20     R        B 14 41REFFLD(MA20)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 56'單位成本:'
     A            DMA34     R        O 14 68REFFLD(MA34)
     A                                      EDTCDE(2)
     A                                 15  2'每箱容積:'
     A            DMA15C         6Y 3O 15 14EDTWRD('   .   ')
     A                                 15 22'l'
     A                                 15 24'空瓶重量     :'
     A            DMA21     R        B 15 41REFFLD(MA21)
     A                                      EDTCDE(2)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 49'（克）'
     A                                 15 59'批發價格:'
     A            DMA45     R        O 15 71REFFLD(MA45)
     A                                      EDTCDE(2)
     A                                 16  2'包裝規格:'
     A            DMA17     R        B 16 14REFFLD(MA17)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16 24'長 :'
     A            DMA35     R        B 16 41REFFLD(MA35)
     A                                      EDTCDE(2)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16 59'零售價格:'
     A            DMA46     R        O 16 71REFFLD(MA46)
     A                                      EDTCDE(2)
     A                                 17  2'組拆碼　:'
     A            DMA54     R        B 17 14REFFLD(MA54)
     A                                      VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 18'(Y, )'
     A                                 18  2'禮盒碼　:'
     A            DMX54     R        B 18 14REFFLD(MA54)
     A                                      VALUES('Y' ' ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 18'(Y, )'
     A                                 18 25'ON-PACK       :'
     A            DMX55     R        B 18 41REFFLD(MA54)
     A                                      VALUES('Y' ' ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 45'(Y, )'
     A                                 17 24'寬 :'
     A            DMA36     R        B 17 41REFFLD(MA36)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(2)
     A                                 17 59'高:'
     A            DMA47     R        B 17 71REFFLD(MA47)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(2)
     A                                 19  2'可寄賣放:'
     A            DMA55     R        B 19 14REFFLD(MA55)
     A                                      VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMA56     R        B 19 16REFFLD(MA56)
     A                                      VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19 18'(Y, )'
     A                                 19 24'保稅倉安全量 :'
     A            DMA37     R        B 19 41REFFLD(MA37)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(2)
     A                                 19 59'最近銷售:'
     A            DMA50          6Y 0O 19 71EDTCDE(Y)
     A                                 20  2'自動轉帳:'
     A            DMA57     R        O 20 14REFFLD(MA57)
     A                                 20 18'(Y, )'
     A                                 20 24'完稅倉安全量 :'
     A            DMA38     R        B 20 41REFFLD(MA38)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(2)
     A                                 20 59'最近進貨:'
     A            DMA51          6Y 0O 20 71EDTCDE(Y)
     A                                 21  2'公賣利益:'
     A            DMA25     R        B 21 14REFFLD(MA25)
     A                                      EDTCDE(2)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 21 26'使用碼     :'
     A            DMA58     R        O 21 41REFFLD(MA58)
     A                                 21 43'(A,I)'
     A                                 21 59'建檔日期:'
     A            DMA53          6Y 0O 21 71EDTCDE(Y)
     A                                 22  2'備註　　:'
     A            DMA23     R        B 22 14REFFLD(MA23)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMA24     R        B 22 47REFFLD(MA24)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 23  2'執行'
     A  28        DMSG          78A  O 23  2DSPATR(HI)
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =視窗查詢'
     A                                 23 42'F5 =參考資料'
     A                                 23 58'F12=回前畫面'
     A  94        ERR0          60A  O 24  2MSGID(UPT 0012 PTMF)
     A                                      DSPATR(HI)
     A  98        ERR1          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          60A  O 24  2MSGID(URE 0040 REMF)
     A                                      DSPATR(HI)
     A  93        ERR5          60A  O 24  2MSGID(URE 0161 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*M003A FOR NEW INPUT
     A*
M003AA          R DSPD3
|    A                                      CSRLOC(D#ROW      D#COL)
|    A                                      CA03(03)
|    A                                      CF04(04)
|    A                                      CF05(05)
|    A                                      CF01(01)
|    A            D#ROW          3S 0H
|    A            D#COL          3S 0H
|    A                                  1  2'MTA001'
|    A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
|    A                                      DSPATR(HI)
|    A                                  1 62'日期:'
|    A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
|    A                                  2  2'SCR002'
|    A                                  2 31'產品基本資料維護'
|    A                                      DSPATR(HI)
|    A                                  2 62'時間:'
|    A                                  2 70TIME
|    A                                      EDTWRD('  :  :  ')
|    A            DFUN           6O  O  3  2DSPATR(RI)
|    A                                  3 62' USER :'
|    A            $USER         10A  O  3 70
|    A                                  4  2'產品代號  :'
|    A            DMA012    R        O    +2REFFLD(MA01)
|    A            DMA03     R             +2REFFLD(MA03)
|    A                                  6  2'產品類別  :'
|    A                                      DSPATR(HI)
|    A            DBE02          2   B    +2
|    A N79                                  DSPATR(HI)
|    A  79                                  DSPATR(PC)
|    A  79                                  DSPATR(RI)
|    A  31                                  DSPATR(UL)
|    A N31                                  DSPATR(PR)
|    A            DBN02         22O  O    +2
|    A                                  8  2'產品原品牌:'
|    A                                      DSPATR(HI)
|    A            DBE03          2   B    +2
|    A N78                                  DSPATR(HI)
|    A  78                                  DSPATR(PC)
|    A  78                                  DSPATR(RI)
|    A  31                                  DSPATR(UL)
|    A N31                                  DSPATR(PR)
|    A            DB1           32O  O    +2
|    A                                 10  2'新產品種類:'
|    A                                      DSPATR(HI)
|    A            DBE04          2   B    +2
|    A N77                                  DSPATR(HI)
|    A  77                                  DSPATR(PC)
|    A  77                                  DSPATR(RI)
|    A  31                                  DSPATR(UL)
|    A N31                                  DSPATR(PR)
|    A            D#703         32O  O    +2
|    A                                 12  2'附屬品牌  :'
|    A                                      DSPATR(HI)
|    A            DBE05          2   B    +2
|    A N76                                  DSPATR(HI)
|    A  76                                  DSPATR(PC)
|    A  76                                  DSPATR(RI)
|    A  31                                  DSPATR(UL)
|    A N31                                  DSPATR(PR)
|    A            DB2           32O  O    +2
|    A                                 14  2'產品分類代碼:'
|    A            DBE06          6   B    +2
|    A N75                                  DSPATR(HI)
|    A  75                                  DSPATR(PC)
|    A  75                                  DSPATR(RI)
|    A  31                                  DSPATR(UL)
|    A N31                                  DSPATR(PR)
|    A  92        ERR6          60A  O 24  2MSGID(UPT 0030 PTMF)
|    A                                      DSPATR(HI)
|    A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
|    A                                      DSPATR(HI)
|    A                                 23 10'F3 =回主畫面'
|    A                                 23 26'F4 =視窗查詢'
|    A                                 23 42'F5 =參考資料'
M003AA                                 23 58'F1 =回前畫面'
