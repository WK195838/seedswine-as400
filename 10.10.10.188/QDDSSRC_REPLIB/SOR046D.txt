     A*%%TS  SD  19930416  113612  D910074     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR046D                                    *
     A*    AUTHOR       : A1087  JOYCE TSAI                          *
     A*    CREATE DATE  : 81/02/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR046                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售訂單系統                              *
     A*    REMARK       :核准簽章明細表                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930416  113612  D910074     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR046'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'核准簽章明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 26'辦事處　:'
     A            COMP           1A  B  5 38VALUES(' ' '1' '2' '3')
     A                                  5 41'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  6 26'通　　路:'
     A                                      DSPATR(HI)
     A            DSC33S    R        B  6 38REFFLD(SC33)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N60                                  DSPATR(HI)
     A                                  6 41'-'
     A            DSC33E    R        B  6 43REFFLD(SC33)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  7 26'地　　區:'
     A                                      DSPATR(HI)
     A            DSC31S    R        B  7 38REFFLD(SC31)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  7 41'-'
     A            DSC31E    R        B  7 43REFFLD(SC31)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                  8 26'截止日期:'
     A            DSC08T    R        B  8 38REFFLD(SC08)
     A                                      EDTWRD('  /  /  ')
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                  9 26'選　　擇:'
     A            DSEL           1A  B  9 38VALUES('1' '2')
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                  9 41'(1=非普銷 2=普銷)'
     A                                 11 19'信用額度＋逾期:'
     A            DSCR01         1A  B 11 38VALUES(' ' 'Y' 'N')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 11 45'(Y 或 N)'
     A                                 12 19'試　飲　　　　:'
     A            DSCR02         1A  B 12 38VALUES(' ' 'Y' 'N')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 12 45'(Y 或 N)'
     A                                 13 19'換　貨　　　　:'
     A            DSCR05         1A  B 13 38VALUES(' ' 'Y' 'N')
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                 13 45'(Y 或 N)'
     A                                 14 19'價格修改　　　:'
     A            DSCR03         1A  B 14 38VALUES(' ' 'Y' 'N')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 14 45'(Y 或 N)'
     A                                 15 19'搭　贈　　　　:'
     A            DSCR04         1A  B 15 38VALUES(' ' 'Y' 'N')
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                 15 45'(Y 或 N)'
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 42'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          63A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          63A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
