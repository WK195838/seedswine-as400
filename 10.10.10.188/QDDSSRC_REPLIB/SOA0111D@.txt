     A*%%TS  SD  20020917  181616  REPGMR      REL-V4R4M0  5769-PW1
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.11 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M002      | 01.10.29 |將試飲核准獨立核准
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SOSMLF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920922  141609  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            HRCDN1         4A  H
     A            HSM04     R        H      REFFLD(SM04)
     A            DOPT1          1A  B 10  2
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      VALUES(' ' '2' '3' '4' '5' '6' '7' -
M002MA*                                     '8')
M002MA                                      '8' '9')
M001MA*           SM06      R        O 10  4EDTWRD('  /  /  ')
M001MA            LSM06          6Y 0O 10  4EDTWRD('  /  /  ')
     A            SM04      R        O 10 13
     A            SM05      R        O 10 19
     A            DME04         14A  O 10 24
     A            SM02      R        O 10 39
     A            DDOC          10A  O 10 52
     A            SM14      R        O 10 63
     A            SM08      R        O 10 67
     A N31                                  DSPATR(ND)
     A            SM09      R        O 10 69
     A N32                                  DSPATR(ND)
     A            SM10      R        O 10 71
     A N33                                  DSPATR(ND)
     A            SM11      R        O 10 73
     A N34                                  DSPATR(ND)
     A            SM12      R        O 10 75
     A N35                                  DSPATR(ND)
     A            SM13      R        O 10 77
     A N36                                  DSPATR(ND)
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920922  141609  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
M002MA*                                 1  2'SOA011'
M002MA                                  1  2'SOA0111'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 28'全省訂單／送貨核准處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  6'2=核准'
     A                                  5 42'4=核准取消'
     A                                  5 58'5=單據明細查詢'
     A                                  5 21'3=單價／搭贈處理'
M002MA*                                 6 42'7=額度查詢'
M002MA*                                 6 58'8=逾期查詢'
M002MA                                  6 27'7=額度查詢'
M002MA                                  6 42'8=逾期查詢'
M002AA                                  6 58'9=試飲處理'
     A                                  7 62'地單額逾搭試換'
     A                                  8  2'A MM/DD/YY客戶代號  客戶簡稱  -
     A                                        單據號碼    單別    區價+
     A                                      度期贈飲貨'
     A                                      DSPATR(UL)
M001MA*           DBGN1     R        B  9  4REFFLD(SM06)
M001MA            DBGN1          6Y 0B  9  4
     A                                      EDTWRD('  /  /  ')
     A            DBGN2     R        B  9 13REFFLD(SM04)
     A            DBGN3     R        B  9 19REFFLD(SM05)
     A            DBGN4     R        B  9 39REFFLD(SM02)
     A                                  7  3'日　　期'
     A                                  6  6'6=單價／搭贈查詢'
     A*------------------------------------------------------------
     A*
     A          R DSPC1
     A*%%TS  SD  19920127  162736  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12 =回前畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(URE 0063 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A* 54                                  SFLNXTCHG
     A          R SFLSR2                    SFL
     A*%%TS  SD  20020917  180920  REPGMR      REL-V4R4M0  5769-PW1
     A            RRN2           4S 0H
     A            DS03      R        O 12  3REFFLD(SD03 *LIBL/SOSDPF)
     A            MA03      R        O 12 13REFFLD(MA03 *LIBL/MTMAPF)
     A            MA13      R        O 12 32REFFLD(MA13 *LIBL/MTMAPF)
     A            DS04      R        O 12 39REFFLD(SD04 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DS05      R        O 12 47REFFLD(SD05 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DS06      R        O 12 53REFFLD(SD06 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DS07      R        O 12 64REFFLD(SD07 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A*------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0008)
     A*                                     ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A* 37                                  FRCDTA
     A* 37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A*
M002MA*                                 1  2'SOA011'
M002MA                                  1  2'SOA0111'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'訂單／送貨核准處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A            SM01      R        O  5 14
     A            D#B02         16A  O  5 17
     A                                  5 34'地區　　:'
     A            SM14      R        O  5 46
     A            #D02      R        O  5 49REFFLD(#D02 *LIBL/PA#DPF)
     A                                  6  2'單據號碼:'
     A            SM02      R        O  6 14
     A                                  6 34'單別　　:'
     A            SM03      R        O  6 46
     A            #203      R        O  6 49REFFLD(#203 *LIBL/PA#2PF)
     A                                  7  2'單據日期:'
M001MA*           SM06      R        O  7 14EDTWRD('  /  /  ')
M001MA            LSM06          6Y 0O  7 14EDTWRD('  /  /  ')
     A                                  7 23'(MM/DD/YY)'
     A                                  7 34'客戶代號:'
     A            SM04      R        O  7 46
     A            SM05      R        O  7 52
     A            ME04      R        O  7 57REFFLD(ME04 *LIBL/MTMEPF)
     A                                  8  2'總金額　:'
     A            SM15      R        O  8 14EDTCDE(1)
     A                                  8 34'發票聯數:'
     A            SM19      R        O  8 46REFFLD(SM19 SOSMLF2)
     A                                 11  3'產品代號／名稱             +
     A                                      單位　　數量 搭贈　　單價-
     A                                           金　額'
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A*                                     CSRLOC(D#ROW      D#COL)
     A          R DSPC2
     A*%%TS  SD  19920127  162736  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A* 54                                  SFLNXTCHG
     A          R SFLSR3                    SFL
     A*%%TS  SD  19920309  163307  REPGMRP     REL-V2R1M0  5738-PW1
     A            RRN3           4S 0H
     A            DS03      R        O 12  3REFFLD(SD03 *LIBL/SOSDPF)
     A            MA03      R        O 12 13REFFLD(MA03 *LIBL/MTMAPF)
     A            MA13      R        O 12 32REFFLD(MA13 *LIBL/MTMAPF)
     A            DS04      R        O 12 39REFFLD(SD04 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DS05      R        O 12 47REFFLD(SD05 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DS06      R        O 12 54REFFLD(SD06 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DS08      R        O 12 64REFFLD(SD08 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DS09      R        O 12 75REFFLD(SD09 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A*------------------------------------------------------------
     A*
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A*%%TS  SD  19920309  163307  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0008)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN3          4S 0H      SFLRCDNBR
M002MA*                                 1  2'SOA011'
M002MA                                  1  2'SOA0111'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 30'訂單／送貨核准處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A            SM01      R        O  5 14
     A            D#B02         16A  O  5 17
     A                                  5 34'地區　　:'
     A            SM14      R        O  5 46
     A            #D02      R        O  5 49REFFLD(#D02 *LIBL/PA#DPF)
     A                                  6  2'單據號碼:'
     A            SM02      R        O  6 14
     A                                  6 34'單別　　:'
     A            SM03      R        O  6 46
     A            #203      R        O  6 49REFFLD(#203 *LIBL/PA#2PF)
     A                                  7  2'單據日期:'
M001MA*           SM06      R        O  7 14EDTWRD('  /  /  ')
M001MA            LSM06          6Y 0O  7 14EDTWRD('  /  /  ')
     A                                  7 23'(MM/DD/YY)'
     A                                  7 34'客戶代號:'
     A            SM04      R        O  7 46
     A            SM05      R        O  7 52
     A            ME04      R        O  7 57REFFLD(ME04 *LIBL/MTMEPF)
     A                                  8  2'總金額　:'
     A            SM15      R        O  8 14EDTCDE(1)
     A                                  8 34'發票聯數:'
     A            SM19      R        O  8 46REFFLD(SM19 SOSMLF2)
     A                                 11  3'產品代號／名稱             單+
     A                                      位　　數量 搭贈  單價　正常單+
     A                                      價　正常搭贈'
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A*                                     CSRLOC(D#ROW      D#COL)
     A          R DSPC3
     A*%%TS  SD  19920127  162736  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A*
M002AA*新增下列所有行數以供試飲核准使用
     A*
     A*----------------------------------------------------------------
     A          R SFLSR4                    SFL
     A*%%TS  SD  20020917  181616  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DSD03     R        O 10  4REFFLD(SD03 *LIBL/SOSDPF)
     A            MA03      R        O 10 14REFFLD(MA03 *LIBL/MTMAPF)
     A            DSD04     R        O 10 34REFFLD(SD04 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DSD05     R        O 10 42REFFLD(SD05 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DSD06     R        O 10 48REFFLD(SD06 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DSD08     R        O 10 58REFFLD(SD08 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DSD09     R        O 10 68REFFLD(SD09 *LIBL/SOSDPF)
     A                                      EDTCDE(1)
     A            DSC26     R        O 10 74REFFLD(SD17 *LIBL/SOSDPF)
     A*------------------------------------------------------------
     A*
     A          R SFLCR4                    SFLCTL(SFLSR4)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      CA03(03)
     A                                      CF07(07)
     A                                      CF08(08)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'SOA0113'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 28'全省訂單／送貨核准處理'
     A                                  3 34'試飲核准'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'單據號碼:'
     A            DSC02     R        O  5 14REFFLD(SC02 *LIBL/SOSCPF)
     A                                  5 41'單別:'
     A            DSC03     R        O  5 49REFFLD(SC03 *LIBL/SOSCPF)
     A            #203      R        O  5 52REFFLD(#203 *LIBL/PA#2PF)
     A                                  6  2'日　　期:'
     A            DSC08          6Y 0O  6 14
     A                                      EDTWRD('  /  /  ')
     A                                  6 41'客戶:'
     A            DSC04     R        O  6 49REFFLD(SC04 *LIBL/SOSCPF)
     A                                  6 55'-'
     A            DSC05     R        O  6 57REFFLD(SC05 *LIBL/SOSCPF)
     A            ME04      R        O  6 62REFFLD(ME04 *LIBL/MTMEPF)
     A                                  8 73'試'
     A                                  9  2'A產品     名稱               -
     A                                      訂購 搭贈   單價  單價 搭+
     A                                      贈 飲'
     A                                      DSPATR(UL)
     A                                  8 59'正常'
     A                                  8 66'正常'
     A*------------------------------------------------------------
     A*
     A          R DSPC4
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12 =回前畫面'
     A                                    +2'F7=核准'
     A                                    +2'F8=核准取消'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(URE 0063 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
