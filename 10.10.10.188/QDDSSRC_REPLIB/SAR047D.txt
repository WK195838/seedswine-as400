     A*%%TS  SD  19930825  172305  D910091     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SAR047D                                    *
     A*    AUTHOR       : A1084  CHARLES                             *
     A*    CREATE DATE  : 81/05/04                                   *
     A*    PROGRAM NAME : SAR047                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :產品銷售統計表                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930825  172305  D910091     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SAR047'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'產品銷售統計表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 28'辦事處　:'
     A            COMP           1A  B  4 40VALUES(' ' '1' '2' '3')
     A                                  4 42'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  5 28'通路　　:'
     A                                      DSPATR(HI)
     A            DSC33S         1A  B  5 40
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N60                                  DSPATR(HI)
     A                                  5 50'-'
     A            DSC33E         1A  B  5 52
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A                                  6 28'型態　　:'
     A                                      DSPATR(HI)
     A            DSC34S         2A  B  6 40
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A                                  6 50'-'
     A            DSC34E         2A  B  6 52
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(HI)
     A                                  7 28'組別　　:'
     A                                      DSPATR(HI)
     A            DSC28S    R        B  7 40REFFLD(SC28)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N70                                  DSPATR(HI)
     A                                  7 50'-'
     A            DSC28E    R        B  7 52REFFLD(SC28)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N71                                  DSPATR(HI)
     A                                  8 28'產品代號:'
     A                                      DSPATR(HI)
     A            DSD03S    R        B  8 40REFFLD(SD03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N60                                  DSPATR(HI)
     A                                  8 50'-'
     A            DSD03E    R        B  8 52REFFLD(SD03)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A                                  9 28'送貨年月:'
     A            DSC08          4Y 0B  9 40
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      EDTWRD('  /  ')
     A                                  9 46'(MM/YY)'
     A                                 10 28'確認年月:'
     A            DSC39          4Y 0B 10 40
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      EDTWRD('  /  ')
     A                                 10 46'(MM/YY)'
     A                                 11 28'統計內容:'
     A            DSEL1          1A  B 11 40VALUES('4' '1' '2' '3')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 12 34'( 1=銷售2=銷售含搭贈3=+
     A                                      銷售含試飲4=全部)'
     A                                 13 28'列印方式:'
     A            DSEL2          1A  B 13 40
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                      VALUES('1' '2')
     A                                 13 42'( 1=分通路2=不分通路)'
     A                                 14 28'分析方式:'
     A            DSEL           1A  B 14 40
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                      VALUES('A' 'B')
     A                                 14 42'( A-禮盒不拆B-禮盒拆單瓶'
     A                                 16 28'執行環境:'
     A            $EVR           1Y 0B 16 41VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 28'列印份數'
     A                                 17 39':'
     A            $CPY           3Y 0B 17 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 28'列表機'
     A                                      DSPATR(HI)
     A                                 18 39':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 41CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 29'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
