     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARA0052D                                   *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/03/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARA005                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款作業                              *
     A*    REMARK       :收款沖帳處理－沖帳修改                    *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
M001DA*           DAD11     R        O 12  3REFFLD(SI21)
M001AA            DAD11          6  0O 12  3
     A                                      EDTWRD('  /  /  ')
     A            AD07      R        O 12 14
     A            SI10      R        O 12 26
     A            SI25      R        O 12 41EDTCDE(1)
     A            SI26      R        O 12 53EDTCDE(1)
     A            SI30      R        O 12 65EDTCDE(1)
M001DA*           DAD12     R        O 13  4REFFLD(SI29)
M001AA            DAD12          6  0O 13  4
     A                                      EDTWRD('  /  /  ')
M001DA*           DSI29     R        O 13 14REFFLD(SI29)
M001AA            DSI29          6  0O 13 14
     A                                      EDTWRD('  /  /  ')
     A            SI11      R        O 13 26
     A            AD08      R        O 13 41EDTCDE(1)
     A            SI27      R        O 13 53EDTCDE(1)
     A            SI28      R        O 13 65EDTCDE(1)
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0005)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'ARI026'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 35'沖帳查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'客戶代號:'
     A            AC02      R        O  4 14
     A            AC03      R        O  4 20
     A            ME04      R        O  4 25
     A                                  5  2'收款日期:'
M001DA*           DAC04     R        O  5 14REFFLD(AC04)
M001AA            DAC04          6  0O  5 14
     A                                      EDTWRD('  /  /  ')
     A                                  5 23'(MM/DD/YY)'
     A                                  4 46'種類　　:'
     A            AC05      R        O  4 58
     A            MTYPE          6A  O  4 61
     A                                  5 46'預兌日期:'
M001DA*           DAC07     R        O  5 58REFFLD(AC07)
M001AA            DAC07          6  0O  5 58
     A                                      EDTWRD('  /  /  ')
     A                                  5 68'(MM/DD/YY)'
     A                                  6  2'收款金額:'
     A            AC08      R        O  6 14EDTCDE(1)
     A                                  6 44'未分配餘額:'
     A            AC10      R        O  6 58EDTCDE(1)
     A                                  7  2'票據號碼:'
     A            AC06      R        O  7 14
     A                                  7 46'兌現日期:'
M001DA*           DAC11     R        O  7 58REFFLD(AC11)
M001AA            DAC11          6  0O  7 58
     A                                      EDTWRD('  /  /  ')
     A                                  7 68'(MM/DD/YY)'
     A            DBGN1     R        B 11 14REFFLD(AD07)
     A            DBGN0          6Y 0B 11  3EDTCDE(Y)
     A                                  9  2'發票日期'
     A                                  9 13'發票號碼'
     A                                  9 26'訂單號碼'
     A                                  9 42'發票金額'
     A                                  9 54'已沖金額'
     A                                  9 66'未沖金額'
     A                                 10  2'分配日期應收日期   送貨單號 -
     A                                            沖銷金額  價差金額  銷+
     A                                      退金額'
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0048 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
