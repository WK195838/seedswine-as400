     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SAR041D                                    *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  : 81/05/05                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SAR041                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款作業                              *
     A*    REMARK       :客戶退貨明細表                            *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    90
     A                                      MSGCON(030 URE9999 REMF)
     A                                      SPACEA(001)
     A                                    96
     A                                      '客戶退貨明細表'
     A                                      SPACEA(001)
     A                                     1
     A                                      '辦事處　:'
     A            COMP           4        +1
     A N21                                91
     A                                      '（產品收回含試飲退回）'
     A N22                                92
     A                                      '（產品收回不含試飲退回）'
     A N23                                96
     A                                      '（僅試飲退回）'
     A                                      SPACEA(001)
     A                                     1
     A                                      '組別代號:'
     A            DSG12S         4        +1
     A                                    +4
     A                                      '-'
     A            DSG12E         4        +1
     A                                      SPACEA(001)
     A                                     1
     A                                      '業務代號:'
     A            DSG11S         6        +1
     A                                    +4
     A                                      '-'
     A            DSG11E         6        +1
     A                                      SPACEA(001)
     A                                     1
     A                                      '出貨物流點:'
     A            DSG04S         5        +1
     A                                    +4
     A                                      '-'
     A            DSG04E         5        +1
     A                                   162
     A                                      '日期:'
     A            $EGMDY         6  0O   170
     A                                      EDTCDE(Y)
     A                                   179
     A                                      '頁次:'
     A                                   187
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '產品代號:'
     A            DSH03S         9        +1
     A                                    +4
     A                                      '-'
     A            DSH03E         9        +1
     A                                   162
     A                                      '時間:'
     A                                   170
     A                                      TIME
     A                                      SPACEA(001)
     A  20                                 1
     A                                      '打單日期:'
     A  20        DSG06S         6  0O    +1
     A                                      EDTCDE(Y)
     A  20                                23
     A                                      '-'
     A  20        DSG06E         6  0O    +1
     A                                      EDTCDE(Y)
     A N20                                 1
     A                                      '確認日期:'
     A N20        DSG26S         6  0O    +1
     A                                      EDTCDE(Y)
     A N20                                23
     A                                      '-'
     A N20        DSG26E         6  0O    +1
     A                                      EDTCDE(Y)
     A                                   163
     A                                      'USER :'
     A            $USER         10   O   170
     A                                   181
     A                                      '<SAR041B>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '===================='
     A                                    21
     A                                      '===================='
     A                                    41
     A                                      '===================='
     A                                    61
     A                                      '===================='
     A                                    81
     A                                      '===================='
     A                                   101
     A                                      '===================='
     A                                   121
     A                                      '===================='
     A                                   141
     A                                      '===================='
     A                                   161
     A                                      '===================='
     A                                   181
     A                                      '================'
     A                                      SPACEA(001)
     A  33                               146
     A                                      '------轉　帳　銷　退　成　本-----
     A                                      --'
     A  33                               187
     A                                      '轉帳銷退'
     A                                      SPACEA(001)
     A                                     1
     A                                      '業務員'
     A                                    12
     A                                      '出貨物流點'
     A                                    47
     A                                      '單據號碼'
     A                                    61
     A                                      '收回日期'
     A                                    71
     A                                      '產品代號'
     A                                    81
     A                                      '產品簡稱'
     A                                   100
     A                                      '數量'
     A                                   117
     A                                      '金額'
     A  33                               128
     A                                      '收回成本'
     A  33                               146
     A                                      'F.O.B.'
     A  33                               161
     A                                      'F.H.I.'
     A  33                               178
     A                                      'DUTY'
     A  33                               188
     A                                      '總成本'
     A                                      SPACEA(001)
     A N33                               188
     A                                      ' '
     A                                      SPACEA(001)
     A                                     1
     A                                      '--------------------'
     A                                    21
     A                                      '--------------------'
     A                                    41
     A                                      '--------------------'
     A                                    61
     A                                      '--------------------'
     A                                    81
     A                                      '--------------------'
     A                                   101
     A                                      '--------------------'
     A                                   121
     A                                      '--------------------'
     A                                   141
     A                                      '--------------------'
     A                                   161
     A                                      '--------------------'
     A                                   181
     A                                      '----------------'
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A N30        MC02      R              1
     A N31        SN03      R             13
     A N31N38                             18
     A                                      '-'
     A N31        SN11      R             19
     A N31        ME04      R             24
     A N32        SN02      R             45
M001DA*N32        PSN05     R             62REFFLD(SN05)
     A N32        PSN05          6  0     62
     A                                      EDTCDE(Y)
     A            SO03      R             72
     A            MA03      R             81
     A            SO05      R             99
     A                                      EDTCDE(J)
     A            AMT0          11  2    108
     A                                      EDTCDE(J)
     A  33        AMT1          11  2    123
     A                                      EDTCDE(J)
     A  33        SO13      R            138
     A                                      EDTCDE(J)
     A  33        SO14      R            153
     A                                      EDTCDE(J)
     A  33        SO15      R            168
     A                                      EDTCDE(J)
     A  33        AMT2          11  2    183
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    67
     A                                      '收貨物流點小計----------->'
     A            SO05C          5  0     99
     A                                      EDTCDE(J)
     A            AMT0C         11  2    108
     A                                      EDTCDE(J)
     A  33        AMT1C         11  2    123
     A                                      EDTCDE(J)
     A  33        SO13C         11  2    138
     A                                      EDTCDE(J)
     A  33        SO14C         11  2    153
     A                                      EDTCDE(J)
     A  33        SO15C         11  2    168
     A                                      EDTCDE(J)
     A  33        AMT2C         11  2    183
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    71
     A                                      '業務員合計----------->'
     A            SO05S          5  0     99
     A                                      EDTCDE(J)
     A            AMT0S         11  2    108
     A                                      EDTCDE(J)
     A  33        AMT1S         11  2    123
     A                                      EDTCDE(J)
     A  33        SO13S         11  2    138
     A                                      EDTCDE(J)
     A  33        SO14S         11  2    153
     A                                      EDTCDE(J)
     A  33        SO15S         11  2    168
     A                                      EDTCDE(J)
     A  33        AMT2S         11  2    183
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD4
     A                                      SPACEB(001)
     A                                    71
     A                                      '期間　總計----------->'
     A            SO05R          5  0     99
     A                                      EDTCDE(J)
     A            AMT0R         11  2    108
     A                                      EDTCDE(J)
     A  33        AMT1R         11  2    123
     A                                      EDTCDE(J)
     A  33        SO13R         11  2    138
     A                                      EDTCDE(J)
     A  33        SO14R         11  2    153
     A                                      EDTCDE(J)
     A  33        SO15R         11  2    168
     A                                      EDTCDE(J)
     A  33        AMT2R         11  2    183
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                      SPACEB(001)
     A                                     1
     A                                      '===================='
     A                                    21
     A                                      '===================='
     A                                    41
     A                                      '===================='
     A                                    61
     A                                      '===================='
     A                                    81
     A                                      '===================='
     A                                   101
     A                                      '===================='
     A                                   121
     A                                      '===================='
     A                                   141
     A                                      '===================='
     A                                   161
     A                                      '===================='
     A                                   181
     A                                      '================'
     A                                      SPACEA(001)
     A                                     2
     A                                      '***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                      SPACEB(001)
     A                                     1
     A                                      '===================='
     A                                    21
     A                                      '===================='
     A                                    41
     A                                      '===================='
     A                                    61
     A                                      '===================='
     A                                    81
     A                                      '===================='
     A                                   101
     A                                      '===================='
     A                                   121
     A                                      '===================='
     A                                   141
     A                                      '===================='
     A                                   161
     A                                      '===================='
     A                                   181
     A                                      '================'
     A                                      SPACEA(001)
     A                                     2
     A                                      '<<SAR041B>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
