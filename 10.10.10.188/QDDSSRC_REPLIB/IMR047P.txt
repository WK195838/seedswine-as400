     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : IMR047P                                    *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : 81/02/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR047                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :產品送貨明細表                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.12.10 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    48MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    54'產品送貨明細表'
     A                                      SPACEA(1)
     A                                    91'日期:'
     A            $EGMDY         6  0O    99EDTCDE(Y)
     A                                   108'頁次:'
     A                                   116PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    91'時間:'
     A                                    99TIME
     A                                      SPACEA(1)
     A N20                                 1'送貨日期:'
     A  20                                 1'確認日期:'
M001MA            DMDYS          6  0     +1EDTWRD('  /  /  ')
M001DA*                                     REFFLD(SE21)
     A                                    +1'-'
M001MA            DMDYE          6  0     +1EDTWRD('  /  /  ')
     A*M001D                                REFFLD(SE21)
     A                                    92'USER :'
     A            $USER         10   O    99
     A                                   110'<IMR047>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ======================'
     A                                      SPACEA(1)
     A                                    90'訂購'
     A                                    99'搭贈'
     A                                   107'試飲'
     A                                   117'送貨'
     A                                      SPACEA(1)
     A                                     1'送貨日期'
     A                                    12'產品代號'
     A                                    22'產品簡稱'
     A                                    41'客戶代號'
     A                                    54'客戶簡稱'
     A                                    73'送貨單號'
     A                                    90'數量'
     A                                    99'數量'
     A                                   107'數量'
     A                                   117'總數'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       ----------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
M001MA* 31        SE21      R              2EDTWRD('  /  /  ')
M001MA  31        PSE21          6  0      2EDTWRD('  /  /  ')
     A  30        SF03      R             13
     A  30        MA03      R             22
     A  29        SE04      R             42
     A  29                                48'-'
     A  29        SE05      R             50
     A  29        ME04      R             54
     A            SF02      R             73
     A            PSF041         5  0     89EDTCDE(J)
     A            SF05      R             99EDTCDE(J)
     A            PSF042         5  0    106EDTCDE(J)
     A            PTOT           5  0    116EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ======================'
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ======================'
     A                                      SPACEA(1)
     A                                     2'<<IMR047>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ======================'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE31                      SPACEA(1)
     A                                     3'もも此範圍無資料もも'
