     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : IMR041P                                    *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : 81/02/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR041                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :送貨揀料彙總表                            *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    31MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    37'送貨揀料彙總表'
     A                                      SPACEA(2)
     A                                     1'物流點　:'
     A            DSE07     R             +1REFFLD(SE07)
     A            #A031     R   22        +2REFFLD(#A03)
     A                                    71'日期:'
     A            $EGMDY         6  0O    78EDTCDE(Y)
     A                                    88'頁次:'
     A                                    95PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'送貨日期:'
     A            DSE06     R             +1EDTWRD('  /  /  ')
     A                                      REFFLD(SE06)
     A                                    71'時間:'
     A                                    78TIME
     A                                    90'<IMR041>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ========================'
     A                                      SPACEA(1)
     A                                     1'產品識別'
     A                                    25'產品代號'
     A                                    35'產品簡稱'
     A                                    53'單位'
     A                                    61'數量'
     A                                    67'送貨單號'
     A                                    80'訂單單號'
     A                                    93'統一發票'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       ------------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A  32        SF031     R    2         1REFFLD(SF03)
     A  32        #A032     R   22         4REFFLD(#A03)
     A  31        SF03      R             26
     A  31        MA03      R             35
     A  31        MA13      R             53
     A            PTOT           5  0     60EDTCDE(J)
     A  30        SF02      R             67
     A  30        SE29      R             80
     A  30        SE30      R             94
     A*----------------------------------------------------------------
     A          R PD2                       SPACEA(1)
     A                                    31'產品小計---------------->'
     A            PRTOT          5  0     60EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3                       SPACEA(1)
     A                                     2'*****送貨單張數:'
     A            PSF02          4  0     +1EDTCDE(J)
     A                                    31'產品合計---------------->'
     A            PRRTOT         5  0     60EDTCDE(J)
     A******                              +0'（資料列印之筆數）'
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ========================'
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ========================'
     A                                      SPACEA(1)
     A                                     2'<<IMR041>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ========================'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE4
     A                                     1'                    +
     A                                                           +
     A                                                           +
     A                                                          '
     A                                      SPACEA(1)
