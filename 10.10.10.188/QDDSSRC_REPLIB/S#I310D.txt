     A*  90/07/11  13:31:43    A1062       REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : S#I310D                                    *
     A*    AUTHOR       : A1062   JEANNY LIN                         *
     A*    CREATE DATE  : 79/06/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : S#I310                                     *
     A*    SYSTEM       :程式安全管制系統                          *
     A*    PURPOSE      :程式基本資料全頁查詢                      *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*  90/07/11  13:31:43    A1062       REL-R02M00  5728-PW1
     A            RRN            4Y 0O  9 10EDTCDE(Z)
     A            SP01      R        O  9 16REFFLD(SP01)
     A            SP02      R        O  9 28REFFLD(SP02)
     A            DSP03          8A  O  9 61
     A            SP04      R        O 10 28REFFLD(SP04)
     A            SP05      R        O 10 44REFFLD(SP05)
     A            SP06      R        O 10 60REFFLD(SP06)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  90/07/02  14:58:26    A1062       REL-R02M00  5728-PW1
     A                                      SFLSIZ(0099)
     A                                      SFLPAG(0006)
     A                                      CF02(02)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A                                      SFLDROP(CF07)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(UPT2030 *LIBL/PTMF)
     A  97                                  SFLMSGID(UPT2040 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'<S#I310D>'
     A                                  1 29'程式安全管制系統'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            DUSER     R        O  2  2REFFLD(SU02)
     A                                  2 27'程式基本資料全頁查詢'
     A                                  4  9'程式編號:'
     A            DSP01     R        B  4 21REFFLD(SP01)
     A                                  4 45'挑選序號:'
     A            DSLT           4Y 0B  4 57EDTCDE(Z)
     A                                      CHECK(RB)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  6  9'序號'
     A                                  6 16'程式編號　　程式名稱'
     A                                  6 60'程式類別'
     A                                  7 28'備用欄１'
     A                                  7 44'備用欄２'
     A                                  7 60'備用欄３'
     A                                  8 10'-----------------------------------
     A                                      --------------------------'
     A          R DSPC1
     A*  90/07/02  14:58:26    A1062       REL-R02M00  5728-PW1
     A  96        ERR1          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF2 =挑選序號'
     A                                 23 20'PF3 =回主畫面'
     A                                 23 38'PF7 =備註說明'
     A                                 23 57'PF12=回前畫面'
     A                                 24  2'PA1 =下一頁'
     A                                 24 20'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
