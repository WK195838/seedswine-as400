     A*%%TS  SD  20090330  141751  CWDEV2      REL-V5R1M0  5722-WDS
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIS009D                                    *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 98/03/11                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :茂世進銷存                                *
     A*    SUBSYSTEM    :                                          *
     A*    REMARK       :後台採購單查詢                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '5')
     A            SBE01H    R        H      REFFLD(BE01 W2BEPF)
     A            SBE01          6Y 0O  9  6EDTWRD('  /  /  ')
     A            SBE03     R        O    +1REFFLD(BE03 W2BEPF)
     A            SBE06     R        O    +1REFFLD(BE06 W2BEPF)
     A            SMB03     R        O    +1REFFLD(MB03 MTMBPF)
     A            SBE02     R        O    +1REFFLD(BE02 W2BEPF)
     A            SME04     R        O    +1REFFLD(ME04 MTMEPF)
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'KIS009'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'採購單查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'5=查詢'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN1          6Y 0B  8  6EDTWRD('  /  /  ')
     A            DBGN2     R        B  8 15REFFLD(BE03 W2BEPF)
     A            DBGN3     R        B  8 28REFFLD(BE06 W2BEPF)
     A            DBGN4     R        B  8 53REFFLD(BE02 W2BEPF)
     A                                  6  3'(月/日/年)'
     A                                  7  3'A 採購日期  採購單號  供應商+
     A                                      代號/名稱      專櫃代號/名-
     A                                      稱      '
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A*%%TS  SD  20090330  141751  CWDEV2      REL-V5R1M0  5722-WDS
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SBF02     R        O  9  2REFFLD(BF02 W2BFPF)
     A            SMA03     R        O  9 12REFFLD(MA03 MTMAPF)
     A            SBF04     R        O  9 32REFFLD(BF04 W2BFPF)
     A                                      EDTCDE(3)
     A            SBF03     R        O  9 44REFFLD(BF03 W2BFPF)
     A                                      EDTCDE(3)
     A            SBH03     R        O  9 54REFFLD(BH03 W2BHPF)
     A                                      EDTCDE(3)
     A            STATUS         8O  O  9 64
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'KIS009'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 34'採購單查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'採購單號:'
     A            DBE03     R        O  5 13REFFLD(BE03 W2BEPF)
     A                                  5 26'專櫃代號:'
     A            DBE02     R        O  5 38REFFLD(BE02 W2BEPF)
     A            DME04     R        O  5 44REFFLD(ME04 MTMEPF)
     A                                  6  1'採購日期:'
     A            DBE01          6Y 0O  6 13EDTWRD('  /  /  ')
     A                                  6 26'供應廠商:'
     A            DBE06     R        O  6 38REFFLD(BE06 W2BEPF)
     A            DMB03     R        O  6 44REFFLD(MB03 MTMBPF)
     A                                  8  2'產品代號 產品名稱         未+
     A                                      稅單價  採購量  到貨量   狀-
                                            態 '
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
