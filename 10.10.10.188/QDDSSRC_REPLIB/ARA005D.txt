     A*%%TS  SD  19930113  120519  D910121     REL-V2R1M0  5738-PW1
     A*  81/02/13  15:20:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARA005D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/03/23                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARA005                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款作業                              *
     A*    REMARK       :收款沖帳處理                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR                     SFL
     A*%%TS  SD  19920610  120354  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B 10  2VALUES(' ' '2' '4' '5' '6' '7')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            AC01      R        O 10  5
     A            AC02      R        O 10 19
     A            AC03      R        O 10 25
     A            AC04      R        H
     A            AC05      R        O 10 30
     A            AC06      R        O 10 33
M001MA*           DAC07     R        O 10 48REFFLD(AC07)
M001MA            DAC07          6  0O 10 48
     A                                      EDTWRD('  /  /  ')
     A            AC08      R        O 10 57EDTCDE(1)
     A            AC10      R        O 10 69EDTCDE(1)
     A            AC11      R        H
M001MA*           DAC04     R        O 10 10REFFLD(AC04)
M001MA            DAC04          6  0O 10 10
     A                                      EDTWRD('  /  /  ')
     A*------------------------------------------------------------
     A*
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920610  134808  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'ARA005'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 35'收款維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  6'2=收款修改'
     A                                  5 20'4=刪除'
     A                                  5 30'5=查詢'
     A                                  5 40'6=沖帳'
     A                                  5 50'7=沖帳修改'
     A            DBGN1     R        B  9  5REFFLD(AC01)
     A            DBGN3     R        B  9 19REFFLD(AC02)
     A            DBGN4     R        B  9 25REFFLD(AC03)
M001MA*           DBGN2     R        B  9 10REFFLD(AC04)
M001MA            DBGN2          6  0B  9 10
     A                                      EDTWRD('  /  /  ')
     A                                  8  2'A公司收款日期客戶代號類+
     A                                      票據號碼    預兌日期  收款金+
     A                                      額未分配餘額'
     A                                      DSPATR(UL)
     A                                  7 47'(MM/DD/YY)'
     A                                  7  9'(MM/DD/YY)'
     A                                  7 29'種'
     A*------------------------------------------------------------
     A*
     A          R DSPC1
     A*%%TS  SD  19920501  155634  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0045 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0047 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0049 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0103 REMF)
     A                                      DSPATR(HI)
     A  84        ERR13         63A  O 24  2MSGID(URE 0123 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R DSPD2
     A*%%TS  SD  19930113  120519  D910121     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CF03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARA005'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'收款維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  7  2'公司　　:'
     A            DAC01     R        B  7 14REFFLD(AC01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            #B03      R        O  7 17
     A                                  9  2'客戶代號:'
     A                                      DSPATR(HI)
     A            DAC02     R        B  9 14REFFLD(AC02)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DAC03     R        B  9 20REFFLD(AC03)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            ME04      R        O  9 25
     A                                 11  2'收款日期:'
M001MA*           DAC04     R        B 11 14REFFLD(AC04)
M001MA            DAC04          6  0B 11 14
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 11 25'(MM/DD/YY)'
     A                                 13  2'種類　　:'
     A                                      DSPATR(HI)
     A            DAC05     R        B 13 14REFFLD(AC05)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 15  2'票據號碼:'
     A                                      DSPATR(HI)
     A            DAC06     R        B 15 14REFFLD(AC06)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
M003AA* 33                                  DSPATR(PR)
     A                                 15 31'預計兌現日期:'
M001MA*           DAC07     R        B 15 47REFFLD(AC07)
M001MA            DAC07          6  0B 15 47
     A                                      EDTWRD('  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 15 57'(MM/DD/YY)'
     A                                 17  2'收款金額:'
     A            DAC08     R        B 17 14REFFLD(AC08)
     A                                      EDTCDE(1)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 17 31'實際兌現日期:'
M001MA*           DAC11     R        O 17 47REFFLD(AC11)
M001MA            DAC11          6  0O 17 47
     A                                      EDTWRD('  /  /  ')
     A                                 17 57'(MM/DD/YY)'
     A                                 19  2'業務員　:'
     A                                      DSPATR(HI)
     A            DAC09     R        B 19 14REFFLD(AC09)
     A N68                                  DSPATR(HI)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            MC02      R        O 19 21
     A                                 19 35'入帳帳號:'
     A                                      DSPATR(HI)
     A            DAC12     R        B 19 47REFFLD(AC12)
     A                                      CHECK(RB)
     A N69                                  DSPATR(HI)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 22  1'執行'
     A                                 22  9'F3 =回主畫面'
     A                                 22 25'F4 =視窗查詢'
     A                                 22 41'F5 =參考資料'
     A                                 22 57'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0142 REMF)
     A                                      DSPATR(HI)
     A  85        ERR9          63A  O 24  2MSGID(URE 0046 REMF)
     A                                      DSPATR(HI)
     A  84        ERR10         63A  O 24  2MSGID(URE 0124 REMF)
     A                                      DSPATR(HI)
     A  83        ERR13         63A  O 24  2MSGID(URE 0145 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A            #A03      R        O 13 19REFFLD(RERFR/#A03 *LIBL/RERF)
