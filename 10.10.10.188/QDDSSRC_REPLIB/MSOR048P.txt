     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : MSOR048P                                   *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 84/04/03                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MSOR048                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :月結系統                                  *
     A*    REMARK       :通路產品平均銷售單價表                  *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    47MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    49'通路產品平均銷售單價表'
     A                                      SPACEA(1)
     A  20                                51'☆★禮盒不拆★☆'
     A                                      SPACEA(2)
     A N20                                50'☆★禮盒拆單瓶★☆'
     A                                      SPACEA(2)
     A            GROUP         10   O    56
     A                                      SPACEA(1)
     A                                     1'辦事處:'
     A            COMP           1   O    +1
     A            COMPC          6   O    +1
     A                                    40'確認年月:'
     A            DCD01S         4  0O    +1EDTWRD('  /  ')
     A                                    +1'-'
     A            DCD01E         4  0O    +1EDTWRD('  /  ')
     A                                      SPACEA(1)
     A                                     1'通路　:'
     A            CD13S          1   O    +1
     A                                    +1'-'
     A            CD13E          1   O    +1
     A                                    40'型態　　:'
     A            DCD14S         2   O    +1
     A                                    +1'-'
     A            DCD14E         2   O    +1
     A                                      SPACEA(1)
     A                                     1'組別　:'
     A            DCD10S         4   O    +1
     A                                    +1'-'
     A            DCD10E         4   O    +1
     A                                    40'業務員　:'
     A            DCD09S         6   O    +1
     A                                    +1'-'
     A            DCD09E         6   O    +1
     A                                    93'日期:'
     A            $EGMDY         6  0O   100EDTCDE(Y)
     A                                   109'頁次:'
     A                                   117PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'產品代號:'
     A            DCD04S         9   O    +1
     A                                    +1'-'
     A            DCD04E         9   O    +1
     A                                    93'時間:'
     A                                   100TIME
     A                                      SPACEA(1)
     A                                     1'客戶代號:'
     A            DCD06S         9   O    +1
     A                                    +1'-'
     A            DCD06E         9   O    +1
     A                                    93' USER :'
     A            $USER         10A  O   100
     A                                   111'<MSOR048>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     1'產品代號'
     A                                    11'產品簡稱'
     A                                    32'通路'
     A                                    46'銷售總金額'
     A                                    +0'銷售量'
     A                                    +0'平均單價'
     A                                    +0'含搭贈銷量'
     A                                    +0'平均單價'
     A                                    +0'含搭贈試飲'
     A                                    +0'平均單價'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A  21                                 2'  '
     A                                      SPACEA(1)
     A N22        CD04      R              2
     A N22        MA03      R             12
     A            CD13      R             +1
     A            #A03A         10        +1
     A            AMOUNT        10  0     +1EDTCDE(J)
     A            QTY1           6  0     +0EDTCDE(J)
     A            PRICE1         9  2     +0EDTCDE(J)
     A            QTY2           6  0     +0EDTCDE(J)
     A            PRICE2         9  2     +0EDTCDE(J)
     A            QTY3           6  0     +0EDTCDE(J)
     A            PRICE3         9  2     +0EDTCDE(J)
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'<<MSOR048>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE9                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
