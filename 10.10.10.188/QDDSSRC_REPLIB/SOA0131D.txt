     A****************************************************************
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : SOA0131D                                   *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  : 91/08/02                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOA0131                                    *
     A*    SYSTEM       :人頭馬寰盛                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :試飲訂單處理 (A/U/D/I)                    *
     A*===============================================================
     A* CODE |  AUTHOR  |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001 | MICHELLE | 02.11.27 | ADD SFLSIZ & ACTIVE F12
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPC3
     A*%%TS  SD  19920127  162226  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A*                                22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A*                                22 58'F12=回前畫面'
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR8          63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  87        ERR9          63A  O 24  2MSGID(URE 0029 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0028 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0059 REMF)
     A                                      DSPATR(HI)
     A  84        ERR13         63A  O 24  2MSGID(URE 0065 REMF)
     A                                      DSPATR(HI)
     A  83        ERR14         63A  O 24  2MSGID(URE 0112 REMF)
     A                                      DSPATR(HI)
     A  82        ERR15         63A  O 24  2MSGID(URE 0113 REMF)
     A                                      DSPATR(HI)
M007AA  74        ERR25         63A  O 24  2MSGID(UPT 5001 PTMF)
M007AA                                      DSPATR(HI)
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 13  3VALUES(' ' '1' '2' '4')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DS704          9   B 13  6
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DS402         18O  O 13 17
     A            HS705          5  0H
     A            DS705          5  0B 13 40EDTCDE(2)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DCHK           1A  H
     A*                                     'Y' -已存檔中--> UPD
     A*                                     ' ' -不存檔中--> ADD
     A          R SFLCR2                    SFLCTL(SFLSR2)
M001MA*                                     SFLSIZ(0048)
M001MA                                      SFLSIZ(0064)
     A                                      SFLPAG(0008)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A*                                     CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOA0131'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'試飲收回資料'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'單據號碼      :'
     A            DSN02     R        O    +1REFFLD(SC0/SC02 *LIBL/SOSCPF)
     A                                  6  2'促銷活動代碼  :'
     A            DSN22     R        O    +1REFFLD(SC0/SC06 *LIBL/SOSCPF)
     A                                  7  2'產品代號／名稱:'
     A            DSO03     R        O    +1REFFLD(SO0/SO03 *LIBL/SOSOPF)
     A            MA03      R        O    +1
     A                                  7 60'總數量:'
     A            DSO06     R        O    +1REFFLD(SO0/SO06 *LIBL/SOSOPF)
     A                                      EDTCDE(2)
     A                                  9  4'1=新增'
     A                                  9 14'2=修改'
     A                                  9 24'4=刪除'
     A            WS705          5  0O 11 40EDTCDE(1)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(RI)
     A                                 12  3'A 客戶代號／名稱'
     A                                      DSPATR(UL)
     A                                 12 42'數量'
     A                                      DSPATR(UL)
