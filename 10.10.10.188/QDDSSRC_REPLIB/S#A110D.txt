     A*  90/09/08  17:15:17    A1062       REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : S#A110D                                    *
     A*    AUTHOR       : A1062   JEANNY LIN                         *
     A*    CREATE DATE  : 79/06/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : S#A110                                     *
     A*    SYSTEM       :程式安全管制系統                          *
     A*    PURPOSE      :使用者基本資料維護                        *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.02 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A*  90/09/08  17:15:17    A1062       REL-R02M00  5728-PW1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  3'<S#A110D>'
     A                                  1 29'程式安全管制系統'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            DUSER     R        O  2  3REFFLD(SU02)
     A                                  2 28'使用者基本資料維護'
     A                                  4  2'使用者編號:'
     A            DSU01     R        B  4 16REFFLD(SU01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  6 26'作業選擇:'
     A            DOPT           1A  B  6 38VALUES('1' '2' '4' '5')
     A                                  6 44'1.新增'
     A                                  7 44'2.修改'
     A                                  8 44'4.刪除'
     A                                  9 44'5.查詢'
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22  2MSGID(US# 0001 S#MF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 22  2MSGID(US# 0002 S#MF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 22  2MSGID(US# 0003 S#MF)
     A                                      DSPATR(HI)
     A                                 23  3'PF3 =回主畫面'
     A                                 23 20'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A          R DSPC2
     A*  90/09/08  17:15:17    A1062       REL-R02M00  5728-PW1
     A                                      CA03(03)
     A                                      CA12(12)
     A                                  1  3'<S#A110D>'
     A                                  1 29'程式安全管制系統'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            DUSER     R        O  2  3REFFLD(SU02)
     A                                  2 28'使用者基本資料維護'
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4  2'使用者編號:'
     A            DSU01     R        O  4 16REFFLD(SU01)
     A                                  6  2'使用者名稱:'
     A            DSU02     R        B  6 16REFFLD(SU02)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A  60                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
     A                                  7  2'授權者編號:'
     A            DSU03     R        O  7 16REFFLD(SU03)
     A                                  8  2'授權者名稱:'
     A            DSU03C    R        O  8 16REFFLD(SU02)
     A                                  9  2'公　　司　:'
     A            DSU07     R        B  9 16REFFLD(SU07)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'部　　門　:'
     A            DSU04     R        B 10 16REFFLD(SU04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  2'倉　　庫　:'
     A            DSU05     R        B 11 16REFFLD(SU05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  2'備　　註　:'
     A            DSU06     R        B 13 16REFFLD(SU06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  2'異動日期　:'
M001MA*           DSU97     R        O 15 16REFFLD(SU97)
M001MA            DSU97          6  0O 15 16
     A                                      EDTWRD('  /  /  ')
     A                                 16  2'異動時間　:'
     A            DSU98     R        O 16 16REFFLD(SU98)
     A                                      EDTWRD('  :  :  ')
     A                                 17  2'異動者別　:'
     A            DSU99     R        O 17 16REFFLD(SU99)
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
