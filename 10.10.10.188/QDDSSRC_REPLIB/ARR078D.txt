     A*%%TS  SD  19940322  091823  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : ARR078D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 03/11/94                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR078                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款系統                              *
     A*    REMARK       :調整原因分析表                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR078'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  2 32'調整原因分析表'
     A                                      DSPATR(HI)
     A                                  5 26'公司別　:'
     A            DIB02S         1A  B  5 38
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                  5 40'-'
     A            DIB02E         1A  B  5 42
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                  6 26'辦事處　:'
     A            COMP           1A  B  6 38VALUES(' ' '1' '2' '3')
     A                                  6 40'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  7 26'調整原因:'
     A                                      DSPATR(HI)
     A            DIB17S    R        B  7 38REFFLD(IB17)
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 48'-'
     A            DIB17E    R        B  7 50REFFLD(IB17)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 26'物流點　:'
     A            DIB03S    R        B  9 38REFFLD(IB03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 48'-'
     A            DIB03E    R        B  9 50REFFLD(IB03)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 10 26'產品代號:'
     A                                      DSPATR(HI)
     A            DIB01S    R        B 10 38REFFLD(IB01)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 10 48'-'
     A            DIB01E    R        B 10 50REFFLD(IB01)
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 12 26'調整日期:'
M001DA*           DIB04S    R        B 12 38EDTWRD('  /  /  ')
M001AA            DIB04S         6  0B 12 38EDTWRD('  /  /  ')
M001DA*                                     REFFLD(IB04)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 12 48'-'
M001DA*           DIB04E    R        B 12 50EDTWRD('  /  /  ')
M001AA            DIB04E         6  0B 12 50EDTWRD('  /  /  ')
M001DA*                                     REFFLD(IB04)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 12 60'(MM/DD/YY)'
     A                                 13 26'報表格式:'
     A            DSEL           1A  B 13 38VALUES('A' 'B' 'C')
     A                                 13 40'( A=品牌B=日期C=物流點)'
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 42'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          63A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          63A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR9          63A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
