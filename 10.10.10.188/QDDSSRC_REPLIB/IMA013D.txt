     A*%%TS  SD  19931124  164036  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : IMA013D                                    *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : 82/11/24                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMA013                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款子系統                            *
     A*    REMARK       :產品單位成本維護 (A/U/D/I)                *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '2' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            MA01      R        O  9  5
     A            MA03      R        O  9 15
     A            MA31      R        O  9 34EDTCDE(M)
     A            MA32      R        O  9 46EDTCDE(M)
     A            MA33      R        O  9 58EDTCDE(M)
     A            MA34      R        O  9 70EDTCDE(M)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'IMA013'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'單位成本調整維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  5'2=修改 5=查詢'
     A                                  7  3'A產品代號中文簡稱          +
     A                                      FOB-UNIT     FHI-UNIT   DUYT-UNIT-
     A                                      　單位成本'
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  5REFFLD(MA01)
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A  96        ERR1          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          60A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          60A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          60A  O 24  2MSGID(URE 0038 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         60A  O 24  2MSGID(URE 0039 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  19931124  164036  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF14(14)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'IMA013'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'單位成本異動維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'產品代號　　　:'
     A            MA01      R        O  5 20REFFLD(MA01)
     A                                  7  2'中文簡稱　　　:'
     A            MA03      R        O  7 20
     A                                 22  3'執行'
     A                                 22 12'F3 =回主畫面'
     A                                 22 28'F12=回前畫面'
     A                                 22 44'F14=成本存檔'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          60A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          60A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          60A  O 24  2MSGID(URE 0062 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0061 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         60A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         60A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  84        ERR13         60A  O 24  2MSGID(URE 0106 REMF)
     A                                      DSPATR(HI)
     A  83        ERR14         60A  O 24  2MSGID(URE 0132 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 13  3'F.O.B.      :'
     A            DMA31          8  2B 13 19
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A                                      EDTCDE(1)
     A                                 14  3'FREIGHT     :'
     A            DMA32          8  2B 14 19EDTCDE(1)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A                                 15  3'DUTY        :'
     A            DMA33          8  2B 15 19EDTCDE(1)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A                                 16  2'單位成本   :'
     A            DMA34          8  2O 16 19EDTCDE(1)
     A                                 13 44'F.O.B.      :'
     A            MA26      R        O 13 60EDTCDE(1)
     A                                 14 44'FREIGHT     :'
     A            MA27      R        O 14 60EDTCDE(1)
     A                                 15 44'DUTY        :'
     A            MA28      R        O 15 60EDTCDE(1)
     A                                 18  3'----------  UNIT ----------'
     A                                 18 44'---------- TOTAL ----------'
