     A****************************************************************
     A*                                                              *
     A*    DSPF NAME    : MTR050P                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 81/09/10                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTR0501                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :新增產品一覽表                            *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    51
     A                                      MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    57
     A                                      DFT('新增產品一覽表')
     A                                      SPACEA(2)
     A                                   101'日期:'
     A            $EGMDY         6  0O   109EDTCDE(Y)
     A                                   118'頁次:'
     A                                   126PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'產品歸屬:'
     A            MA07      R        O    +1
     A                                   101'時間:'
     A                                   109TIME
     A                                      SPACEA(1)
     A                                     1'建檔日期:'
     A            DMA53S         6  0     +1EDTWRD('  /  /  ')
     A                                    +0'-'
     A            DMA53E         6  0     +0EDTWRD('  /  /  ')
     A                                   101' USER :'
     A            $USER         10   O   109
     A                                   120'<MTR050>'
     A                                      SPACEA(1)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================='
     A                                      SPACEA(1)
     A                                     1'產品代號'
     A                                    12'中文簡稱'
     A                                    31'中文全稱'
     A                                    66'產品種類'
     A                                    77'供應商'
     A                                    87'採購單位'
     A                                    99'庫存單位'
     A                                   111'單位轉換'
     A                                   123'包裝規格'
     A                                      SPACEA(1)
     A                                    12'英文簡稱'
     A                                    31'英文全稱'
     A                                      SPACEA(1)
     A                                     1'-------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            MA01      R        O     2
     A            MA03      R        O    13
     A            MA02      R        O    32
     A            MA09      R        O    69
     A            MA12      R        O    78
     A            MA13      R        O    89
     A            MA14      R        O   101
     A            MA16      R        O   115
     A            MA17      R        O   124
     A                                      SPACEA(1)
     A            MA06      R        O    14
     A            MA04      R        O    33
     A            MA05      R        O    64
     A*----------------------------------------------------------------
     A          R PE2                       SPACEA(1)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================='
     A                                      SPACEA(1)
     A                                     2'<<MTR050>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE1                       SPACEA(1)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE9                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
