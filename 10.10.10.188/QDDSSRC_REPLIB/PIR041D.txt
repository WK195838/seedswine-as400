     A*%%TS  SD  19921209  134400  D910121     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PIR041D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/04/24                                   *
     A*    UPDATE DATE  : 11/17/92 JUDY                              *
     A*    PROGRAM NAME : PIR041                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :盤點系統                                  *
     A*    REMARK       :盤點單                                    *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921209  134400  D910121     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PIR041'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'盤點單列印'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'物流點　:'
     A            DIO02     R        B  6 38REFFLD(IO02)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A            DSTOR         22A  O  6 44
     A                                  8 26'位置份數:'
     A            DNUM           2Y 0B  8 38
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                      EDTCDE(Z)
     A                                      RANGE(0 99)
     A                                 10 26'選　項　:'
     A            DSEL           1A  B 10 38
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                      VALUES('A' 'B' 'C')
     A                                 11 29'A.正常列印'
     A                                 12 29'B.夾紙重印-'
     A                                 12 43'起始頁次:'
     A            DPNO           2Y 0B 12 55EDTCDE(2)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 29'C.挑選單號-'
     A                                      DSPATR(HI)
     A            DIO03S    R        B 13 43REFFLD(IO03)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A            DIO03E    R        B 13 55REFFLD(IO03)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 43'(1-即時 2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0O 17 38EDTCDE(3)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備套版紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR13         50A  O 24  2MSGID(UPT 5017 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 13 53'-'
