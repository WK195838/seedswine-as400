     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYR8083P                                   *
     A*    PROGRAM NAME :產品別銷售分析明細表                      *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/01/24                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :人事薪資                                  *
     A****************************************************************
     A                                      REF(PYRF)
      *----------------------------------------------------------------
     A          R PH1
      *----------------------------------------------------------------
     A                                      SKIPB(002)
     A            CM03      R             20
     A                                      SPACEA(001)
     A                                    25
     A                                      '產品別銷售分析明細表'
     A                                      SPACEA(002)
     A                                    51
     A                                      '日期:'
     A            DDATE          6  0     59
     A                                      EDTWRD('0  /  /  ')
     A                                    68
     A                                      '頁次:'
     A                                    76
     A                                      PAGNBR
     A                                      SPACEA(002)
     A                                     1
     A                                      '廠商別:'
     A            PWF03          5A       11
     A            PWF03N        18O       16
      *
     A                                    34
     A                                      '銷售年月:'
     A            PSU02          4  0     45
     A                                      EDTWRD('0  /  ')
     A                                    53
     A                                      '時間:'
     A                                    61
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    71
     A                                      '<PYR8084P>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '產品代號'
     A                                    13
     A                                      '產品名稱'
     A                                    30
     A                                      '單位'
     A                                    38
     A                                      '銷售量'
     A                                    53
     A                                      '售價'
     A                                    64
     A                                      '銷售金額'
     A                                      SPACEA(001)
      *----------------------------------------------------------------
     A          R PH2
      *----------------------------------------------------------------
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEA(001)
     A                                     1
     A                                      '專櫃代號'
     A            PWF04     R             12
     A                                      REFFLD(WF04 MTMAWF)
     A                                    23
     A                                      '專櫃名稱'
     A            PWF04N    R             34
     A                                      REFFLD(ME04 MTMEPF)
      *
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEA(001)
      *----------------------------------------------------------------
     A          R PD1
      *----------------------------------------------------------------
     A            PWF01     R              3
     A                                      REFFLD(WF01 MTMAWF)
     A            PMA03     R             12
     A                                      REFFLD(MA03 MTMAPF)
     A            PMA13     R             30
     A                                      REFFLD(MA13 MTMAPF)
     A            PWF06     R             36
     A                                      REFFLD(WF06 MTMAWF)
     A                                      EDTCDE(1)
     A            PWF07     R             49
     A                                      REFFLD(WF07 MTMAWF)
     A                                      EDTCDE(1)
     A            PTOT          11S 2     62
     A                                      EDTCDE(1)
     A                                      SPACEA(002)
      *----------------------------------------------------------------
     A          R PT1
      *----------------------------------------------------------------
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEA(001)
     A                                    26
     A                                      '小計'
     A            PTO01         10S 2     35
     A                                      EDTCDE(1)
     A            PTO02         10S 2     48
     A                                      EDTCDE(1)
     A            PTO03         12S 2     61
     A                                      EDTCDE(1)
     A                                      SPACEA(001)
     A*
      *----------------------------------------------------------------
     A          R PE2
      *----------------------------------------------------------------
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========='
     A                                      SPACEA(001)
     A                                     1
     A                                      'PYR8084P--->報表結束'
      *----------------------------------------------------------------
     A          R PE9
      *----------------------------------------------------------------
     A                                      SPACEB(001)
     A                                    16
     A                                      '無符合列印範圍之資料'
