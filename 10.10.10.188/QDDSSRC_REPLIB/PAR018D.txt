     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PAR018D                                    *
     A*    AUTHOR       : MICHELLE                                   *
     A*    CREATE DATE  : 90/05/29                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PAR018                                     *
     A*    SYSTEM       :人頭馬寰盛                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :新產品種類代碼列印                        *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(D#ROW D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3  0H
     A            D#COL          3  0H
     A                                  1  2
     A                                      'PAR018'
     A                                  1 26
     A                                      MSGCON(30 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62
     A                                      '日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2
     A                                      'SCR001'
     A                                  2 30
     A                                      '新產品種類代碼列印'
     A                                      DSPATR(HI)
     A                                  2 62
     A                                      '時間:'
     A                                  2 70
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62
     A                                      ' USER :'
     A            $USER         10   O  3 70
     A                                  7 18
     A                                      '新產品種類代碼:'
     A            D#702S    R        B    +2REFFLD(#A02)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                    +2'-'
     A***
     A            D#702E    R        B    +2REFFLD(#A02)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                 16 26
     A                                      '執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 43
     A                                      '(1-即時   2-批次)'
     A                                 17 26
     A                                      '列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26
     A                                      '列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38
     A                                      DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 26
     A                                      '***請準備　８０格報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2
     A                                      '執行'
     A                                    +2
     A                                      'F3 =回主畫面'
     A                                    +2
     A                                      'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66
     A                                      MSGCON(14 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
