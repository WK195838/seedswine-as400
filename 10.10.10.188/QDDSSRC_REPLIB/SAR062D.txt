     A*%%TS  SD  19921111  144358  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SAR062D                                    *
     A*    AUTHOR       : A1084  CHARLES                             *
     A*    CREATE DATE  : 81/05/04                                   *
     A*    PROGRAM NAME : SAR062                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :通路產品銷售週報表                        *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921111  144358  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SAR062'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'通路產品銷售週報表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 26'辦事處'
     A                                  5 36':'
     A            COMP           1A  B  5 38VALUES(' ' '1' '2' '3')
     A                                  5 41'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  7 26'通路　　:'
     A                                      DSPATR(HI)
     A            DSC33S    R        B  7 38REFFLD(SC33)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N60                                  DSPATR(HI)
     A                                  7 48'-'
     A            DSC33E    R        B  7 50REFFLD(SC33)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A                                  9 26'產品代號:'
     A                                      DSPATR(HI)
     A            DSD03S    R        B  9 38REFFLD(SD03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A                                  9 48'-'
     A            DSD03E    R        B  9 50REFFLD(SD03)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(HI)
     A                                 11 26'確認日期:'
M001DA*           DSC39S    R        B 11 38REFFLD(SC39)
M001AA            DSC39S         6  0B 11 38
     A                                      EDTWRD('  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 11 48'-'
M001DA*           DSC39E    R        B 11 50REFFLD(SC39)
M001AA            DSC39E         6  0B 11 50
     A                                      EDTWRD('  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 11 61'(MM/DD/YY)'
     A                                 16 26'執行環境 :'
     A            $EVR           1Y 0B 16 39VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 41'(1-即時   2-批次)'
     A                                 17 26'列印份數'
     A                                 17 37':'
     A            $CPY           3Y 0B 17 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機'
     A                                      DSPATR(HI)
     A                                 18 37':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A  89        ERR12         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
