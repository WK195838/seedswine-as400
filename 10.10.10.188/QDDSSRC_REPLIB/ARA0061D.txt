     A*%%TS  SD  19931216  084923  D910074     REL-V2R2M0  5738-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARA0061D                                   *
     A*    AUTHOR       : A1118  PHILIP TSAI                         *
     A*    CREATE DATE  : 81/03/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARA0061                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款作業系統                          *
     A*    REMARK       :票據兌現處理－兌現 (A/U/D/I)              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLSR1                    SFL
     A*%%TS  SD  19931216  083336  D910074     REL-V2R2M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 13  2VALUES(' ' '6')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DAC07          6Y 0O 13  4EDTCDE(Y)
     A            AC06      R        O 13 13
     A            AC08      R        O 13 28EDTCDE(1)
     A            AC02      R        O 13 40
     A            AC03      R        O 13 46
     A            ME04      R        O 13 51
     A            AC01      R        O 13 70
     A            AC04      R        H      REFFLD(AC04)
     A            DAC04          6Y 0O 13 73EDTCDE(Y)
     A*           DAC04     R        O 13 73REFFLD(AC04)
     A*                                     EDTWRD('  /  /  ')
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19931216  083336  D910074     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0008)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A* 53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'ARA0061'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 33'票據兌現處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司　　:'
     A            DAE01     R        O  4 14REFFLD(AE01)
     A            #B03      R        O  4 17
     A                                  5  2'帳戶號碼:'
     A            DAE03     R        O  5 14REFFLD(AE03)
     A                                  5 34'入帳日期:'
     A            DAE02          6Y 0O  5 46EDTCDE(Y)
     A                                  5 57'(MM/DD/YY)'
     A                                  7  1'輸入作業選擇:'
     A                                  8  6'6=兌現'
     A                                 10  3'預兌日期'
     A                                 10 73'收款'
     A                                 11  2'A MM/DD/YY 票據號碼       票+
     A                                      據金額　客戶代號　客戶簡稱　　+
     A                                      　 公司　日期　'
     A                                      DSPATR(UL)
     A            DBGN2          6Y 0B 12  4EDTCDE(Y)
     A            DBGN3     R        B 12 13REFFLD(AC06)
     A*  81/01/21  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R DSPC1
     A*%%TS  SD  19920324  134224  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0144 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
