     A*%%TS  SD  19960409  165308  DERLERN     REL-V3R1M0  5763-PW1
     A****************************************************************
     A*    DSPF NAME    : SOA0311D                                   *
     A*    AUTHOR       : A1092  TAYLOR                              *
     A*    CREATE DATE  : 81/02/10                                   *
     A*    UPDATE DATE  : 81/04/17 PHILIP                            *
     A*    PROGRAM NAME : SOA0311                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SOSVPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR2                    SFL
     A*%%TS  SD  19960409  165308  DERLERN     REL-V3R1M0  5763-PW1
     A  54                                  SFLNXTCHG
     A            DNEW2          1A  H
     A            RRN2           3Y 0H
     A            HSV02     R        H      REFFLD(SV02)
     A            DDEL2          1A  B  9  3VALUES(' ' 'D')
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
M001MA*           DSV02     R        B  9  8REFFLD(SV02)
M001MA            DSV02          4  0B  9  8
     A                                      EDTWRD('  /  ')
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A            DSV03     R        B  9 19REFFLD(SV03)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A            DSV04     R        B  9 33REFFLD(SV04)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A            DSV06     R        B  9 47REFFLD(SV06)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19960409  165308  DERLERN     REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0012)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN2          3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'SOA0311D'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DATE           6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  1
     A                                  2 31'銷售目標資料維護'
     A                                      DSPATR(RI)
     A                                  2 70TIME
     A                                  4  2'產品代號:'
     A            MA01      R        O  4 14REFFLD(MA01 RERF)
     A            MA02      R        O  4 26REFFLD(MA02 RERF)
     A                                  6  3'D'
     A                                  6  7'年月'
     A                                  6 18'企劃預估'
     A                                  6 32'業務預估'
     A                                  6 46'目標量'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
M001MA*           SSV02     R        B  8  8REFFLD(SV02)
M001MA            SSV02          4  0B  8  8
     A                                      DSPATR(UL)
     A                                      CHANGE(27)
     A*----------------------------------------------------------------
     A          R DSPC2
     A*%%TS  SD  19960328  173223  D910341     REL-V3R1M0  5763-PW1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  88
     AO 99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  1'執行'
     A                                 23  9'PF3 =回主畫面'
     A                                 23 26'PF5 =參考資料'
     A                                 23 43'PF12=回前畫面'
     A                                 24  9'PA1 =下一頁'
     A                                 24 26'PA2 =上一頁'
