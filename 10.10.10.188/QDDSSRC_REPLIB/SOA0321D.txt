     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : WOA032D                                    *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  : 11/26/96                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : WOA032                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.05 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19961126  142809  D910454     REL-V3R1M0  5763-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'SOA0321'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 29'發票（應收）資料設定'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6  2'公司代號:'
     A            DSI01     R        B  6 14REFFLD(SI01)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            D#B03     R        O  6 22REFFLD(#B03)
     A                                  8  2'發票號碼:'
     A            DSI02     R        O  8 14REFFLD(SI02)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10  2'客戶代號:'
     A            DSI08     R        B 10 14REFFLD(SI08)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DME04     R        O 10 22REFFLD(ME04)
     A                                 12  2'發票金額:'
     A            DSI25     R        B 12 14REFFLD(SI25)
     A                                      EDTCDE(J)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  2'應收日期:'
M001MA*           DSI29     R        B 14 14REFFLD(SI29)
M001MA            DSI29          6  0B 14 14
     A                                      EDTCDE(Y)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16  2'已沖金額:'
     A            DSI26     R        O 16 14REFFLD(SI26)
     A                                      EDTCDE(J)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F5 =參考資料'
     A                                 23 42'F12=回前畫面'
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
