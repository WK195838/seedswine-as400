     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : IMR043AP                                    *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : 81/02/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR043                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :分車送貨彙總表                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.16 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    27MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    33'分車送貨彙總表'
     A                                      SPACEA(2)
     A                                     1'物流點　:'
     A            DSE07     R             +1REFFLD(SE07)
     A            #A031     R   22        +2REFFLD(#A03)
     A                                    57'日期:'
     A            $EGMDY         6  0O    65EDTCDE(Y)
     A                                    75'頁次:'
     A                                    82PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'派車日期:'
M001MA            DSE06          6  0     +1EDTWRD('  /  /  ')
     A                                    57'時間:'
     A                                    65TIME
     A                                      SPACEA(1)
     A                                     1'車號　　:'
     A            SE22      R             +1
     A                                    +1'-'
     A            DSE23     R             +1REFFLD(SE23)
     A                                    57' USER :'
     A            $USER         10   O    65
     A                                    77'<IMR043>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================+
     A                                       ======='
     A                                      SPACEA(1)
     A                                     1'產品代號'
     A                                    12'產品簡稱'
     A                                    32'單位'
     A                                    42'數量'
     A                                    50'備註'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       -------------------+
     A                                       -------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            SF03      R              2
     A            MA03      R             12
     A            MA13      R             32
     A            PTOT           5  0     41EDTCDE(J)
     A                                    51'_________________________+
     A                                       __________'
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================+
     A                                       ======='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================+
     A                                       ======='
     A                                      SPACEA(1)
     A                                    28'TOTAL----->'
     A            PRTOT          5  0     41EDTCDE(J)
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================+
     A                                       ======='
     A                                      SPACEA(1)
     A                                     2'<<IMR043>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================+
     A                                       ======='
     A                                      SPACEA(1)
