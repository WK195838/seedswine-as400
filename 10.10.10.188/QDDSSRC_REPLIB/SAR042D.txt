     A*%%TS  SD  19941216  180213  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SAR041D                                    *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  : 81/04/02                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SAR041                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款作業                              *
     A*    REMARK       :客戶退貨彙總表                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19941216  180213  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SAR042'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'產品退貨彙總表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 26'公　司　:'
     A            DSG01     R        B  4 38REFFLD(RERFR/SG01 *LIBL/RERF)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  5 26'產品組別:'
     A            DMA11          1A  B  5 38VALUES('1' '2' '3' ' ')
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A                                  5 42'(1-BRANDY 2-WHISKY 3-WINE " "-ALL)'
     A                                  6 26'產品代號:'
     A                                      DSPATR(HI)
     A            DSH03S         9A  B  6 38
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  6 48'-'
     A            DSH03E         9A  B  6 51
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  9 26'打單日期:'
     A            DSG06S         6Y 0B  9 38EDTWRD('  /  /  ')
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  9 48'-'
     A            DSG06E         6Y 0B  9 51EDTWRD('  /  /  ')
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 10 60'(MM/DD/YY)'
     A                                 10 26'確認日期:'
     A            DSG26S         6Y 0B 10 38EDTWRD('  /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 10 48'-'
     A            DSG26E         6Y 0B 10 51EDTWRD('  /  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                  9 60'(MM/DD/YY)'
     A                                 12 26'選擇內容:'
     A            DATA           1A  B 12 38VALUES('A' 'B')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 12 40'A-銷貨退回'
     A                                 13 40'B-產品收回'
     A            DATA1          1A  B 13 53VALUES(' ' '1' '2' '3')
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                 13 55'1-含試飲退回'
     A                                 14 55'2-不含試飲退回'
     A                                 15 55'3-僅含試飲退回'
     A                                  7 26'物流點　:'
     A            DSG07S    R        B  7 38REFFLD(SG07)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                  7 48'-'
     A            DSG07E    R        B  7 51REFFLD(SG07)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                 17 26'執行環境:'
     A            $EVR           1Y 0B 17 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 43'(1-即時 2-批次)'
     A                                 18 26'列印份數:'
     A            $CPY           3Y 0B 18 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 21 27'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
