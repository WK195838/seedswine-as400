     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIS005D                                    *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 98/03/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :茂世進銷存                                *
     A*    SUBSYSTEM    :                                          *
     A*    REMARK       :後台訂單處理維護                          *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  3VALUES(' ' '2' '4' '5'     '8')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SBC01          6Y 0O 10  6EDTWRD('  /  /  ')
     A            SBC01H    R        H      REFFLD(BC01 W2BCPF)
     A            SBC02     R        O 10 17REFFLD(BC02 W2BCPF)
     A            SBC03     R        O 10 26REFFLD(BC03 W2BCPF)
     A            BC04      R        H      REFFLD(BC04 W2BCPF)
     A            STATUS         8O  O 10 46
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'KIS005'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'後台訂單處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  6  8'2=修改'
     A                                      COLOR(BLU)
     A                                  6 17'4=刪除'
     A                                      COLOR(BLU)
     A                                  6 26'5=查詢'
     A                                      COLOR(BLU)
     A                                  6 35'8=還原'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN1          6Y 0B  9  6EDTWRD('  /  /  ')
     A            DBGN2          5A  B  9 17
     A                                  7  3'(月/日/年)'
     A                                  8  3'A 訂單日期  專櫃  專櫃訂貨單+
     A                                      號        狀態 '
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F6 = 新增訂貨單'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99N97     ERRMSG        60A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A  99 97     @MSG          60A  O 24  2
     A                                      DSPATR(HI)
      *此燈號控制新增成功以及修改成功專用
     A  91        ERR0          63A  O 23  2MSGID(MSG 1000 R1MF)
     A                                      DSPATR(HI)
     A  92        ERR1          63A  O 23  2MSGID(MSG 1001 R1MF)
     A                                      DSPATR(HI)
     A  93        ERR2          63A  O 23  2MSGID(MSG 1002 R1MF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 23  2MSGID(MSG 1003 R1MF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 23  2MSGID(MSG 1004 R1MF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B  9  2VALUES(' ' '1' '2' '4')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            SBD03     R        O  9  4REFFLD(BD03 W2BDPF)
     A            SBD02     R        B  9 10REFFLD(BD02 W2BDPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32N61                               DSPATR(UL HI)
     A N32                                  DSPATR(PR)
     A            SMA03     R        O  9 20REFFLD(MA03 MTMAPF)
     A            SBD04     R        O  9 39REFFLD(BD04 W2BDPF)
     A                                      EDTCDE(3)
     A            SBD05          5  0B  9 50
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                      EDTCDE(Q)
     A            SIA06     R        O  9 58REFFLD(IA06 IMIAPF)
     A                                      EDTCDE(P)
     A            SBD08     R   -2   B  9 66REFFLD(BD08 W2BDPF)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DCHK           1A  H
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A  33                                  CF04(04)
     A  33                                  CF05(05)
     A  34                                  CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'KIS005'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'後台訂單處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'訂單日期:'
     A            DBC01          6Y 0B    +1EDTWRD('  /  /  ')
     A  71                                  DSPATR(PC RI)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A                                    +1'專櫃代號:'
     A            DBC02     R        B    +1REFFLD(BC02 W2BCPF)
     A  72                                  DSPATR(PC RI)
     A  35N72                               DSPATR(UL HI)
     A N35                                  DSPATR(PR)
     A            DME04     R        O    +1REFFLD(ME04 MTMEPF)
     A                                  6  1'訂單號碼:'
     A            DBC03     R        O    +1REFFLD(BC03 W2BCPF)
     A                                  7  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  7 17'1=新增'
     A                                      COLOR(BLU)
     A                                  7 26'2=修改'
     A                                      COLOR(BLU)
     A                                  7 35'4=刪除'
     A                                      COLOR(BLU)
     A                                  8  2'A供應商產品代號         -
     A                                              訂購量 確認量 庫存量-
                                               備註     '
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +1'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  33                                +1'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  33                                +1'F5 =參考資料'
     A                                      COLOR(BLU)
     A  34                                +1'F9 =確認'
     A                                      COLOR(BLU)
     A                                    +1'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
