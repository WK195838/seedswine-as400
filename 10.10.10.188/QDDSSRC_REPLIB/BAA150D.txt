     A****************************************************************
     A*    DSPF NAME    : BAA150D                                    *
     A*    AUTHOR       : A1034  STEPHANIE                           *
     A*    CREATE DATE  : 79/07/30                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : BAA150                                     *
     A*    SYSTEM       :
     A*    SUBSYSTEM    :基本建檔作業系統
     A*    REMARK       :供應商零件對應資料維護                    *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/BARF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A                                  1  2'<BAA150D>'
     A            CONAME        40A  O  1 20MSGID(US#9001 S#MF)
     A                                  1 64'日期:'
     A            $CHYMD         6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 26'供應商零件對應資料維護'
     A                                  4  2'供商代號:'
     A            DQE01     R        B  4 14REFFLD(QE01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR4          50A  O 22  2MSGID(USP 0002 SPMF)
     A                                      DSPATR(HI)
     A  90        ERR5          50A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 24 67'【泛太資訊】'
     A          R DSPC1
     A                                  1  2'<BAA150D>'
     A            CONAME        40A  O  1 20MSGID(US#9001 S#MF)
     A                                  1 64'日期:'
     A            $CHYMD         6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 26'供應商零件對應資料維護'
     A                                  4  2'供商代號:'
     A            DQE01     R        O  4 14REFFLD(QE01)
     A            QB02      R        O  4 23
     A                                  5  2'幣　　別:'
     A            QB07      R        O  5 14
     A                                  7  2'零件料號'
     A                                  7 23'生效日期'
     A                                  7 35'最近報價'
     A                                  7 47'折扣率％'
     A                                  7 59'交貨天數'
     A                                  7 70'採購量'
     A                                  8  3'==================================-
     A                                      ===================================-
     A                                      ======'
     A          R SFLSR                     SFL
     A            RRN            4S 0H
     A            QE02      R        O  9  3
     A            QE03      R        O  9 24EDTWRD('  /  /  ')
     A            QE04      R        O  9 36EDTCDE(3)
     A            QE05      R        O  9 50EDTCDE(3)
     A            QE06      R        O  9 62EDTCDE(3)
     A            QE07      R        O  9 72EDTCDE(3)
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0009)
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF13(13)
     A                                      CF12(12)
     A                                      CF19(19)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                 18  3'==================================-
     A                                      ===================================-
     A                                      ======'
     A                                 20  2'功能:'
     A            DOPT           1   B 20 10VALUES('1' '2' '4')
     A                                    +2'( 1.新增2.修改4.刪除)'
     A            DQE02     R        B 21  3
     A                                      REFFLD(QE02)
     A  60                                  DSPATR(PC RI)
     A            DQE03     R        B 21 24EDTCDE(3)
     A                                      CHECK(RB)
     A                                      REFFLD(QE03)
     A  61                                  DSPATR(PC RI)
     A            DQE04     R        B 21 36EDTCDE(3)
     A                                      CHECK(RB)
     A                                      REFFLD(QE04)
     A  62                                  DSPATR(PC RI)
     A            DQE05     R        B 21 50EDTCDE(3)
     A                                      CHECK(RB)
     A                                      REFFLD(QE05)
     A            DQE06     R        B 21 62EDTCDE(3)
     A                                      CHECK(RB)
     A                                      REFFLD(QE06)
     A            DQE07     R        B 21 72EDTCDE(3)
     A                                      CHECK(RB)
     A                                      REFFLD(QE07)
     A
     A  96        ERR1          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          50A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          50A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          50A  O 22  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          50A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          50A  O 22  2MSGID(USP 0003 SPMF)
     A                                      DSPATR(HI)
     A  88        ERR9          50A  O 22  2MSGID(USP 0001 SPMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                    +2'PF5 =參考資料'
     A                                    +2'PF12=回前畫面'
     A                                    +2'PF13=整筆刪除'
     A                                 24  2'PF19=零件明細'
     A                                    +2'PA1 =下一頁　'
     A                                    +2'PA2 =上一頁　'
