     A*%%TS  SD  19921028  113157  D910074     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : IMR053D                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/02/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR053                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :專櫃產品存貨彙總表                        *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921022  165334  D910074     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'IMR053'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'專櫃產品存貨彙總表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 29'公司別　'
     A                                  6 40':'
     A            DIA01     R        B  6 42REFFLD(IA01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  8 29'物流點　'
     A                                  8 40':'
     A            DIA02S    R        B  8 42REFFLD(IA02)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A  38                                  DSPATR(UL)
     A N38                                  DSPATR(PR)
     A                                  8 48'-'
     A            DIA02E    R        B  8 50REFFLD(IA02)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A  38                                  DSPATR(UL)
     A N38                                  DSPATR(PR)
     A                                 16 29'執行環境'
     A                                 16 40':'
     A            $EVR           1Y 0B 16 42VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 47'(1-即時  2-批次)'
     A                                 17 29'列印份數'
     A                                 17 40':'
     A            $CPY           3Y 0B 17 42EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 29'列表機'
     A                                      DSPATR(HI)
     A                                 18 40':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 42CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 29'***請準備８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 1003 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 10 29'產品代號 :'
     A                                      DSPATR(HI)
     A            DIA03S         9   B 10 42
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A  38                                  DSPATR(UL)
     A N38                                  DSPATR(PR)
     A N63                                  DSPATR(HI)
     A                                 10 52'-'
     A            DIA03E         9   B 10 54
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A  38                                  DSPATR(UL)
     A N38                                  DSPATR(PR)
     A N64                                  DSPATR(HI)
