     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PAR003D                                    *
     A*    AUTHOR       : A1005  ROGER CHIEN                         *
     A*    CREATE DATE  : 80/11/20                                   *
     A*    UPDATE DATE  : 81/01/14 BY STEPHANIE                      *
     A*    PROGRAM NAME : PAR003                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :部門成本中心對照資料列印                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1                     CSRLOC(D#ROW D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3  0H
     A            D#COL          3  0H
     A                                  1  2'PAR010'
     A                                  1 26MSGCON(30 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 28'部門成本中心對照資料列印'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                               O  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10   O  3 70
     A                                  6 26'部門代號起:'
     A            D#401S    R        B  6 40REFFLD(#401)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  8 26'　　　　迄:'
     A            D#401E    R        B  8 40REFFLD(#401)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                 16 26'執行環境　:'
     A            $EVR           1Y 0B 16 40VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 45'(1-即時   2-批次)'
     A                                 17 26'列印份數　:'
     A            $CPY           3Y 0B 17 40EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 40
     A                                      DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備　８０格報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(14 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
