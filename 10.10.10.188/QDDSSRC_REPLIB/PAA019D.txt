     A****************************************************************
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PAA019D                                    *
     A*    AUTHOR       : MICHELLE                                   *
     A*    CREATE DATE  : 91/08/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PAA019                                     *
     A*    SYSTEM       :人頭馬寰盛                                *
     A*    SUBSYSTEM    :基本資料建檔子系統                        *
     A*    REMARK       :ＡＭＰ代碼維護 (A/U/D/I)                  *
     A*===============================================================
     A* CODE | AUTHOR |  DATE  | DESCRIPTION
     A*---------------------------------------------------------------
     A*      |        |        |
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  4VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            NG02           6   O  9  9
     A            NG03          30O  O  9 19
     A            NG04           1   O  9 52
     A            DESC          20   O  9 55
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'PAA002'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'ＡＭＰ代碼維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  5'2=修改'
     A                                  5 15'4=刪除'
     A                                  5 25'5=查詢'
     A                                  7  4'A'
     A                                      DSPATR(UL)
     A                                  7  8'ＡＭＰ'
     A                                      DSPATR(UL)
     A                                  7 19'代碼說明             '
     A                                      DSPATR(UL)
     A                                  7 51'類別'
     A                                      DSPATR(UL)
     A            DBGN1          6A  B  8  9
     A          R DSPC11
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A                                    +2'F9 =列印'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPC2
     A                                      CA03(03)
     A                                      CF12(12)
     A                                  1  2'PAA019'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'ＡＭＰ代碼維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'ＡＭＰ'
     A                                  4 17':'
     A            DNG02          6   B  4 19
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  6  2'代碼說明     :'
     A            DNG03         30O  B  6 19
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'類　　別     :'
     A            DNG04          1   B  8 19VALUES('A' 'S')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DESC          20   O    +1
     A                                 10  2'代碼長度     : 002'
     A                                 12  2'代碼說明長度 : 030'
     A                                 18  1'異動日期:'
     A            DNG98          6  0O 18 13
     A                                      EDTCDE(Y)
     A                                 18 22'(MM/DD/YY)'
     A                                 19  1'異動時間:'
     A            DNG97          6  0O 19 13
     A                                      EDTWRD('  :  :  ')
     A                                 20  1'使用者　:'
     A            DNG99         10   O 20 13
     A                                 21  2'執行'
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'F3 =回主畫面'
     A                                 23 18'F12=回前畫面'
     A  98        ERR1          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(URE 0019 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
