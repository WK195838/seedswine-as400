     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : KIS021D                                    *
     A*    PROGRAM NAME :專櫃銷售日報表                            *
     A*    AUTHOR       : A1491 EASON                                *
     A*    CREATE DATE  : 2009/07/06                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :專櫃系統                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF07(07)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'<KIS021D>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 33'專櫃銷售日報表'
     A                                      DSPATR(RI)
     A                                  7 23'通路代號:'
     A            DSC33S    R        B  7 35REFFLD(SC33 SOSCPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A                                  7 37'~'
     A            DSC33E    R        B  7 39REFFLD(SC33 SOSCPF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A                                  9 23'客戶代號:'
     A            DSC04S    R        B  9 35REFFLD(SC04 SOSCPF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(HI)
     A                                  9 41'~'
     A            DSC04E    R        B  9 43REFFLD(SC04 SOSCPF)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64                                  DSPATR(HI)
     A                                 11 23'產品代號:'
     A            DSD03S    R        B 11 35REFFLD(SD03 SOSDPF)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
     A                                 11 45'~'
     A            DSD03E    R        B 11 47REFFLD(SD03 SOSDPF)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N66                                  DSPATR(HI)
     A                                 13 23'確認日期:'
     A            DSC39S    R   -2   B 13 35REFFLD(SC39 SOSCPF)
     A                                      EDTCDE(Y)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 13 44'~'
     A            DSC39E    R   -2   B 13 46REFFLD(SC39 SOSCPF)
     A                                      EDTCDE(Y)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A*                                18  2'執行環境:'
     A*           $PENV          1A  B 18 14VALUES('0' '1' '2')
     A*                                18 18'(0-查詢 1-即時 2-批次)'
     A  27                             21 27'    請輸入挑選區間      '
     A                                      DSPATR(RI)
     A  28                             21 27'    報表處理中，請稍候    '
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'PF03 =回主畫面'
     A                                 23 28'PF04 =欄位查詢'
     A                                 23 46'PF07 =檔案下載'
     A                                 24 67'【泛太資訊】'
