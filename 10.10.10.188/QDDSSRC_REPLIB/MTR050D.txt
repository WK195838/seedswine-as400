     A*%%TS  SD  19930209  173403  D910074     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*                                                              *
     A*    DSPF NAME    : MTR050D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 81/09/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTR050                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :新增產品一覽表                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.05 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930209  173403  D910074     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CF04(04 'F4 HELP')
     A                                      CA03(03 '結束執行')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MTR050'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'新增產品一覽表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 28'產品歸屬:'
     A            DMA07S    R        B  5 40REFFLD(MA07)
     A                                      VALUES(' ' 'R' 'C' 'W')
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  7 28'產品代號:'
     A                                      DSPATR(HI)
     A            DMA01S    R        B  7 40REFFLD(MA01)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  7 50'-'
     A            DMA01E    R        B  7 52REFFLD(MA01)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  9 28'建檔日期:'
M001MA*           DMA53S    R        B  9 40REFFLD(MA53)
M001MA            DMA53S         6  0B  9 40
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                      EDTWRD('  /  /  ')
     A                                  9 49'-'
M001MA*           DMA53E    R        B  9 51REFFLD(MA53)
M001MA            DMA53E         6  0B  9 51
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                      EDTWRD('  /  /  ')
     A                                 16 28'執行環境:'
     A            $EVR           1Y 0B 16 40VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 45'(1-即時   2-批次)'
     A                                 17 28'列印份數:'
     A            $CPY           3Y 0B 17 40EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 28'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 40CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 28'*** 請準備１３２格報表紙 ***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 64MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
