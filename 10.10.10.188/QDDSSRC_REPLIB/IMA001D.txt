     A*%%TS  SD  19930428  090838  D910121     REL-V2R1M0  5738-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : IMA001D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/03/04                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMA001                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :送貨確認作業處理 (A/U/D/I)                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.13 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19930428  090838  D910121     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  2
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      VALUES(' ' '4' '5' '6')
M001MA            DSE06          6Y 0O 10  6
     A                                      EDTWRD('  /  /  ')
     A            SE02      R        O 10 28
     A            SE07      R        O 10 19
     A            SE04      R        O 10 47
     A            SE05      R        O 10 53
M001MA            DSE24          6Y 0O 10 60
     A                                      EDTWRD('  /  /  ')
     A            DTYPE          1A  O 10 77
     A            D#203         18A  O 11 27
     A            ME04      R        O 11 46
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19930428  090838  D910121     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0005)
     A                                      SFLDROP(CF01)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'IMA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'送貨確認作業處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'輸入作業選擇:'
     A                                  6 20'5=查詢'
     A                                  6 34'6=確認'
     A                                  7 19'送貨'
     A                                  8  2'A   MM/DD/YY    物流點 送貨單+
     A                                      號         客戶代號   確認日+
     A                                      期      認'
     A                                      DSPATR(UL)
M001MA            DBGN1          6Y 0B  9  6
     A                                      EDTWRD('  /  /  ')
     A            DBGN2     R        B  9 19REFFLD(SE07)
     A N58                                  DSPATR(UL)
     A  58                                  DSPATR(PR)
     A            DBGN3     R        B  9 28REFFLD(SE02)
     A                                  7 75'確'
     A                                  6  5'4=確認取銷'
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                    +2'F1 =明細顯示'
     A                                    +2'F3 =回主畫面'
     A*                                   +2'F6 =新增'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0121 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0156 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPC2
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A***                                 +2'F14=確認'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0037 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A*%%TS  SD  19930428  090838  D910121     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SF03      R        O 11  5
     A            MA03      R        O 11 15
     A            MA13      R        O 11 34
     A            DATA1          5Y 0O 11 43EDTCDE(1)
     A            DATA2          5Y 0B 11 53EDTCDE(1)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            HDAT2          5S 0H
     A*------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*
     A*                                     CSRLOC(D#ROW      D#COL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A****                                  ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A***                                   CF14(14)
     A                                      OVERLAY
     A* 37                                  FRCDTA
     A* 37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A*           D#ROW          3S 0H
     A*           D#COL          3S 0H
     A                                  1  2'IMA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'送貨確認作業處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A            SE01      R        O  4 14
     A            #B03      R        O  4 17
     A                                  5  2'送貨單號:'
     A            SE02      R        O  5 14
     A                                  5 48'單別:'
     A            SE03      R        O  5 56
     A            D#203         18A  O  5 59
     A                                  6  2'客戶代號:'
     A            SE04      R        O  6 14
     A                                  6 20'-'
     A            SE05      R        O  6 22
     A            ME04      R        O  6 27
     A                                  7  2'物流點起:'
     A            SE07      R        O  7 14
     A            DSTOR1        22A  O  7 20
     A                                  7 44'送貨日期:'
M001MA            DSE06          6Y 0O  7 56
     A                                      EDTWRD('  /  /  ')
     A                                  7 65'(MM/DD/YY)'
     A                                  8  2'物流點迄:'
     A            SE26      R        O  8 14
     A            DSTOR2        22A  O  8 20
     A                                  8 44'確認日期:'
M001MA            DSE24          6Y 0O  8 56
     A                                      EDTWRD('  /  /  ')
     A                                  8 65'(MM/DD/YY)'
     A                                 10  4'產品代號／名稱　　　　　　　　單+
     A                                      位　送貨數量　確認數量'
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A          R DSPC3
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R SFLSR3                    SFL
     A*
     A  54                                  SFLNXTCHG
     A            RRN3           4S 0H
     A            SF03      R        O 11  5
     A            MA03      R        O 11 15
     A            MA13      R        O 11 34
     A            DATA1          5  0O 11 43
     A                                      EDTCDE(1)
     A            DATA2          5  0O 11 53
     A                                      EDTCDE(1)
     A*------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A*
     A*                                     CSRLOC(D#ROW      D#COL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A****                                  ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A* 37                                  FRCDTA
     A* 37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN3          4S 0H      SFLRCDNBR
     A*           D#ROW          3S 0H
     A*           D#COL          3S 0H
     A                                  1  2'IMA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 31'送貨確認作業處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A            SE01      R        O  4 14
     A            #B03      R        O  4 17
     A                                  5  2'送貨單號:'
     A            SE02      R        O  5 14
     A                                  5 48'單別:'
     A            SE03      R        O  5 56
     A            D#203         18A  O  5 59
     A                                  6  2'客戶代號:'
     A            SE04      R        O  6 14
     A                                  6 20'-'
     A            SE05      R        O  6 22
     A            ME04      R        O  6 27
     A                                  7  2'物流點起:'
     A            SE07      R        O  7 14
     A            DSTOR1        22A  O  7 20
     A                                  7 44'送貨日期:'
M001MA            DSE06          6Y 0O  7 56
     A                                      EDTWRD('  /  /  ')
     A                                  7 65'(MM/DD/YY)'
     A                                  8  2'物流點迄:'
     A            SE26      R        O  8 14
     A            DSTOR2        22A  O  8 20
     A                                  8 44'確認日期:'
M001MA            DSE24          6Y 0O  8 56
     A                                      EDTWRD('  /  /  ')
     A                                  8 65'(MM/DD/YY)'
     A                                 10  4'產品代號／名稱　　　　　　　　單+
     A                                      位　送貨數量　確認數量'
     A                                      DSPATR(UL)
