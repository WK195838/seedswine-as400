     A*%%TS  SD  19940305  103732  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : KIB004D                                    *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 83/03/05                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : KIB004                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :盤點系統                                  *
     A*    REMARK       :專櫃盤點年終成本庫存更新                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19920225  112554  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF14(14 '處理作業')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'KIB004'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 28'專櫃盤點年終成本庫存更新'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'物流點　　:'
     A            DIU03S    R        O  6 40REFFLD(IU03)
     A                                  6 50'-'
     A            DIU03E    R        O  6 52REFFLD(IU03)
     A                                  8 26'盤點日期　:'
M001MA            DIU04S         6Y 0B  8 40
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                      EDTWRD('  /  /  ')
     A                                  8 50'-'
M001MA            DIU04E         6Y 0B  8 52
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                      EDTWRD('  /  /  ')
     A                                  8 63'(MM/DD/YY)'
     A                                 10 26'年終年月　:'
     A            DMMYY          4  0B 10 40EDTWRD('  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 10 50'(MM/YY)'
     A                                 12 21'+----------------------------------
     A                                      --------------+'
     A                                 13 21'|　　此作業將更新專櫃成本檔及庫-
     A                                      存檔資料'
     A                                 13 69'|'
     A                                 14 21'|　　請按F3放棄此作業或F14執-
     A                                      行此作業'
     A                                 14 69'|'
     A                                 15 21'+----------------------------------
     A                                      --------------+'
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F14=處理作業'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  86        ERR15         50A  O 24  2MSGID(URE 0122 REMF)
     A                                      DSPATR(HI)
     A  85        ERR16         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A  84        ERR17         50A  O 24  2MSGID(URE 0138 REMF)
     A                                      DSPATR(HI)
     A  83        ERR18         50A  O 24  2MSGID(URE 0139 REMF)
     A                                      DSPATR(HI)
     A                                 24 63MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
