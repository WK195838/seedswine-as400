     A*%%TS  SD  19960408  101345  DERLERN     REL-V3R1M0  5763-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : WOA100D                                    *
     A*    AUTHOR       : A1092  TAYLOR                              *
     A*    CREATE DATE  : 81/02/10                                   *
     A*    UPDATE DATE  : 81/04/17 PHILIP                            *
     A*    PROGRAM NAME : WOA100                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR1                    SFL
     A*%%TS  SD  19960408  101345  DERLERN     REL-V3R1M0  5763-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  2VALUES(' ' '6')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            MA01      R        O 10  7
     A            MA02      R        O 10 18
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19960408  101345  DERLERN     REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(1200)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                      CSRLOC(#LIN       #COL)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'SOA031'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'銷貨目標資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'輸入作業選擇:'
     A                                  5  4'6=每月目標設定'
     A                                  6  2'==================================-
     A                                      ===================================-
     A                                      =========='
     A                                  7  7'產品代號'
     A                                  7 18'產品名稱'
     A                                  8  2'==================================-
     A                                      ===================================-
     A                                      =========='
     A            DMA01     R        B  9  7REFFLD(MA01)
     A                                      CHANGE(27)
     A*
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24  2'執行'
     A                                 24 10'F3 =回主畫面'
