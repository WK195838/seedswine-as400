     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : WOR1201P                                   *
     A*    AUTHOR       : SAM                                        *
     A*    CREATE DATE  : 85/08/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : WOR1201                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :主檔                                      *
     A*    REMARK       :客戶清冊                                  *
     A****************************************************************
     A                                      REF(WORF)
     A          R PH1                       SKIPB(2)
     A                                    61MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    70'客戶清冊'
     A                                      SPACEA(1)
     A                                     1'通　路'
     A                                    11':'
     A            DWE07S    R             12REFFLD(WE07)
     A                                    19'∼'
     A            DWE07E    R             24REFFLD(WE07)
     A                                      SPACEA(1)
     A                                     1'地　區'
     A                                    11':'
     A            DWE15S         2        12
     A                                    19'∼'
     A            DWE15E         2        24
     A                                   127'日期:'
     A            $EGMDY         6  0O   134EDTCDE(Y)
     A                                   143'頁次:'
     A                                   150PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'業務員'
     A                                    11':'
     A            DWE18S    R        O    12REFFLD(WE18)
     A                                    19'∼'
     A            DWE18E    R        O    24REFFLD(WE18)
     A                                   127'時間:'
     A                                   134TIME
     A                                      SPACEA(1)
     A                                     1'等　級='
     A                                    15'REMY  :'
     A            DWE1S          1   O    23
     A                                    24'-'
     A            DWE1E          1   O    25
     A                                    28'BRANDY:'
     A            DWE2S          1   O    36
     A                                    37'-'
     A            DWE2E          1   O    38
     A                                    41'WHISKEY:'
     A            DWE3S          1   O    50
     A                                    51'-'
     A            DWE3E          1   O    52
     A                                      SPACEA(1)
     A                                    15'LIQUOR:'
     A            DWE4S          1   O    23
     A                                    24'-'
     A            DWE4E          1   O    25
     A                                    28'WINE  :'
     A            DWE5S          1   O    36
     A                                    37'-'
     A            DWE5E          1   O    38
     A                                    41'OTHER  :'
     A            DWE6S          1   O    50
     A                                    51'-'
     A            DWE6E          1   O    52
     A                                   127' USER :'
     A            $USER         10   O   134
     A                                   145'<WOR100P>'
     A                                      SPACEA(1)
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==='
     A                                      SPACEA(1)
     A                                     1'客戶代號'
     A                                    12'客戶全稱'
     A                                    59'通路'
     A                                    65'地區'
     A                                    71'型態'
     A                                    77'地址'
     A                                   110'負責人'
     A                                   123'電話一'
     A                                   137'業務員'
     A                                      SPACEA(1)
     A*                                  133'等級'
     A                                    12'REMY  BRANDY  WHISKEY'
     A                                    35'LIQUOR  WINE  OTHER'
     A                                    59'統一編號'
     A                                   110'聯絡人'
     A                                   123'傳真機'
     A                                   137'信用額度(元)'
     A                                      SPACEA(1)
     A                                     1'------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       ------------------------------+
     A                                       ---'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1
     A            WE01      R              2
     A            WE02      R             12
     A            WE07      R             61
     A            WE10      R             67
     A            WE08      R             73
     A            WE04      R             77
     A            WE12      R            110
     A            WE15      R            123
     A            WE18      R            138
     A            MC02      R            146REFFLD(MC02 MTMCPF)
     A*----------------------------------------------------------------
     A                                      SPACEA(1)
     A            PWE1           1   O    14
     A            PWE2           1   O    21
     A            PWE3           1   O    29
     A            PWE4           1   O    38
     A            PWE5           1   O    45
     A            PWE6           1   O    51
     A            WE09      R             59
     A            WE041     R             71
     A            WE13      R            104
     A            WE17      R            123
     A            PMD22     R            133REFFLD(MD22 MTMDPF)
     A                                      EDTCDE(A $)
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==============================+
     A                                       ==='
     A                                      SPACEA(1)
     A                                     2'<<WOR120>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE9                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
