     A*%%TS  SD  19920127  161553  REPGMR      REL-V2R1M0  5738-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PIA002D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/04/23                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PIA002                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :盤點系統                                  *
     A*    REMARK       :盤點資料輸入                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.16 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A          R DSPD1
     A*
     A**                                    CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A**                                    CF14(14 '處理作業')
     A**          D#ROW          3S 0H
     A**          D#COL          3S 0H
     A                                  1  2'PIA002'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'盤點資料輸入'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A*                                 6 27'公司代號　:'
     A*           DIO01     R        B  6 41REFFLD(IO01)
     A* 60                                  DSPATR(RI)
     A* 60                                  DSPATR(PC)
     A*                                 6 51'(產品歸屬)'
     A                                  8 27'物流點　　:'
     A            DIO02     R        B  8 41REFFLD(IO02)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                 10 27'盤點單號起:'
     A            DIO03S    R        B 10 41REFFLD(IO03)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 10 51'-'
     A            DIO03E    R        B 10 54REFFLD(IO03)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 12 27'盤點日期　:'
M001MA            DIO08          6Y 0B 12 41
     A                                      EDTWRD('  /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 12 51'(MM/DD/YY)'
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPC2
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F5 =參考資料'
     A                                    +2'F12=回前畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0106 REMF)
     A                                      DSPATR(HI)
     A  88        ERR13         50A  O 24  2MSGID(UPT 5017 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R SFLSR1                    SFL
     A*
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 11  2
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      VALUES(' ' '2' '4')
     A            DIO03     R        O 11  4REFFLD(IO03)
     A            DIO01     R        O 11 14REFFLD(IO01)
     A            DIO04     R        B 11 18REFFLD(IO04)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            MA03      R        O 11 28
     A            DIO06     R        B 11 47REFFLD(IO06)
     A                                      EDTCDE(1)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DIO07     R        B 11 56REFFLD(IO07)
     A                                      EDTCDE(1)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DIO05     R        O 11 65REFFLD(IO05)
     A                                      EDTCDE(1)
M001MA            WIO08          6Y 0O 11 73
     A                                      EDTWRD('  /  /  ')
     A            HIO04     R        H      REFFLD(IO04)
     A            HIO09     R        H      REFFLD(IO09)
     A            HIO10     R        H      REFFLD(IO10)
     A            DCHK           1A  H
     A*                                     'Y' -已存檔中--> UPD
     A*                                     ' ' -不存檔中--> ADD
     A*------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF05(05)
     A                                      OVERLAY
     A* 37                                  FRCDTA
     A* 37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A**53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PIA002'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'盤點資料輸入'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A***         DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A*                                 4  2'公司代號:'
     A*           DIO01     R        O  4 14REFFLD(IO01)
     A*           #A03      R        O  4 20
     A                                  5  2'物流點　:'
     A            DIO02     R        O  5 14REFFLD(IO02)
     A            DSTOR         22A  O  5 20
     A                                  6  2'盤點日期:'
M001MA            DIO08          6Y 0O  6 14
     A                                      EDTWRD('  /  /  ')
     A                                  6 24'(MM/DD/YY)'
     A                                  7  2'輸入作業選擇:'
     A                                  8  6'2=修改   4=作廢'
     A                                  9 73'盤點日'
     A                                 10  2'A盤點單號　公　產品代號／名稱　+
     A                                      　　　　　　　良品量　壞品量　+
     A                                      盤點總數MM/DD/YY'
     A                                      DSPATR(UL)
