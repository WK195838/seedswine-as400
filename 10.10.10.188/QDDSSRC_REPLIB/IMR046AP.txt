     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : IMR046P                                    *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : 81/02/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR046                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :客戶送貨明細表                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.12.10 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    47MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    53'客戶送貨明細表'
     A                                      SPACEA(1)
     A                                    91'日期:'
     A            $EGMDY         6  0O    99EDTCDE(Y)
     A                                   108'頁次:'
     A                                   116PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    91'時間:'
     A                                    99TIME
     A                                      SPACEA(1)
     A                                     1'送貨日期:'
M001MA            DSE21S         6  0     +1EDTWRD('  /  /  ')
     A*M001D                                REFFLD(SE21)
     A                                    +1'-'
M001MA            DSE21E         6  0     +1EDTWRD('  /  /  ')
     A*M001D                                REFFLD(SE21)
     A                                    91' USER :'
     A            $USER         10   O    99
     A                                   110'<IMR046>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                    90'訂購'
     A                                    98'搭贈'
     A                                   106'試飲'
     A                                   115'送貨'
     A                                      SPACEA(1)
     A                                     1'送貨日期'
     A                                    12'客戶代號'
     A                                    25'客戶簡稱'
     A                                    44'送貨單號'
     A                                    59'產品代號'
     A                                    69'產品簡稱'
     A                                    90'數量'
     A                                    98'數量'
     A                                   106'數量'
     A                                   115'總數'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
M001MA  31        PSE21          6  0      2EDTWRD('  /  /  ')
     A*M001D                                REFFLD(SE21)
     A  30        SE04      R             13
     A  30                                19'-'
     A  30        SE05      R             21
     A  30        ME04      R             25
     A  29        SE02      R             45
     A            SF03      R             60
     A            MA03      R             69
     A            PSF041         5  0     89EDTCDE(J)
     A            SF05      R             98EDTCDE(J)
     A            PSF042         5  0    105EDTCDE(J)
     A            PTOT           5  0    114EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'<<IMR046>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE31                      SPACEA(1)
     A                                     3'もも此範圍無資料もも'
