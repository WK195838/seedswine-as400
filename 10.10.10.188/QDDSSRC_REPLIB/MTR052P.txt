     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : MTR052P                                    *
     A*    AUTHOR       : D910074 TINA                               *
     A*    CREATE DATE  : 81/09/22                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTR052                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :收款用客戶明細表                          *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    42
     A                                      MSGCON(030 URE9999 REMF)
     A                                      SPACEA(001)
     A                                    47
     A                                      DFT('收款用客戶明細表')
     A                                      SPACEA(001)
     A                                     1'通　路　:'
     A            DMD07S    R             +1REFFLD(MD07)
     A                                    +6'-'
     A            DMD07E    R             +1REFFLD(MD07)
     A                                      SPACEA(001)
     A                                     1'地　區　:'
     A            DME07S    R             +1REFFLD(ME07)
     A                                    +5'-'
     A            DME07E    R             +1REFFLD(ME07)
     A                                    88
     A                                      DFT('日期:')
     A            $EGMDY         6  0O    98
     A                                      EDTCDE(Y)
     A                                   108
     A                                      DFT('頁次:')
     A                                   116
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'業務員　:'
     A            DME15S    R             +1REFFLD(ME15)
     A                                    +1'-'
     A            DME15E    R             +1REFFLD(ME15)
     A                                    88
     A                                      DFT('時間:')
     A                                    98
     A                                      TIME
     A                                   108
     A                                      DFT('<MTR052>')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('客戶代號:')
     A            DMD01S    R        O    +1
     A                                      REFFLD(MD01)
     A                                    +2
     A                                      DFT('-')
     A            DMD01E    R        O    +1
     A                                      REFFLD(MD01)
     A                                    88' USER :'
     A            $USER         10A  O    98
     A                                      SPACEA(1)
     A                                     1DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =======================')
     A                                     1
     A                                      DFT('客戶代號／簡稱')
     A                                      SPACEB(001)
     A                                    24
     A                                      DFT('額度（元）')
     A                                    39
     A                                      DFT('信用')
     A                                    45
     A                                      DFT('期限')
     A                                    51
     A                                      DFT('結算')
     A                                    57
     A                                      DFT('收款')
     A                                    63
     A                                      DFT('地　　址')
     A                                   111
     A                                      DFT('業務人員')
     A                                    39
     A                                      DFT('條件')
     A                                      SPACEB(001)
     A                                    45
     A                                      DFT('天數')
     A                                    51
     A                                      DFT('日　')
     A                                    57
     A                                      DFT('日　')
     A                                     1
     A                                      DFT('-------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------------------')
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A            ME01      R        O     1
     A            ME04      R        O     6
     A            MD22      R        O    24EDTCDE(J)
     A            PMD23          6   O    39
     A            MD25      R        O    47
     A                                      EDTCDE(Z)
     A            MD24      R        O    53
     A                                      EDTCDE(Z)
     A            MD26      R        O    59
     A                                      EDTCDE(Z)
     A            ME05      R        O    63
     A            ME06      R        O    95
     A            MC02      R        O   111
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PE1
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =======================')
     A                                     2
     A                                      DFT('***續下頁***')
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =======================')
     A                                     2
     A                                      DFT('<<MTR052>>--->報表結束')
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9                       SPACEB(1)
     A                                     3'もも此範圍無資料もも'
