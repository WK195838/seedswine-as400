     A*%%TS  SD  19920609  105935  REPGMRP     REL-V2R1M0  5738-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : SAA001D                                    *
     A*    AUTHOR       : A1084  CHARLES                             *
     A*    CREATE DATE  : 81/05/01                                   *
     A*    PROGRAM NAME : SAA001                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :銷售目標達成率設定 (A/U/D/I)              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920502  081304  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  2VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SK01      R        O 10  4
     A            SK02      R        O 10  9EDTCDE(2)
     A            SK07      R        O 10 13
     A            SK03      R        O 10 18
     A            SK04      R        O 10 21
     A            SK05      R        O 10 26
     A            SK06      R        O 10 37
     A            SK08      R        O 10 51EDTCDE(2)
     A            SK09      R        O 10 64EDTCDE(2)
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920609  105935  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'SAA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'銷售目標達成率設定'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A            DBGN1     R        B  9  4REFFLD(SK01)
     A            DBGN2     R        B  9  9REFFLD(SK02)
     A                                      EDTCDE(2)
     A            DBGN3     R        B  9 13REFFLD(SK07)
     A            DBGN4     R        B  9 18REFFLD(SK03)
     A            DBGN5     R        B  9 21REFFLD(SK04)
     A            DBGN6     R        B  9 26REFFLD(SK05)
     A            DBGN7     R        B  9 37REFFLD(SK06)
     A                                  5  2'輸入作業選擇:'
     A                                  5 18'2=修改'
     A                                  5 28'4=刪除'
     A                                  5 38'5=查詢'
     A                                  8  2'A司 度區路態 客戶代號 -
     A                                      產品代號 銷售目標數量 銷售目+
     A                                      標金額'
     A                                      DSPATR(UL)
     A                                  7  3'公'
     A                                  7  8'年地通型'
     A*
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R DSPC1
     A*%%TS  SD  19920501  110550  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F6 =新增'
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A  80                             24  2'訂單號碼'
     A                                      DSPATR(HI)
     A  80        DSK02     R        O 24 13REFFLD(SK02)
     A  80                                  DSPATR(RI)
     A  80                             24 26'新增成功'
     A                                      DSPATR(HI)
     A          R DSPD2
     A*%%TS  SD  19920519  082716  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SAA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'銷售目標達成率設定'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'公司代號:'
     A            DSK01     R        B  5 13REFFLD(SK01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            #B03      R        O  5 20
     A                                  5 47'年度:'
     A            DSK02     R        B  5 55REFFLD(SK02)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  6  1'通路代號:'
     A            DSK03     R        O  6 13REFFLD(SK03)
     A            D#A03         22O  O  6 20
     A                                  6 43'型態代號:'
     A            DSK04     R        O  6 55REFFLD(SK04)
     A            #C03      R        O  6 58
     A                                  7  1'地區代號:'
     A            DSK07     R        O  7 13REFFLD(SK07)
     A            #D02      R        O  7 20
     A                                  7 47'客戶:'
     A            DSK05     R        B  7 55REFFLD(SK05)
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            ME04      R        O  7 62
     A                                  8  1'產品代號:'
     A            DSK06     R        B  8 13REFFLD(SK06)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A N66                                  DSPATR(HI)
     A            MA03      R        O  8 24
     A                                  8 47'單位:'
     A            MA13      R        O  8 55
     A                                  9  1'銷售數量:'
     A            DSK08     R        O  9 13REFFLD(SK08)
     A                                      EDTCDE(2)
     A                                  9 43'銷售金額:'
     A            DSK09     R        O  9 55REFFLD(SK09)
     A                                      EDTCDE(2)
     A                                 12 12'4 /'
     A            SK021     R        O 12 16REFFLD(SK02)
     A                                 12 24'5 /'
     A            SK022     R        O 12 28REFFLD(SK02)
     A                                 12 36'6 /'
     A            SK023     R        O 12 40REFFLD(SK02)
     A                                 12 47'7 /'
     A            SK024     R        O 12 51REFFLD(SK02)
     A                                 12 60'8 /'
     A            SK025     R        O 12 64REFFLD(SK02)
     A                                 12 72'9 /'
     A            SK026     R        O 12 76REFFLD(SK02)
     A                                 13  1'數量'
     A            DSK10     R        B 13 10REFFLD(SK10)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK11     R        B 13 22REFFLD(SK11)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK12     R        B 13 34REFFLD(SK12)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK13     R        B 13 46REFFLD(SK13)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK14     R        B 13 58REFFLD(SK14)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK15     R        B 13 70REFFLD(SK15)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  1'金額'
     A            DSK22     R        B 14  8REFFLD(SK22)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK23     R        B 14 20REFFLD(SK23)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK24     R        B 14 32REFFLD(SK24)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK25     R        B 14 44REFFLD(SK25)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK26     R        B 14 56REFFLD(SK26)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK27     R        B 14 68REFFLD(SK27)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16 11'10 /'
     A            SK027     R        O 16 16REFFLD(SK02)
     A                                 16 23'11 /'
     A            SK028     R        O 16 28REFFLD(SK02)
     A                                 16 35'12 /'
     A            SK029     R        O 16 40REFFLD(SK02)
     A                                 16 47'1 /'
     A            SK0210    R        O 16 51REFFLD(SK02)
     A                                 16 60'2 /'
     A            SK0211    R        O 16 64REFFLD(SK02)
     A                                 16 72'3 /'
     A            SK0212    R        O 16 76REFFLD(SK02)
     A                                 17  1'數量'
     A            DSK16     R        B 17 10REFFLD(SK16)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK17     R        B 17 22REFFLD(SK17)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK18     R        B 17 34REFFLD(SK18)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK19     R        B 17 46REFFLD(SK19)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK20     R        B 17 58REFFLD(SK20)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK21     R        B 17 70REFFLD(SK21)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18  1'金額'
     A            DSK28     R        B 18  8REFFLD(SK28)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK29     R        B 18 20REFFLD(SK29)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK30     R        B 18 32REFFLD(SK30)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK31     R        B 18 44REFFLD(SK31)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK32     R        B 18 56REFFLD(SK32)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSK33     R        B 18 68REFFLD(SK33)
     A                                      EDTCDE(2)
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A                                 22 58'F12=回前畫面'
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0024 REMF)
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
