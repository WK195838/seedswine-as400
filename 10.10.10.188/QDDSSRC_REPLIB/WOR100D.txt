     A*%%TS  SD  19931115  103628  D910121     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : WOR100D                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/02/13                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : WOR100                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :主檔                                      *
     A*    REMARK       :客戶清冊                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/WORF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19931115  103628  D910121     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'WOR100'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 35'客戶清冊'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 25'通路　　 :'
     A                                      DSPATR(HI)
     A            DWE07S    R        B  5 38REFFLD(WE07)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A N66                                  DSPATR(HI)
     A                                  5 45'-'
     A            DWE07E    R        B  5 47REFFLD(WE07)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(HI)
     A                                  6 25'型態　　 :'
     A                                      DSPATR(HI)
     A            DWE08S    R        B  6 38REFFLD(WE08)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A N71                                  DSPATR(HI)
     A                                  6 45'-'
     A            DWE08E    R        B  6 47REFFLD(WE08)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A N72                                  DSPATR(HI)
     A                                  8 25'地區'
     A                                  8 36':'
     A            DWE10S         1   B  8 38
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  8 45'-'
     A            DWE10E         1   B  8 47
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                  8 49'(1-北區2-中區 3-南區)'
     A                                  9 25'區域'
     A                                      DSPATR(HI)
     A                                  9 36':'
     A            DWE11S         2   B  9 38
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A N73                                  DSPATR(HI)
     A                                  9 45'-'
     A            DWE11E         2   B  9 47
     A  74                                  DSPATR(RI)
     A  74                                  DSPATR(PC)
     A N74                                  DSPATR(HI)
     A                                 11 25'業務員　 :'
     A            DWE18S    R        B 11 38REFFLD(WE18)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A                                 11 45'-'
     A            DWE18E    R        B 11 47REFFLD(WE18)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A N65                                  DSPATR(HI)
     A                                 12 25'客戶代號'
     A                                 12 36':'
     A            DWE01S    R        B 12 38REFFLD(WE01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                 12 45'-'
     A            DWE01E    R        B 12 47REFFLD(WE01)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                 16 25'執行環境'
     A                                 16 36':'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                 16 42'(1-即時   2-批次)'
     A                                 18 25'列表機'
     A                                      DSPATR(HI)
     A                                 18 36':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A            ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
