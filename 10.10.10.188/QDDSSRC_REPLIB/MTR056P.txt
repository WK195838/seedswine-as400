     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:3459696    *
     A*                                                              *
     A*    DSPF NAME    : MTR056P                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 08/02/94                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTR056                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :產品基本資料卡列印                        *
     A*===============================================================
     A* MODI CODE | AUTHOR |  DATE  | DESCRIPTION
     A*---------------------------------------------------------------
     A*  M001     | MICH   | 000824 | DETAIL MODIFY
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1
     A                                  6  2'MTR056'
     A                                  6 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                  6 62'日期:'
     A            $EGMDY         6  0O  6 70EDTCDE(Y)
     A                                  7 31'產品基本資料維護'
     A                                  7 62'時間:'
     A                                  7 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6   O  8  2
     A                                  8 62' USER :'
     A            $USER         10A  O  8 70
     A                                  9  2'產品代號:'
     A            MA01      R           9 14
     A                                      UNDERLINE
     A                                 10  2'中文全稱:'
     A            MA02      R          10 14
     A                                      UNDERLINE
     A                                 10 49'中文簡稱:'
     A            MA03      R          10 61
     A                                      UNDERLINE
     A                                 11  2'英文全稱:'
     A            MA04      R          11 14
     A                                      UNDERLINE
     A            MA05      R          11 46
     A                                      UNDERLINE
     A                                 12  2'英文簡稱:'
     A            MA06      R          12 14
     A                                      UNDERLINE
     A                                 12 42'產品轉帳:'
     A            MA07      R          12 54
     A                                      UNDERLINE
     A            DCMP           6A  O 12 57
     A                                      UNDERLINE
     A                                 13  2'識別碼　:'
     A            MA08      R        O 13 14
     A                                      UNDERLINE
     A            DID           22A  O 13 18
     A                                 13 42'種類　　:'
     A            MA09      R          13 54
     A                                      UNDERLINE
     A            DGROUP        22A  O 13 57
     A                                 14  2'品牌　　:'
     A            MA10      R        O 14 14
     A                                      UNDERLINE
     A            DBRAND        22A  O 14 18
     A                                 14 42'Ａ＆Ｐ　:'
     A            MA11      R          14 54
     A                                      UNDERLINE
     A                                 15  2'課稅別  :'
     A            MA59      R          15 14
     A                                      UNDERLINE
     A                                 15 22'(D-應稅, B-免稅, Z-零稅)'
     A                                 16  2'庫存單位:'
     A            MA13      R          16 14
     A                                      UNDERLINE
     A                                 16 29'主要供應商   :'
     A            MA12      R          16 46
     A                                      UNDERLINE
     A            DMB03         12A  O 16 52
     A*                                16 60'F.O.B.   :'
     A*           MA31      R        O 16 71
     A*                                     EDTCDE(2)
     A                                 17  2'採購單位:'
     A            MA14      R          17 14
     A                                      UNDERLINE
     A                                 17 29'前置時間     :'
     A            MA18      R          17 46
     A                                      UNDERLINE
     A                                      EDTCDE(2)
     A                                 17 51'日'
     A*                                17 61'FHI      :'
     A*           MA32      R        O 17 71
     A*                                     EDTCDE(2)
     A                                 18  2'單位容積:'
     A            MA15      R          18 14
     A                                      UNDERLINE
     A                                      EDTCDE(2)
     A                                 18 20'l'
     A                                 18 29'採購人員     :'
     A            MA19      R          18 46
     A                                      UNDERLINE
     A            DMC02         10A  O 18 54
     A*                                18 60'DUTY     :'
     A*           MA33      R        O 18 71
     A*                                     EDTCDE(2)
     A                                 19  2'單位轉換:'
     A            MA16      R          19 14
     A                                      UNDERLINE
     A                                      EDTCDE(2)
     A                                 19 29'原產地       :'
     A            MA20      R          19 46
     A                                      UNDERLINE
     A*                                19 59'單位成本:'
     A*           MA34      R        O 19 71
     A*                                     EDTCDE(2)
     A                                 20  2'每箱容積:'
     A            DMA15C         6  3O 20 14EDTWRD('   .   ')
     A                                      UNDERLINE
     A                                 20 22'l'
     A                                 20 29'空瓶重量     :'
     A            MA21      R          20 46
     A                                      UNDERLINE
     A                                      EDTCDE(2)
     A                                 20 52'（克）'
     A*                                20 59'批發價格:'
     A*           MA45      R        O 20 71
     A*                                     EDTCDE(2)
     A                                 21  2'包裝規格'
     A            MA17      R          21 14
     A                                      UNDERLINE
     A                                 21 29'最小採購批量 :'
     A            MA35      R          21 46
     A                                      UNDERLINE
     A                                      EDTCDE(2)
     A*                                21 59'零售價格:'
     A*           MA46      R        O 21 71
     A*                                     EDTCDE(2)
     A                                 22  2'禮盒碼  :'
     A            MA54      R          22 14
     A                                      UNDERLINE
     A                                 22 18'(Y, )'
     A                                 22 29'最大採購批量 :'
     A            MA36      R          22 46
     A                                      UNDERLINE
     A                                      EDTCDE(2)
     A*                                22 59'免稅價格:'
     A*           MA47      R        O 22 71
     A*                                     UNDERLINE
     A                                 23  2'可寄賣放:'
     A            MA55      R          23 14
     A                                      UNDERLINE
     A            MA56      R          23 16
     A                                      UNDERLINE
     A                                 23 18'(Y, )'
     A                                 23 29'保稅倉安全量 :'
     A            MA37      R          23 46
     A                                      UNDERLINE
     A                                      EDTCDE(2)
     A*                                23 59'最近銷售:'
     A*           DMA50          6  0O 23 71EDTCDE(Y)
     A                                 24  2'自動轉帳:'
     A            MA57      R          24 14REFFLD(MA57)
     A                                      UNDERLINE
     A                                 24 18'(Y, )'
     A                                 24 29'完稅倉安全量 :'
     A            MA38      R          24 46
     A                                      UNDERLINE
     A                                      EDTCDE(2)
     A*                                24 59'最近進貨:'
     A*           DMA51          6  0O 24 71EDTCDE(Y)
     A                                 25  2'公賣利益:'
     A            MA25      R          25 14
     A                                      UNDERLINE
     A                                      EDTCDE(2)
     A                                 25 31'使用碼     :'
     A            MA58      R          25 46
     A                                 25 48'(A,I)'
     A*                                25 59'建檔日期:'
     A*           DMA53          6  0O 25 71
     A*                                     EDTCDE(Y)
     A                                 26  2'備註　　:'
     A            MA23      R          26 14
     A            MA24      R          26 47
     A                                 27  5'********************+
     A                                       牌****價****+
     A                                       資****料****+
     A                                       *******************'
     A                                 29  7'經銷牌價:'
     A                                 29 44'零售牌價:'
     A                                 29 19'___________'
     A                                 29 56'___________'
     A                                 31  7'牌價生效日期:'
     A                                 31 23'      /      /       +
     A                                       (MM/DD/YY)'
     A          R PH2
     A                                 32  5'********************+
     A                                       禮****盒****+
     A                                       結****構****+
     A                                       *******************'
     A                                 33  5'產品代號／簡稱'
     A                                 33 35'單位'
     A                                 33 45'數量'
     A                                 33 55'附屬品'
     A          R PD2                       SPACEB(1)
     A            MF02      R              5
     A            PROD          12        16
     A            MF03      R             35
     A            MF04      R             46EDTCDE(2)
     A            MF07      R             58
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ============'
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ============'
     A                                      SPACEA(1)
     A                                     2'<<MTR056>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       ------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE4                       SPACEB(1)
     A                                     1'***** 禮盒結構未建，+
     A                                      請建禮盒結構！ *****'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE5
     A                                 59  1'==================================+
     A                                      ===================================+
     A                                      ==========='
     A                                 60  1'製表人:______'
     A                                 60 16'企劃主管:______'
     A                                 60 33'財務部:______'
M001MA*                                60 48'副總經理:______'
M001MA*                                60 65'電腦室:______'
M001MA                                 60 48'總經理:______'
M001MA                                 60 63'電腦室:________'
     A*----------------------------------------------------------------
     A          R PE9                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
