     A*%%TS  SD  19930709  133030  D910091     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SAR059D                                    *
     A*    AUTHOR       : 0070   JUDY                                *
     A*    CREATE DATE  : 81/09/23                                   *
     A*    PROGRAM NAME : SAR059                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :產品客戶銷售（退）排行榜                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930709  133030  D910091     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SAR059'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 27'產品客戶銷售（退）排行榜      '
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 28'產品代號:'
     A                                      DSPATR(HI)
     A            DSD03S    R    9   B  4 40REFFLD(SD03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N60                                  DSPATR(HI)
     A            DSD03E    R    9   B  4 52REFFLD(SD03)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A                                  8 28'客戶　　:'
     A                                      DSPATR(HI)
     A            DSC04S    R        B  8 40REFFLD(SC04)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A            DSC04E    R        B  8 52REFFLD(SC04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(HI)
     A                                 14 28'確認日期:'
     A            DSC39S         6  0B 14 40
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      EDTWRD('  /  /  ')
     A            DSC39E         6  0B 14 52
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                      EDTWRD('  /  /  ')
     A                                 16 28'執行環境:'
     A            $EVR           1Y 0B 16 41VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 28'列印份數'
     A                                 17 39':'
     A            $CPY           3Y 0B 17 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 28'列表機'
     A                                      DSPATR(HI)
     A                                 18 39':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 41CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 29'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 14 63'(MM/DD/YY)'
     A                                  5 28'通路　　:'
     A                                      DSPATR(HI)
     A            DSC33S         1A  B  5 40
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A            DSC33E         1A  B  5 52
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                  6 28'型態　　:'
     A                                      DSPATR(HI)
     A            DSC34S         2A  B  6 40
     A N68                                  DSPATR(HI)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A            DSC34E         2A  B  6 52
     A N69                                  DSPATR(HI)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                  7 28'地區　　:'
     A                                      DSPATR(HI)
     A            DSC31S         2A  B  7 40
     A N70                                  DSPATR(HI)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A            DSC31E         2A  B  7 52
     A N71                                  DSPATR(HI)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                  9 28'前幾名內:'
     A            DSORT          4Y 0B  9 40TEXT('前幾名內')
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A                                      EDTCDE(Z)
     A                                      RANGE(0 9999)
     A                                 11 28'排序別  :'
     A            DSORT1         1A  B 11 40TEXT('排序別：')
     A                                      VALUES('A' 'B')
     A                                 11 44'A.依銷售公升數'
     A                                 12 44'B.依銷售金額'
     A                                 13 28'送貨日期:'
     A            DSC08S         6  0B 13 40
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                      EDTWRD('  /  /  ')
     A            DSC08E         6  0B 13 52
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                      EDTWRD('  /  /  ')
     A                                 13 63'(MM/DD/YY)'
     A                                  4 50'-'
     A                                  8 50'-'
     A                                  5 50'-'
     A                                  6 50'-'
     A                                  7 50'-'
     A                                 14 50'-'
     A                                 13 50'-'
