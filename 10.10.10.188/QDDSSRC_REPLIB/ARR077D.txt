     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : ARR077D                                    *
     A*    AUTHOR       : MERCURY                                    *
     A*    CREATE DATE  : 83/02/15                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR077                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :進銷存系統                                *
     A*    REMARK       :折讓單簽收明細表                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR077'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'折讓單簽收明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 25'公司別　:'
     A            DSG01          1A  B  5 37
     A                                      VALUES('W' 'R' ' ')
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                  5 42'(W=匯齡R=匯東" "=全部)'
     A                                  7 25'辦事處　:'
     A            COMP           1A  B  7 37
     A                                      VALUES(' ' '1' '2' '3' )
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                  7 42'(1=台北2=台中3=高雄 " "=+
     A                                      全部)'
     A                                  9 25'折讓日期:'
     A            DSG26S         6Y 0B  9 37EDTWRD('  /  /  ')
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  9 46'-'
     A            DSG26E         6Y 0B  9 48EDTWRD('  /  /  ')
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                 11 25'業務員:'
     A                                      DSPATR(HI)
     A            DSG11S    R        B 11 37REFFLD(SG11)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                 11 47'-'
     A            DSG11E    R        B 11 49REFFLD(SG11)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                 13 25'客戶代號:'
     A                                      DSPATR(HI)
     A            DSG04S    R        B 13 37REFFLD(SG04)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A N66                                  DSPATR(HI)
     A                                 13 47'-'
     A            DSG04E    R        B 13 49REFFLD(SG04)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(HI)
     A                                 16 25'執行環境:'
     A            $EVR           1Y 0B 16 37VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 42'(1-即時 2-批次)'
     A                                 17 25'列印份數:'
     A            $CPY           3Y 0B 17 37EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 25'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 37CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備　８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A                                 24 62MSGCON(018 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
