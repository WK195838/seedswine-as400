     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : MTR044P                                    *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : 81/01/24                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTR044                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :客戶明細表                                *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    37
     A                                      MSGCON(030 URE9999 REMF)
     A                                      SPACEA(001)
     A                                    43
     A                                      DFT('客戶信用明細表')
     A                                      SPACEA(001)
     A                                     1'通　路　:'
     A            DMD07S    R             +1REFFLD(MD07)
     A                                    +6'-'
     A            DMD07E    R             +1REFFLD(MD07)
     A                                      SPACEA(001)
     A                                     1'地　區　:'
     A            DME07S    R             +1REFFLD(ME07)
     A                                    +5'-'
     A            DME07E    R             +1REFFLD(ME07)
     A                                      SPACEA(001)
     A                                     1'組　別　:'
     A            DMC03S    R             +1REFFLD(MC03)
     A                                    +5'-'
     A            DMC03E    R             +1REFFLD(MC03)
     A                                    73
     A                                      DFT('日期:')
     A            $EGMDY         6  0O    80
     A                                      EDTCDE(Y)
     A                                    90
     A                                      DFT('頁次:')
     A                                    97
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'業務員　:'
     A            DME15S    R             +1REFFLD(ME15)
     A                                    +1'-'
     A            DME15E    R             +1REFFLD(ME15)
     A                                    73
     A                                      DFT('時間:')
     A                                    80
     A                                      TIME
     A                                    90
     A                                      DFT('<MTR044>')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('客戶代號:')
     A            DMD01S    R        O    +1
     A                                      REFFLD(MD01)
     A                                    +2
     A                                      DFT('-')
     A            DMD01E    R        O    +1
     A                                      REFFLD(MD01)
     A                                      SPACEA(001)
     A                                     1'往來狀態:'
     A            STATUS         8   O    +1
     A                                    73' USER :'
     A            $USER         10O  O    80
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================')
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('客戶代號／簡稱')
     A                                      SPACEB(001)
     A                                    28
     A                                      DFT('信用額度（仟）')
     A                                    44
     A                                      DFT('信用期限')
     A                                    54
     A                                      DFT('期限天數')
     A                                    64
     A                                      DFT('結算日期')
     A                                    74
     A                                      DFT('收款日')
     A                                    82
     A                                      DFT('業務人員')
     A                                   101
     A                                      DFT('修改額度（仟）')
     A                                     1
     A                                      DFT('-------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------')
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A            MD01      R        O     1
     A            ME04      R        O     7
     A            PMD22          7  0O    28
     A                                      EDTCDE(J)
     A            MD23      R        O    45
     A            PMD23          6   O    47
     A            MD25      R        O    58
     A                                      EDTCDE(Z)
     A            MD24      R        O    68
     A                                      EDTCDE(Z)
     A            MD26      R        O    76
     A                                      EDTCDE(Z)
     A            ME15      R        O    83
     A            MC02      R        O    89
     A                                   101
     A                                      DFT('_______________')
     A*----------------------------------------------------------------
     A          R PE1
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================')
     A                                     2
     A                                      DFT('***續下頁***')
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================')
     A                                     2
     A                                      DFT('<<MRT044>>--->報表結束')
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9                       SPACEB(1)
     A                                     3'もも此範圍無資料もも'
