     A*%%TS  SD  20021220  172743  REPGMR      REL-V4R4M0  5769-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARI0231D                                   *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/03/10                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARI0231                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款系統                              *
     A*    REMARK       :應收帳款查詢－帳款明細查詢                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.10.22 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M004      | 00.10.16 |顯示正負數
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920805  114048  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 12  2VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SI21      R        H
M001MA*           DSI21     R        O 12  5REFFLD(SI21)
M001MA            DSI21          6  0O 12  5
     A                                      EDTWRD('  /  /  ')
     A            SI10      R        O 12 27
M004MA*           SI25      R        O 12 41EDTCDE(1)
M004MA*           SI30      R        O 12 54EDTCDE(1)
M004MA            SI25      R        O 12 41EDTCDE(J)
M004MA            SI30      R        O 12 54EDTCDE(J)
M001MA*           DSI29     R        O 12 67REFFLD(SI29)
M001MA            DSI29          6  0O 12 67
     A                                      EDTWRD('  /  /  ')
     A            SI01      R        H
     A            SI02      R        O 12 15
     A            SI18      R        O 12 79
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920805  114048  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'ARI023'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 32'應收帳款明細查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  3'客戶代號:'
     A            DSI12     R        O  5 16REFFLD(SI12)
     A            ME04      R        O  5 22
     A                                  6  3'應收帳款:'
     A            DARTO          9Y 0O  6 16EDTCDE(1)
     A                                  8  5'5=發票明細查詢'
     A                                 10  2'A 發票日期發票號碼  訂單號碼-
     A                                             發票金額   未收餘額應+
     A                                      收日期 S'
     A                                      DSPATR(UL)
     A                                  9  4'(MM/DD/YY)'
     A                                  9 66'(MM/DD/YY)'
     A            DBGN           6Y 0B 11  5EDTCDE(Y)
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPC2
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A*%%TS  SD  20021220  172743  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SJ03      R        O 12  2
     A            D#A            5Y 0O 12 13EDTCDE(J)
     A            SJ13           4Y 0O 12 21EDTCDE(1)
     A            DATA1          9Y 2O 12 27EDTCDE(3)
     A            DATA2          9Y 0O 12 38EDTCDE(M)
     A            SJ14           5Y 0O 12 51EDTCDE(3)
     A            SJ15           4Y 0O 12 61EDTCDE(1)
     A            SJ09           9Y 0O 12 67EDTCDE(1)
     A*------------------------------------------------------------
     A*
     A*                                     CSRLOC(D#ROW      D#COL)
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19920716  100455  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0008)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'ARI023'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR004'
     A                                  2 32'發票明細查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司代號:'
     A            SI01      R        O  4 13
     A            #B03      R        O  4 16
     A                                  5  1'地區代號:'
     A            ME07      R        O  5 13
     A            #D02      R        O  5 16
     A                                  6  1'客戶代號:'
     A            SI08      R        O  6 13
     A                                  6 19'-'
     A            SI09      R        O  6 21
     A            ME04      R        O  6 26
     A                                  7  1'訂單號碼:'
     A            SI10      R        O  7 13
     A                                  8  1'發票號碼:'
     A            SI02      R        O  8 13
     A                                  9  1'出貨日期:'
M001MA*           DSI06     R        O  9 13REFFLD(SI06)
M001MA            DSI06          6  0O  9 13
     A                                      EDTWRD('  /  /  ')
     A                                  9 24'(MM/DD/YY)'
     A                                 11  1'產品代號   數量 數量  售價-
     A                                        發票金額訂購數量搭贈數量  -
     A                                      銷退金額'
     A                                      DSPATR(UL)
     A                                 10  1'             訂購 搭贈        -
     A                                                      銷退    銷退   -
     A                                               '
