     A*%%TS  SD  20090422  110658  CWDEV3      REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIS011D                                    *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 98/03/24                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :茂世進銷存                                *
     A*    SUBSYSTEM    :                                          *
     A*    REMARK       :採購訂單查詢                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '5')
     A            SBC01H    R        H      REFFLD(BC01 W2BCPF)
     A            SBC01          6Y 0O  9  6EDTWRD('  /  /  ')
     A            SBC03     R        O    +1REFFLD(BC03 W2BCPF)
     A            STATUS         8A  O    +1
     A            SBC02H    R        H      REFFLD(BC02 W2BCPF)
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'KIS011'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'採購訂單查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'專櫃代號:'
     A            DBC02     R        B    +1REFFLD(BC02 W2BCPF)
     A  62                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A03     R        O    +1REFFLD(#A03 PA#APF)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'5=查詢'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN1          6Y 0B  8  6EDTWRD('  /  /  ')
     A            DBGN2     R        B  8 15REFFLD(BE03 W2BEPF)
     A                                  6  3'(月/日/年)'
     A                                  7  3'A 訂單日期  訂單單號  狀態 '
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A*%%TS  SD  20090422  110658  CWDEV3      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SBD02     R        O  9  2REFFLD(BD02 W2BDPF)
     A            SMA03     R        O  9 12REFFLD(MA03 MTMAPF)
     A            SBD04     R        O  9 32REFFLD(BD04 W2BDPF)
     A                                      EDTCDE(3)
     A            SBD05     R        O  9 42REFFLD(BD05 W2BDPF)
     A                                      EDTCDE(3)
     A            SBF03     R        O  9 52REFFLD(BF03 W2BFPF)
     A                                      EDTCDE(3)
     A            SBD07     R        O  9 62REFFLD(BD07 W2BDPF)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'KIS011'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'採購訂單查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'專櫃代號:'
     A            DBC022    R        O  5 13REFFLD(BC02 W2BCPF)
     A            D#A032    R        O  5 19REFFLD(#A03 PA#APF)
     A                                  5 52'訂單日期:'
     A            DBC012         6Y 0O  5 64EDTWRD('  /  /  ')
     A                                  6  1'訂單號碼:'
     A            DBC03     R        O  6 13REFFLD(BC03 W2BCPF)
     A                                  8  2'產品代號                    訂-
     A                                      貨量  確認量  驗收量  -
     A                                      採購單號   '
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
