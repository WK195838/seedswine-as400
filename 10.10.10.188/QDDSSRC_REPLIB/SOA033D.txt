     A*%%TS  SD  19920610  092449  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : SOA033D                                    *
     A*    AUTHOR       : MICHELLE                                   *
     A*    CREATE DATE  : 90/06/11                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOA033                                     *
     A*    SYSTEM       :人頭馬寰盛                                *
     A*    SUBSYSTEM    :銷售分析子系統                            *
     A*    REMARK       :業績目標輸入維護 (A/U/D/I)                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*           |          |
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B 10  2VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DBB02          4  0O 10  6EDTWRD('  /  ')
     A            BB05           6   O    +5
     A            HBB02          6  0H
     A            DMC02         12O  O    +5
     A            BB03           2   O    +5
     A            BB04           2   O    +5
     A            BB10           2   O    +5
     A            BB06           2   O    +5
     A            BB07           9  2O    +5
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'SOA033'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'銷售目標輸入維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  5'2=修改'
     A                                  5 15'4=刪除'
     A                                  5 25'5=查詢'
     A                                  7  6'目標年月'
     A                                  8  2'A  (YY/MM)   業務員代號+
     A                                         姓　　名　大區域 通路 品牌+
     A                                        種類  箱數'
     A                                      DSPATR(UL)
     A            DBGN2          4  0B  9  6EDTWRD('  /  ')
     A            DBGN5          6   B    +5
     A            DBGN3          2   B    +5
     A            DBGN4          2   B    +5
     A            DBG10          2   B    +5
     A            DBGN6          2   B    +5
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A  96        ERR1          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          60A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          60A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          60A  O 24  2MSGID(URE 0038 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         60A  O 24  2MSGID(URE 0039 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  1  2'SOA033'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'銷售目標輸入維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'目標年月  :'
     A            DBB02          4Y 0B  4 16
     A                                      EDTWRD('  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  4 22'(西元年月YY/MM )'
     A                                  6  2'業務員代號:'
     A            DBB05          6   B    +2
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DMC02     R             +2REFFLD(MC02)
     A                                  8  2'大區域別  :'
     A            DBB03          2   B  8 16REFFLD(AA03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            D#L02         22O       +2
     A                                 10  2'通路      :'
     A            DBB04          2   B 10 16
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DBM02         10O       +2
     A                                 12  2'品牌      :'
     A            DBB10     R        B 12 14REFFLD(MA10)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A03     R             +2REFFLD(#A03)
     A                                 14  2'迄止號碼:'
     A            DAA06     R     Y  B 14 14REFFLD(AA06)
     A                                      DSPATR(MDT)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16  2'使用號碼:'
     A            AA07      R     Y  O 16 14
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          60A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          60A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
