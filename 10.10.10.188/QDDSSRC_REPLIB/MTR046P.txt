     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : MTR046P                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/01/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POR046                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :主檔                                      *
     A*    REMARK       :禮盒結構資料清冊                          *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    26MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    34'禮盒結構表'
     A                                      SPACEA(1)
     A                                    50'日期:'
     A            $EGMDY         6  0O    57EDTCDE(Y)
     A                                    67'頁次:'
     A                                    74PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    50'時間:'
     A                                    57TIME
     A                                    68'<POR046>'
     A                                      SPACEA(1)
     A                                     1'禮盒代號'
     A                                    11':'
     A            DMF01S    R             +1REFFLD(MF0/MF01 *LIBL/MTMFPF)
     A                                    23'∼'
     A            DMF01E    R             +0REFFLD(MF0/MF01 *LIBL/MTMFPF)
     A                                    50' USER :'
     A            $USER         10A  O    57
     A                                      SPACEA(1)
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ==================')
     A                                      SPACEA(1)
     A                                     1'禮盒代號／簡稱'
     A                                    29'產品代號／簡稱'
     A                                    59'單位'
     A                                    65'數量'
     A                                    73'附屬品'
     A                                      SPACEA(1)
     A                                     1
     A                                      DFT('-------------------------------
     A                                      ------------------------------------
     A                                      ------------------')
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A  30        MF01      R              2
     A  30        PMA031    R             12REFFLD(MA0/MA03 *LIBL/MTMAPF)
     A            MF02      R             30
     A            PMA032    R             40REFFLD(MA0/MA03 *LIBL/MTMAPF)
     A            MF03      R             59
     A            MF04      R             67EDTCDE(J)
     A            MF07      R             76
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ==================')
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ==================')
     A                                      SPACEA(1)
     A                                     2'<<MTR046>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE9                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
