     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIP008D                                    *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 98/04/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :茂世進銷存                                *
     A*    SUBSYSTEM    :調撥                                      *
     A*    REMARK       :專櫃調撥處理(前台)                      *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '5' '6')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SAF01          6Y 0O    +2EDTWRD('  /  /  ')
     A            SAF02     R        O    +1REFFLD(AF02 W1AFPF)
     A            SAF03     R        O    +1REFFLD(AF03 W1AFPF)
     A            S#A03     R  -19   O    +1REFFLD(#A03 PA#APF)
     A            SAF04     R        O    +1REFFLD(AF04 W1AFPF)
     A            S#A04     R  -19   O    +1REFFLD(#A03 PA#APF)
     A            SAF05          8A  O    +1
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A  29                                  CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(MSG1006 *LIBL/R1MF)
     A                                  1  2'KIP008'
     A                                  1 26MSGCON(030 MSG9999 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'專櫃調撥處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'專櫃代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 13REFFLD(AF04 W1AFPF)
     A                                      COLOR(BLU)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            D#A03     R        O    +1REFFLD(#A03 PA#APF)
     A                                      COLOR(BLU)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                    +1'5=查詢'
     A                                      COLOR(BLU)
     A  29                                +1'6=確認'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN3          6Y 0B  8  6EDTWRD('  /  /  ')
     A                                  6  5'調撥日期'
     A                                      COLOR(BLU)
     A                                  7  3'A  MM/DD/YY 單據號碼  -
     A                                      出貨物流點        進貨物流點-
     A                                              狀態 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  29                             22 25'F6 =新增調撥單'
     A                                      COLOR(BLU)
     A  97        @MSG          60A  O 23  2DSPATR(HI)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 MSG9996 *LIBL/R1MF)
     A                                      DSPATR(HI)
      *MSG1003--->查詢成功
      *MSG1007--->調撥成功
     A  91        ERR1          60A  O 23  2MSGID(MSG1003 R1MF)
     A                                      DSPATR(HI)
     A  92        ERR2          60A  O 23  2MSGID(MSG1007 R1MF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SAG02     R        B  9  3REFFLD(AG02 W1AGPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32N61                               DSPATR(UL HI)
     A N32                                  DSPATR(PR)
     A            SMA03     R        O    +1REFFLD(MA03 MTMAPF)
     A            SMA13     R        O    +1REFFLD(MA13 MTMAPF)
     A            SAG03     R        B    +1REFFLD(AG03 W1AGPF)
     A                                      EDTCDE(4)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            SAG04          5  0B    +5
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            SIA10     R        H      REFFLD(IA10 IMIAPF)
     A*                                     EDTCDE(3)
     A            SAG05     R        H      REFFLD(AG05 W1AGPF)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CA03(03)
     A  32                                  CF04(04)
     A  34                                  CF05(05)
     A  34                                  CF09(09)
     A                                      CA12(12)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(MSG1006 *LIBL/R1MF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'KIP008'
     A                                  1 26MSGCON(030 MSG9999 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'專櫃調撥處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'進貨專櫃  :'
     A                                      COLOR(BLU)
     A            DAF04     R        B    +1REFFLD(AF04 W1AFPF)
     A                                      COLOR(BLU)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            D#A03A    R        O    +1REFFLD(#A03 PA#APF)
     A                                      COLOR(BLU)
     A                                  5  1'出貨物流點:'
     A                                      COLOR(BLU)
     A            DAF03     R        B    +1REFFLD(AF03 W1AFPF)
     A                                      COLOR(BLU)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            D#A031    R        O    +1REFFLD(#A03 PA#APF)
     A                                      COLOR(BLU)
     A                                  6  1'調撥日期  :'
     A                                      COLOR(BLU)
     A            DAF01          6Y 0B    +1EDTWRD('  /  /  ')
     A                                      COLOR(BLU)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                    +1'單據號碼:'
     A                                      COLOR(BLU)
     A            DAG01     R        O    +1REFFLD(AG01 W1AGPF)
     A                                      COLOR(BLU)
     A                                  8  2'產品代號／名稱              單-
     A                                      位調撥數量確認數量 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  34                                +2'F5 =參考資料'
     A                                      COLOR(BLU)
     A  34                                +2'F9 =確認'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 MSG9996 *LIBL/R1MF)
     A                                      DSPATR(HI)
