     A*%%TS  SD  19920609  104952  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : IMA010D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/04/14                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMA010                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :禮盒組拆作業處理 (A/U/D/I)                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.16 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR                     SFL
     A*%%TS  SD  19920609  100711  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  2VALUES(' ' '2' '4' '5' '6')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
M001MA            DIM10          6Y 0O  9  4
     A                                      EDTWRD('  /  /  ')
     A            IM10      R        H
     A            IM02      R        O  9 13
     A            TYPE           6A  O  9 26
     A            IM05      R        O  9 33
     A            MA03      R        O  9 43
     A            IM03      R        O  9 62
     A            IM06      R        O  9 68EDTCDE(1)
     A            IM07      R        O  9 77
     A            IM09      R        H
     A*------------------------------------------------------------
     A*
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920609  104952  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'IMA010'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'禮盒組拆作業處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  5'2=修改'
     A                                  5 15'4=刪除'
     A                                  5 25'5=查詢'
     A                                  5 35'6=確認'
     A                                  7  2'A MM/DD/YY單據號碼　　　型態　禮+
     A                                      盒代號 禮盒簡稱　　　　　物流點+
     A                                      　數量　確認'
     A                                      DSPATR(UL)
M001MA            DBGN1          6Y 0B  8  4
     A                                      EDTWRD('  /  /  ')
     A            DBGN2     R        B  8 13REFFLD(IM02)
     A                                  6  3'組拆日期'
     A*------------------------------------------------------------
     A*
     A          R DSPC1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A*
     A  70                             24  2'單據號碼'
     A                                      DSPATR(HI)
     A  70        DIM02     R             +1REFFLD(IM02)
     A                                      DSPATR(RI)
     A  70                                +1'新增成功'
     A                                      DSPATR(HI)
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0018 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0063 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0064 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CF03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A            HIM05     R        H      REFFLD(IM05)
     A            HIM03     R        H      REFFLD(IM03)
     A            HIM06     R        H      REFFLD(IM06)
     A                                  1  2'IMA010'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'禮盒組拆作業處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A            DIM01     R        B  5 15REFFLD(IM01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            #B03      R        O  5 18
     A                                  7  2'單據號碼:'
     A            DIM02     R        O  7 15REFFLD(IM02)
     A                                  7 37'型態:'
     A            DIM04     R        B  7 45REFFLD(IM04)
     A                                      VALUES('0' '1')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  7 48'(0:組裝, 1:拆解)'
     A                                  9  2'組拆日期:'
M001MA            DIM10          6Y 0B  9 15
     A                                      EDTCDE(Y)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 24'(MM/DD/YY)'
     A                                 11  2'禮盒代號:'
     A                                      DSPATR(HI)
     A            DIM05     R        B 11 15REFFLD(IM05)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 33'禮盒簡稱:'
     A            MA03      R        O 11 45
     A                                 13  2'物流點　:'
     A                                      DSPATR(HI)
     A            DIM03     R        B 13 15REFFLD(IM03)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A03         22A  O 13 21
     A                                 15  2'數量　　:'
     A            DIM06     R        B 15 15REFFLD(IM06)
     A                                      EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  2'確認日期:'
M001MA            DIM08          6Y 0O 17 15
     A                                      EDTWRD('  /  /  ')
     A                                 17 24'(MM/DD/YY)'
     A                                 22  1'執行'
     A                                 22  9'F3 =回主畫面'
     A                                 22 25'F4 =視窗查詢'
     A                                 22 41'F5 =參考資料'
     A                                 22 57'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0016 REMF)
     A                                      DSPATR(HI)
     A  86        ERR10         63A  O 24  2MSGID(URE 0064 REMF)
     A                                      DSPATR(HI)
     A  85        ERR11         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  84        ERR12         63A  O 24  2MSGID(URE 0015 REMF)
     A                                      DSPATR(HI)
     A  83        ERR13         63A  O 24  2MSGID(URE 0085 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
