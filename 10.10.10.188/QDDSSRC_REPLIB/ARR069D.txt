     A*%%TS  SD  19931104  113801  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*    DSPF NAME    : ARR069D                                    *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 82/04/22                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR069                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款子系統                            *
     A*    REMARK       :產品銷貨成本明細表                        *
     /*=============================================================*/
     /* CODE   AUTHOR.     DATE    DESCRIPTION                   */
     /*-------------------------------------------------------------*/
     /* M001  MICHELLE   98.12.23  Y2K MODIFY                    */
     /*=============================================================*/
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19931104  113801  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR069'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'產品銷貨成本明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 26'公司別　:'
     A            DSI01          1A  B  5 38VALUES('R' 'W')
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  5 40'(W=匯齡R=匯東)'
     A                                  6 26'辦事處　:'
     A            COMP           1A  B  6 38DSPATR(HI)
     A                                      VALUES(' ' '1' '2' '3')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                  6 42'(1=台北2=台中3=高雄 " " =+
     A                                      全部)'
     A                                  8 26'轉　　帳:'
     A            DSI10          1A  B  8 38VALUES('1' '2' '3')
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  8 42'(1=非轉帳2=轉帳3=全部)'
     A                                 10 26'確認日期:'
M001MA*           DSI35S    R        B 10 38REFFLD(SI21)
M001MA            DSI35S         6  0B 10 38
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 10 47'-'
M001MA*           DSI35E    R        B 10 49REFFLD(SI21)
M001MA            DSI35E         6  0B 10 49
     A                                      EDTWRD('  /  /  ')
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 10 58'(MM/DD/YY)'
     A                                 12 26'選　　項:'
     A            DSEL           1A  B 12 38VALUES('A' 'B' 'C')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 12 40'(A=一般B=僅含專櫃C=寄賣)'
     A                                 14 26'分析方式:'
     A            DSEL1          1A  B 14 38
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                      VALUES('A' 'B')
     A                                 14 40'(A=禮盒不拆B=禮盒拆單瓶)'
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備１３２格報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
