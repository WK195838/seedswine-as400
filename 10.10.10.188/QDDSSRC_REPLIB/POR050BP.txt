     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : POR050B                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/01/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POR050                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :驗收單明細表                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.12.17 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    63MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    70'驗收單明細表'
     A                                      SPACEA(2)
     A                                     1'公司別　　:'
     A            DPI01     R        O    +1REFFLD(PI01)
     A            #B03      R        O    +2
     A                                      SPACEA(1)
     A                                     1'單別　　　:'
     A            DDOC           2A  O    +1
     A            #203      R        O    +2
     A                                   123'日期:'
     A            $EGMDY         6  0O   131EDTCDE(Y)
     A                                   140'頁次:'
     A                                   148PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'供應商代號:'
     A            DPH06S         5   O    +1
     A                                    +2'-'
     A            DPH06E         5   O    +1
     A                                   123'時間:'
     A                                   131TIME
     A                                      SPACEA(1)
     A                                     1'驗收日期　:'
     A            DPI05S         6  0O    +1EDTCDE(Y)
     A                                    +2'-'
     A            DPI05E         6  0O    +1EDTCDE(Y)
     A                                   123' USER :'
     A            $USER         10   O   131
     A                                   142'<POR050B>'
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================')
     A                                      SPACEB(1)
     A                                     1'驗收日期'
     A                                      SPACEB(1)
     A                                    11'驗收單號'
     A                                    24'採購日期'
     A                                    34'採購單號'
     A                                    47'產品代號'
     A                                    58'產品簡稱'
     A                                    74'單位'
     A                                    83'數量'
     A                                    91'採購原幣金額'
     A                                   111'F.O.B.'
     A                                   121'FREIGHT'
     A                                   133'HANDLE'
     A                                   147'金額'
     A                                     1
     A                                      DFT('-------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------')
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEB(1)
M001MA* 30        PI05      R              2EDTWRD('  /  /  ')
M001MA  30        LPI05          6  0      2EDTWRD('  /  /  ')
     A  31        PI02      R             12
M001MA* 32        PB04      R             25EDTWRD('  /  /  ')
M001MA  32        LPB04          6  0     25EDTWRD('  /  /  ')
     A  32        PI03      R             35
     A            PI04      R             48
     A            MA06      R             59
     A            MA14      R             75
     A            PI06      R             82EDTCDE(J)
     A            DPC76          8  0O    94EDTCDE(J)
     A            PI07           8  0O   107EDTCDE(J)
     A            PI08           6  0O   121EDTCDE(J)
     A            PI09           6  0O   135EDTCDE(J)
     A            PI10           8  0O   146EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PH2                       SPACEB(1)
     A                                    47'驗收單號小計---------------------
     A                                      -------->'
     A*----------------------------------------------------------------
     A          R PD2                       SPACEA(1)
     A            DPI07          9  0O   107EDTCDE(J)
     A            DPI08          9  0O   121EDTCDE(J)
     A            DPI09          8  0O   135EDTCDE(J)
     A            DPI10         10  0O   146EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PH3
     A                                    47'驗收日期合計---------------------
     A                                      -------->'
     A*----------------------------------------------------------------
     A          R PD3                       SPACEA(1)
     A            DAT07          9  0O   107EDTCDE(J)
     A            DAT08          9  0O   121EDTCDE(J)
     A            DAT09          8  0O   135EDTCDE(J)
     A            DAT10         10  0O   146EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PH4
     A                                    47'總　　　　計---------------------
     A                                      -------->'
     A*----------------------------------------------------------------
     A          R PD4                       SPACEA(1)
     A            TOTA7          9  0O   107EDTCDE(J)
     A            TOTA8          9  0O   121EDTCDE(J)
     A            TOTA9          8  0O   135EDTCDE(J)
     A            TOTAL         10  0O   146EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================')
     A                                      SPACEB(1)
     A                                     2'***續下頁***'
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================')
     A                                      SPACEB(1)
     A                                     2'<<POR050B>>--->報表結束'
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================')
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE9                       SPACEB(1)
     A                                     3'もも此範圍無資料もも'
