     A*%%TS  SD  19920805  105527  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : POI021D                                    *
     A*    AUTHOR       : A1034  STEPHAIE                            *
     A*    CREATE DATE  : 81/01/14                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POI021                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購子系統                                *
     A*    REMARK       :採購狀況（依產品）                        *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR                     SFL
     A*%%TS  SD  19920805  105527  REPGMRP     REL-V2R1M0  5738-PW1
     A            RRN            4S 0H
     A            PC03      R        O  8  2
     A            MA06      R        O  8 12
     A            PC02      R        O  8 28
     A            PC06      R        O  8 41EDTCDE(1)
     A            PC11      R        O  8 49EDTCDE(1)
     A            DNET      R        O  8 57REFFLD(PC06)
     A                                      EDTCDE(1)
M001DA*           DPB14     R        O  8 65REFFLD(PB14)
M001AA            DPB14          6  0O  8 65
     A                                      EDTCDE(Y)
     A            PC05      R        O  8 74
     A            MA03      R        O  9 11
     A            PC10      R        O  9 36REFFLD(PC0/PC10 *LIBL/POPCPF)
     A                                      EDTCDE(Z)
     A                                  9 40'/'
     A            PC09      R        O  9 42
M001DA*           DPB15     R        O  9 65REFFLD(PB15)
M001AA            DPB15          6  0O  9 65
     A                                      EDTCDE(Y)
     A*--------------------------------------------------------------*
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920608  175449  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      SFLDROP(CF01)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'POI021'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'採購狀況（依產品）'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6  2'產品代號／名稱         採購單+
     A                                      號    採購量　已收量　未收量ETD-
     A                                      /ETA供應商'
     A                                      DSPATR(UL)
     A            DBGN1     R        B  7  2REFFLD(PC03)
     A*--------------------------------------------------------------*
     A          R DSPC1
     A                                 22  2'執行'
     A                                    +2'F1 =明細顯示'
     A                                    +2'F3 =回主畫面'
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
