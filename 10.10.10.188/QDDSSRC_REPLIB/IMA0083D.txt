     A*%%TS  SD  19920902  182708  REPGMRP     REL-V2R1M0  5738-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : IMA0083D                                   *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/02/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMA008                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :送貨分車資料輸入－指派取消                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.16 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920902  182708  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  2VALUES(' ' '3' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
M001MA            DSE06          6Y 0O 10  4
     A                                      EDTWRD('  /  /  ')
     A            SE34      R        O 10 15
     A            SE35      R        O 10 21
     A            SE02      R        O 10 57
     A            SE03      R        O 10 71
     A            SE10      R        O 10 27
     A            SE11      R        O 10 33
     A            ME04      R        O 10 38
     A*------------------------------------------------------------
     A*
     A*                                     CSRLOC(D#ROW      D#COL)
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920902  182708  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'IMA008'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR004'
     A                                  2 31'送貨分車資料輸入'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3  2'指派取消'
     A                                      DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'物流點:'
     A            IK02      R        O  4 12
     A            D#A03         22A  O  4 18
     A                                  4 41'分車日期　:'
M001MA            DIK03          6Y 0O  4 55
     A                                      EDTWRD('  /  /  ')
     A                                  4 65'(MM/DD/YY)'
     A                                  5  2'車號　:'
     A            IK04      R        O  5 12
     A                                  5 41'送貨單張數:'
     A            IK06      R        O  5 55
     A                                  6 41'發票張數  :'
     A            IK07      R        O  6 55
     A                                  7  2'3=送貨指派取消'
     A                                  7 21'5=送貨明細查詢'
     A                                  8  3'送貨日期'
     A                                  9  2'A MM/DD/YY 地區　區域 送貨客戶-
     A                                       客戶簡稱         送貨單號   +
     A                                      單別'
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
