     A*%%TS  SD  19930327  100807  D910074     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARI028D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 07/06/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARI028                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :票據未兌現查詢                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            AC02      R        O  7  2
     A            AC03      R        O  7  8
     A            ME04      R        O  7 13
     A            WAC04          6  0O  7 32EDTWRD('  /  /  ')
     A            AC06      R        O  7 41
     A            AC01      R        O  7 56
     A            AC08      R        O  7 59EDTCDE(J)
     A            WAC07          6  0O  7 72EDTWRD('  /  /  ')
     A*------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920728  105208  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'ARI028'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'票據未兌現查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'客戶代號／簡稱             +
     A                                      收款日期　　票據號碼 +
     A                                      公司　　金額　　預兌日期 '
     A                                      DSPATR(UL)
     A            DBGN1     R        B  6  2REFFLD(AC02)
     A            DBGN2     R        B  6  8REFFLD(AC03)
M001DA*           DBGN3     R        B  6 32REFFLD(AC04)
M001AA            DBGN3          6  0B  6 32
     A                                      EDTWRD('  /  /  ')
     A*------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
