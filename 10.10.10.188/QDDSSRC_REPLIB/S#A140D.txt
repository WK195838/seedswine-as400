     A*  90/07/03  15:20:19    A1062       REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : S#A140D                                    *
     A*    AUTHOR       : A1062   JEANNY LIN                         *
     A*    CREATE DATE  : 79/07/03                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : S#A140                                     *
     A*    SYSTEM       :程式安全管制系統                          *
     A*    PURPOSE      :程式基本資料維護                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A*  90/07/03  15:20:19    A1062       REL-R02M00  5728-PW1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  3'<S#A140D>'
     A                                  1 29'程式安全管制系統'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            DUSER     R        O  2  3REFFLD(SU02)
     A                                  2 29'程式基本資料維護'
     A                                  4  2'程式編號:'
     A            DSP01     R        B  4 14REFFLD(SP01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  6 27'作業選擇:'
     A            DOPT           1A  B  6 39VALUES('1' '2' '4' '5')
     A                                  6 43'1.新增'
     A                                  7 43'2.修改'
     A                                  8 43'4.刪除'
     A                                  9 43'5.查詢'
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22  2MSGID(US# 0010 S#MF)
     A                                      DSPATR(HI)
     A                                 23  3'PF3 =回主畫面'
     A                                 23 20'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A          R DSPC2
     A*  90/07/03  15:20:19    A1062       REL-R02M00  5728-PW1
     A                                      CA03(03)
     A                                      CA12(12)
     A                                  1  3'<S#A140D>'
     A                                  1 29'程式安全管制系統'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            DUSER     R        O  2  3REFFLD(SU02)
     A                                  2 29'程式基本資料維護'
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4  2'程式編號:'
     A            DSP01     R        O  4 14REFFLD(SP01)
     A                                  6  2'程式名稱:'
     A            DSP02     R        B  6 14REFFLD(SP02)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A  60                                  DSPATR(PC)
     A N31                                  DSPATR(PR)
     A                                  7  2'程式類別:'
     A            DSP03     R        B  7 14REFFLD(SP03)
     A                                      VALUES('M' 'P' 'S')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7 19'(M :主畫面  P :主程式  S :-
     A                                      副程式)'
     A                                  8  2'備註欄１:'
     A            DSP04     R        B  8 14REFFLD(SP04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  2'備註欄２:'
     A            DSP05     R        B  9 14REFFLD(SP05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'備註欄３:'
     A            DSP06     R        B 10 14REFFLD(SP06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A*
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 24 67'【泛太資訊】'
