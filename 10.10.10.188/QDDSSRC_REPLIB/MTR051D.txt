     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : MTR051D                                    *
     A*    AUTHOR       : 0070 JUDY                                  *
     A*    CREATE DATE  : 81/09/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTR051                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :產品簡明表                                *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MTR051'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 35'產品簡明表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'產品歸屬:'
     A                                  6 49'-'
     A            DMA07S    R    1   B  6 39REFFLD(MA0/MA07 *LIBL/MTMAPF)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A            DMA07E    R    1   B  6 52REFFLD(MA0/MA07 *LIBL/MTMAPF)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                  8 26'產品代號:'
     A                                      DSPATR(HI)
     A                                  8 49'-'
     A            DMA01S    R        B  8 39REFFLD(MA0/MA01 *LIBL/MTMAPF)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A            DMA01E    R        B  8 52REFFLD(MA0/MA01 *LIBL/MTMAPF)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                 10 26'使用碼  :'
     A                                 10 41'( A-使用 I-不使用 " "-全部)'
     A            DMA58          1   B 10 39VALUES('I' 'A' ' ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備８０格報表紙*  **'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
