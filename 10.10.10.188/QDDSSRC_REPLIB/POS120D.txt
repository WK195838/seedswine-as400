     A*  90/11/03  09:49:24    SSPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : POS120D                                    *
     A*    AUTHOR       : A1034  STEPHANIE                           *
     A*    CREATE DATE  : 79/07/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POS120                                     *
     A*    SYSTEM       :銷售系統                                  *
     A*    SUBSYSTEM    :車輛採購                                  *
     A*    REMARK       :車型基本資料全頁查詢                      *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PORF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A*  90/11/03  09:49:24    SSPGMR      REL-R02M00  5728-PW1
     A                                  1  2'<POS120D>'
     A            CONAME        40A  O  1 20MSGID(US#9001 S#MF)
     A                                  1 64'日期:'
     A            $CHYMD         6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 27'車型基本資料全頁查詢'
     A                                  4  2'NO 車型編號'
     A                                  4 21'車型名稱'
     A                                  4 60'供應廠商'
     A                                  5  2'-- --------------- ----------------
     A                                      ----------------------- ------------
     A                                      --------'
     A          R SFLSR                     SFL
     A*  90/11/03  09:49:24    SSPGMR      REL-R02M00  5728-PW1
     A            RRN            2Y 0O  6  2EDTCDE(4)
     A            ZC01      R        O  6  5
     A            DZC02         38   O  6 21
     A            ZC04      R        O  6 60
     A            QB02      R        O  6 69REFFLD(QB02 *LIBL/BARF)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  90/11/03  09:49:24    SSPGMR      REL-R02M00  5728-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF02(02)
     A                                      ROLLUP(25)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A                                 21  1'車型編號:'
     A            DZC01     R        B 21 13REFFLD(ZC01)
     A  32                             21 43'明細序號:'
     A  32        DOP1      R        B 21 55REFFLD(RRN *SRC)
     A                                      EDTCDE(4)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                 21 61'挑選序號:'
     A            DOP2      R        B 21 73REFFLD(RRN *SRC)
     A                                      EDTCDE(4)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  96        ERR1          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF2 =挑選序號'
     A                                 23 20'PF3 =回主畫面'
     A                                 23 38'PF12=回前畫面'
     A                                 23 56'PA1 =下一頁'
     A                                 24  2'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
