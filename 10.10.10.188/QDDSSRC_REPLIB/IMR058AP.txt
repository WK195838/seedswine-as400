     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    27
     A                                      MSGCON(030 URE9999 REMF)
     A                                      SPACEA(001)
     A                                    31
     A                                      DFT('倉庫預訂送貨明細表      ')
     A                                      SPACEA(001)
     A                                    35
     A                                      DFT('依產品彙總')
     A                                      SPACEA(002)
     A                                     1
     A                                      DFT('通　　路:')
     A            DSEL1          1   O    +1
     A  20                                15'不含酒店'
     A  21                                15'只含酒店'
     A  22                                15'全部通路'
     A
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('物流點　:')
     A            DSC11          5   O    +1
     A                                    52
     A                                      DFT('日期:')
     A            $EGMDY         6  0O    59
     A                                      EDTCDE(Y)
     A                                    69
     A                                      DFT('頁次:')
     A                                    77
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('送貨日期:')
M001MA*           DSC08     R        O    +1
M001MA            DSC08          6  0O    +1
M001DA*                                     REFFLD(SC08)
     A                                      EDTCDE(Y)
     A                                    52
     A                                      DFT('時間:')
     A                                    59
     A                                      TIME
     A                                    70
     A                                      DFT('<IMR058AP>')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('含未核准:')
     A            DSC13     R        O    +1REFFLD(SC13)
     A                                    52' USER :'
     A            $USER         10   O    59
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ==================')
     A                                      SPACEA(001)
     A                                     2
     A                                      DFT('產品代號')
     A                                    20
     A                                      DFT('產品簡稱')
     A                                    50
     A                                      DFT('單位')
     A                                    68
     A                                      DFT('數量')
     A                                    75
     A                                      DFT('核准備註')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('-------------------------------
     A                                      ------------------------------------
     A                                      ------------------')
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEA(001)
     A  29                                 1
     A                                      DFT(' ')
     A                                      SPACEA(001)
     A            WF5201    R        O     2
     A                                      REFFLD(SD03)
     A            MA03      R        O    20
     A                                      REFFLD(MA03)
     A            MA13      R        O    50
     A                                      REFFLD(MA13)
     A            WF5202         5  0O    68
     A                                      EDTCDE(J)
     A            WF5203         1   O    79
     A*----------------------------------------------------------------
     A          R PT1
     A                                      SPACEA(001)
     A                                    45
     A                                      DFT('合　　計------------>')
     A            T1QTY1         5  0O    68
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      =================')
     A                                      SPACEA(001)
     A                                     2
     A                                      DFT('***續下頁***')
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ==================')
     A                                      SPACEA(001)
     A                                     2
     A                                      DFT('<<IMR058AP>>--->報表結束')
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ==================')
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PE31                      SPACEA(1)
     A                                     3'もも此範圍無資料もも'
