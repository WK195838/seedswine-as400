     A* 00A需求單號: 1050388
     A* 00A 2016/08/26 SHARON產生為EXCEL格式
     A****************************************************************
     A*%%TS  SD  19930730  110321  D910091     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : ARR067D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 82/01/18                                   *
     A*    PROGRAM NAME : ARR067                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款系統                              *
     A*    REMARK       :收款分配明細表                            *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930730  110321  D910091     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR067'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'收款分配明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 26'發票公司:'
     A            DAD10S    R        B  5 38REFFLD(AD10)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5 41'-'
     A            DAD10E    R        B  5 43REFFLD(AD10)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 26'收款種類:'
     A                                      DSPATR(HI)
     A            DAD05S    R        B  7 38REFFLD(AD05)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 41'-'
     A            DAD05E    R        B  7 43REFFLD(AD05)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  7 47'99表價差'
     A                                  9 26'客戶代號:'
     A                                      DSPATR(HI)
     A            DAD02S    R        B  9 38REFFLD(AD02)
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                  9 44'-'
     A            DAD02E    R        B  9 46REFFLD(AD02)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 11 26'分配日期:'
M001DA*           DAD12S    R        B 11 38REFFLD(AD12)
M001AA            DAD12S         6  0B 11 38
     A                                      EDTWRD('  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 11 47'-'
M001DA*           DAD12E    R        B 11 49REFFLD(AD12)
M001AA            DAD12E         6  0B 11 49
     A                                      EDTWRD('  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 11 57'(MM/DD/YY)'
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 39VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 41'(1-即時   2-批次)'
     A                                 17 26'列印份數'
     A                                 17 37':'
     A            $CPY           3Y 0B 17 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機'
     A                                      DSPATR(HI)
     A                                 18 37':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備　８０行報表紙***'
     A                                      DSPATR(HI)
 00A+A                                 21 27'***產生Ｅｘｃｅｌ檔案***'
 00A+A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A  89        ERR12         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
