     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : MTR045P                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/02/10                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTR045                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :員工清冊列印                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.02 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    27
     A                                      MSGCON(030 URE9999 REMF)
     A                                    36
     A                                      DFT('員工清冊')
     A                                      SPACEB(001)
     A                                    51
     A                                      DFT('日期:')
     A                                      SPACEB(002)
     A            $EGMDY         6  0O    59
     A                                      EDTCDE(Y)
     A                                    68
     A                                      DFT('頁次:')
     A                                    76
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*----------
     A                                    51
     A                                      DFT('時間:')
     A                                      SPACEB(001)
     A                                    59
     A                                      TIME
     A                                    70
     A                                      DFT('<MTR045>')
     A*----------
     A                                     1
     A                                      DFT('部門組別:')
     A                                      SPACEB(001)
     A            #A03      R        O    +2
     A                                    51' USER :'
     A            $USER         10A  O    59
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===============')
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('員工代號／姓名')
     A                                      SPACEB(001)
     A                                    19
     A                                      DFT('職稱')
     A                                    34
     A                                      DFT('到職日期')
     A                                    44
     A                                      DFT('出生日期')
     A                                    54
     A                                      DFT('身份証號')
     A                                    65
     A                                      DFT('聯絡電話')
     A                                     1
     A                                      DFT('-------------------------------
     A                                      ------------------------------------
     A                                      ---------------')
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A            MC01      R        O     2
     A            MC02      R        O     8
     A            MC04      R        O    20
M001MA*           MC05      R        O    35
M001MA            LMC05          6  0O    35
     A                                      EDTWRD('  /  /  ')
M001MA*           MC08      R        O    45
M001MA            LMC08          6  0O    45
     A                                      EDTWRD('  /  /  ')
     A            MC07      R        O    55
     A            MC12      R        O    66
     A*----------------------------------------------------------------
     A          R PE1
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===============')
     A                                     2
     A                                      DFT('<<MTR045>>--->報表結束')
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===============')
     A                                     2
     A                                      DFT('***續下頁***')
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===============')
     A*----------------------------------------------------------------
     A          R PE9                       SPACEB(1)
     A                                     3'もも此範圍無資料もも'
