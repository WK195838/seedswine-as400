     A*%%TS  SD  19920901  192815  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : IMR044D                                    *
     A*    AUTHOR       : A1118  PHILIP                              *
     A*    CREATE DATE  : 81/04/22                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR044                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :送貨單列印                                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.16 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19920901  192815  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
M002AA                                      CF14(14 'F14確認執行')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'IMR044'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'送貨單列印'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                 13 28'物流點　'
     A                                 13 39':'
     A            DSE07     R        B 13 41REFFLD(SE07)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A  38                                  DSPATR(UL)
     A N38                                  DSPATR(PR)
     A                                 14 28'送貨日期'
     A                                 14 39':'
M001MA            DSE06          6Y 0B 14 41
     A                                      EDTWRD('  /  /  ')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 14 51'(MM/DD/YY)'
     A            DSE02S    R        B 11 44REFFLD(SE02)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A                                 11 57'-'
     A            DSE02E    R        B 11 59REFFLD(SE02)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A N65                                  DSPATR(HI)
     A                                 16 28'執行環境'
     A                                 16 39':'
     A            $EVR           1Y 0B 16 41VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 44'(1-即時   2-批次)'
     A                                 17 28'列印份數'
     A                                 17 39':'
     A            $CPY           3Y 0B 17 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 28'列表機'
     A                                      DSPATR(HI)
     A                                 18 39':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 41CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 31'***請準備套版紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
M002AA  70                             22 42'F14 =確認執行'
M002AA  70                                  DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                  5 28'通　路　 :'
     A            DPATH          1A  B  5 41VALUES(' ' '1' '2' '3')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5 47'1.不含酒店'
     A                                  6 47'2.只選酒店'
     A                                  7 47'3.全　部　'
     A                                  8 28'選　項　 :'
     A            DSEL           1A  B  8 41VALUES(' ' 'A' 'B' 'C')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
M002AA  70                              8 43'將清除委外送貨檔，請確認執行'
M002AA  70                                  DSPATR(HI)
     A                                  9 29'A.正常列印'
     A                                 10 29'B.夾紙重印-'
     A                                 11 29'C.挑選單號-'
     A                                      DSPATR(HI)
     A                                 10 43'起始頁次:'
     A            DPNO           2S 0B 10 55
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
