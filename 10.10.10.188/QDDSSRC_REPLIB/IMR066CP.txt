     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : IMR066CP                                   *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 82/03/15                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR066CP                                   *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存資料系統                              *
     A*    REMARK       :調撥明細／彙總表                          *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    36MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    42'產品調撥彙總表'
     A                                      SPACEA(1)
     A                                    43'依出貨物流點'
     A                                      SPACEA(2)
     A                                     1'產品代號:'
     A            DIJ03S         9        +1
     A                                    +1'-'
     A            DIJ03E         9        +1
     A                                      SPACEA(1)
     A                                     1'出貨物流點:'
     A            DIJ07S         5        +1
     A                                    +1'-'
     A            DIJ07E         5        +1
     A                                    74'日期:'
     A            $EGMDY         6  0O    81EDTCDE(Y)
     A                                    91'頁次:'
     A                                    98PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'收貨物流點:'
     A            DIJ08S         5        +1
     A                                    +1'-'
     A            DIJ08E         5        +1
     A                                    74'時間:'
     A                                    81TIME
     A                                      SPACEA(1)
     A N20                                 1'調撥日期:'
     A  20                                 1'入庫日期:'
     A N20        DII05S         6  0     13EDTCDE(Y)
     A  20        DII06S         6  0     13EDTCDE(Y)
     A                                    22'-'
     A N20        DII05E         6  0     24EDTCDE(Y)
     A  20        DII06E         6  0     24EDTCDE(Y)
     A                                    75'USER :'
     A            $USER         10   O    82
     A                                    93'<IMR066CP>'
     A                                      SPACEA(1)
     A                                     1'=====================+
     A                                       =====================+
     A                                       =====================+
     A                                       =====================+
     A                                       ==================='
     A                                      SPACEA(1)
     A                                     1'產品代號／簡稱'
     A                                    30'出貨物流點'
     A                                    57'收貨物流點'
     A                                    83'調撥數量'
     A                                    93'確認數量'
     A                                      SPACEA(1)
     A                                     1'---------------------+
     A                                       ---------------------+
     A                                       ---------------------+
     A                                       ---------------------+
     A                                       -------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A N21                                 1' '
     A N21                                  SPACEA(1)
     A N30        IJ03      R              2
     A N30        MA03      R             11
     A N31        IJ07      R             30
     A N31        D#A037        22        35
     A N32        IJ08      R             58
     A N32        D#A038        22        63
     A            IJ04T0    R             86EDTCDE(1)
     A                                      REFFLD(IJ04)
     A            IJ05T0    R             96EDTCDE(2)
     A                                      REFFLD(IJ04)
     A*----------------------------------------------------------------
     A          R PT1                       SPACEA(1)
     A                                    58'物流點小計------>'
     A            IJ04T1         6  0     85
     A                                      EDTCDE(1)
     A            IJ05T1         6  0     95
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R PT2                       SPACEA(1)
     A                                    58'產品　總計------>'
     A            IJ04T2         6  0     85
     A                                      EDTCDE(1)
     A            IJ05T2         6  0     95
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'=====================+
     A                                       =====================+
     A                                       =====================+
     A                                       =====================+
     A                                       ==================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'=====================+
     A                                       =====================+
     A                                       =====================+
     A                                       =====================+
     A                                       ==================='
     A                                      SPACEA(1)
     A                                     2'<<IMR066CP>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE9                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
