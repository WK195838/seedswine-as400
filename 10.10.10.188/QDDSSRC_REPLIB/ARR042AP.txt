     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : ARR042P                                    *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : 81/04/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR042                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :帳齡分析表                                *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    52MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    60'帳齡分析表'
     A                                      SPACEA(2)
     A                                   100'日期:'
     A            $EGMDY         6  0O   108EDTCDE(Y)
     A                                   117'頁次:'
     A                                   125PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'公司別  :'
     A            SI01      R             12
     A            D#B03         16   O    14
     A                                   100'時間:'
     A                                   108TIME
     A                                   120'<ARR042AP>'
     A                                      SPACEA(1)
     A                                     1'確認日期:'
     A            YDATES         6  0O    12EDTWRD('  /  /  ')
     A                                   100
     A                                      DFT(' USER :')
     A            $USER         10A  O   108
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ==========='
     A                                      SPACEA(1)
     A                                     1'客戶代號／名稱'
     A                                    30'未到期'
     A                                    42'未超過30天'
     A                                    60'31∼60天'
     A                                    75'61∼90天'
     A                                    90'91∼180天'
     A                                   108'180天以上'
     A                                   127'總額'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       -----------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            SI12      R              2
     A            ME04      R              7
     A            DSI30O         9  0     26EDTCDE(J)
     A            DSI30A         9  0     44EDTCDE(J)
     A            DSI30B         9  0     60EDTCDE(J)
     A            DSI30C         9  0     75EDTCDE(J)
     A            DSI30D         9  0     91EDTCDE(J)
     A            DSI30E         9  0    107EDTCDE(J)
     A            TOTAL          9  0    121EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3                       SPACEA(1)
     A                                     7'合　　　　　　計'
     A            ENDO           9  0     26EDTCDE(J)
     A            ENDA           9  0     44EDTCDE(J)
     A            ENDB           9  0     60EDTCDE(J)
     A            ENDC           9  0     75EDTCDE(J)
     A            ENDD           9  0     91EDTCDE(J)
     A            ENDE           9  0    107EDTCDE(J)
     A            ENDTOT         9  0    121EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ==========='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ==========='
     A                                      SPACEA(1)
     A                                     2'<<ARR042AP>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ==========='
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE4
     A                                     1'                    +
     A                                                           +
     A                                                           +
     A                                                           +
     A                                                           +
     A                                                           +
     A                                                 '
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE5                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
