     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIS004D                                    *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 98/03/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :茂世進銷存                                *
     A*    SUBSYSTEM    :                                          *
     A*    REMARK       :採購訂單維護                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 11  3VALUES(' ' '2' '4' '5' '6')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SBC01          6Y 0O 11  6EDTWRD('  /  /  ')
     A            SBC01H    R        H      REFFLD(BC01 W2BCPF)
     A            SBC02H    R        H      REFFLD(BC02 W2BCPF)
     A            SBC03     R        O 11 17REFFLD(BC03 W2BCPF)
     A            SBC04     R        H      REFFLD(BC04 W2BCPF)
     A            STATUS         8A  O 11 33
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'KIS004'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'採購訂單維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'專櫃代號:'
     A            DBC02     R        B    +1REFFLD(BC02 W2BCPF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A  62                                  DSPATR(PC RI)
     A            D#A03     R        O    +1REFFLD(#A03 PA#APF)
     A                                  6  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  6 17'2=修改'
     A                                      COLOR(BLU)
     A                                  6 26'4=刪除'
     A                                      COLOR(BLU)
     A                                  6 35'5=查詢'
     A                                      COLOR(BLU)
     A                                  6 44'6=上傳'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN1          6Y 0B 10  6EDTWRD('  /  /  ')
     A            DBGN2         12A  B 10 17
     A                                  8  3'(月/日/年)'
     A                                  9  3'A 訂單日期  訂單單號       +
     A                                          狀態 '
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 30'F6 =新增訂單'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99N97     ERRMSG        60A  O 23  2MSGID(&ERRID &ERRF )
     A                                      DSPATR(HI)
     A                                 23 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A  99 97     @MSG          60A  O 24  2
     A                                      DSPATR(HI)
      *此燈號控制新增成功以及修改成功專用
     A  91        ERR0          63A  O 23  2MSGID(MSG 1000 R1MF)
     A                                      DSPATR(HI)
     A  92        ERR1          63A  O 23  2MSGID(MSG 1001 R1MF)
     A                                      DSPATR(HI)
     A  93        ERR2          63A  O 23  2MSGID(MSG 1002 R1MF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 23  2MSGID(MSG 1003 R1MF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 23  2MSGID(MSG 1004 R1MF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SBD02     R        B  9  2REFFLD(BD02 W2BDPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N33                                  DSPATR(PR)
     A  33N61                               DSPATR(UL HI)
     A            SMA03     R        O  9 12REFFLD(MA03 MTMAPF)
     A            SBD04          5  0B  9 32
     A                                      EDTCDE(4)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N33                                  DSPATR(PR)
     A  33                                  DSPATR(UL)
     A            SBD03     R        O  9 40REFFLD(BD03 W2BDPF)
     A            SMB03     R        O  9 46REFFLD(MB03 MTMBPF)
     A            SBD08     R   -2O  B  9 65REFFLD(BD08 W2BDPF)
     A N33                                  DSPATR(PR)
     A  33                                  DSPATR(UL)
     A            SBD02H    R        H      REFFLD(BD02 W2BDPF)
     A            SBD04H    R        H      REFFLD(BD04 W2BDPF)
     A            SBD08H    R   -2O  H      REFFLD(BD08 W2BDPF)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A  32                                  CF04(04)
     A  32                                  CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'KIS004'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'採購訂單維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'專櫃代號:'
     A            DBC022    R        B  5 13REFFLD(BC02 W2BCPF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            D#A032    R        O  5 19REFFLD(#A03 PA#APF)
     A                                  6  1'訂單日期:'
     A            DBC01          6Y 0B  6 13EDTWRD('  /  /  ')
     A N34                                  DSPATR(PR)
     A  34                                  DSPATR(UL)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  6 22'(月/日/年)'
     A                                  6 40'訂單編號:'
     A            DBC03     R        O  6 52REFFLD(BC03 W2BCPF)
     A                                      DSPATR(HI)
     A                                  8  2'產品代號 產品名稱         數+
     A                                      量  供應商                 備+
     A                                      註          '
     A                                      DSPATR(UL)
     A            SBC032    R        H      REFFLD(BC03 W2BCPF)
      *----------------------------------------------------------------
     A          R DSPC2
     A*                                22  2'執行'
     A*                                     COLOR(BLU)
     A                                 22  1'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  32                                +1'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  32                                +1'F5 =參考資料'
     A                                      COLOR(BLU)
     A                                    +1'F12=回前畫面'
     A                                      COLOR(BLU)
     A                                    +1'PAGE DOWN =下頁'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
