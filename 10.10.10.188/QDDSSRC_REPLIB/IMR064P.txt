     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    31
     A                                      MSGCON(030 URE9999 REMF)
     A                                      SPACEA(001)
     A                                    38
     A                                      DFT('內陸運輸保險表 ')
     A                                      SPACEA(002)
     A                                    57
     A                                      DFT('日期:')
     A            $EGMDY         6  0O    66
     A                                      EDTCDE(Y)
     A                                    76
     A                                      DFT('頁次:')
     A                                    84
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                    57
     A                                      DFT('時間:')
     A                                    66
     A                                      TIME
     A                                    76
     A                                      DFT('<IMR064P>')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('送貨日期:')
     A            DSE06S    R        O    +1REFFLD(SE06)
     A                                      EDTCDE(Y)
     A                                    +1'-'
     A            DSE06E    R        O    +1REFFLD(SE06)
     A                                      EDTCDE(Y)
     A                                    57' USER :'
     A            $USER         10   O    66
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ========================')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('送貨日期')
     A                                    11
     A                                      DFT('產品代號／品名')
     A                                    41
     A                                      DFT('單價')
     A                                    48
     A                                      DFT('數量／單位')
     A                                    62
     A                                      DFT('金　額')
     A                                    70
     A                                      DFT('　備　　註')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('-------------------------------
     A                                      ------------------------------------
     A                                      ------------------------')
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEA(001)
     A N32        P5601     R        O     1EDTWRD('  /  /  ')
     A                                      REFFLD(SE06)
     A            WF5602    R        O    11
     A                                      REFFLD(SF03)
     A            MA03      R        O    22REFFLD(MA03)
     A            PRC       R        O    40EDTCDE(J)
     A                                      REFFLD(SF15)
     A            QTY       R        O    47REFFLD(SF04)
     A                                      EDTCDE(J)
     A            MA13      R        O    53REFFLD(MA13)
     A            AMT       R        O    58REFFLD(SF13)
     A                                      EDTCDE(J)
     A                                    70'____________________'
     A*----------------------------------------------------------------
     A          R PT1
     A                                      SPACEA(001)
     A                                    22
     A                                      DFT('日期小計------------>')
     A            QTY2      R        O    47REFFLD(SF04)
     A                                      EDTCDE(J)
     A            AMT2      R        O    58REFFLD(SF13)
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT2
     A                                      SPACEA(001)
     A                                    22
     A                                      DFT('合　　計------------>')
     A            QTY3      R        O    47REFFLD(SF04)
     A                                      EDTCDE(J)
     A            AMT3      R        O    58REFFLD(SF13)
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      =======================')
     A                                      SPACEA(001)
     A                                     2
     A                                      DFT('***續下頁***')
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ========================')
     A                                      SPACEA(001)
     A                                     2
     A                                      DFT('<<IMR064P>>')
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ========================')
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PE31                      SPACEA(1)
     A                                     3'もも此範圍無資料もも'
