     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:27313250   *
     A*                                                              *
     A*    PROGRAM-ID   : PYA808D                                    *
     A*    PROGRAM NAME :產品別銷售分析明細表　　                  *
     A*    AUTHOR       : A1491                                      *
     A*    CREATE DATE  : 97/01/22                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :康齡系統                                  *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PYRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF07(07)
     A                                      CF13(13)
     A                                  1  2'<PYA808.1>'
     A            CONAME        32A  O  1 25MSGID(UPT 9900 PTMF)
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTWRD('  /  /  ')
     A            $USERN        10A  O  2  2
     A                                  2 30'產品別銷售分析明細表'
     A                                      DSPATR(RI)
     A                                  8 30'專櫃年月:'
     A            DYM            4Y 0B  8 42EDTWRD('0  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8 48'(MM/YY)'
     A                                 10 30'1.產品銷售明細表'
     A                                      COLOR(BLU)
     A                                 11 30'2.廠商銷售明細表'
     A                                      COLOR(BLU)
     A                                 13 30'選項:'
     A            DOPT           1A  B 13 38VALUES('1' '2')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 17  2'執行環境:'
     A            $PENV          1A  B 17 14VALUES('0' '1' '2')
     A                                 17 18'(0-查詢 1-即時 2-批次)'
     A                                 18  2'列印份數:'
     A            $CPY           3Y 0B 18 14EDTCDE(3)
     A                                      CHECK(RB)
     A            $PRTCD         2A  H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'PF03 =回主畫面'
     A                                 23 20'PF07 =資料下載'
     A                                 23 38'PF13 =處理開始'
     A                                 24 67'【泛太資訊】'
