     A*%%TS  SD  19920820  082914  REPGMRP     REL-V2R1M0  5738-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : MTA004D                                    *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  : 80/11/19                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTA004                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料建檔子系統                        *
     A*    REMARK       :客戶分店基本資料維護 (A/U/D/I)            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.04 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLSR                     SFL
     A*%%TS  SD  19920704  102403  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  2VALUES(' ' '2' '3' '4' '5')
     A            ME01      R        O  9  4REFFLD(RERFR/ME01 *LIBL/RERF)
     A            ME02      R        O  9 10REFFLD(RERFR/ME02 *LIBL/RERF)
     A            ME04      R        O  9 15REFFLD(RERFR/ME04 *LIBL/RERF)
     A            ME10      R        O  9 34REFFLD(RERFR/ME10 *LIBL/RERF)
     A            ME12      R        O  9 47REFFLD(RERFR/ME12 *LIBL/RERF)
     A            ME07      R        O  9 63REFFLD(RERFR/ME07 *LIBL/RERF)
     A            ME08      R        O  9 66REFFLD(RERFR/ME08 *LIBL/RERF)
     A            MC02      R        O  9 69REFFLD(RERFR/MC02 *LIBL/RERF)
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920704  102403  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A                                      CF04(04)
     A                                      CF06(06)
     A                                      CF08(08)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'MTA004'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  5'2=修改'
     A                                  5 25'4=刪除'
     A                                  5 36'5=查詢'
     A                                  6  2' 客戶                     -
     A                                                                      +
     A                                       地區'
     A                                  7  2'A代號分店分店簡稱     -
     A                                          聯絡人     電話一       +
     A                                       區域業務員   '
     A                                      DSPATR(UL)
     A                                  2 29'客戶分店基本資料維護'
     A                                      DSPATR(HI)
     A            DBGN1          5A  B  8  4DSPATR(HI)
     A            DBGN2          4A  B  8 10DSPATR(HI)
     A                                  5 15'3=拷備'
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R DSPC11
     A*%%TS  SD  19920810  161221  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F6 =新增'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 22 38'F4 =客戶簡稱查詢'
     A                                 22 58'F8 =客戶全稱查詢'
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R DSPC2
     A*%%TS  SD  19920820  082914  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CF03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CSRLOC(D#ROW      D#COL)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MTA004'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'客戶代號:'
     A                                  5  1'分店代號:'
     A                                  7  1'分店全稱:'
     A                                  8  1'分店簡稱:'
     A                                  9  1'分店地址:'
     A                                 11  1'地區　　:'
     A                                      DSPATR(HI)
     A                                 12  1'區域　　:'
     A                                      DSPATR(HI)
     A                                 13  1'負責人　:'
     A                                 14  1'聯絡人　:'
     A                                 15  1'收貨人　:'
     A                                 16  1'電話一　:'
     A                                 17  1'電話二　:'
     A                                 19  1'業務人員:'
     A                                      DSPATR(HI)
     A                                 20  1'備註　　:'
     A                                 22  2'執行'
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A                                 22 10'F3 =回主畫面'
     A  98        ERR1          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                  2 29'客戶分店基本資料維護'
     A                                      DSPATR(HI)
     A            SME01     R        B  4 13REFFLD(RERFR/ME01 *LIBL/RERF)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A            SME02     R        B  5 13REFFLD(RERFR/ME02 *LIBL/RERF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A            SME03     R        B  7 13REFFLD(RERFR/ME03 *LIBL/RERF)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            SME05     R        B  9 13REFFLD(RERFR/ME05 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            SME07     R        B 11 13REFFLD(RERFR/ME07 *LIBL/RERF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            SME09     R        B 13 13REFFLD(RERFR/ME09 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            SME06     R        B 10 13REFFLD(RERFR/ME06 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            SME11     R        B 15 13REFFLD(RERFR/ME11 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            SME12     R        B 16 13REFFLD(RERFR/ME12 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            SME10     R        B 14 13REFFLD(RERFR/ME10 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            SME13     R        B 17 13REFFLD(RERFR/ME13 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 18  1'傳真機　:'
     A            SME14     R        B 18 13REFFLD(RERFR/ME14 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            SME15     R        B 19 13REFFLD(RERFR/ME15 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A            SME16     R        B 20 13REFFLD(RERFR/ME16 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 18 48'最近交易:'
     A                                 19 48'建檔日期:'
     A            SME17     R        B 20 47REFFLD(RERFR/ME17 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            DME04     R        O  4 20REFFLD(RERFR/ME04 *LIBL/RERF)
     A            SME04     R        B  8 13REFFLD(RERFR/ME04 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A            #D02      R        O 11 16REFFLD(RERFR/#D02 *LIBL/RERF)
     A            MC02      R        O 19 20REFFLD(RERFR/MC02 *LIBL/RERF)
M001MA            DME18          6  0O 18 60
M001MA            DME19          6  0O 19 60
     A            SME08     R        B 12 13REFFLD(RERFR/ME08 *LIBL/RERF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A            #E03      R        O 12 16REFFLD(RERFR/#E03 *LIBL/RERF)
     A                                  5 40'往來狀態:'
     A            DME20     R        B  5 52REFFLD(ME20)
     A                                      VALUES(' ' 'A' 'I')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  5 54'(A=ACTION I=INACTION)'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A                                 22 58'F12=回前畫面　'
