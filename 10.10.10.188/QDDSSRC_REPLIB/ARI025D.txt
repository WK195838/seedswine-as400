     A*%%TS  SD  20021008  174038  REPGMR      REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARI025D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/04/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARI025                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款作業                              *
     A*    REMARK       :銷貨折讓查詢（依產品）                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M004      | 00.06.12 |菸酒稅法實施，新增銷退稅額
     A*================================================================
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920722  083340  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  2VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            AG03      R        O  9  4
     A            AG09      R        O  9 15
     A            AG10      R        O  9 21
     A            ME04      R        O  9 26
     A            AG01      R        O  9 46
     A*--------------------------------------------------------------*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920722  083340  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'ARI025'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'銷貨折讓查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  4'5=明細查詢'
     A                                  7  2'A產品代號 客戶代號            -
     A                                              公司'
     A                                      DSPATR(UL)
     A            DBGN12    R        B  8  4REFFLD(AG03)
     A            DBGN13    R        B  8 15REFFLD(AG09)
     A            DBGN14    R        B  8 21REFFLD(AG10)
     A*--------------------------------------------------------------*
     A          R DSPC1
     A*%%TS  SD  19920217  160736  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A          R SFLSR2                    SFL
     A*%%TS  SD  20021008  174038  REPGMR      REL-V4R4M0  5769-PW1
     A            RRN2           4S 0H
     A            AG11      R        H
     A            DAG11          6Y 0O 11  2EDTWRD('  /  /  ')
     A            AG02      R        O 11 11
     A            SG02      R        O 11 24
     A            AG04      R        O 11 37
     A            AG12      R        O 11 48EDTCDE(3)
     A            AG13      R        O 11 54EDTCDE(3)
     A            AG06      R        O 11 59EDTCDE(3)
     A            W0721          9Y 0O 11 70EDTCDE(3)
     A            AG21           9Y 0O 12 68EDTCDE(3)
     A*--------------------------------------------------------------*
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19920611  102246  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
M004MA*                                     SFLPAG(0010)
M004MA                                      SFLPAG(0005)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'ARI025'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'銷貨折讓查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司　　:'
     A            AG01      R        O  4 14
     A            #B03      R        O  4 17
     A                                  5  2'產品代號:'
     A            AG03      R        O  5 14
     A            MA03      R        O  5 26
     A                                  6  2'客戶代號:'
     A            AG09      R        O  6 14
     A            AG10      R        O  6 20
     A            ME04      R        O  6 26
     A                                  8  2'折讓日期'
     A                                  9  2'MM/DD/YY 折讓單號  退貨單號   -
     A                                      對應發票#  數量 數量 單價  -
M004MA*                                     銷退金額'
M004MA                                      銷退稅額'
     A                                      DSPATR(UL)
     A                                  8 49'訂購'
     A                                  8 56'搭贈'
M004MA*                                 8 63'銷退'
M004MA                                  8 63'銷退　銷退總金額'
M001DA*           DBGN2     R        B 10  2REFFLD(AG11)
M001AA            DBGN2          6  0B 10  2
     A                                      EDTWRD('  /  /  ')
     A*--------------------------------------------------------------*
     A          R DSPC2
     A*%%TS  SD  19920514  152830  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
