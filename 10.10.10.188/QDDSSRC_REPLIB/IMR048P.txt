     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : IMR048P                                    *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : 81/04/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR048                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :進銷存彙總表                              *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    29MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    36'進銷存彙總表'
     A                                      SPACEA(1)
     A                                    50'日期:'
     A            $EGMDY         6  0O    58EDTCDE(Y)
     A                                    67'頁次:'
     A                                    75PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'物流點　:'
     A            DIR02     R        O    13REFFLD(IR02 *LIBL/IMIRLF)
     A            NAME          22   O    19
     A                                    50'時間:'
     A                                    58TIME
     A                                      SPACEA(1)
     A                                     1'異動日期:'
     A            DIR04S         6  0O    13EDTWRD('  /  /  ')
     A                                    22'-'
     A            DIR04E         6  0O    24EDTWRD('  /  /  ')
     A                                    50' USER :'
     A            $USER         10   O    58
     A                                    69'<IMR048>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     1'產品代號'
     A                                    12'產品簡稱'
     A                                    30'單位'
     A                                    37'進貨量'
     A                                    46'出倉量'
     A                                    55'期間結餘'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            TIR03     R              2REFFLD(IR03 *LIBL/IMIRLF)
     A            MA03      R             12
     A            MA13      R             30
     A            DIN            6  0     37EDTCDE(J)
     A            DOUT           6  0     46EDTCDE(J)
     A            DTOT           6  0     57EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'<<IMR048>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE31                      SPACEA(1)
     A                                     3'もも此範圍無資料もも'
