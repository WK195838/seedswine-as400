     A*%%TS  SD  19920304  093703  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : IMA0081D                                   *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/03/01                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMA0081                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :送貨單明細查詢                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.16 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR                     SFL
     A            RRN            4S 0H
     A            SF03      R        O 11  4
     A            MA03      R        O 11 14
     A            MA13      R        O 11 33
     A            SF04      R        O 11 43EDTCDE(1)
     A            SF05      R        O 11 54EDTCDE(1)
     A            PTOT           5  0O 11 63EDTCDE(1)
     A*--------------------------------------------------------------*
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920122  174051  REPGMR      REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F12=回前畫面'
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A          R DSPC1
     A*%%TS  SD  19920304  093703  REPGMRP     REL-V2R1M0  5738-PW1
     A                                  1  2'IMA008'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR005'
     A                                  2 32'送貨單明細查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號'
     A                                  5 13':'
     A            SE01      R        O  5 15
     A            D#B03     R   14   O  5 18REFFLD(#B03)
     A                                  5 34'地區　　'
     A                                  5 45':'
     A            SE34      R        O  5 47
     A            #D02      R        O  5 51
     A                                  6  2'送貨單號'
     A                                  6 13':'
     A            SE02      R        O  6 15
     A                                  6 34'單別　　'
     A                                  6 45':'
     A            SE03      R        O  6 47
     A            #203      R        O  6 51
     A                                  7  2'送貨日期'
     A                                  7 13':'
M001MA            DSE06          6Y 0O  7 15
     A                                      EDTWRD('  /  /  ')
     A                                  7 34'客戶代號'
     A                                  7 45':'
     A            SE04      R        O  7 47
     A            SE05      R        O  7 54
     A            ME04      R        O  7 61
     A                                 10  3'產品代號／名稱　　　　　　　　單+
     A                                      位 訂購數量　搭贈數量　　總數+
     A                                      量'
     A                                      DSPATR(UL)
