     A*%%TS  SD  19921228  171346  D910074     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    :                                            *
     A*    AUTHOR       :                                            *
     A*    CREATE DATE  : 81/12/29                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOA018                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售訂單系統                              *
     A*    REMARK       :客戶產品報價還原作業                      *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.11 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921228  171346  D910074     REL-V2R1M0  5738-PW1
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CF14(14 '處理作業')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOA018'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 27'客戶產品報價拷貝還原作業'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 26'客戶代號:'
     A                                      DSPATR(HI)
     A            DSB01S    R        B  5 38REFFLD(SB01)
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  5 48'-'
     A            DSB01E    R        B  5 50REFFLD(SB01)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  7 26'產品代號:'
     A                                      DSPATR(HI)
     A            DSB02S    R        B  7 38REFFLD(RERFR/SB02 *LIBL/RERF)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  7 48'-'
     A            DSB02E    R        B  7 50REFFLD(RERFR/SB02 *LIBL/RERF)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                  9 26'折扣別  :'
     A            DSB04          1A  B  9 38VALUES('N' 'S' 'A')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                  9 40'(N-普通S-特別A-搭贈)'
     A                                 11 23'生效起訖日期'
     A            DSB05          6Y 0B 11 38EDTWRD('  /  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 11 47'訖'
     A            DSB13          6Y 0B 11 52EDTWRD('  /  /  ')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 11 60'(MM/DD/YY)'
     A                                 15 19'+----------------------------------
     A                                      --------------+'
     A                                 16 19'|'
     A                                 16 67'|'
     A                                 17 19'|請按F14以便處理此作業，否則按-
     A                                      F3結束此作業|'
     A                                 18 19'|'
     A                                 18 67'|'
     A                                 19 19'+----------------------------------
     A                                      --------------+'
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F14=處理作業'
     A  98        ERR1          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(UPT 2111 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR12         50A  O 24  2MSGID(URE 0108 REMF)
     A                                      DSPATR(HI)
     A  87        ERR13         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  86        ERR14         50A  O 24  2MSGID(URE 0064 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
