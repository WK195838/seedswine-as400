     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : IMR062P                                    *
     A*    AUTHOR       : ANDY                                       *
     A*    CREATE DATE  : 81/08/12                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR0621                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料子系統                            *
     A*    REMARK       :報廢明細表                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.12.11 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    42MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    48'產品報廢明細表'
     A                                      SPACEA(2)
     A                                     1'公司別　:'
     A            DIE01          1        +1
     A                                    89'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A                                    +1'頁次:'
     A                                    +1PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'物流點　:'
     A            DIE03     R             +1REFFLD(IE03)
     A            #A03      R             +5
     A                                    89'時間:'
     A                                    +1TIME
     A                                    +2'<IMR062P>'
     A                                      SPACEA(1)
     A                                     1'報廢日期:'
M001MA*           DIE05S    R             +1REFFLD(IE05)
M001MA            DIE05S         6  0     +1
     A                                      EDTCDE(Y)
     A                                    +1'-'
M001MA*           DIE05E    R             +1REFFLD(IE05)
M001MA            DIE05E         6  0     +1
     A                                      EDTCDE(Y)
     A                                    89' USER :'
     A            $USER         10A  O    +1
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       '
     A                                      SPACEA(1)
     A                                     1'產品代號'
     A                                    13'產品簡稱'
     A                                    28'單位'
     A                                    34'日期'
     A                                    43'單據號碼'
     A                                    53'數量'
     A                                    +5'FOB-TOTAL'
     A                                    +6'FHI-TOTAL'
     A                                    +6'DUTY-TOTAL'
     A                                    +2'報廢總成本'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       '
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A  30        IF03      R              2
     A  30        MA03      R             +0
     A                                    +0'瓶'
M001MA            PIE05          6  0     +0EDTWRD('  /  /  ')
M001DA*                                     REFFLD(IE05)
     A            IE02      R             +0
     A            IF04      R             +0EDTCDE(J)
     A            IF08      R             +0EDTCDE(J)
     A            IF09      R             +0EDTCDE(J)
     A            IF10      R             +0EDTCDE(J)
     A            IF05      R             +0EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3                        SPACEA(1)
     A                                    13'產品小計'
     A                                    23'--------------------'
     A                                    43'--------->'
     A            IF04P     R             +0REFFLD(IF04)
     A                                      EDTCDE(J)
     A            IF08P     R             +0REFFLD(IF08)
     A                                      EDTCDE(J)
     A            IF09P     R             +0REFFLD(IF09)
     A                                      EDTCDE(J)
     A            IF10P     R             +0REFFLD(IF10)
     A                                      EDTCDE(J)
     A            IF05P     R             +0REFFLD(IF05)
     A                                      EDTCDE(J)
     A N99                                  SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE1                        SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       '
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2                       SPACEA(1)
     A                                    13'公司總計'
     A                                    23'--------------------'
     A                                    43'--------->'
     A            IF04T     R             +0REFFLD(IF04)
     A                                      EDTCDE(J)
     A            IF08T     R             +0REFFLD(IF08)
     A                                      EDTCDE(J)
     A            IF09T     R             +0REFFLD(IF09)
     A                                      EDTCDE(J)
     A            IF10T     R             +0REFFLD(IF10)
     A                                      EDTCDE(J)
     A            IF05T     R             +0REFFLD(IF05)
     A                                      EDTCDE(J)
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       '
     A                                      SPACEA(1)
     A                                     2'<<IMR062P>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
