     A*%%TS  SD  19960809  141643  D910454     REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : IMI021D                                    *
     A*    AUTHOR       : A1092  TAYLOR                              *
     A*    CREATE DATE  : 81/02/17                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMI021                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存子系統                                *
     A*    REMARK       :產品存貨狀況                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR                     SFL
     A*%%TS  SD  19921002  082343  D910074     REL-V2R1M0  5738-PW1
     A            RRN            4S 0H
     A            IA02      R        O 11  3
     A            DFLD          22O  O 11  9
     A            IA01      R        O 11 32
     A            IA06      R        O 11 36EDTCDE(J)
     A            IA07      R        O 11 45EDTCDE(J)
     A            IA08      R        O 11 54EDTCDE(J)
     A            IA09      R        O 11 63EDTCDE(J)
     A            IA10      R        O 11 72EDTCDE(J)
     A*--------------------------------------------------------------*
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19960809  141643  D910454     REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'IMI021'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'產品存貨狀況'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  9  2'物流點                    公司+
     A                                      　庫存量 良品量 壞品量 保留+
     A                                      量 可用量'
     A                                      DSPATR(UL)
     A                                  4  4'產品代號:'
     A            IA03      R        O  4 16
     A            MA03      R        O  4 26
     A                                  5  4'保稅庫存:'
     A            MA29      R        O  5 16EDTCDE(J)
     A                                  5 42'完稅庫存量:'
     A            MA30      R        O  5 56EDTCDE(J)
     A            MA37      R        O  6 16EDTCDE(J)
     A                                  6  2'保稅安全量:'
     A                                  6 42'完稅安全量:'
     A            MA38      R        O  6 56EDTCDE(J)
     A            DBGN1     R        B 10  3REFFLD(IA02)
     A                                  7  2'　　公賣價:'
     A            DMA46          7Y 0O  7 17EDTCDE(J)
     A                                  7 42'包裝規格　:'
     A            MA17      R        O  7 56
     A*--------------------------------------------------------------*
     A          R DSPC1
     A*%%TS  SD  19920320  201019  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 11'F3 =回主畫面'
     A                                 22 28'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
