     A****************************************************************
     A*    DSPF NAME    : PII023D                                    *
     A*    AUTHOR       : MERCURY                                    *
     A*    CREATE DATE  : 83/03/15                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PII023                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :盤點子系統                                *
     A*    REMARK       :盤點更新查詢                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DIU04          6Y 0O  8  3EDTCDE(Y)
     A            IU05      R        O  8 13
     A            MA03      R        O  8 23
     A            IU03      R        O  8 45
     A            #A03A     R        O  8 51REFFLD(#A03)
     A            IU06      R        O  9 13EDTCDE(J)
     A            IU07      R        O  9 24EDTCDE(J)
     A            IU08      R        O  9 33EDTCDE(J)
     A            IU09      R        O  9 49EDTCDE(J)
     A            IU10      R        O  9 65EDTCDE(J)
     A*--------------------------------------------------------------*
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0063)
     A                                      SFLPAG(0007)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A* 53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'<PII023>'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'盤點更新查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'盤點日期　產品／簡稱'
     A                                  5 44'物流點'
M001DA*           DBGN1     R        B  7  3REFFLD(IU04)
M001AA            DBGN1          6  0B  7  3
     A                                      EDTCDE(Y)
     A            DBGN2     R        B  7 13REFFLD(IU05)
     A            DBGN3     R        B  7 45REFFLD(IU03)
     A                                  6 12'良品異動'
     A                                  6 23'壞品異動'
     A                                  6 40'FOB-TOT'
     A                                  6 56'FHI-TOT'
     A                                  6 71'DUTY-TOT'
     A*--------------------------------------------------------------*
     A          R DSPC1
     A                                 23  2'執行'
     A                                 23 11'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 63MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
