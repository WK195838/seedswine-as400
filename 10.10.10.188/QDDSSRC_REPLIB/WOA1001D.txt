     A*%%TS  SD  19960711  165007  D910454     REL-V3R1M0  5763-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : WOA100D                                    *
     A*    AUTHOR       : A1092  TAYLOR                              *
     A*    CREATE DATE  : 81/02/10                                   *
     A*    UPDATE DATE  : 81/04/17 PHILIP                            *
     A*    PROGRAM NAME : WOA100                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/WORF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19960711  165007  D910454     REL-V3R1M0  5763-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'WOA100'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'客戶基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'客戶代號:'
     A            DWE01     R        O  4 14REFFLD(WE01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  6  2'客戶全稱:'
     A            DWE02     R        B  6 14REFFLD(WE02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7  2'客戶簡稱:'
     A            DWE03     R        B  7 14REFFLD(WE03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7 37'統一編號:'
     A            DWE09     R        B  7 49REFFLD(WE09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'客戶地址:'
     A            DWE04     R        B  8 14REFFLD(WE04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DWE041    R        B  8 47REFFLD(WE041)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  2'發票地址:'
     A            DWE05     R        B  9 14REFFLD(WE05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DWE051    R        B  9 47REFFLD(WE051)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10  2'通路　　:'
     A            DWE07     R        B 10 14REFFLD(WE07)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A03         22A  O 10 17
     A                                 10 44'型態　　:'
     A                                      DSPATR(HI)
     A            DWE08     R        B 10 56REFFLD(WE08)
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#C03     R        O 10 59REFFLD(#C03 *LIBL/RERF)
     A                                 11  2'地區　　:'
     A            DWE10     R        B 11 14REFFLD(WE10)
     A                                      VALUES('1' '2' '3' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 16'1.北區 2.中區 3.南區'
     A                                 11 44'區域　　:'
     A                                      DSPATR(HI)
     A            DWE11     R        B 11 56REFFLD(WE11)
     A N74                                  DSPATR(HI)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#D02     R        O 11 59REFFLD(#D02 RERF)
     A                                 12  2'負責人:'
     A            DWE12     R        B 12 12REFFLD(WE12)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 27'聯絡人:'
     A            DWE13     R        B 12 37REFFLD(WE13)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 52'業務員:'
     A            DWE18     R        B 12 62REFFLD(WE18)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMC02     R        O 12 69REFFLD(MC02 *LIBL/RERF)
     A                                 13  2'電話一  :'
     A            DWE15     R        B 13 14REFFLD(WE15)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 37'傳真機  :'
     A            DWE17     R        B 13 49REFFLD(WE17)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  2'等級:'
     A                                 15 10'REMY'
     A            DWF08A         1A  B 15 15RANGE(' ' 'Z')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 20'BRANDY'
     A            DWF08B         1A  B 15 27RANGE(' ' 'Z')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 33'WHISKEY'
     A            DWF08C         1A  B 15 41RANGE(' ' 'Z')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 47'LIQUOR'
     A            DWF08D         1A  B 15 54RANGE(' ' 'Z')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 59'WINE'
     A            DWF08E         1A  B 15 64RANGE(' ' 'Z')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 70'OTHER'
     A            DWF08F         1A  B 15 76RANGE(' ' 'Z')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16  2'開幕日期:'
     A            DWF14          6Y 0B 16 14
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTWRD('  /  /  ')
     A                                 16 37'營業時間:'
     A            DWF12     R        B 16 49REFFLD(WF12)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTWRD('  :  ')
     A                                 16 56'-'
     A            DWF13     R        B 16 58REFFLD(WF13)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTWRD('  :  ')
     A                                 16 66'(24HR)'
     A                                 17 52'桌數　　:'
     A            DWF11     R        B 17 64REFFLD(WF13)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  2'坪數　　:'
     A            DWF09     R        B 17 14REFFLD(WF09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 27'貴賓室　:'
     A            DWF10     R        B 17 39REFFLD(WF10)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18  2'供應商　:'
     A                                 18 15'1.'
     A            DWF02     R        B 18 18REFFLD(WF02)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DWE02A        22   O 18 24
     A                                 18 47'2.'
     A            DWF03     R        B 18 50REFFLD(WF03)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DWE03A        22   O 18 56
     A                                 19 15'3.'
     A            DWF04     R        B 19 18REFFLD(WF04)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DWE04A        22   O 19 24
     A                                 19 47'4.'
     A            DWF05     R        B 19 50REFFLD(WF05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A            DWE05A        22   O 19 56
     A                                 20  2'往來狀態:'
     A            DWE20     R        B 20 14REFFLD(WE20)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20 17'(1.往來'
     A                                 20 28'2.不往來'
     A                                 20 40'3.停業)'
     A                                 20 52'客戶原始代號:'
     A            DWF06     R        B 20 68REFFLD(WF06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A            DWF07     R        B 20 74REFFLD(WF07)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 21  2'備註    :'
     A            DWE19     R        B 21 14REFFLD(WE19)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 26'F4 =視窗查詢'
     A                                 23 42'F5 =參考資料'
     A                                 23 58'F12=回前畫面'
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 24  2MSGID(&ERRID &ERRF)
