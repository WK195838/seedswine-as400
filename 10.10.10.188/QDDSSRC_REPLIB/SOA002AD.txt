     A*%%TS  SD  20021015  092233  REPGMR      REL-V4R4M0  5769-PW1
     A*  81/02/14  15:16:50    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : SOA002D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/02/14                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOA002                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售訂單作業                              *
     A*    REMARK       :客戶產品報價設定 (A/U/D/I)                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.11 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR                     SFL
     A*%%TS  SD  20021003  155955  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  8  3
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      VALUES(' ' '2' '3' '4' '5' '6')
     A            SB01      R        O  8  6
     A            ME04      R        O  8 12
     A            SB02      R        O  8 31
     A            SB04      R        O  8 41
     A            DSALE          6A  O  8 43
     A            DSB06          6Y 4O  8 50EDTCDE(1)
     A            DSB05          6Y 0O  8 58EDTWRD('  /  /  ')
     A            DSB13          6Y 0O  8 68EDTWRD('  /  /  ')
     A            SB14      R        O  8 78
     A*------------------------------------------------------------
     A*
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920918  190723  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A* 53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'SOA002'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'客戶產品報價設定'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  5'2=修改'
     A                                  5 15'3=拷貝'
     A                                  5 36'5=查詢'
     A                                  5 51'6=確認'
     A                                  6  3'A  客戶代號／簡稱　　　　　產品+
     A                                      代號　折扣別　折扣率　生效日期　+
     A                                      截止日期 F'
     A                                      DSPATR(UL)
     A            DBGN1     R        B  7  6REFFLD(SB01)
     A            DBGN2     R        B  7 31REFFLD(SB02)
     A                                  5 25'4=刪除'
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A  97        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR9          63A  O 24  2MSGID(URE 0018 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R DSPD2
     A*%%TS  SD  20021015  092233  REPGMR      REL-V4R4M0  5769-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CF03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOA002'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'客戶產品報價設定'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  3'客戶代號:'
     A                                      DSPATR(HI)
     A            DSB01     R        B  5 16REFFLD(SB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            ME04      R        O  5 26
     A                                  5 51'確認:'
     A            DSB14     R        O  5 59REFFLD(SB14)
     A                                  7  3'產品代號:'
     A                                      DSPATR(HI)
     A            DSB02     R        B  7 16REFFLD(SB02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DMA03         22O  O  7 26
     A                                  7 51'單位:'
     A            MA13      R        O  7 59
     A                                  9  3'折扣別　:'
     A            DSB04     R        B  9 16REFFLD(SB04)
     A                                      VALUES('N' 'S' 'A')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 26'N :普通折扣'
     A                                  9 44'S :特別折扣'
     A                                  9 60'A :搭贈'
     A                                 11  3'折扣率:'
     A            DSB06          6Y 4B 11 13EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 21'%'
     A                                 11 43'經銷價:'
     A            DMA40          9Y 2O 11 53EDTCDE(3)
     A                                 12 43'零售價:'
     A            DMA41          9Y 2O 12 53EDTCDE(3)
     A                                 13  3'搭贈比率:'
     A            DSB07     R        B 13 16REFFLD(SB07)
     A                                      EDTCDE(Z)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 20'/'
     A            DSB08     R        B 13 22REFFLD(SB08)
     A                                      EDTCDE(Z)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 28'(搭贈數量/訂購數量)'
     A            DSB09     R        B 14 16REFFLD(SB09)
     A                                      EDTCDE(Z)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 20'/'
     A            DSB10     R        B 14 22REFFLD(SB10)
     A                                      EDTCDE(Z)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSB11     R        B 15 16REFFLD(SB11)
     A                                      EDTCDE(Z)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 20'/'
     A            DSB12     R        B 15 22REFFLD(SB12)
     A                                      EDTCDE(Z)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  3'生效日期:'
     A            DSB05          6Y 0B 17 16EDTWRD('  /  /  ')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 28'(MM/DD/YY)'
     A                                 19  3'截止日期:'
     A            DSB13          6Y 0B 19 16EDTWRD('  /  /  ')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19 28'(MM/DD/YY)'
     A                                 22  1'執行'
     A                                 22  9'F3 =回主畫面'
     A                                 22 25'F4 =視窗查詢'
     A                                 22 41'F5 =參考資料'
     A                                 22 57'F12=回前畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR8          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR10         63A  O 24  2MSGID(URE 0020 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 11 23'售價:'
     A                                      DSPATR(RI)
     A            PRICE          9Y 2O 11 31EDTCDE(3)
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
