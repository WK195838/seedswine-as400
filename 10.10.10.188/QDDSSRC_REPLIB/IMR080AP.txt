     A*===============================================================
     A* CODE | AUTHOR |  DATE  | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001 |MICHELLE|03.07.21|為表達單據異動日與確認日之不同
     A*      |        |        |特加上打單日期以及單據號碼,查核用
     A*---------------------------------------------------------------
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    35MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    47'存貨明細帳'
     A                                      SPACEA(2)
     A                                     1'公司別　:'
     A            DIN01     R             +1REFFLD(IN01)
     A            #A03      R             +1
     A                                    84'日期:'
     A            $EGMDY         6  0O    92EDTCDE(Y)
     A                                   101'頁次:'
     A                                   109PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'產品代號:'
     A            DIN04S    R             13REFFLD(IN04)
     A                                    +1'-'
     A            DIN04E    R             +1REFFLD(IN04)
     A                                    84'時間:'
     A                                    92TIME
     A                                   101'<IMR080A>'
     A                                      SPACEA(1)
     A                                     1'確認年月:'
     A            DIN02S         4  0     +1EDTWRD('  /  ')
     A*                                   +1'-'
     A*           DIN02E         4  0     +1EDTWRD('  /  ')
     A                                    84' USER :'
     A            $USER         10A       92
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
M001AA                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                    56'----------異　動---------'
     A                                    84'----------餘　額---------'
     A                                      SPACEA(1)
     A                                     1'產品代號／簡稱'
     A                                    29'確認日期'
     A                                    39'單別／說明'
     A                                    61'數量'
     A                                    77'金額'
     A                                    89'數量'
     A                                   105'金額'
M001AA                                    +2'單據日期'
  "  A                                    +2'單據號碼'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
M001AA                                       --------------------+
     A                                       --------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            WFB201         9         1
     A            MA03          18        11
     A N20                                30'期初'
     A  20                                30'期末'
     A            TQTY2          8  0     84
     A                                      EDTCDE(M)
     A            TCOST2    R             96REFFLD(MA26)
     A                                      EDTCDE(M)
     A  20                                  SPACEA(1)
     A  20                                 1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
M001AA                                       --------------------+
     A                                       --------------------'
     A*----------------------------------------------------------------
     A          R PD2                       SPACEA(1)
     A N30        PB202          6  0     30EDTWRD('  /  /  ')
     A            WFB203         2        +2
     A            P#203         16        +0
     A            TQTY1          8  0     58
     A                                      EDTCDE(M)
     A            TCOST1    R             68REFFLD(MA26)
     A                                      EDTCDE(M)
     A            TQTY2          8  0     84
     A                                      EDTCDE(M)
     A            TCOST2    R             96REFFLD(MA26)
     A                                      EDTCDE(M)
M001AA            WFB211    R             +4REFFLD(#AXX)
  "  A                                      EDTWRD('    /  /  ')
M001AA            WFB212    R             +2REFFLD(SC02)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
M001AA                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
M001AA                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'<<IMR080A>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE9                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
