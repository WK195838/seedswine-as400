     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : ARR058D                                    *
     A*    AUTHOR       : ANDY                                       *
     A*    CREATE DATE  : 81/08/20                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR0581                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購資料子系統                            *
     A*    REMARK       :客戶收款未分配餘額表                      *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    43MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    46'客戶收款未分配餘額表'
     A                                      SPACEA(1)
     A                                    85'日期:'
     A            $EGMDY         6  0O    +1EDTCDE(Y)
     A                                    +1'頁次:'
     A                                    +1PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'客戶代號:'
     A            DAC23S         9        +1
     A                                    +1'-'
     A            DAC23E         9        +1
     A                                    85'時間:'
     A                                    +1TIME
     A                                      SPACEA(1)
     A                                     1'收款日期:'
M001DA*           DAC04S    R             +1REFFLD(PF11)
M001AA            DAC04S         6  0     +1
     A                                      EDTWRD('  /  /  ')
     A                                    +2'-'
M001DA*           DAC04E    R             +1REFFLD(PF11)
M001AA            DAC04E         6  0     +1
     A                                      EDTWRD('  /  /  ')
     A                                    85' USER :'
     A            $USER         10   O    +1
     A                                    +2'<ARR058P>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =============='
     A                                      SPACEA(1)
     A                                     1'客戶代號／客戶簡稱'
     A                                    32'收款日期'
     A                                    43'公司'
     A                                    49'收款種類'
     A                                    61'票據號碼'
     A                                    78'收款金額'
     A                                    91'未分配餘額'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A  30        AC02      R              2
     A  30                                 7'-'
     A  30        AC03      R              8
     A  30        ME04      R             12
M001DA*           PAC04     R             33REFFLD(AC04)
M001AA            PAC04          6  0     33
     A                                      EDTCDE(Y)
     A            AC01      R             45REFFLD(AC01)
     A            AC05      R             49REFFLD(AC05)
     A            MARK           8O       52
     A            AC06      R             61REFFLD(AC06)
     A            AC08      R             76EDTCDE(J)
     A                                      REFFLD(AC08)
     A            AC10      R             91EDTCDE(J)
     A                                      REFFLD(AC10)
     A*----------------------------------------------------------------
     A          R PD2                       SPACEA(1)
     A                                     1'客戶小計------------------
     A                                      -----------------------------
     A                                      ----------------->'
     A            AC08C     R             76EDTCDE(J)
     A                                      REFFLD(AC08)
     A            AC10C     R             91EDTCDE(J)
     A                                      REFFLD(AC10)
     A*----------------------------------------------------------------
     A          R PD3
     A                                     1' '
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =============='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'總　計--------------------
     A                                      -----------------------------
     A                                      ----------------->'
     A            AC08T     R             76EDTCDE(J)
     A                                      REFFLD(AC08)
     A            AC10T     R             91EDTCDE(J)
     A                                      REFFLD(AC10)
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =============='
     A                                      SPACEA(1)
     A                                     2'<<ARR058P>>--->報表結束'
