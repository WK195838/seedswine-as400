     A****************************************************************
     A*                                                              *
     A*    DSPF NAME    : CHK001D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 06/08/94                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : CHK001                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款子系統                            *
     A*    REMARK       :檢核月檔沖帳檔                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'CHK001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'檢核月檔沖帳檔'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  7 26'公司別　:'
     A            DAK03S    R        B  7 38REFFLD(AK03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 44'-'
     A            DAK03E    R        B  7 46REFFLD(AK03)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 26'客戶代號'
     A                                      DSPATR(HI)
     A            DAK04S    R        B  9 38REFFLD(AK04)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 44'-'
     A            DAK04E    R        B  9 46REFFLD(AK04)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 11 26'年月　　:'
     A            DAK01S         4Y 0B 11 38EDTWRD('  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 11 44'-'
     A            DAK01E         4Y 0B 11 46EDTWRD('  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 11 52'(MM/YY)'
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備　８０格報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          63A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          63A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR8          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
