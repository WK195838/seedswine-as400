     A*%%TS  SD  19930326  132919  D910091     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : ARR068D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 82/03/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR068                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :出貨統一發票核對表                        *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930326  132919  D910091     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR068'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'出貨統一發票核對表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 28'公司別　'
     A                                  6 39':'
     A            DSI01S    R        B  6 41REFFLD(SI01)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                  6 50'-'
     A            DSI01E    R        B  6 52REFFLD(SI01)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                  8 28'物流點　'
     A                                  8 39':'
     A            DSI05S    R        B  8 41REFFLD(SI05)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  8 50'-'
     A            DSI05E    R        B  8 52REFFLD(SI05)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 10 28'送貨日期'
     A                                 10 39':'
M001DA*           DSI06S    R        B 10 41REFFLD(SI06)
M001AA            DSI06S         6  0B 10 41
     A                                      EDTWRD('  /  /  ')
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                 10 50'-'
M001DA*           DSI06E    R        B 10 52REFFLD(SI06)
M001AA            DSI06E         6  0B 10 52
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 10 61'(MM/DD/YY)'
     A                                 16 28'執行環境'
     A                                 16 39':'
     A            $EVR           1Y 0B 16 41VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 44'(1-即時   2-批次)'
     A                                 17 28'列印份數'
     A                                 17 39':'
     A            $CPY           3Y 0B 17 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 28'列表機'
     A                                      DSPATR(HI)
     A                                 18 39':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 41CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          63A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          63A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
