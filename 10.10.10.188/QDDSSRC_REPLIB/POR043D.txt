     A*%%TS  SD  19921002  104758  D910095     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : POR043D                                    *
     A*    AUTHOR       : A1118  PHILP                               *
     A*    CREATE DATE  : 81/04/23                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POR043                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購資料子系統                            *
     A*    REMARK       :採購單                                    *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921002  104758  D910095     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'POR043'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 36'採購單'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 26'公司別  :'
     A            DPB01     R        B  5 38REFFLD(PB01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                 12 27'C.挑選單號:'
     A            DPB02S    R        B 12 41REFFLD(PB02)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 12 54'-'
     A            DPB02E    R        B 12 56REFFLD(PB02)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 16 26'執行環境:'
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 31'***請準備套版紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                  7 26'採購日期:'
M001DA*           DPB04S    R        B  7 38REFFLD(RERFR/PB04 *LIBL/RERF)
M001AA            DPB04S         6  0B  7 38
     A                                      EDTCDE(Y)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                  7 51'-'
M001DA*           DPB04E    R        B  7 53REFFLD(RERFR/PB04 *LIBL/RERF)
M001AA            DPB04E         6  0B  7 53
     A                                      EDTCDE(Y)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                  7 63'(MM/DD/YY)'
     A                                  6 26'供應商　:'
     A            DPB05S    R        B  6 38REFFLD(RERFR/PB05 *LIBL/RERF)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                  6 51'-'
     A            DPB05E    R        B  6 53REFFLD(RERFR/PB05 *LIBL/RERF)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                  9 26'選　項　:'
     A            DSEL           1A  B  9 38VALUES(' ' 'A' 'B' 'C')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 10 27'A.正常列印'
     A                                 11 27'B.夾紙重印'
     A                                 11 40'-'
     A                                 11 42'起始頁次:'
     A            PNBR           2S 0B 11 54
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A            $EVR           1Y 0O 16 40EDTCDE(3)
