     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIS000D3                                   *
     A*    AUTHOR       : A1491   EASON                              *
     A*    CREATE DATE  : 98/05/15                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : KIS000C3                                   *
     A*    SYSTEM       :茂世後台                                  *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :主畫面                                    *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPC1
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                  1  2'KIS000C3'
     A                                  1 25MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'專櫃報表管理主畫面'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70DSPATR(UL)
     A                                  5  5'採購作業'
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2'01.進貨明細表'
     A                                  7  2'02.國內驗收金額月彙總表'
     A                                  8  2'03.部門銷售金額總表'
     A                                  9  2'04.茂世產品彙總表'
     A                                 10  2'05.其他產品彙總表'
     A                                 11  2'06.大榮送貨單'
     A                                 12  2'07.專櫃茂世產品彙總'
     A                                 13  2'08.進貨分析表'
     A                                 14  2'09.物流點庫存報表'
     A                                 15  2'10.物流點庫存報表-年終'
     A                                 16  2'11.產品名稱報表'
     A                                 17  2'12.專櫃銷售日報表'
     A                                  5 34'銷售作業'
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 60'調撥作業'
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20 29'請輸入選擇項目:'
     A            DOPID          2A  B 20 47DSPATR(UL)
     A                                      VALUES('01' '02' '03' '04' '05' '06-
     A                                      ' '07' '08' '09' '10' '11' '12')
     A                                 23  2'執行   F3 = 回主畫面'
     A                                      COLOR(BLU)
     A  98        ERR1          60A  O 24  2MSGID(UPT 2023 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
