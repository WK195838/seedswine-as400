     A*%%TS  SD  19961128  135030  D910454     REL-V3R1M0  5763-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : SOA032D                                    *
     A*    AUTHOR       : A1092  TAYLOR                              *
     A*    CREATE DATE  : 11/26/96                                   *
     A*    UPDATE DATE  :   /  /   PHILIP                            *
     A*    PROGRAM NAME : SOA032                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            DNEW2          1A  H
     A            RRN2           4S 0H
     A            DOPT2          1A  B 10  2VALUES(' ' '1' '2' '4')
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DSJ03     R        B 10  5REFFLD(SJ03)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            DMA03     R        O 10 16REFFLD(MA03)
     A            DSJ04     R        B 10 36REFFLD(SJ04)
     A                                      EDTCDE(O)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DSJ05     R        B 10 46REFFLD(SJ05)
     A                                      EDTCDE(O)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A            DSJ07     R        B 10 55REFFLD(SJ07)
     A                                      EDTCDE(O)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A            DSJ06     R        O 10 68REFFLD(SJ06)
     A                                      EDTCDE(O)
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(1200)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF05(05)
     A                                      CF08(08)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                      CSRLOC(#LIN       #COL)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'SOA032'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'退貨無發票資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公　　司:'
     A            PSI01     R        O  4 14REFFLD(SI01)
     A            D#B03     R        O  4 27REFFLD(#B03)
     A                                  5  2'發票號碼:'
     A            PSI02     R        O  5 14REFFLD(SI02)
     A            DME04     R        O  5 27REFFLD(ME04)
     A                                  7  2'輸入作業選擇:'
     A                                  7 20'1=新增     2=修改     4=刪除'
     A                                  9  2'A  產品代號　　產品簡稱　　　　+
     A                                      　　數量　　　單價　　　　折扣　+
     A                                      　　　　　金額'
     A                                      DSPATR(UL)
     A*           DMA01     R        B  9  5REFFLD(MA01)
     A*                                     CHANGE(27)
     A          R DSPC1
     A*%%TS  SD  19961128  130615  D910454     REL-V3R1M0  5763-PW1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24  2'執行'
     A                                 24 12'F3 =離開'
     A                                 24 26'F12 =回主畫面'
     A                                 24 46'F5 =參考資料'
     A                                 24 66'F8 =REFRESH'
