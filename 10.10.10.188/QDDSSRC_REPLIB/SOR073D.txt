     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : SOR073D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 05/20/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR073                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :訂單系統                                  *
     A*    REMARK       :組別產品銷售彙總表                        *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR073'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'組別產品銷售彙總表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 26'辦事處　:'
     A            COMP           1A  B  5 38VALUES(' ' '1' '2' '3')
     A                                  5 40'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  6 26'部門組別:'
     A                                      DSPATR(HI)
     A            DSC28S    R        B  6 38REFFLD(SC28)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N60                                  DSPATR(HI)
     A                                  6 47'-'
     A            DSC28E    R        B  6 49REFFLD(SC28)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  7 26'業務員　:'
     A                                      DSPATR(HI)
     A            DSC27S    R        B  7 38REFFLD(SC27)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A N66                                  DSPATR(HI)
     A                                  7 47'-'
     A            DSC27E    R        B  7 49REFFLD(SC27)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A N66                                  DSPATR(HI)
     A                                  8 26'產品代號:'
     A                                      DSPATR(HI)
     A            DSD03S    R        B  8 38REFFLD(SD03)
     A N68                                  DSPATR(HI)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                  8 48'-'
     A            DSD03E    R        B  8 50REFFLD(SD03)
     A N69                                  DSPATR(HI)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                  9 26'客戶代號:'
     A                                      DSPATR(HI)
     A            DSC04S    R        B  9 38REFFLD(SC04)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A N70                                  DSPATR(HI)
     A                                  9 47'-'
     A            DSC04E    R        B  9 49REFFLD(SC04)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A N71                                  DSPATR(HI)
     A                                 11 26'送貨日期:'
M001DA*           DSC08S    R        B 11 38REFFLD(SC39)
M001AA            DSC08S         6  0B 11 38
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
M001DA*           YSC39S    R        H      REFFLD(SC39)
M001AA            YSC39S         6  0H
     A                                 11 47'-'
M001DA*           DSC08E    R        B 11 49REFFLD(SC39)
M001AA            DSC08E         6  0B 11 49
     A                                      EDTWRD('  /  /  ')
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
M001DA*           YSC39E    R        H      REFFLD(SC39)
M001AA            YSC39E         6  0H
     A                                 11 60'(MM/DD/YY)'
     A                                 12 26'確認日期:'
M001DA*           DSC39S    R        B 12 38REFFLD(SC39)
M001AA            DSC39S         6  0B 12 38
     A                                      EDTWRD('  /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 12 47'-'
M001DA*           DSC39E    R        B 12 49REFFLD(SC39)
M001AA            DSC39E         6  0B 12 49
     A                                      EDTWRD('  /  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 12 60'(MM/DD/YY)'
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 42'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備　８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          63A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          63A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR9          63A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
