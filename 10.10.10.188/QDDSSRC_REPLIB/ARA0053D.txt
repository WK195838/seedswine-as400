     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.09 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M004      | 00.10.16 |顯示正負數
     A*---------------------------------------------------------------
     A*%%TS  SD  19940531  170358  D910121     REL-V2R2M0  5738-PW1
     A*  81/02/13  15:20:16    REPGMR      REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19930111  112334  D910091     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            AD01      R        O 10  2
     A            AD02      R        O 10  5
     A            AD03      R        O 10 11
     A            ME04D         16   O 10 16
M001MA*           DAD04     R        O    +1REFFLD(AD04)
M001MA            DAD04          6  0O    +1
     A                                      EDTCDE(Y)
     A            AD05C          6O  O    +1
     A            AD06D         12   O    +1
M001MA*           DAD12     R        O    +1REFFLD(AD12)
M001MA            DAD12          6  0O    +1
     A                                      EDTWRD('  /  /  ')
     A            AD08      R        O    +1EDTCDE(3)
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19940531  170358  D910121     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'ARA005'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR004'
     A                                  2 29'發票收款狀態查詢設定'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'發票公司:'
     A            SI01      R        O  4 14
     A            #B03      R        O  4 17REFFLD(#B03)
     A                                  4 40'發票號碼:'
     A            SI02      R        O  4 52
     A                                  5  2'發票金額:'
M004MA*           SI25      R        O  5 14EDTCDE(1)
M004MA            SI25      R        O  5 14EDTCDE(J)
     A                                  6  2'已沖金額:'
     A            SI26      R        O  6 14EDTCDE(1)
     A                                  6 40'未沖金額:'
M004MA*           SI30      R        O  6 52EDTCDE(1)
M004MA            SI30      R        O  6 52EDTCDE(J)
     A                                  7  2'價差金額:'
     A            SI27      R        O  7 14EDTCDE(1)
     A                                  7 40'銷退金額:'
     A            SI28      R        O  7 52EDTCDE(1)
     A                                  9  1'司客戶代號                 +
     A                                      收款日期種類 票據號碼    日+
     A                                      期  沖銷金額'
     A                                      DSPATR(UL)
     A                                  8 42'收款'
     A                                  4 63'                  '
     A                                  4 34'     '
     A                                  8 63'分配'
     A                                  8  1'公'
     A*------------------------------------------------------------
     A*
     A          R DSPC1
     A*%%TS  SD  19920423  155729  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                    +2'F12 =回前畫面'
     A  97        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR9          63A  O 24  2MSGID(URE 0018 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 64MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
