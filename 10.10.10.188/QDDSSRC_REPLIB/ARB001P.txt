     A*================================================================
     A* MODI CODE |  AUTHOR  |  DATE  | DESCRIPTION
     A*----------------------------------------------------------------
     A* M004      | MICHELLE |00.05.15|菸酒稅法實施
     A*================================================================
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    27
     A                                      MSGCON(030 URE9999 REMF)
     A                                    61'日期:'
     A            $EGMDY         6  0O    69EDTCDE(Y)
     A                                    78'頁次:'
     A                                    86PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    34
     A                                      DFT('檢核銷退價差')
     A                                    61'時間:'
     A                                    69TIME
     A                                      SPACEA(1)
     A                                    61' USER :'
     A            $USER         10   O    69
     A                                    80'<ARB001>'
     A                                      SPACEA(1)
     A                                     1'==============================-
     A                                      ===================================-
     A                                      ========================='
     A                                      SPACEA(1)
     A                                     1'銷退價差'
     A                                    20'發票號碼'
     A                                    37'發票金額'
     A                                    52'折讓金額'
M004AA                                    67'折讓稅額'
     A                                    82'差異金額'
     A                                      SPACEA(1)
     A                                     1'-------------------------------
     A                                      ------------------------------------
     A                                      -------------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            STAT           6   O     2
     A            SI02      R        O    20
     A            SI278     R        O    35EDTCDE(J)
     A                                      REFFLD(SI27)
     A            AGF07     R        O    50EDTCDE(J)
     A                                      REFFLD(SI27)
M004AA            AGF09     R        O    65EDTCDE(J)
M004AA                                      REFFLD(SI27)
     A            DIF       R        O    80EDTCDE(J)
     A                                      REFFLD(SI27)
     A*----------------------------------------------------------------
     A          R PE1                       SPACEA(1)
     A                                     1'==============================-
     A                                      ===================================-
     A                                      ========================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2                       SPACEA(1)
     A                                     1'==============================-
     A                                      ===================================-
     A                                      ========================='
     A                                      SPACEA(1)
     A                                     2'<<ARB001P>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
