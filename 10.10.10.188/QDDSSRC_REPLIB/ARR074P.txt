     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    PRTF NAME    : ARR074P                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 12/01/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR074                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :轉帳銷退折讓單明細表                      *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    45
     A                                      MSGCON(030 URE9999 REMF)
     A                                      SPACEA(001)
     A                                    42'營業人進（銷）貨退出或折讓證明表-
     A                                      '
     A                                      SPACEA(002)
     A                                     1'辦事處:'
     A            COMP           1   O    +1
     A            TEXT           6   O    +1
     A                                    91'日期:'
     A            $EGMDY         6  0O    99EDTCDE(Y)
     A                                   109'頁次:'
     A                                   116PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司別:'
     A            DSG01S         2   O    +1
     A                                    +1'-'
     A            DSG01E         2   O    +1
     A                                    91'時間:'
     A                                    99TIME
     A                                      SPACEA(001)
     A                                     1'折讓日期:'
     A            DSG26S         6  0O    +1EDTCDE(Y)
     A                                    +1'-'
     A            DSG26E         6  0O    +1EDTCDE(Y)
     A                                    91' USER :'
     A            $USER         10A  O    99
     A                                   109'<ARR074P>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEA(001)
     A                                     1'聯式'
     A                                     8'年'
     A                                    13'月'
     A                                    18'日'
     A                                    23'字軌'
     A                                    30'號碼'
     A                                    39'品名'
     A                                    58'數量'
     A                                    65'單價'
     A                                    71'金額（不含稅之進貨額）'
     A                                    95'營業稅額'
     A                                   107'折讓單號'
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------'
     A                                      SPACEA(001)
     A          R PD1
     A                                      SPACEA(001)
     A            SI17      R        O     4
     A            PSIYY          2   O     9
     A            PSIMM          2   O    14
     A            PSIDD          2   O    19
     A            PSI02F         2   O    25
     A            PSI02B         8   O    30
     A            MA03      R        O    39
     A            QTY       R        O    57
     A                                      REFFLD(AG05)
     A                                      EDTCDE(1)
     A            AMT       R        O    64
     A                                      REFFLD(AG06)
     A                                      EDTCDE(1)
     A            DATA1         10  0O    78
     A                                      EDTCDE(1)
     A            DATA2          5  0O    97
     A                                      EDTCDE(1)
     A            DFLD           4   O   105
     A            AG02      R        O   109
     A*----------------------------------------------------------------
     A          R PT1
     A                                      SPACEA(001)
     A                                    57
     A                                      '搭贈折讓金額------>'
     A            T1TOT1        10  0O    78
     A                                      EDTCDE(J)
     A            T1TOT2         5  0O    97
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT2
     A                                      SPACEA(001)
     A                                    57
     A                                      '每頁小計---------->'
     A            T2TOT1        10  0O    78
     A                                      EDTCDE(J)
     A            T2TOT2         5  0O    97
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT3
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEA(001)
     A                                    57
     A                                      '總　　計---------->'
     A            T3TOT1        10  0O    78
     A                                      EDTCDE(J)
     A            T3TOT2         5  0O    97
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEA(001)
     A                                     2
     A                                      '***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEA(001)
     A                                     2
     A                                      '<<ARR074P>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                      SPACEA(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
