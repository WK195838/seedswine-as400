     A*%%TS  SD  19920602  125519  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PAA010D                                    *
     A*    AUTHOR       : A1005  ROGER CHIEN                         *
     A*    CREATE DATE  : 80/11/20                                   *
     A*    UPDATE DATE  : 81/01/14 BY STEPHANIE                      *
     A*    PROGRAM NAME : PAA010                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料建檔子系統                        *
     A*    REMARK       :業務員部門對照資料維護                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.10.26 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19920530  082821  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  4VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            #601      R        O  9  8
     A            ME04      R        O  9 19
     A            #602      R        O  9 40
     A            #603      R        O  9 46
     A            ME04B     R        O  9 57REFFLD(ME04)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920602  125519  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'PAA013'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 28'業務員部門對照資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  5'2=修改'
     A                                  5 15'4=刪除'
     A                                  5 25'5=查詢'
     A                                  7  4'A  業務員   業務員說明     -
     A                                          部門            部門說明   -
     A                                      '
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  8REFFLD(#601)
     A          R DSPC11
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A                                    +2'F9 =列印'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPC2
     A*%%TS  SD  19920127  161113  REPGMR      REL-V2R1M0  5738-PW1
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  1  2'PAA010'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 28'業務員部門對照資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'業務員代號:'
     A            D#601     R        B  4 16REFFLD(#601)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            ME04      R        O  4 22REFFLD(ME04)
     A                                  5  2'部門代號:'
     A            D#602     R        B  5 14REFFLD(#602)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#603     R        B  5 20REFFLD(#603)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            ME04B     R        O  5 26REFFLD(ME04)
     A                                 19  2'異動日期:'
M001MA*           D#6XX     R        O 19 14REFFLD(#6XX)
M001MA            D#6XX          6  0O 19 14
     A                                      EDTCDE(Y)
     A                                 19 23'(MM/DD/YY)'
     A                                 20  2'異動時間:'
     A            D#6YY     R        O 20 14REFFLD(#6YY)
     A                                      EDTWRD('  :  :  ')
     A                                 21  2'使用者　:'
     A            D#6ZZ     R        O 21 14REFFLD(#6ZZ)
     A                                 22  2'執行'
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F12=回前畫面'
     A  98        ERR1          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
