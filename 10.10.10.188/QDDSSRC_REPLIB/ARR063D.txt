     A****************************************************************
     A* 00A需求單號: 1040761                                       *
     A* 00A 2015/08/17 DEREK產生為EXCEL 格式                     *
     A****************************************************************
     A*%%TS  SD  19930209  190700  D910074     REL-V2R1M0  5738-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930209  190700  D910074     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CA04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR063'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'應收帳款彙總表'
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  7 26'公司別　:'
     A            DAK03S    R        B  7 38REFFLD(AK03)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 41'-'
     A            DAK03E    R        B  7 43REFFLD(AK03)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 26'客戶代號'
     A                                      DSPATR(HI)
     A            DAK04S    R        B  9 38REFFLD(AK04)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 44'-'
     A            DAK04E    R        B  9 46REFFLD(AK04)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 11 26'業務員　'
     A                                      DSPATR(HI)
     A            DME15S    R        B 11 38REFFLD(ME15)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 11 45'-'
     A            DME15E    R        B 11 47REFFLD(ME15)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 13 26'年月　　:'
     A            DAK01          4Y 0B 13 38EDTWRD('  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 13 44'(MM/YY)'
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 39VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 41'(1-即時   2-批次)'
     A                                 17 26'列印份數'
     A                                 17 37':'
     A            $CPY           3Y 0B 17 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機'
     A                                      DSPATR(HI)
     A                                 18 37':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
 00A+A                                 21 27'***產生Ｅｘｃｅｌ檔案***'
 00A+A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A  89        ERR12         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
