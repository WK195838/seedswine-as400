     A*  90/07/03  17:15:42    A1062       REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : S#A130D                                    *
     A*    AUTHOR       : A1062   JEANNY LIN                         *
     A*    CREATE DATE  : 79/06/29                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : S#A130                                     *
     A*    SYSTEM       :程式安全管制系統                          *
     A*    PURPOSE      :程式管制資料維護                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A*  90/07/03  17:15:42    A1062       REL-R02M00  5728-PW1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  3'<S#A130D>'
     A                                  1 29'程式安全管制系統'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            DUSER     R        O  2  3REFFLD(SU02)
     A                                  2 29'程式管制資料維護'
     A                                  4  2'程式編號:'
     A            DSD02     R        B  4 14REFFLD(SD02)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  5  2'使用者　:'
     A            DSD011    R        B  5 14REFFLD(SD01)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  5 25'-'
     A            DSD012    R        B  5 27REFFLD(SD01)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  7 27'作業選擇:'
     A            DOPT           1A  B  7 39VALUES('1' '2' '4' '5')
     A                                  7 43'1.新增'
     A                                  8 43'2.修改'
     A                                  9 43'4.刪除'
     A                                 10 43'5.查詢'
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(US# 0003 S#MF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22  2MSGID(UPT 0042 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 22  2MSGID(UPT 2023 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 22  2MSGID(US# 0008 S#MF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 22  2MSGID(US# 0009 S#MF)
     A                                      DSPATR(HI)
     A  90        ERR9          50A  O 22  2MSGID(US# 0007 S#MF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 20'PF18=程式基本資料全頁查詢'
     A                                 24 67'【泛太資訊】'
     A          R SFLSR                     SFL
     A*  90/06/29  18:05:25    A1062       REL-R02M00  5728-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0O  8  3
     A            DUSE           1A  B  8  9VALUES('Y' ' ')
     A                                      CHECK(RB)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            SD01      R        O  8 12REFFLD(SD01)
     A            SU02      R        O  8 24REFFLD(SU02)
     A            SD03      R        B  8 37VALUES('Y' ' ')
     A  31N60                               DSPATR(UL)
     A N31
     AO 60                                  DSPATR(PR)
     A            SD04      R        B  8 40VALUES('Y' ' ')
     A  31N61                               DSPATR(UL)
     A N31
     AO 61                                  DSPATR(PR)
     A            SD05      R        B  8 43VALUES('Y' ' ')
     A  31N62                               DSPATR(UL)
     A N31
     AO 62                                  DSPATR(PR)
     A            SD06      R        B  8 46VALUES('Y' ' ')
     A  31N63                               DSPATR(UL)
     A N31
     AO 63                                  DSPATR(PR)
     A            SD07      R        B  8 53VALUES('Y' ' ')
     A  31N64                               DSPATR(UL)
     A N31
     AO 64                                  DSPATR(PR)
     A            SD08      R        B  8 64VALUES('Y' ' ')
     A  31N65                               DSPATR(UL)
     A N31
     AO 65                                  DSPATR(PR)
     A            SD09      R        B  8 75VALUES('Y' ' ')
     A  31N66                               DSPATR(UL)
     A N31
     AO 66                                  DSPATR(PR)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  90/06/28  18:18:52    A1062       REL-R02M00  5728-PW1
     A                                      SFLSIZ(0099)
     A                                      SFLPAG(0014)
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CA12(12)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(UPT2030 *LIBL/PTMF)
     A  97                                  SFLMSGID(UPT2040 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  3'<S#A130D>'
     A                                  1 29'程式安全管制系統'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            DUSER     R        O  2  3REFFLD(SU02)
     A                                  2 29'程式管制資料維護'
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  4  2'程式編號:'
     A            DSD02     R        O  4 14REFFLD(SD02)
     A            SP02      R        O  4 25REFFLD(SP02)
     A                                  4 60'類別:'
     A            DSP03          8O  O  4 68
     A                                  5  2'使用者　:'
     A            DSD011    R        O  5 14REFFLD(SD01)
     A                                  5 25'-'
     A            DSD012    R        O  5 27REFFLD(SD01)
     A                                  6  2'序號USE使用者編號　使用者名稱'
     A                                  6 37'A'
     A                                  6 40'U'
     A                                  6 43'D'
     A                                  6 46'I'
     A            SP04      R        O  6 48
     A            SP05      R        O  6 59
     A            SP06      R        O  6 70
     A                                  7  3'---- --- ---------- ------------  -
     A                                      -  -  -  - ---------- ---------- ---
     A                                      --------'
     A          R DSPC2
     A*  90/07/03  17:15:42    A1062       REL-R02M00  5728-PW1
     A  96        ERR1          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 20'PF5 =重新授權'
     A                                 23 38'PF12=回前畫面'
     A                                 24  2'PA1 =下一頁'
     A                                 24 20'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
