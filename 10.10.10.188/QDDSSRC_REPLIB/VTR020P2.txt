     A*===============================================================
     F*  CODE | AUTHOR |  DATE  |  DESCRIPTION
     F*================================================================
     F*       |        |        |
     F*----------------------------------------------------------------
     F*================================================================
     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    54MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    56'媒體申報人工輸入彙總表'
     A                                      SPACEA(1)
     A                                     1'公司別  :'
     A            DVR91          1A       14
     A            NAME           6O       17
     A                                      SPACEA(1)
     A                                     1'辦事處　:'
     A            DVR92          1A       14
     A            OFFICE         6O       17
     A                                      SPACEA(1)
     A                                     1'申報年月:'
     A            DVR04S         4  0     14EDTWRD('0 /  ')
     A                                    20'-'
     A            DVR04E         4  0     22EDTWRD('0 /  ')
     A                                    +2'(MM/YY)'
     A                                      SPACEA(1)
     A                                     1'資料選擇:'
     A            DOPT           1        14
                  DMARK         10O       +2
     A                                      SPACEA(1)
     A                                     1'課稅別  :  1應稅2零稅率 +
     A                                      3免稅'
     A                                   101'日期:'
     A            $EGMDY         6  0O   109EDTCDE(Y)
     A                                   119'頁次:'
     A                                   127PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'員工編號:'
     A            DVR96          6        14
     A            EMPNAM        12O       +2
     A                                   101'時間:'
     A                                   109TIME
     A                                      SPACEA(1)
     A                                     1'異動日期:'
     A            DVR97          6  0     14EDTWRD('  /  /  ')
     A                                   102'USER :'
     A            $USER         10       109
     A                                   120'<VTR020P2>'
     A                                      SPACEA(1)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================'
     A                                      SPACEA(1)
     A                                     3'年月'
     A                                    17'格式'
     A                                    31'稅別'
     A                                    45'銷售額'
     A                                    71'營業稅'
     A                                    84'張數'
     A                                      SPACEA(1)
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEA(001)
     A  21                                 1
     A                                      DFT(' ')
     A                                      SPACEA(001)
     A N31        WFD04     R              2EDTWRD('    /  ')
     A                                      REFFLD(VR04)
     A N30        WFD01     R             19
     A                                      REFFLD(VR01)
     A            WFD08     R             33
     A                                      REFFLD(VR08)
     A            WFD07     R             37EDTCDE(J)
     A                                      REFFLD(VR07)
     A            WFD09     R             64EDTCDE(J)
     A                                      REFFLD(VR09)
     A            WFD18     R             84EDTCDE(J)
     A                                      REFFLD(VR18)
     A*----------------------------------------------------------------
     A          R PT1
     A                                      SPACEA(001)
     A                                    15
     A                                      DFT('格式小計---->')
     A            T1VR07        12  0     37EDTCDE(J)
     A            T1VR09        10  0    +11EDTCDE(J)
     A            T1VR18         6  0     +3EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT2
     A                                      SPACEA(001)
     A                                    15
     A                                      DFT('年月小計---->')
     A            T2VR07        12  0     37EDTCDE(J)
     A            T2VR09        10  0    +11EDTCDE(J)
     A            T2VR18         6  0     +3EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT3
     A                                      SPACEA(001)
     A            PLACE          6O       15
     A                                    +0
     A                                      DFT('辦事處小計-->')
     A            T3VR07        12  0     37EDTCDE(J)
     A            T3VR09        10  0    +11EDTCDE(J)
     A            T3VR18         6  0     +3EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT4
     A                                      SPACEA(001)
     A                                    15
     A                                      DFT('總　　計---->')
     A            T4VR07        12  0     37EDTCDE(J)
     A            T4VR09        10  0    +11EDTCDE(J)
     A            T4VR18         6  0     +3EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================')
     A                                      SPACEA(001)
     A                                     2
     A                                      DFT('***續下頁***')
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================')
     A                                      SPACEA(001)
     A                                     2
     A                                      DFT('<<VTR020P2>>--->報表結束')
     A*----------------------------------------------------------------
     A          R PE9                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
