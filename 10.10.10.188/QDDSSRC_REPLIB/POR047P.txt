     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : POR047P                                    *
     A*    AUTHOR       : A1118  PHILIP                              *
     A*    CREATE DATE  : 81/01/17                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POR047                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購資料子系統                            *
     A*    REMARK       :國外採購到貨成本單                        *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.12.16 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    41MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    45'國外採購到貨成本單'
     A                                      SPACEA(1)
     A                                    90'日期:'
     A            $EGMDY         6  0O    97EDTCDE(Y)
     A                                   110'頁次:'
     A            PPGE           4  0O   117EDTCDE(Z)
     A                                      SPACEA(1)
     A                                    90'時間:'
     A                                    97TIME
     A                                      SPACEA(1)
     A                                     1'憑証單號'
     A                                    +0':'
     A            PD02      R             +1
     A                                    90' USER :'
     A            $USER         10   O    97
     A                                   110'<POR047P>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     1'供應商名稱'
     A                                    15':'
     A            PD07      R             17
     A            MB02      R             24
     A                                      SPACEA(1)
     A                                     1'幣別'
     A                                    15':'
     A            #A03      R             17
     A                                    95'進貨日期'
     A                                   108':'
M001MA*           PD04      R            112EDTCDE(Y)
M001MA            LPD04          6  0    112EDTCDE(Y)
     A
     A                                      SPACEA(1)
     A                                     1'原幣F.O.B.'
     A                                    15':'
     A            PD10      R             17
     A                                      EDTCDE(J)
     A                                    95'匯率'
     A                                   108':'
     A            PD09      R            111
     A                                      EDTCDE(4)
     A                                      SPACEA(1)
     A                                     1'原幣FREIGHT'
     A                                    15':'
     A            PD11      R             17
     A                                      EDTCDE(M)
     A                                    95'台幣F.O.B.'
     A                                   108':'
     A            PD12      R            110
     A                                      EDTCDE(M)
     A                                      SPACEA(1)
     A                                    95'台幣FREIGHT'
     A                                   108':'
     A            DFRI      R            110REFFLD(PD13)
     A                                      EDTCDE(M)
     A                                      SPACEA(1)
     A                                    95'完稅報關費'
     A                                   108':'
     A            PD14      R            113
     A                                      EDTCDE(M)
     A                                      SPACEA(1)
     A                                    95'調撥運費'
     A                                   108':'
     A            PD15      R            113
     A                                      EDTCDE(M)
     A                                      SPACEA(1)
     A                                    95'保稅報關費'
     A                                   108':'
     A            PD16      R            113
     A                                      EDTCDE(M)
     A                                      SPACEA(1)
     A                                    95'保險費'
     A                                   108':'
     A            PD17      R            113
     A                                      EDTCDE(M)
     A                                      SPACEA(1)
     A                                    95'空運運費'
     A                                   108':'
     A            PD18      R            113
     A                                      EDTCDE(M)
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------'
     A                                      SPACEA(1)
     A                                    29'--採購--'
     A                                    41'單位'
     A                                    52'--庫存--'
     A                                    67'--------- TOTAL ----------'
     A                                   100'----- UNIT ------'
     A                                      SPACEA(1)
     A                                     1'採購單號'
     A                                    15'產品代號'
     A                                    28'數量'
     A                                    34'單位'
     A                                    41'轉換'
     A                                    51'數量'
     A                                    57'單位'
     A                                    67'F.O.B.'
     A                                    76'FREIGHT'
     A                                    88'費用'
     A                                   100'F.O.B.'
     A                                   110'FREIGHT'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            PE03      R              2
     A            PE04      R             16
     A            PE06      R             26EDTCDE(M)
     A            PC09      R             35
     A            PC10      R             42EDTCDE(M)
     A            AMT1           7  0     47EDTCDE(M)
     A            MA13      R             58
     A            PE07      R             64EDTCDE(M)
     A            PE08      R             76EDTCDE(M)
     A            PE09      R             86EDTCDE(M)
     A            UPE07          9  2     94EDTCDE(M)
     A            UPE089         9  2    105EDTCDE(M)
     A*----------------------------------------------------------------
     A*         R PD2
     A*                                    1'====================+
     A*                                      ====================+
     A*                                      ====================+
     A*                                      ====================+
     A*                                      ====================+
     A*                                      ===================='
     A*                                     SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE1                       SPACEB(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2                       SPACEB(1)
     A                                     1'合計'
     A            PE07T          8  0     63EDTCDE(M)
     A            PE08T          6  0     76EDTCDE(M)
     A            PE09T          6  0     86EDTCDE(M)
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'<<POR047P>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE9                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
