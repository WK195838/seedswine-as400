     A*%%TS  SD  19961212  104954  D910454     REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : MTA009D                                    *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 82/09/08                                   *
     A*    PROGRAM NAME : MTA009                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    REMARK       :專櫃ＰＬＵ資料維護 (A/U/D/I)              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.05 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  5VALUES(' ' '3' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            MG01      R        O  9  9
     A            MG02      R        O  9 13
     A            MA03      R        O  9 24
     A            MG03      R        O  9 44
     A            S#A03         16   O  9 48
M001MA*           SMG04     R        O  9 66REFFLD(MG04)
M001MA            SMG04          6  0O  9 66
     A                                      EDTWRD('  /  /  ')
     A            MG05      R        O  9 77
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'MTA009'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'專櫃ＰＬＵ資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  6'2=修改'
     A                                  5 16'3=拷備'
     A                                  5 26'4=刪除'
     A                                  7  5'A   PLU產品代號／產品簡稱       +
     A                                      　　部門代號／簡稱 -
     A                                      　　建檔日期　使用 '
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  9REFFLD(MG01)
     A                                  5 37'5=查詢'
     A          R DSPC1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F6 =新增'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPD2
     A*%%TS  SD  19961212  104954  D910454     REL-V3R1M0  5763-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MTA009'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'專櫃ＰＬＵ資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6  3'PLU代號:'
     A            DMG01     R        B  6 14REFFLD(MG01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  8  2'產品代號:'
     A                                      DSPATR(HI)
     A            DMG02     R        B  8 14REFFLD(MG02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 35'產品簡稱:'
     A            MA03      R        O  8 47
     A                                 10  2'部門代號:'
     A                                      DSPATR(HI)
     A            DMG03     R        B 10 14REFFLD(MG03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 10 35'部門說明:'
     A            D#A03         16A  O 10 47
     A                                 12  2'產品條號:'
     A            DMG06         20A  B 12 14
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  2'建檔日期:'
M001MA*           DMG04     R        O 14 14REFFLD(MG04)
M001MA            DMG04          6  0O 14 14
     A                                      EDTCDE(Y)
     A                                 16  2'使用碼　:'
     A            DMG05          1A  B 16 14VALUES('A' 'I')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16 18'(A,I)'
     A  77                             19  2'該產品已被設定PLU'
     A  77        DXX            3A    19 22COLOR(PNK)
     A                                 22  2'執行'
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 41'F5 =參考資料'
     A                                 22 57'F12=回前畫面'
     A  98        ERR1          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR3          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
