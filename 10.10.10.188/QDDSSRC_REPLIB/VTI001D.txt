     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : VTI001D                                    *
     A*    AUTHOR       : MICHELLE                                   *
     A*    CREATE DATE  : 92/05/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : VTI001                                     *
     A*    SYSTEM       :人頭馬寰盛                                *
     A*    SUBSYSTEM    :媒體申報系統                              *
     A*    REMARK       :發票號碼查詢                              *
      *==============================================================*
      * CODE |  AUTHOR  |  DATE  | DESCRIPTION
      *--------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR                     SFL
     A*%%TS  SD  19920825  115346  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DVQ16         10   O  7  4
     A            VQ91      R        O  7 16
     A            VQ19      R        O  7 20
     A            VQ04      R        O  7 33EDTWRD('    /  ')
     A            VQ01      R        O  7 41
     A            VQ07      R        O  7 44EDTCDE(3)
     A            VQ09      R        O  7 57EDTCDE(3)
     A            VQ15      R        O  7 70
     A*------------------------------------------------------------
     A*
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0028)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'VTI001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'進項發票號碼查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A            DBGN2         10   B  6  4
     A                                  5  3'發票號碼 公司單據號碼 -
     A                                      申報年月格式  -
     A                                      銷售額     -
     A                                      稅額 對方統編'
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
