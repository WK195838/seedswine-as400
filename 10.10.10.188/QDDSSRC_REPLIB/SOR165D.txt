     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR165D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 96/09/06                                   *
     A*    PROGRAM NAME : SOR165                                     *
     A*    SYSTEM       :康齡                                      *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :產品銷售分佈彙總表                        *
     A*===============================================================
     A* MODI CODE |   DATE   |  DESCRIPTION
     A*================================================================
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CF07(07)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR165'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'品牌銷售週報表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  7 28'辦事處　:'
     A            COMP           1A  B  7 40VALUES(' ' '1' '2' '3')
     A                                  7 42'(1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  8 28'通　　路:'
     A                                      DSPATR(HI)
     A            DSC33S    R        B  8 40REFFLD(SC33)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 43'-'
     A            DSC33E    R        B  8 45REFFLD(SC33)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 28'品牌:'
     A                                      DSPATR(HI)
     A            DSD03S         2A  B  9 40
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  9 43'-'
     A            DSD03E         2A  B  9 45
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                 10 28'客戶代號:'
     A                                      DSPATR(HI)
     A            DSC45S         9A  B 10 40
     A N74                                  DSPATR(HI)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A                                 10 50'-'
     A            DSC45E         9A  B 10 52
     A N75                                  DSPATR(HI)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A                                 11 28'送貨日期:'
     A            DSC08S         6Y 0B 11 40EDTWRD('  /  /  ')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                 11 50'-'
     A            DSC08E         6Y 0B 11 52EDTWRD('  /  /  ')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                 11 63'(MM/DD/YY)'
     A                                 12 28'確認日期:'
     A            DSC39S         6Y 0B 12 40EDTWRD('  /  /  ')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                 12 50'-'
     A            DSC39E         6Y 0B 12 52EDTWRD('  /  /  ')
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                 12 63'(MM/DD/YY)'
     A                                 13 28'分析方式:'
     A            DSEL2          1A  B 13 40VALUES('A' 'B')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 13 43'A-禮盒不拆'
     A                                 14 43'B-禮盒拆單瓶'
     A                                 18 28'執行環境:'
     A            $EVR           1Y 0B 18 41VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 18 43'(1-即時   2-批次)'
     A                                 19 28'列印份數'
     A                                 19 39':'
     A            $CPY           3Y 0B 19 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 20 28'列表機'
     A                                      DSPATR(HI)
     A                                 20 39':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 20 41CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 21 29'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F7 =檔案下載'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A  86        ERR13         50A  O 24  2MSGID(UPT 0043 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR12         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A  88        ERR9          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
