     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIP012D                                    *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 98/04/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :茂世進銷存                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :                                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '5' '6' '7')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SBA01          6Y 0O    +2EDTWRD('  /  /  ')
     A            SBA03     R        O    +1REFFLD(BA03 W2BAPF)
     A            SBA02     R        O    +2REFFLD(BA02 W2BAPF)
     A            S#A03     R        O    +1REFFLD(#A03 PA#APF)
     A            HBA01     R        H      REFFLD(BA01 W2BAPF)
     A            SBA06     R        H      REFFLD(BA06 W2BAPF)
     A            SBA08     R        H      REFFLD(BA08 W2BAPF)
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(MSG1006 *LIBL/R1MF)
     A                                  1  2'KIP012'
     A                                  1 26MSGCON(030 MSG9999 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'失敗銷售訂單處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'專櫃代號:'
     A                                      COLOR(BLU)
     A            DBA02     R        B    +1REFFLD(BA02 W2BAPF)
     A  69                                  DSPATR(PC RI)
     A                                      COLOR(BLU)
     A            D#A031    R        O    +1REFFLD(#A03 PA#APF)
     A                                      COLOR(BLU)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                    +1'5=查詢'
     A                                      COLOR(BLU)
     A                                    +1'6=轉檔'
     A                                      COLOR(BLU)
     A                                    +1'7=還原'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN3          6Y 0B  8  6EDTWRD('  /  /  ')
     A                                  6  3'(月/日/年)'
     A                                      COLOR(BLU)
     A                                  7  3'A 訂單日期訂單號碼  專櫃代號-
     A                                       專櫃名稱                   '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 MSG9996 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SBB03     R        O  9  2REFFLD(BB03 W2BBPF)
     A            SMA03     R   -1   O  9 12REFFLD(MA03 MTMAPF)
     A            SAMT1     R   -2   O  9 30REFFLD(AA04 W1AAPF)
     A                                      EDTCDE(3)
     A            SAMT           9Y 2O  9 41EDTCDE(3)
     A            SBB04     R   -1   O  9 52REFFLD(BB04 W2BBPF)
     A                                      EDTCDE(3)
     A            SBB05     R   -1   O  9 60REFFLD(BB05 W2BBPF)
     A                                      EDTCDE(3)
     A            SERR          12A  O  9 68
     A            HERR           2A  H
     A            SBB11     R        H      REFFLD(BB11 W2BBPF)
     A            SBB02     R        H      REFFLD(BB02 W2BBPF)
     A            SBB06     R        H      REFFLD(BB06 W2BBPF)
     A            SBB07     R        H      REFFLD(BB07 W2BBPF)
     A            SBB08     R        H      REFFLD(BB08 W2BBPF)
     A            SBB08A    R        H      REFFLD(BB08A W2BBPF)
     A            SBB08B    R        H      REFFLD(BB08B W2BBPF)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CA03(03)
     A  33                                  CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(MSG1006 *LIBL/R1MF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'KIP012'
     A                                  1 26MSGCON(030 MSG9999 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'失敗銷售訂單處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'專櫃代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        O    +1REFFLD(AF04 W1AFPF)
     A                                      COLOR(BLU)
     A            D#A03     R        O    +1REFFLD(#A03 PA#APF)
     A                                      COLOR(BLU)
     A                                  6  1'訂單日期:'
     A                                      COLOR(BLU)
     A            DBA01          6Y 0O    +1EDTWRD('  /  /  ')
     A                                      COLOR(BLU)
     A                                    +1'單據號碼:'
     A                                      COLOR(BLU)
     A            DBA03     R        O    +1REFFLD(AG01 W1AGPF)
     A                                      COLOR(BLU)
     A                                  8  2'產品代號／名稱               -
     A                                      牌價    單價   銷售  搭贈  -
     A                                      失敗原因   '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  33                                +2'F9 =確認'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 MSG9996 *LIBL/R1MF)
     A                                      DSPATR(HI)
