     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : IMR041P                                    *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : 81/02/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR041                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :送貨揀料彙總表                            *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    31MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    37'送貨揀料彙總表'
     A                                      SPACEA(1)
     A                                     1'通路　　:'
     A            DSEL           1   O    13
     A  21                                15'不含酒店'
     A  22                                15'只選酒店'
     A  23                                15'全部'
     A                                    71'日期:'
     A            $EGMDY         6  0O    78EDTCDE(Y)
     A                                    88'頁次:'
     A                                    95PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'物流點　:'
     A            DSE07     R             +1REFFLD(SE07)
     A            #A031     R   22        +2REFFLD(#A03)
     A                                    71'時間:'
     A                                    78TIME
     A                                      SPACEA(1)
     A                                     1'送貨日期:'
M001MA            DSE06          6  0     +1EDTWRD('  /  /  ')
     A
     A                                    71' USER :'
     A            $USER         10   O    78
     A                                    90'<IMR041>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ========================'
     A                                      SPACEA(1)
     A                                     1'產品識別'
     A                                    25'產品代號'
     A                                    35'產品簡稱'
     A                                    53'單位'
     A                                    61'數量'
     A                                    67'送貨單號'
     A                                    80'訂單單號'
     A                                    93'統一發票'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       ------------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A  32        WF3617         2         1
     A  32        #A032     R              4REFFLD(#A03)
     A  31        WF3616    R             26REFFLD(SF03)
     A  31        MA03      R             35
     A  31        MA13      R             53
     A            PTOT           5  0     60EDTCDE(J)
     A  30        WF3615    R             67REFFLD(SF02)
     A  30        WF3613    R             80REFFLD(SE29)
     A  30        WF3614    R             94REFFLD(SE30)
     A*----------------------------------------------------------------
     A          R PD2                       SPACEA(1)
     A                                    31'產品小計---------------->'
     A            PRTOT          5  0     60EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3                       SPACEA(1)
     A                                    31'識別小計---------------->'
     A            PRTOT1         5  0     60EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD4                       SPACEA(1)
     A                                     2'*****送貨單張數:'
     A            PSF02          4  0     +1EDTCDE(J)
     A                                    31'合　　計---------------->'
     A            PRRTOT         5  0     60EDTCDE(J)
     A******                              +0'（資料列印之筆數）'
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ========================'
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ========================'
     A                                      SPACEA(1)
     A                                     2'<<IMR041>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ========================'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PE4
     A                                     1'                    +
     A                                                           +
     A                                                           +
     A                                                          '
     A                                      SPACEA(1)
