     A*%%TS  SD  19920609  110329  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : IMI021D                                    *
     A*    AUTHOR       : A1092  TAYLOR                              *
     A*    CREATE DATE  : 81/02/17                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMI021                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存子系統                                *
     A*    REMARK       :產品存貨狀況                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920528  132729  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  2VALUES(' ' '5' '6')
     A            MA01      R        O 10  5
     A            MA03      R        O 10 15
     A            MA29      R        O 10 35EDTCDE(J)
     A            MA30      R        O 10 45EDTCDE(J)
     A            MA37      R        O 10 57EDTCDE(J)
     A            MA38      R        O 10 69EDTCDE(J)
     A*--------------------------------------------------------------*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920609  110329  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'IMI021'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'產品存貨狀況'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'輸入作業選擇:'
     A                                  6  6'5=各物流點數量查詢'
     A                                  6 28'6=各物流點庫存金額查詢'
     A                                  8  2'A 產品代號／名稱             +
     A                                      保稅庫存　完稅庫存　保稅安全量　+
     A                                      完稅安全量'
     A                                      DSPATR(UL)
     A            DBGN1     R        B  9  5REFFLD(MA01)
     A*--------------------------------------------------------------*
     A          R DSPC1
     A*%%TS  SD  19920217  145312  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 11'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
