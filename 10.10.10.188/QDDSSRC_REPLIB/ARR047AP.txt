     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : ARR047A                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/04/02                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR047                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款作業                              *
     A*    REMARK       :銷退折讓明細表                            *
     A*===============================================================
     A* CODE |  AUTHOR  |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M004 | MICHELLE | 00.06.08 |新增稅額欄
     A*================================================================
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    61
     A                                      MSGCON(030 URE9999 REMF)
     A                                      SPACEA(001)
     A                                    67
     A                                      '銷退折讓明細表'
     A                                      SPACEA(002)
     A                                     1
     A                                      '公　　司:'
     A            SG01      R        O    13
     A            #B03      R        O    18
     A                                      SPACEA(001)
     A                                     1
     A                                      '通　　路:'
     A            DMD07S    R             13
     A                                      REFFLD(MD07)
     A                                    15
     A                                      '-'
     A            DMD07E    R             17
     A                                      REFFLD(MD07)
     A                                   121
     A                                      '日期:'
     A            $EGMDY         6  0O   129
     A                                      EDTCDE(Y)
     A                                   139
     A                                      '頁次:'
     A                                   146
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '客戶代號:'
     A            DSG04S         9        13
     A                                    +1
     A                                      '-'
     A            DSG04E         9        +1
     A                                   121
     A                                      '時間:'
     A                                   129
     A                                      TIME
     A                                   140
     A                                      '<ARR047A>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '日　　期:'
     A            DSG26S         6  0O    13
     A                                      EDTCDE(Y)
     A                                    21
     A                                      '∼'
     A            DSG26E         6  0O    25
     A                                      EDTCDE(Y)
     A                                   121
     A                                      ' USER :'
     A            $USER         10A  O   129
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =================='
     A                                      SPACEA(001)
     A                                     1
     A                                      '日　　期'
     A                                      SPACEB(001)
     A                                    11
     A                                      '折讓單號'
     A                                    23
     A                                      '客戶／名稱'
     A                                    51
     A                                      '發票號碼'
     A                                    62
     A                                      '產品代號'
     A                                    73
     A                                      '訂購數'
     A                                    81
     A                                      '搭贈數'
     A                                    93
     A                                      '銷退單價'
M004MA*                                  104'金額'
M004MA*                                  110'退貨日期'
     A                                   105
     A                                      '未稅金額'
     A                                   120
     A                                      '銷退稅額'
     A                                   134
     A                                      '銷退總金額'
     A                                   146
     A                                      '退貨日期'
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------'
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEA(001)
M001DA* 30        SG26      R              2EDTWRD('  /  /  ')
     A  30        DSG26          6  0      2
     A                                      EDTWRD('  /  /  ')
     A  31        SG09      R             11
     A  31        SG04      R             24
     A  31                                29
     A                                      '-'
     A  31        SG05      R             30
     A  31        ME04      R             34
     A  32        AG04      R             52
     A            AG03      R             63
     A            AG12      R             74
     A                                      EDTCDE(J)
     A            AG13      R             83
     A                                      EDTCDE(J)
     A            AG06      R             89
     A                                      EDTCDE(J)
M004MA*           AG07      R            101EDTCDE(J)
     A            AG07      R            102
     A                                      EDTCDE(J)
     A            AG21           9  0    118
     A                                      EDTCDE(J)
     A            W0721         11  0    132
     A                                      EDTCDE(J)
M001DA*           SG06      R            110EDTWRD('  /  /  ')
     A            DSG06          6  0    148
     A                                      EDTWRD('  /  /  ')
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEA(001)
     A                                    51
     A                                      '折讓單小計-----------------------
     A                                      ---------->'
M004MA*           STAG07         9  0O    98EDTCDE(J)
     A            STAG07         9  0O   102
     A                                      EDTCDE(J)
     A            STAG21        11  0O   115
     A                                      EDTCDE(J)
     A            ST0721        11  0O   132
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEA(001)
     A                                     1
     A                                      '合　　計-------------------------
     A                                      ------------------------------------
     A                                      ------------------------->'
M004MA*           TOTAL         10  0O    96EDTCDE(J)
     A            TOTAL         10  0O   100
     A                                      EDTCDE(J)
     A            TAG21         11  0O   115
     A                                      EDTCDE(J)
     A            T0721         11  0O   132
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =================='
     A                                      SPACEA(001)
     A                                     2
     A                                      '***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =================='
     A                                      SPACEA(001)
     A                                     2
     A                                      '<<ARR047A>>--->報表結束'
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =================='
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PE4
     A                                      SPACEA(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
