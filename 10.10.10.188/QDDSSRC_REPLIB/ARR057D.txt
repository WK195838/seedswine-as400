     A*%%TS  SD  19930921  090811  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : ARR057D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/05/01                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR057                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款系統                              *
     A*    REMARK       :折讓單                                    *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930921  090811  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR057'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 36'折讓單'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 21'辦事處　:'
     A            COMP           1A  B  5 33VALUES(' ' '1' '2' '3')
     A                                  5 35'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  8 21'業務員　:'
     A                                      DSPATR(HI)
     A            DSG11S    R        B  8 33REFFLD(SG11)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N60                                  DSPATR(HI)
     A                                  8 43'-'
     A            DSG11E    R        B  8 45REFFLD(SG11)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  9 21'客戶代號:'
     A                                      DSPATR(HI)
     A            DSG45S         9A  B  9 33
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  9 43'-'
     A            DSG45E         9A  B  9 45
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 11 21'折讓日期:'
M001DA*           DSG26S    R        B 11 33REFFLD(SG26)
M001AA            DSG26S         6  0B 11 33
     A                                      EDTWRD('  /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 11 43'-'
M001DA*           DSG26E    R        B 11 45REFFLD(SG26)
M001AA            DSG26E         6  0B 11 45
     A                                      EDTWRD('  /  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 13 21'選擇　　:'
     A            DSEL           1A  B 13 33VALUES('A' 'B')
     A                                 13 36'A=銷貨折讓 '
     A                                 14 36'B=價差折讓 '
     A                                 16 24'執行環境:'
     A            $EVR           1Y 0B 16 36VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 41'(1-即時 2-批次)'
     A                                 17 24'列印份數:'
     A            $CPY           3Y 0B 17 36EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 24'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 36CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 30'***請準備套版紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 12 17'套表列印日期:'
M001DA*           CHDATE    R        B 12 33REFFLD(SG26)
M001AA            CHDATE         6  0B 12 33
     A                                      EDTWRD('  /  /  ')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 12 55'(中國年YY/MM/DD)'
     A                                 11 55'(西元年MM/DD/YY)'
     A                                  6 21'公　司　:'
     A            DSG01S         2   B  6 33
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                  6 36'-'
     A            DSG01E         2   B  6 38
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
