     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PIR043A                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/04/22                                   *
     A*    UPDATE DATE  : 12/01/92 JUDY                              *
     A*    PROGRAM NAME : PIR043                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :盤點系統                                  *
     A*    REMARK       :盤盈虧表－各物流點                        *
      *===============================================================
      * MODI CODE | AUTHOR |  DATE  | DESCRIPTION
      *---------------------------------------------------------------
      *  M001     |MICHELLE|01.10.25|保稅倉不應包含成本DUTY
      *---------------------------------------------------------------
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    40MSGCON(030 URE9999 REMF)
     A                                    52'盤盈虧表'
     A                                      SPACEB(1)
     A                                    51'(各物流點)'
     A                                      SPACEB(1)
     A*                                    1'公司　:'
     A*                                     SPACEB(2)
     A*           IO01      R        O    +1
     A*           #B03      R        O    23
     A                                    76'日期:'
     A                                      SPACEB(2)
     A            $EGMDY         6  0O    84EDTCDE(Y)
     A                                    93'頁次:'
     A                                   101PAGNBR EDTCDE(Z)
     A                                    76'時間:'
     A                                      SPACEB(1)
     A                                    84TIME
     A                                     1'物流點:'
     A                                      SPACEB(1)
     A            IO02      R        O    +2
     A            DSTOR         22A  O    18
     A                                    76' USER :'
     A            $USER         10   O    84
     A                                    95'<PIR043A>'
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =====')
     A                                      SPACEB(1)
     A                                     1'公司　產品代號　產品簡稱'
     A                                      SPACEB(1)
     A                                    35'單位'
     A                                    45'成本　帳面數量　實際數量　盤盈虧'
     A                                    83'盤盈虧金額　盤點單號'
     A                                     1
     A                                      DFT('-------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----')
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEB(1)
     A            IO01      R              2
     A            IO04      R              8
     A            MA03      R             17
     A            MA13      R             35
M001MA*           IV05      R             41EDTCDE(J)
M001MA            WIV05          8  2     41EDTCDE(J)
     A            IT06      R             53EDTCDE(J)
     A            IO05      R             63EDTCDE(J)
     A            DATA1          6  0O    71EDTCDE(J)
     A            DATA2         11  2O    80EDTCDE(J)
     A            IO03      R             96
     A*----------------------------------------------------------------
     A          R PD2                       SPACEB(1)
     A                                     1'合　　計 ----------------------
     A                                      ------------------>'
     A            TTIT06    R             53REFFLD(IT06)
     A                                      EDTCDE(J)
     A            TTIO05    R             63REFFLD(IO05)
     A                                      EDTCDE(J)
     A            TDATA1         6  0O    71EDTCDE(J)
     A            TDATA2        11  2O    80EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =====')
     A                                      SPACEB(1)
     A                                     2'***續下頁***'
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =====')
     A                                      SPACEB(1)
     A                                     2'<<PIR043A>>--->報表結束'
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =====')
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE9                       SPACEB(1)
     A                                     3'もも此範圍無資料もも'
