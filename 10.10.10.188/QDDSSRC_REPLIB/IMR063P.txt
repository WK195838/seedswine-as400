     A****************************************************************
     A*    DSPF NAME    : IMR063P                                    *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR063                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :產品保留量彙總表                          *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    36MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    42'產品保留量彙總表'
     A                                      SPACEA(1)
     A                                     1'公司別　:'
     A            WF5101    R             +1REFFLD(WF5101 *LIBL/REWF51)
     A                                      SPACEA(1)
     A                                     1'物流點　:'
     A            WF5102    R             +1REFFLD(WF5102 *LIBL/REWF51)
     A            #A03      R             +1
     A                                    64'日期:'
     A            $EGMDY         6  0O    71EDTCDE(Y)
     A                                    81'頁次:'
     A                                    88PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'產品代號:'
     A            DIA03S         9        +1
     A                                    +1'∼'
     A            DIA03E         9        +1
     A                                    64'時間:'
     A                                    71TIME
     A                                    83'<IMR063>'
     A                                      SPACEA(1)
     A                                     1'日　期　:'
     A            DATES          6  0     +1EDTCDE(Y)
     A                                    +1'∼'
     A            DATEE          6  0     +1EDTCDE(Y)
     A                                    65'USER :'
     A            $USER         10   O    72
     A                                      SPACEA(1)
     A                                     1'=====================+
     A                                       =====================+
     A                                       =====================+
     A                                       =====================+
     A                                       =====================+
     A                                       ============='
     A                                      SPACEA(1)
     A                                    33'---------------------'
     A                                    +1'保留量'
     A                                    +1'---------------------'
     A                                      SPACEA(1)
     A                                     1'產品代號／名稱'
     A                                    31'銷售訂單數量'
     A                                    47'送貨單數量'
     A                                    62'調撥數量'
     A                                    72'禮盒組拆數量'
     A                                    87'總保留量'
     A                                    98'IA保留量'
     A                                   109'不合註記'
     A                                      SPACEA(1)
     A                                     1'---------------------+
     A                                       ---------------------+
     A                                       ---------------------+
     A                                       ---------------------+
     A                                       ---------------------+
     A                                       -------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            WF5103    R              2REFFLD(WF5103 *LIBL/REWF51)
     A            MA03      R             +1
     A            WF5104    R             37REFFLD(WF5104 *LIBL/REWF51)
     A                                      EDTCDE(1)
     A            WF5105    R             51REFFLD(WF5105 *LIBL/REWF51)
     A                                      EDTCDE(1)
     A            WF5106    R             64REFFLD(WF5106 *LIBL/REWF51)
     A                                      EDTCDE(1)
     A            WF5107    R             78REFFLD(WF5107 *LIBL/REWF51)
     A                                      EDTCDE(1)
     A            WFTOT     R             89REFFLD(WF5107 *LIBL/REWF51)
     A                                      EDTCDE(1)
     A            IA09      R            100
     A                                      EDTCDE(1)
     A            MARK           1       114
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'=====================+
     A                                       =====================+
     A                                       =====================+
     A                                       =====================+
     A                                       =====================+
     A                                       ============='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'=====================+
     A                                       =====================+
     A                                       =====================+
     A                                       =====================+
     A                                       =====================+
     A                                       ============='
     A                                      SPACEA(1)
     A                                     2'<<IMR063>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
