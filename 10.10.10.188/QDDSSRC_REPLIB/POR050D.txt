     A*%%TS  SD  19920127  164326  REPGMR      REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : POR050D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/01/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POR050                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :驗收單明細表                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19920127  164326  REPGMR      REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'POR050'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'驗收單明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 24'公司別　　:'
     A            DPI01     R        B  5 38REFFLD(PI0/PI01 *LIBL/REWF01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  7 24'單別　　　:'
     A            DDOC           2A  B  7 38VALUES('I1' 'I3')
     A                                    +3'( I1:國外進貨 I3:國內進貨)'
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  9 24'供應商代號:'
     A                                      DSPATR(HI)
     A            DPH06S    R        B  9 38REFFLD(PH0/PH06 *LIBL/POPHPF)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                  9 45'-'
     A            DPH06E    R        B  9 48REFFLD(PH0/PH06 *LIBL/POPHPF)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 11 24'驗收日期　:'
     A            DPI05S         6Y 0B 11 38EDTWRD('  /  /  ')
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 11 47'-'
     A            DPI05E         6Y 0B 11 49EDTWRD('  /  /  ')
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 11 58'(MM/DD/YY)'
     A                                 13 24'選擇內容　:'
     A            DATA           1A  B 13 38VALUES('A' 'B')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 13 42'A-不列印金額'
     A                                 14 42'B-列印金額'
     A                                 16 24'執行環境　:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時 2-批次)'
     A                                 17 24'列印份數　:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 24'列表機　　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 23'***Ａ格式請準備　８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 21 23'***Ｂ格式請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
