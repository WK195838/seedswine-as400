     A*%%TS  SD  19950519  153709  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : SAR075D                                    *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 84/05/17                                   *
     A*    PROGRAM NAME : SAR075                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :客戶產品銷售管理追蹤表　　　              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19950519  153709  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 '視窗查詢')
     A                                      CF05(05 '參考資料')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SAR075'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 28'客戶產品銷售管理追蹤表　'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 25'辦事處　:'
     A            COMP           1A  B  4 38VALUES(' ' '1' '2' '3')
     A                                  4 40'( 1=台北2=台中3=高雄"*"=全+
     A                                      部)'
     A                                  5 25'通路　　:'
     A                                      DSPATR(HI)
     A            DSC33S    R        B  5 38REFFLD(SC33)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N60                                  DSPATR(HI)
     A                                  5 48'-'
     A            DSC33E    R        B  5 50REFFLD(SC33)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N60                                  DSPATR(HI)
     A                                  6 25'型態　　:'
     A                                      DSPATR(HI)
     A            DSC34S    R        B  6 38REFFLD(SC34)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  6 48'-'
     A            DSC34E    R        B  6 50REFFLD(SC34)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  7 25'客戶代號:'
     A                                      DSPATR(HI)
     A            DSC04S         5A  B  7 38
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  7 48'-'
     A            DSC04E         5A  B  7 50
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  8 25'業務組別:'
     A                                      DSPATR(HI)
     A            DSC28S    R        B  8 38REFFLD(SC28)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                  8 48'-'
     A            DSC28E    R        B  8 50REFFLD(SC28)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                  9 25'產品組別:'
     A            DMA11          1A  B  9 38VALUES('1' '2' '3' ' ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 40'(1-BRANDY 2-WHISKY 3-WINE " "-ALL)'
     A                                 10 25'產品代號:'
     A                                      DSPATR(HI)
     A            DSD03S         9A  B 10 38
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64                                  DSPATR(HI)
     A                                 10 48'-'
     A            DSD03E         9A  B 10 50
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 11 25'送貨日期:'
M001MA*           DSC08S    R        B 11 38REFFLD(SC39)
M001MA            DSC08S         6  0B 11 38
     A                                      EDTWRD('  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 11 48'-'
M001MA*           DSC08E    R        B 11 50REFFLD(SC39)
M001MA            DSC08E         6  0B 11 50
     A                                      EDTWRD('  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 11 61'(MM/DD/YY)'
     A                                 12 25'確認日期:'
M001MA*           DSC39S    R        B 12 38REFFLD(SC39)
M001MA            DSC39S         6  0B 12 38
     A                                      EDTWRD('  /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 12 48'-'
M001MA*           DSC39E    R        B 12 50REFFLD(SC39)
M001MA            DSC39E         6  0B 12 50
     A                                      EDTWRD('  /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 12 61'(MM/DD/YY)'
     A                                 13 25'分析方式:'
     A            DSEL           1A  B 13 38VALUES('A' 'B')
     A                                 13 41'A-依總公司'
     A                                 14 41'B-依分公司'
     A                                 17 26'執行環境 :'
     A            $EVR           1Y 0B 17 39EDTCDE(3)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 41'(1-即時   2-批次)'
     A                                 18 26'列印份數'
     A                                 18 37':'
     A            $CPY           3Y 0B 18 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 26'列表機'
     A                                      DSPATR(HI)
     A                                 19 37':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 21 27'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 25'F4 =視窗查詢'
     A                                 23 40'F5 =參考資料'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A  89        ERR12         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR13         50A  O 24  2MSGID(URE 0128 REMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A  86        ERR14         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
