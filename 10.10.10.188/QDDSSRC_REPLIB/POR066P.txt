     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : POR066D                                    *
     A*    AUTHOR       : A1491 EASON                                *
     A*    CREATE DATE  : 03/10/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POR066                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購資料子系統                            *
     A*    REMARK       :國內供應商採購明細表                      *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    46
     A*                                     MSGCON(030 URE9999 REMF)
     A                                      '  康齡企業有限公司    '
     A                                      SPACEA(001)
     A                                    49
     A                                      '國內供應商採購明細'
     A                                      SPACEA(002)
     A                                     1
     A                                      '供應商　　:'
     A            DPB05S    R             15
     A                                      REFFLD(PB05)
     A                                    +1
     A                                      '-'
     A            DPB05E    R             +1
     A                                      REFFLD(PB05)
     A                                      SPACEA(001)
     A                                     1
     A                                      '產品代號　:'
     A            DPC03S    R             15
     A                                      REFFLD(PC03)
     A                                    +1
     A                                      '-'
     A            DPC03E    R             +1
     A                                      REFFLD(PC03)
     A                                    97
     A                                      '日期:'
     A            $EGMDY         6  0O    +1
     A                                      EDTCDE(Y)
     A                                    +1
     A                                      '頁次:'
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '進貨物流點:'
     A            DPB16S         5A       15
     A                                    +1
     A                                      '-'
     A            DPB16E         5A       +1
     A                                    97
     A                                      '時間:'
     A                                    +1
     A                                      TIME
     A                                      SPACEA(001)
     A                                     1
     A                                      '採購日期　:'
     A            DPB04S         6  0     15
     A                                      EDTWRD('  /  /  ')
     A                                    +1
     A                                      '-'
     A            DPB04E         6  0     +1
     A                                      EDTWRD('  /  /  ')
     A                                    97
     A                                      ' USER :'
     A            $USER         10   O    +1
     A                                    +2
     A                                      '<POR066P>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==========================='
     A                                      SPACEA(001)
     A                                     1
     A                                      '供應商代號／簡稱'
     A                                    24
     A                                      '產品代號／簡稱'
     A                                    52
     A                                      '採購日期'
     A                                    62
     A                                      '進貨物流點'
     A                                    85
     A                                      '單位'
     A                                    92
     A                                      '採購數量'
     A                                   105
     A                                      '單價'
     A                                   117
     A                                      '金　額'
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEA(001)
     A*供應商
     A N30        WF01           5A        2
     A N30        WF02          18O        7
     A*產品簡稱
     A N31        WF03           9A       25
     A N31        WF04          18O       34
     A*日期
     A N32        WF05Q          6  0     53
     A                                      EDTWRD('  /  /  ')
     A*物流點
     A            WF06           5A       62
     A            WF07          18O       67
     A*單位
     A            WF08           6O       85
     A*數量
     A            WF09           8  0     92
     A                                      EDTCDE(J)
     A*單價
     A            WF10           8  2    103
     A                                      EDTCDE(J)
     A*金額
     A            WF11          13  2    114
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT1
     A                                      SPACEA(001)
     A                                    77
     A                                      '小計------->'
     A            T0T06          8  0     92
     A                                      EDTCDE(J)
     A  34        T0T07         13  2    114
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==========================='
     A                                      SPACEA(001)
     A                                     2
     A                                      '***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ==========================='
     A                                      SPACEA(001)
     A                                     2
     A                                      '<<POR066P>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                      SPACEA(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
