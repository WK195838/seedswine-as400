     A*%%TS  SD  19920611  110801  REPGMRP     REL-V2R1M0  5738-PW1
     A*  81/01/28  16:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : DPA001D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/01/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : DPA001                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :主檔資料作業                              *
     A*    REMARK       :產品牌價資料維護                          *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.05 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920611  110423  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  5VALUES(' ' '2' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            MA01      R        O  9  8
     A            DMA03         16A  O  9 19
     A            MA06      R        O  9 37
     A            DMA40     R        O  9 54REFFLD(RERFR/MA40 *LIBL/RERF)
     A                                      EDTCDE(1)
     A            DMA41     R        O  9 64REFFLD(RERFR/MA41 *LIBL/RERF)
     A                                      EDTCDE(1)
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920611  110423  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'DPA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'產品牌價資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  5'2=修改'
     A                                  5 15'5=查詢'
     A            DBGN1     R        B  8  8REFFLD(MA01)
     A                                  7  5'A 產品代號  中文簡稱       +
     A                                      英文名稱       批發價  零售價'
     A                                      DSPATR(UL)
     A                                  6 54'---今日牌價---'
     A*------------------------------------------------------------
     A*
     A          R DSPC1
     A*%%TS  SD  19920127  162736  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A*                                     CSRLOC(D#ROW      D#COL)
     A          R DSPD2
     A*%%TS  SD  19920127  162736  REPGMR      REL-V2R1M0  5738-PW1
     A                                      CA03(03)
     A                                      CA12(12)
     A                                  1  2'DPA001'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'產品牌價資料維護'
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'產品代號    :'
     A            MA01      R        O  4 18
     A                                  6  2'中文全稱    :'
     A            MA02      R        O  6 18
     A                                  7  2'英文全稱    :'
     A            MA04      R        O  7 18
     A                                  8  2'              :'
     A            MA05      R        O  8 18
     A                                 10  2'牌價有效日期:'
M001MA*           MA39      R        B 10 18EDTWRD('  /  /  ')
M001MA            DMA39          6  0B 10 18EDTWRD('  /  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 10 28'(MM/DD/YY)'
     A                                 12 25'有效日期前之牌價'
     A                                 12 45'有效日期後之牌價'
     A                                 13 13'批發價格'
     A            MA40      R        B 13 33EDTCDE(1)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            MA45      R        B 13 53EDTCDE(1)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 14 13'零售價格'
     A            MA41      R        B 14 33EDTCDE(1)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            MA46      R        B 14 53EDTCDE(1)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 15 13'免稅價格'
     A            MA42      R        B 15 33EDTCDE(1)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            MA47      R        B 15 53EDTCDE(1)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F12=回前畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
