     A*%%TS  SD  19960730  091150  D910454     REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : MTR043D                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/02/13                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTR043                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :主檔                                      *
     A*    REMARK       :客戶清冊                                  *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19960730  091150  D910454     REL-V3R1M0  5763-PW1
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MTR043'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 35'客戶清冊'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 25'通　　路 :'
     A                                      DSPATR(HI)
     A            DMD07S    R        B  5 38REFFLD(RERFR/MD07 *LIBL/RERF)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A N66                                  DSPATR(HI)
     A                                  5 45'-'
     A            DMD07E    R        B  5 47REFFLD(RERFR/MD07 *LIBL/RERF)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(HI)
     A                                  7 25'地　　區 :'
     A                                      DSPATR(HI)
     A            DME07S    R        B  7 38REFFLD(ME0/ME07 *LIBL/MTMEPF)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  7 45'-'
     A            DME07E    R        B  7 47REFFLD(ME0/ME07 *LIBL/MTMEPF)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                  9 25'形　　態 :'
     A                                      DSPATR(HI)
     A            DMD08S    R        B  9 38REFFLD(RERFR/MD08 *LIBL/RERF)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A N68                                  DSPATR(HI)
     A                                  9 45'-'
     A            DMD08E    R        B  9 47REFFLD(RERFR/MD08 *LIBL/RERF)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A N68                                  DSPATR(HI)
     A                                 11 25'業務員　 :'
     A                                      DSPATR(HI)
     A            DME15S    R        B 11 38REFFLD(RERFR/ME15 *LIBL/RERF)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A                                 11 45'-'
     A            DME15E    R        B 11 48REFFLD(RERFR/ME15 *LIBL/RERF)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A N65                                  DSPATR(HI)
     A                                 12 25'客戶代號 :'
     A                                      DSPATR(HI)
     A            DME01S    R        B 12 38REFFLD(ME0/ME01 *LIBL/MTMEPF)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                 12 45'-'
     A            DME01E    R        B 12 48REFFLD(ME0/ME01 *LIBL/MTMEPF)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                 13 25'選擇內容 :'
     A            DCH            1A  B 13 38VALUES('A' 'B')
     A                                 13 42'(A=依客戶別排序  B=依地區別排+
     A                                      序)'
     A                                 14 20'不往來客戶列印:'
     A            DSEL           1A  B 14 38VALUES('Y' 'N')
     A                                 14 42'(Y=列印  N=不列印)'
     A                                 16 25'執行環境'
     A                                 16 36':'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 42'(1-即時   2-批次)'
     A                                 17 25'列印份數'
     A                                 17 36':'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 25'列表機'
     A                                      DSPATR(HI)
     A                                 18 36':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
