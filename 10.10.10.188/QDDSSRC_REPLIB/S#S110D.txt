     A*  90/07/07  13:23:58    A1062       REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : S#S110D                                    *
     A*    AUTHOR       : A1062   JEANNY LIN                         *
     A*    CREATE DATE  : 79/06/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : S#S110                                     *
     A*    SYSTEM       :程式安全管制系統                          *
     A*    PURPOSE      :使用者基本資料全頁查詢                    *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S#RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*  90/07/07  13:23:58    A1062       REL-R02M00  5728-PW1
     A            RRN            4Y 0O  8  3EDTCDE(Z)
     A            SU01      R        O  8  9
     A            SU02      R        O  8 21
     A            SU03      R        O  8 33
     A            SU03C     R        O  8 45REFFLD(SU02)
     A            SU06      R        O  8 57REFFLD(SU06)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  90/06/27  17:30:08    A1062       REL-R02M00  5728-PW1
     A                                      SFLSIZ(0099)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      CF02(02)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(UPT2030 *LIBL/PTMF)
     A  97                                  SFLMSGID(UPT2040 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  3'<S#S110D>'
     A                                  1 29'程式安全管制系統'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A            DUSER     R        O  2  3REFFLD(SU02)
     A                                  2 26'使用者基本資料全頁查詢'
     A                                  4  2'使用者編號:'
     A            DSU01     R        B  4 16REFFLD(SU01)
     A                                  4 45'挑選序號:'
     A            DSLT           4Y 0B  4 57EDTCDE(Z)
     A                                      CHECK(RB)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  6  2'序號　使用者編號　使用者名稱　授-
     A                                      權者編號　授權者名稱'
     A                                  6 57'備　　註'
     A                                  7  3'==================================-
     A                                      ===================================-
     A                                      ========'
     A          R DSPC1
     A*  90/07/07  13:23:58    A1062       REL-R02M00  5728-PW1
     A  96        ERR1          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF2 =挑選序號'
     A                                 23 20'PF3 =回主畫面'
     A                                 23 38'PF12=回前畫面'
     A                                 24  2'PA1 =下一頁'
     A                                 24 20'PA2 =上一頁'
     A                                 24 67'【泛太資訊】'
