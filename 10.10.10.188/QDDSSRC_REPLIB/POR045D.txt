     A*%%TS  SD  19920127  163753  REPGMR      REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : POR045D                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/01/23                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POR045                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :供應商採購彙總表                          *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19920127  163753  REPGMR      REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'POR045'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'供應商採購彙總表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 25'公司別'
     A                                  6 36':'
     A            DPC01S    R        B  6 38REFFLD(PC0/PC01 *LIBL/POPCPF)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  6 44'-'
     A            DPC01E    R        B  6 46REFFLD(PC0/PC01 *LIBL/POPCPF)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  8 25'月份'
     A                                  8 36':'
     A            DPC04S         4Y 0B  8 38EDTCDE(Y)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  8 44'-'
     A            DPC04E         4Y 0B  8 46EDTCDE(Y)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                  8 53'(MM/YY)'
     A                                 10 25'產品識別'
     A                                 10 36':'
     A            DMA08S    R        B 10 38REFFLD(MA0/MA08 *LIBL/MTMAPF)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 10 44'-'
     A            DMA08E    R        B 10 46REFFLD(MA0/MA08 *LIBL/MTMAPF)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 16 25'執行環境'
     A                                 16 36':'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 42'(1-即時   2-批次)'
     A                                 17 25'列印份數'
     A                                 17 36':'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 25'列表機'
     A                                      DSPATR(HI)
     A                                 18 36':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
