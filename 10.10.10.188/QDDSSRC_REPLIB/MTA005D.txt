     A*%%TS  SD  19920819  132503  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : MTA005D                                    *
     A*    AUTHOR       : A1034  STEPHAIE                            *
     A*    CREATE DATE  : 81/01/16                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTA005                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    REMARK       :員工資料維護 (A/U/D/I)                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.05 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR                     SFL
     A*%%TS  SD  19920819  132503  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  5VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            MC01      R        O  9  9
     A            MC02      R        O  9 16
     A            MC03      R        O  9 30REFFLD(MC03)
     A            DCENTR        22A  O  9 36
     A            DMC06          6  0O  9 60EDTWRD('  /  /  ')
M001DA*                                     REFFLD(MC06)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920819  132503  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'MTA005'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'員工基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  6'2=修改'
     A                                  5 16'4=刪除'
     A                                  5 26'5=查詢'
     A            DBGN1     R        B  8  9REFFLD(MC01)
     A                                      DSPATR(HI)
     A                                  7  5'A  員工代號／名稱     部門／名+
     A                                      稱　                離職日期'
     A                                      DSPATR(UL)
     A          R DSPC1
     A*%%TS  SD  19920123  211740  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F4 =視窗查詢'
     A                                    +2'F6 =新增'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPD2
     A*%%TS  SD  19920729  180629  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MTA005'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR005'
     A                                  2 31'員工基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'員工代號:'
     A            DMC01     R        B  4 14REFFLD(MC01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  6  2'員工姓名:'
     A            DMC02     R        B  6 14REFFLD(MC02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7  2'部門組別:'
     A                                      DSPATR(HI)
     A            DMC03     R        B  7 14REFFLD(MC03)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DCENTR        22A  O  7 20
     A                                  8  2'職　　稱:'
     A            DMC04     R        B  8 14REFFLD(MC04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  2'到職日期:'
M001MA*           DMC05     R        B  9 14REFFLD(MC05)
M001MA            DMC05          6  0B  9 14
     A                                      EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9 23'(MM/DD/YY)'
     A                                 10  2'離職日期:'
M001MA*           DMC06     R        B 10 14REFFLD(MC06)
M001MA            DMC06          6  0B 10 14
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTWRD('  /  /  ')
     A                                 10 23'(MM/DD/YY)'
     A                                 11  2'身份証號:'
     A            DMC07     R        B 11 14REFFLD(MC07)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 53'姓別:'
     A            DMC23     R        O 11 61REFFLD(MC23)
     A                                 11 63'( M-男 F-女)'
     A                                 12  2'出生日期:'
M001MA*           DMC08     R        B 12 14REFFLD(MC08)
M001MA            DMC08          6  0B 12 14
     A                                      EDTWRD('  /  /  ')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 23'(MM/DD/YY)'
     A                                 13  2'籍　　貫:'
     A            DMC09     R        B 13 14REFFLD(MC09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 53'印表機:'
     A            DMC26     R        B 13 63REFFLD(MC26)
     A N68                                  DSPATR(HI)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                      EDTCDE(2)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  2'戶籍地址:'
     A            DMC10     R        B 14 14REFFLD(MC10)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  2'通訊地址:'
     A            DMC11     R        B 15 14REFFLD(MC11)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16  2'學　　歷:'
     A            DMC13     R        B 16 14REFFLD(MC13)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  2'聯絡電話:'
     A            DMC12     R        B 17 14REFFLD(MC12)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 53'採購負責人代號:'
     A            DMC16     R        B 17 71REFFLD(MC16)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18  2'物流點　:'
     A                                      DSPATR(HI)
     A            DMC15     R        B 18 14REFFLD(MC15)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSTOR         22A  O 18 20
     A                                 18 53'調撥負責人代號:'
     A            DMC17     R        B 18 71REFFLD(MC17)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19  2'物流類別:'
     A            DMC24     R        B 19 14REFFLD(MC24)
     A                                      VALUES('A' 'W' 'T' 'B' 'V' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19 16'(A經銷W完稅T保稅B專櫃+
     A                                       V普銷)'
     A                                 19 60'價額逾搭試換'
     A                                 20  2'建檔日期:'
M001MA*           DMC14     R        O 20 14REFFLD(MC14)
M001MA            DMC14          6  0O 20 14
     A                                      EDTWRD('  /  /  ')
     A                                 20 23'(MM/DD/YY)'
     A                                 20 43'核準權限設定　:'
     A            DMC18     R        B 20 61REFFLD(MC18)
     A                                      VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMC19     R        B 20 63REFFLD(MC19)
     A                                      VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMC20     R        B 20 65REFFLD(MC20)
     A                                      VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMC21     R        B 20 67REFFLD(MC21)
     A                                      VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMC22     R        B 20 69REFFLD(MC22)
     A                                      VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMC25     R        B 20 71REFFLD(MC25)
     A                                      VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 21  2'異動日期:'
M001MA*           DMCXX     R        O 21 14REFFLD(MCXX)
M001MA            DMCXX          6  0O 21 14
     A                                      EDTWRD('  /  /  ')
     A                                 21 23'(MM/DD/YY)'
     A                                 21 49'使用者　:'
     A            DMCZZ     R        O 21 61REFFLD(MCZZ)
     A                                 22  2'執行'
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A                                 22 58'F12=回前畫面'
     A  98        ERR1          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          60A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
