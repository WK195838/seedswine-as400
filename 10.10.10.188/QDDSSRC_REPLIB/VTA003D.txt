     A****************************************************************
     A****************************************************************
     A*                                                              *
     A*    DSPF NAME    : VTA003D                                    *
     A*    AUTHOR       : MICHELLE                                   *
     A*    CREATE DATE  : 04/22/02                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : VTA003                                     *
     A*    SYSTEM       :寰盛洋酒                                  *
     A*    SUBSYSTEM    :媒體申報                                  *
     A*    REMARK       :進項資料人工輸入                          *
     A****************************************************************
      *===============================================================
      * MODI CODE | AUTHOR |  DATE  | DESCRIPTION
      *---------------------------------------------------------------
      *           |        |        |
      *---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CA03(03)
     A*                                     CF18(18)
     A                                      CF04(04)
     A*                                     CF13(13)
     A                                      CSRLOC(#LIN       #COL)
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A            ERRID          7A  H
     A            ERRF          10A  H
     A                                  1  2'VTA003.1'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DATE           6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  1
     A                                  2 30'進項資料人工輸入'
     A                                      DSPATR(RI)
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'公司別  :'
     A            DVR91          1   B  4 13VALUES('W' 'R' 'K' 'S')
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A  60                                  DSPATR(PC)
     A N32                                  DSPATR(PR)
     A            NAME           6   O  4 16
M002AA                                  4 28'員工代號：'
M002AA                                      DSPATR(HI)
M002AA            DVR96          6   B    +2
M002AA  63                                  DSPATR(RI)
M002AA  32                                  DSPATR(UL)
M002AA  63                                  DSPATR(PC)
M002AA N32                                  DSPATR(PR)
M002AA N63                                  DSPATR(HI)
M002AA            SU02          10O  O    +2
     A                                  5  1'辦事處  :'
     A            DVR92          1   B  5 13VALUES('1' '2' '3')
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A            OFFICE         6   O  5 16
     A                                  6  1'歸屬年月:'
     A                                      DSPATR(HI)
     A            DVR04          4  0B  6 13EDTWRD('0 /  ')
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                    +2'(MM/YY)'
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0B  7 13EDTCDE(4)
     A                                      VALUES(1 2 4 5)
     A                                  7 15'( 1.新增 2.修改 4.刪除 5.+
     A                                      查詢 )'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----'
     A  88
     AO 99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 11'PF3 =結束'
     A*                                23 25'PF4 =欄位查詢'
     A*                                23 43'PF13=列印設定'
     A*                                23 43'PF18=全頁查詢'
     A*                                24 66'【寰盛洋酒】'
     A                                 24 64MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            DNEW           1A  H
     A            RRN            3Y 0H
M003AA            HVR07          9  0H
M003AA            HVR09          9  0H
     A            DDEL2          1A  B 11  2
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A                                      VALUES(' ' 'D' '/')
     A            DVR16         14   B 11  4
     A  71                                  DSPATR(RI)
     A  38                                  DSPATR(UL)
     A  71                                  DSPATR(PC)
     A N38                                  DSPATR(PR)
     A            DVR17          6  0B 11 19
     A                                      EDTWRD('  /  /  ')
     A  72                                  DSPATR(RI)
     A  35                                  DSPATR(UL)
     A  72                                  DSPATR(PC)
     A N35                                  DSPATR(PR)
     A            DVR15          8   B 11 28
     A  73                                  DSPATR(RI)
     A  35                                  DSPATR(UL)
     A  73                                  DSPATR(PC)
     A N35                                  DSPATR(PR)
     A            DVR07          9  0B 11 37
     A                                      EDTCDE(K)
     A  74                                  DSPATR(RI)
     A  35                                  DSPATR(UL)
     A  74                                  DSPATR(PC)
     A N35                                  DSPATR(PR)
     A            DVR09          9  0B 11 50
     A                                      EDTCDE(K)
     A  76                                  DSPATR(RI)
     A  35                                  DSPATR(UL)
     A  76                                  DSPATR(PC)
     A N35                                  DSPATR(PR)
     A            DVR01          2   B 11 63
     A                                      VALUES('21' '22' '23' '24' '25' +
     A                                      '26' '27' '28' '  ' '29')
     A  75                                  DSPATR(RI)
     A  38                                  DSPATR(UL)
     A  75                                  DSPATR(PC)
     A N38                                  DSPATR(PR)
     A            DVR08          1   B 11 67
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A                                      VALUES('1' '2' '3' ' ')
     A            DVR10          1   B 11 70
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  77                                  DSPATR(RI)
     A  77                                  DSPATR(PC)
     A                                      VALUES('1' '2' ' ')
     A            DVR21          4  0B 11 72EDTCDE(2)
     A  38                                  DSPATR(UL)
     A N38                                  DSPATR(PR)
     A  80                                  DSPATR(RI)
     A  80                                  DSPATR(PC)
     A*           DVR13          1   B 11 74
     A* 35                                  DSPATR(UL)
     A*N35                                  DSPATR(PR)
     A* 78                                  DSPATR(RI)
     A* 78                                  DSPATR(PC)
     A*                                     VALUES('A' ' ')
     A*           DVR18          4  0B 11 76
     A*                                     EDTCDE(4)
     A* 35                                  DSPATR(UL)
     A*N35                                  DSPATR(PR)
     A* 79                                  DSPATR(RI)
     A* 79                                  DSPATR(PC)
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0052)
M003MA*                                     SFLPAG(0011)
M003MA                                      SFLPAG(0010)
     A                                      CA03(03)
     A*                                     CF04(04)
     A                                      CF14(14)
     A                                      CF12(12)
     A*                                     CF18(18)
     A                                      ROLLUP(25)
     A                                      CSRLOC(#LIN       #COL)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A            DRRN           3S 0H      SFLRCDNBR
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'VTA003.1'
     A            CONAME        32A  O  1 25MSGID(US# 9001 S#MF)
     A            DATE           6Y 0O  1 70EDTCDE(Y)
     A            $USERN        10A  O  2  1
     A                                  2 30'進項資料人工輸入'
     A                                      DSPATR(RI)
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  1'公司別  :'
     A            DVR91          1   O  4 13
     A            NAME           6   O  4 16
M002AA                                  4 28'員工代號：'
M002AA            DVR96          6   O    +2
M002AA            SU02          10O  O    +2
     A                                  5  1'辦事處  :'
     A            DVR92          1   O  5 13
     A            OFFICE         6   O  5 16
     A                                  6  1'歸屬年月:'
     A            DVR04          4  0O  6 13EDTWRD('0 /  ')
     A                                    +2'(MM/YY)'
     A                                  7  1'作業選擇:'
     A            DOPT           1Y 0O  7 13
     A                                  7 15'( 1.新增 2.修改 4.刪除 5.+
     A                                      查詢 )'
     A                                  8  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----'
     A                                  9 17'發票日期'
     A*                                 9 61'格　稅　扣　彙　張'
     A                                  9 61'格　稅　扣　序'
     A                                 10  2'D'
     A                                 10  6'發票號碼'
     A                                 10 17'(MM/DD/YY)'
     A                                 10 28'銷售統編'
     A                                 10 41'銷售額'
     A                                 10 53'稅額'
     A*                                10 61'式　別　抵　加　數'
     A                                 10 61'式　別　抵　號'
M003AA          R DSPD2
M003AC                                 22 29'合計：'
M003AA            TVR07         11  0O 22 37EDTCDE(K)
M003AA            TVR09         11  0O 22 50EDTCDE(K)
     A*----------------------------------------------------------------
     A          R DSPC1
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3=回主畫面'
     A                                 23 25'F12=回前畫面'
     A                                 23 41'F14=確認'
     A*                                24 66'【寰盛洋酒】'
     A                                 24 64MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
