     A*%%TS  SD  19931106  121131  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR075D                                    *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 82/08/23                                   *
     A*    PROGRAM NAME : SOR075                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :產品退貨分佈明細表                        *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19931106  121131  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR075'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'產品退貨分佈明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 26'辦事處'
     A                                  4 36':'
     A            COMP           1A  B  4 39VALUES(' ' '1' '2' '3')
     A                                  4 41'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  6 26'產品代號:'
     A                                      DSPATR(HI)
     A            DSH03S    R        B  6 38REFFLD(SH03)
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  6 48'-'
     A            DSH03E    R        B  6 50REFFLD(SH03)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 26'通路　　:'
     A                                      DSPATR(HI)
     A            DSC33S    R        B  7 38REFFLD(SC33)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 40'-'
     A            DSC33E    R        B  7 42REFFLD(SC33)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8 26'型態　　:'
     A                                      DSPATR(HI)
     A            DSC34S    R        B  8 38REFFLD(SC34)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64                                  DSPATR(HI)
     A                                  8 41'-'
     A            DSC34E    R        B  8 43REFFLD(SC34)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
     A                                  9 26'地區　　:'
     A                                      DSPATR(HI)
     A            DSG15S    R        B  9 38REFFLD(SG15)
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                  9 41'-'
     A            DSG15E    R        B  9 43REFFLD(SG15)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 10 26'客戶代號:'
     A                                      DSPATR(HI)
     A            DSG04S         9   B 10 38
     A N72                                  DSPATR(HI)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                 10 48'-'
     A            DSG04E         9   B 10 50
     A N73                                  DSPATR(HI)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                 12 26'退貨日期:'
M001MA*           DSG06S    R        B 12 38REFFLD(SG06)
M001MA            DSG06S         6  0B 12 38
     A                                      EDTWRD('  /  /  ')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                 12 47'-'
M001MA*           DSG06E    R        B 12 49REFFLD(SG06)
M001MA            DSG06E         6  0B 12 49
     A                                      EDTWRD('  /  /  ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                 12 59'(MM/DD/YY)'
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 39VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 41'(1-即時   2-批次)'
     A                                 17 26'列印份數'
     A                                 17 37':'
     A            $CPY           3Y 0B 17 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機'
     A                                      DSPATR(HI)
     A                                 18 37':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A  89        ERR12         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 13 26'折讓日期:'
M001MA*           DSG26S    R        B 13 38REFFLD(SG26)
M001MA            DSG26S         6  0B 13 38
     A                                      EDTWRD('  /  /  ')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                 13 47'-'
M001MA*           DSG26E    R        B 13 49REFFLD(SG26)
M001MA            DSG26E         6  0B 13 49
     A                                      EDTWRD('  /  /  ')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                 13 59'(MM/DD/YY)'
