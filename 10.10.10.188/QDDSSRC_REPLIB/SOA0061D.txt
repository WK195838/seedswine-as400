     A*%%TS  SD  20021018  150855  REPGMR      REL-V4R4M0  5769-PW1
     A*  81/01/21  17:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : SOA006D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/01/29                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOA006                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售訂單系統                              *
     A*    REMARK       :訂單／送貨核准發放處理                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.11 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M002      | 00.04.27 | FOR DUTY菸酒稅法實施
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SOSMLF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920914  143935  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            HRCDN1         4A  H
     A            HSM04     R        H      REFFLD(SM04)
     A            DOPT1          1A  B 10  2
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      VALUES(' ' '2' '3' '4' '5' '6' '7' -
     A                                      '8')
M001MA*           SM06      R        O 10  4EDTWRD('  /  /  ')
M001MA            LSM06          6Y 0O 10  4EDTWRD('  /  /  ')
     A            SM04      R        O 10 13
     A            SM05      R        O 10 19
     A            DME04         14A  O 10 24
     A            SM02      R        O 10 39
     A            DDOC          10A  O 10 52
     A            SM14      R        O 10 63
     A            SM08      R        O 10 67
     A            SM09      R        O 10 69
     A            SM10      R        O 10 71
     A            SM11      R        O 10 73
     A            SM12      R        O 10 75
     A            SM13      R        O 10 77
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920922  141511  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'SOA006'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'訂單／送貨核准處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  6'2=核准'
     A                                  5 42'4=核准取消'
     A                                  5 58'5=單據明細查詢'
     A                                  5 21'3=單價／搭贈處理'
     A                                  6 42'7=額度查詢'
     A                                  6 58'8=逾期查詢'
     A                                  6  6'6=單價／搭贈查詢'
     A                                  7 62'地單額逾搭試換'
     A                                  8  2'A MM/DD/YY客戶代號  客戶簡稱  -
     A                                        單據號碼    單別    區價+
     A                                      度期贈飲貨'
     A                                      DSPATR(UL)
M001MA*           DBGN1     R        B  9  4REFFLD(SM06)
M001MA            DBGN1          6Y 0B  9  4
     A                                      EDTWRD('  /  /  ')
     A            DBGN2     R        B  9 13REFFLD(SM04)
     A            DBGN3     R        B  9 19REFFLD(SM05)
     A            DBGN4     R        B  9 39REFFLD(SM02)
     A                                  7  3'日　　期'
     A*------------------------------------------------------------
     A*
     A          R DSPC1
     A*%%TS  SD  19920127  162736  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12 =回前畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(URE 0063 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A* 54                                  SFLNXTCHG
     A          R SFLSR2                    SFL
     A*%%TS  SD  20021018  150855  REPGMR      REL-V4R4M0  5769-PW1
     A            RRN2           4S 0H
     A            DS03      R        O 12  3REFFLD(SD03 *LIBL/SOSDPF)
     A            MA03      R        O 12 13REFFLD(MA03 *LIBL/MTMAPF)
     A            MA13      R        O 12 32REFFLD(MA13 *LIBL/MTMAPF)
     A            DS04      R        O 12 40REFFLD(SD04 *LIBL/SOSDPF)
     A                                      EDTCDE(3)
     A            DS05      R        O 12 48REFFLD(SD05 *LIBL/SOSDPF)
     A                                      EDTCDE(3)
     A            DS06      R        O 12 53REFFLD(SD06 *LIBL/SOSDPF)
     A                                      EDTCDE(3)
     A            DS07      R        O 12 64REFFLD(SD07 *LIBL/SOSDPF)
     A                                      EDTCDE(3)
     A*------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0008)
     A*                                     ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A* 37                                  FRCDTA
     A* 37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A*
     A                                  1  2'SOA006'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'訂單／送貨核准處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A            SM01      R        O  5 14
     A            D#B02         16A  O  5 17
     A                                  5 34'地區　　:'
     A            SM14      R        O  5 46
     A            #D02      R        O  5 49REFFLD(#D02 *LIBL/PA#DPF)
     A                                  6  2'單據號碼:'
     A            SM02      R        O  6 14
     A                                  6 34'單別　　:'
     A            SM03      R        O  6 46
     A            #203      R        O  6 49REFFLD(#203 *LIBL/PA#2PF)
     A                                  7  2'單據日期:'
M001MA*           SM06      R        O  7 14EDTWRD('  /  /  ')
M001MA            LSM06          6Y 0O  7 14EDTWRD('  /  /  ')
     A                                  7 23'(MM/DD/YY)'
     A                                  7 34'客戶代號:'
     A            SM04      R        O  7 46
     A            SM05      R        O  7 52
     A            ME04      R        O  7 57REFFLD(ME04 *LIBL/MTMEPF)
     A                                  8  2'總金額　:'
     A            SM15      R        O  8 14EDTCDE(1)
M003AA                                  8 32'含稅總金額:'
M003AA            DTOT          10  0O  8 46EDTCDE(1)
M003AA                                  9  2'總稅額　:'
M003AA            DTAX           7  0O  9 16EDTCDE(1)
     A                                 11  3'產品代號／名稱             +
     A                                      單位　　數量 搭贈　　單價-
     A                                           金　額'
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A*                                     CSRLOC(D#ROW      D#COL)
     A          R DSPC2
     A*%%TS  SD  19920127  162736  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(URE 0063 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A* 54                                  SFLNXTCHG
     A          R SFLSR3                    SFL
     A*%%TS  SD  20021018  150855  REPGMR      REL-V4R4M0  5769-PW1
     A            RRN3           4S 0H
     A            DS03      R        O 12  3REFFLD(SD03 *LIBL/SOSDPF)
     A            MA03      R        O 12 13REFFLD(MA03 *LIBL/MTMAPF)
     A            MA13      R        O 12 32REFFLD(MA13 *LIBL/MTMAPF)
     A            DS04      R        O 12 40REFFLD(SD04 *LIBL/SOSDPF)
     A                                      EDTCDE(3)
     A            DS05      R        O 12 47REFFLD(SD05 *LIBL/SOSDPF)
     A                                      EDTCDE(3)
     A            DS06      R        O 12 52REFFLD(SD06 *LIBL/SOSDPF)
     A                                      EDTCDE(3)
     A            DS08      R        O 12 63REFFLD(SD08 *LIBL/SOSDPF)
     A                                      EDTCDE(3)
     A            DS09      R        O 12 75REFFLD(SD09 *LIBL/SOSDPF)
     A                                      EDTCDE(3)
     A*------------------------------------------------------------
     A*
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A*%%TS  SD  19920309  163307  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0008)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN3          4S 0H      SFLRCDNBR
     A                                  1  2'SOA006'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 30'訂單／送貨核准處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A            SM01      R        O  5 14
     A            D#B02         16A  O  5 17
     A                                  5 34'地區　　:'
     A            SM14      R        O  5 46
     A            #D02      R        O  5 49REFFLD(#D02 *LIBL/PA#DPF)
     A                                  6  2'單據號碼:'
     A            SM02      R        O  6 14
     A                                  6 34'單別　　:'
     A            SM03      R        O  6 46
     A            #203      R        O  6 49REFFLD(#203 *LIBL/PA#2PF)
     A                                  7  2'單據日期:'
M001MA*           SM06      R        O  7 14EDTWRD('  /  /  ')
M001MA            LSM06          6Y 0O  7 14EDTWRD('  /  /  ')
     A                                  7 23'(MM/DD/YY)'
     A                                  7 34'客戶代號:'
     A            SM04      R        O  7 46
     A            SM05      R        O  7 52
     A            ME04      R        O  7 57REFFLD(ME04 *LIBL/MTMEPF)
     A                                  8  2'總金額　:'
     A            SM15      R        O  8 14EDTCDE(1)
M003AA                                  8 32'含稅總金額:'
M003AA            DTOT          10  0O  8 46EDTCDE(1)
M003AA                                  9  2'總稅額　:'
M003AA            DTAX           7  0O  9 16EDTCDE(1)
     A                                 11  3'產品代號／名稱             單+
     A                                      位　　數量 搭贈  單價　正常單+
     A                                      價　正常搭贈'
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A*                                     CSRLOC(D#ROW      D#COL)
     A          R DSPC3
     A*%%TS  SD  19920127  162736  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(URE 0063 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
