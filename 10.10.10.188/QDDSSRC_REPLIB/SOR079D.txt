     A*%%TS  SD  19950221  110826  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : SOR079D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 09/17/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR079                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析作業                              *
     A*    REMARK       :試飲訂單明細分析表                        *
     A*===============================================================
     A* MODI CODE |  AUTHOR  |  DATE  | DESCRIPTION
     A*---------------------------------------------------------------
     A*  M003     | MICHELLE | 991227 |增加試飲原因選項
     A*---------------------------------------------------------------
     A*  M004     | MICHELLE | 011116 |增加試飲客戶名稱
     A*===============================================================
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19950221  110826  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR079'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'試飲分析表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 25'辦事處　:'
     A            COMP           1A  B  4 37VALUES(' ' '1' '2' '3')
     A                                  4 39'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  5 25'業務員　:'
     A                                      DSPATR(HI)
     A            DSC27S         6A  B  5 37
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A N60                                  DSPATR(HI)
     A                                  5 47'-'
     A            DSC27E         6A  B  5 49
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  6 25'費用單位:'
     A                                      DSPATR(HI)
     A            DSC04S    R        B  6 37REFFLD(SC04)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A                                  6 47'-'
     A            DSC04E    R        B  6 49REFFLD(SC04)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
M004AA                                  7 25'客戶代號:'
M004AA                                      DSPATR(HI)
M004AA            DSC16S    R        B  7 37REFFLD(SC04)
M004AA  70                                  DSPATR(RI)
M004AA  70                                  DSPATR(PC)
M004AA N70                                  DSPATR(HI)
M004AA                                  7 47'-'
M004AA            DSC16E    R        B  7 49REFFLD(SC04)
M004AA  70                                  DSPATR(RI)
M004AA  70                                  DSPATR(PC)
M004AA N70                                  DSPATR(HI)
     A                                  8 25'產品組別:'
     A            DMA11          1A  B  8 37VALUES('1' '2' '3' ' ')
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A                                  8 39'(1-BRANDY 2-WHISKY 3-WINE " "-ALL)'
     A                                  9 25'產品代號:'
     A                                      DSPATR(HI)
     A            DSD03S    R        B  9 37REFFLD(SD03)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 47'-'
     A            DSD03E    R        B  9 49REFFLD(SD03)
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 10 25'確認日期:'
     A            DSC39S         6Y 0B 10 37EDTWRD('  /  /  ')
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 10 47'-'
     A            DSC39E         6Y 0B 10 49EDTWRD('  /  /  ')
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
M003AA                                 11 25'試飲原因:'
M003AA                                      DSPATR(HI)
M003AA            DSC10S    R        B 11 37REFFLD(SC10)
M003AA N68                                  DSPATR(HI)
M003AA  68                                  DSPATR(PC)
M003AA  68                                  DSPATR(RI)
M003AA                                 11 47'-'
M003AA            DSC10E    R        B 11 49REFFLD(SC10)
M003AA N69                                  DSPATR(HI)
M003AA  69                                  DSPATR(PC)
M003AA  69                                  DSPATR(RI)
     A                                 12 25'排序內容:'
     A            DSEL           1A  B 12 37VALUES('A' 'B' 'C' 'D' 'E')
     A                                 12 42'A-業務員'
     A                                 13 42'B-產品代號'
     A                                 14 42'C-費用單位'
     A                                 15 42'D-送貨日期'
M004AA                                 16 42'E-客戶代號'
     A                                 17 25'執行環境:'
     A            $EVR           1Y 0B 17 37VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 42'(1-即時 2-批次)'
     A                                 18 25'列印份數:'
     A            $CPY           3Y 0B 18 37EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 25'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 37CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
