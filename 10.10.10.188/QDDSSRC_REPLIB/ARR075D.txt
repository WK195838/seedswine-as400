     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : ARR075D                                    *
     A*    AUTHOR       : AN MAIN HSIA                               *
     A*    CREATE DATE  : 01/13/94                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR075                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款系統                              *
     A*    REMARK       :應收帳款週轉率分析表                      *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR075'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'應收帳款週轉率分析表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'公司    :'
     A                                  6 47'-'
     A            DCD03S         1   B  6 38
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A            DCD03E         1   B  6 49
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  7 26'辦事處　:'
     A            COMP           1A  B  7 38VALUES(' ' '1' '2' '3')
     A                                  7 40'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  8 26'通路    :'
     A                                      DSPATR(HI)
     A                                  8 47'-'
     A            DCD13S    R        B  8 38REFFLD(CD13)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A            DCD13E    R        B  8 49REFFLD(CD13)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                  9 26'客戶代號:'
     A                                      DSPATR(HI)
     A            DCD06S    R        B  9 38REFFLD(CD06)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A                                  9 47'-'
     A            DCD06E    R        B  9 49REFFLD(CD06)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A N65                                  DSPATR(HI)
     A                                 10 26'業務員　:'
     A                                      DSPATR(HI)
     A            DCD09S    R        B 10 38REFFLD(CD09)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A N66                                  DSPATR(HI)
     A                                 10 47'-'
     A            DCD09E    R        B 10 49REFFLD(CD09)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(HI)
     A                                 12 26'確認年月:'
M001MA*           DCD01S    R        B 12 38REFFLD(CD01)
M001MA            DCD01S         4  0B 12 38
     A                                      EDTWRD('  /  ')
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                 12 47'-'
M001MA*           DCD01E    R        B 12 49REFFLD(CD01)
M001MA            DCD01E         4  0B 12 49
     A                                      EDTWRD('  /  ')
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                 12 60'(MM/YY)'
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 42'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備　８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          63A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          63A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          63A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          63A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR9          63A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
