     A*%%TS  SD  19920805  110403  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : POI022D                                    *
     A*    AUTHOR       : A1034  STEPHAIE                            *
     A*    CREATE DATE  : 81/01/14                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POI022                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購子系統                                *
     A*    REMARK       :採購狀況（依產品）                        *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR                     SFL
     A*%%TS  SD  19920805  110403  REPGMRP     REL-V2R1M0  5738-PW1
     A            RRN            4S 0H
     A            PC05      R        O  8  2
     A            PC02      R        O  8 10
     A            PC03      R        O  8 23
     A            MA06      R        O  8 33
     A            PC06      R        O  8 49EDTCDE(1)
     A            PC11      R        O  8 57EDTCDE(1)
     A            DNET      R        O  8 65REFFLD(PC06)
     A                                      EDTCDE(1)
M001DA*           DPB14     R        O  8 73REFFLD(PB14)
M001AA            DPB14          6  0O  8 73
     A                                      EDTCDE(Y)
     A            MA03      R        O  9 32
     A            PC10      R        O  9 60REFFLD(PC0/PC10 *LIBL/POPCPF)
     A                                      EDTCDE(Z)
     A                                  9 64'/'
     A            PC09      R        O  9 66
M001DA*           DPB15     R        O  9 73REFFLD(PB15)
M001AA            DPB15          6  0O  9 73
     A                                      EDTCDE(Y)
     A*--------------------------------------------------------------*
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920608  175814  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      SFLDROP(CF01)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'POI022'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'採購狀況（依供應商）'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'供應商 採購單號   產品代號／+
     A                                      名稱          採購量已收量+
     A                                      未收量ETD/ETA'
     A                                  6  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A            DBGN1          5A  B  7  2
     A            DBGN2         12A  B  7 10
     A*--------------------------------------------------------------*
     A          R DSPC1
     A*%%TS  SD  19920116  095212  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F1 =明細顯示'
     A                                    +2'F3 =回主畫面'
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
