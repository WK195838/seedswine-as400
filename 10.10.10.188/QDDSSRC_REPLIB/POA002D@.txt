     A*%%TS  SD  19920811  131258  REPGMRP     REL-V2R1M0  5738-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PAA002D                                    *
     A*    AUTHOR       : A1092  TAYLOR                              *
     A*    CREATE DATE  : 81/01/18                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PAA002                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                        *
     A*    REMARK       :採購單資料維護 (A/U/D/I)                  *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.10 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M003      | 99.12.24 |顯示負數於數量，金額欄位
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920608  102120  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  3VALUES(' ' '2' '4' '5' '6' '7')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            PB02      R        O 10 16
M001MA*           PB04      R        O 10  6EDTWRD('  /  /  ')
M001MA            LPB04          6  0O 10  6EDTWRD('  /  /  ')
     A            PB05      R        O 10 30
     A            MB03      R        O 10 38
     A            PB19      R        O 10 70
M001MA*           DPB06     R        O 10 58REFFLD(RERFR/PB15 *LIBL/RERF)
M001MA            DPB06          6  0O 10 58
     A                                      EDTCDE(Y)
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920604  170815  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'POA002'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'　採購單資料維護　'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'輸入作業選擇:'
     A                                  5  4'2=修改'
     A                                  5 14'4=刪除'
     A                                  5 25'5=查詢'
     A                                  5 35'6=採購明細'
     A                                  5 49'7=裝船資料維護'
     A            DBGN1     R        B  9 16REFFLD(PB02)
     A            DBGN0          6Y 0B  9  6EDTCDE(Y)
     A                                  7  5'(MM/DD/YY)'
     A                                  7 29'供應商'
     A                                  7 57'預　計'
     A                                  8  3'A 採購日期採購單號    代　號-
     A                                      　供應商簡稱        交貨日  -
     A                                      結案'
     A                                      DSPATR(UL)
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R DSPC1
     A*%%TS  SD  19920128  141805  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A  80                             24  2'採購單號'
     A                                      DSPATR(HI)
     A  80        DPB02     R             +1REFFLD(PB02)
     A                                      DSPATR(RI)
     A  80                                +1'新增成功'
     A                                      DSPATR(HI)
     A*
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR8          63A  O 24  2MSGID(URE 0005 REMF)
     A                                      DSPATR(HI)
     A  87        ERR9          63A  O 24  2MSGID(URE 0006 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0007 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0149 REMF)
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPD2
     A*%%TS  SD  19920731  091642  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'POA002'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'　採購單資料維護　'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號  :'
     A            DPB01     R        B  4 16REFFLD(PB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            #B03      R        O  4 19
     A                                  5  2'採購單號  :'
     A            DPB02     R        O  5 16REFFLD(PB02)
     A                                  6  2'單別      :'
     A            DPB03     R        O  6 16REFFLD(PB03)
     A            #203      R        O  6 19
     A                                  7  2'採購日期  :'
M001MA            DPB04          6  0B  7 16
     A                                      EDTWRD('  /  /  ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7 25'(MM/DD/YY)'
     A                                  7 41'預計交貨日:'
M001MA            DPB06          6  0B  7 55
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7 64'(MM/DD/YY)'
     A                                  9  2'供應商代號:'
     A                                      DSPATR(HI)
     A            DPB05     R        B  9 16REFFLD(PB05)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            MB02      R        O  9 22
     A                                 10  2'幣別      :'
     A            DPB07     R        B 10 16REFFLD(PB07)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            D#A032        22A  O 10 20
     A                                 10 43'總金額  :'
     A            DPB08     R        O 10 55REFFLD(PB08)
     A  35                                  DSPATR(ND)
M003MA*                                     EDTCDE(2)
M003MA                                      EDTCDE(K)
     A                                 12  2'付款條件  :'
     A                                      DSPATR(HI)
     A            DPB09     R        B 12 16REFFLD(PB09)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A033        22A  O 12 20
     A                                 12 43'價格條件:'
     A                                      DSPATR(HI)
     A            DPB10     R        B 12 55REFFLD(PB10)
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A034        22A  O 12 58
     A                                 13  2'運輸方式  :'
     A                                      DSPATR(HI)
     A            DPB12     R        B 13 16REFFLD(PB12)
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A035        22A  O 13 20
     A                                 13 43'採購備註:'
     A                                      DSPATR(HI)
     A            DPB13     R        B 13 55REFFLD(PB13)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  2'參考單號  :'
     A            DPB11     R        B 15 16REFFLD(PB11)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 16  2'裝船日ETD :'
M001MA            DPB14          6  0B 16 16
     A                                      EDTWRD('  /  /  ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 16 25'(MM/DD/YY)'
     A                                 17  2'到貨日ETA :'
M001MA            DPB15          6  0B 17 16
     A                                      EDTWRD('  /  /  ')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 17 25'(MM/DD/YY)'
     A                                 18  2'I/P         :'
     A            DPB16     R        B 18 16REFFLD(PB16)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 19  2'P-INVOICE   :'
     A            DPB17     R        B 19 16REFFLD(PB17)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 20  2'INSURANCE   :'
     A            DPB18     R        B 20 16REFFLD(PB18)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A                                 22 58'F12=回前畫面'
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR8          63A  O 24  2MSGID(URE 0008 REMF)
     A                                      DSPATR(HI)
     A  87        ERR9          63A  O 24  2MSGID(URE 0016 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0017 REMF)
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 11 43'總稅額  :'
     A            DPB22     R        O 11 56REFFLD(RERFR/PB22 *LIBL/RERF)
     A  35                                  DSPATR(ND)
M003MA*                                     EDTCDE(1)
M003MA                                      EDTCDE(J)
     A                                 15 41'採購員代碼:'
     A            DMC161         1   B 15 55
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A          R DSPC3
     A*%%TS  SD  19920127  161833  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A                                 22 58'F12=回前畫面'
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR8          63A  O 24  2MSGID(URE 0008 REMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR11         63A  O 24  2MSGID(URE 0091 REMF)
     A                                      DSPATR(HI)
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R SFLSR2                    SFL
     A*%%TS  SD  19920811  120346  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 11  3VALUES(' ' '1' '2' '4')
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A            DPC03     R        B 11  6REFFLD(PC03)
     A N75                                  DSPATR(HI)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            MA06      R        O 11 16
     A            DPC09     R        O 11 32REFFLD(PC09)
     A            DPC06     R        B 11 40REFFLD(PC06)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
M003MA*                                     EDTCDE(2)
M003MA                                      EDTCDE(K)
     A            DPC07     R        O 11 49REFFLD(PC07)
     A  35                                  DSPATR(ND)
     A*                                     EDTCDE(2)
M003MA                                      EDTCDE(K)
     A            DPC08     R        O 11 61REFFLD(PC08)
     A  35                                  DSPATR(ND)
     A*                                     EDTCDE(2)
M003MA                                      EDTCDE(K)
     A            DCHK           1A  H
     A            DPC10     R        O 11 76REFFLD(RERFR/PC10 *LIBL/RERF)
     A                                      EDTCDE(2)
     A*                                     DSPATR(BL)
     A*                                     DSPATR(RI)
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19920811  131258  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'POA002'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 32'採購單資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號  :'
     A            DPB01     R        O  4 16REFFLD(PB01)
     A            #B03      R        O  4 19
     A                                  4 38'單別  :'
     A            DPB03     R        O  4 48REFFLD(PB03)
     A            #203      R        O  4 51
     A                                  5  2'採購單號  :'
     A            DPB02     R        O  5 16REFFLD(PB02)
     A                                  5 36'採購日期:'
M001MA            DPB04          6  0O  5 48
     A                                      EDTWRD('  /  /  ')
     A                                  5 57'預計交貨日:'
M001MA            DPB06          6  0O  5 71
     A                                      EDTWRD('  /  /  ')
     A                                  6  2'供應商代號:'
     A            DPB05     R        O  6 16REFFLD(PB05)
     A            MB02      R        O  6 22
     A                                  6 65'幣別:'
     A            DPB07     R        O  6 73REFFLD(PB07)
     A                                  8  4'1=新增'
     A                                  8 16'2=修改'
     A                                  8 28'4=刪除'
     A                                 10  3'A 產品代號／名稱           單+
     A                                      位　　　數量   單價        金+
     A                                      額   轉換'
     A                                      DSPATR(UL)
     A                                  7  2'總金額　　:'
     A            DPB08     R        O  7 16REFFLD(RERFR/PB08 *LIBL/RERF)
     A  35                                  DSPATR(ND)
M003MA*                                     EDTCDE(1)
M003MA                                      EDTCDE(J)
     A            CPB08     R        H      REFFLD(RERFR/PB08 *LIBL/RERF)
     A                                  7 31'總稅額:'
     A            DPB22     R        O  7 41REFFLD(RERFR/PB22 *LIBL/RERF)
     A  35                                  DSPATR(ND)
M003MA*                                     EDTCDE(1)
M003MA                                      EDTCDE(J)
     A            CPB22     R        H      REFFLD(RERFR/PB22 *LIBL/RERF)
     A                                  7 55'含稅金額:'
     A            DAMT      R        O  7 67REFFLD(RERFR/PB08 *LIBL/RERF)
     A  35                                  DSPATR(ND)
M003MA*                                     EDTCDE(1)
M003MA                                      EDTCDE(J)
     A                                  9 74'單位'
