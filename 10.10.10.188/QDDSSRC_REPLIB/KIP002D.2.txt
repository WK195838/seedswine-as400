     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIP002D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 98/04/15                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :茂世進銷存                                *
     A*    SUBSYSTEM    :                                          *
     A*    REMARK       :銷售訂單維護                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  3VALUES(' ' '5' '6' '7'     '8')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SBA01          6Y 0O 10  6EDTWRD('  /  /  ')
     A            SBA03     R        O 10 16REFFLD(BA03 W2BAPF)
     A            SBA02     R        O 10 31REFFLD(BA02 W2BAPF)
     A            SBA02W    R  -16   O 10 38REFFLD(#A03 PA#APF)
     A            STAT1         10O  O 10 56
     A            STAT2          8O  O 10 68
     A            SBA01H    R        H      REFFLD(BA01 W2BAPF)
     A            SBA02H    R        H      REFFLD(BA02 W2BAPF)
     A            SBA05     R        H      REFFLD(BA05 W2BAPF)
     A            SBA06     R        H      REFFLD(BA06 W2BAPF)
     A            SBA07     R        H      REFFLD(BA07 W2BAPF)
     A            SBA08     R        H      REFFLD(BA08 W2BAPF)
     A            SBA10     R        H      REFFLD(BA10 W2BAPF)
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF07(07)
     A                                      CF08(08)
     A*                                     CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'KIP002'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'銷售訂單確認'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'5=查詢'
     A                                      COLOR(BLU)
     A                                  5 26'6=確認'
     A                                      COLOR(BLU)
     A                                  5 35'7=預售確認'
     A                                      COLOR(BLU)
     A                                  5 48'8=還原'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  7  3'(月/日/年)'
     A                                  8  3'A 訂單日期  訂單單號   專櫃 -
     A                                         專櫃代號        單別     -
                                            狀態  '
     A                                      DSPATR(UL)
     A            DBGN1          6Y 0B  9  6EDTWRD('  /  /  ')
     A            DBGN2         12A  B  9 16
     A            DBGN3          5A  B  9 31
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +1'F6 =新增銷售'
     A                                      COLOR(BLU)
     A                                    +1'F7 =新增試飲'
     A                                      COLOR(BLU)
     A                                    +1'F8 =新增預售'
     A                                      COLOR(BLU)
     A*                                   +1'F9 =新增預收'
     A*                                     COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99N97     ERRMSG        60A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23 66MSGCON(014 MSG9996 *LIBL/R1MF)
     A                                      DSPATR(HI)
      *MSG1000--->新增成功
      *MSG1001--->修改成功
      *MSG1002--->刪除成功
      *MSG1003--->查詢成功
      *MSG1004--->還原成功
      *MSG1005--->作業完成
     A  97        @MSG          60A  O 24  2DSPATR(HI)
     A  91        ERR0          63A  O 23  2MSGID(MSG 1000 R1MF)
     A                                      DSPATR(HI)
     A  92        ERR1          63A  O 23  2MSGID(MSG 1001 R1MF)
     A                                      DSPATR(HI)
     A  93        ERR2          63A  O 23  2MSGID(MSG 1002 R1MF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 23  2MSGID(MSG 1003 R1MF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 23  2MSGID(MSG 1004 R1MF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SBB03     R        B 11  2REFFLD(BB03 W2BBPF)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N33                                  DSPATR(PR)
     A  33N71                               DSPATR(UL)
     A  33N71                               DSPATR(HI)
     A            SMA03     R        O 11 12REFFLD(MA03 MTMAPF)
     A            SBB04          5  0B 11 36
     A                                      EDTCDE(4)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A N33                                  DSPATR(PR)
     A  33                                  DSPATR(UL)
     A            SBB07     R        B 11 47REFFLD(BB07 W2BBPF)
     A                                      EDTCDE(4)
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A N33                                  DSPATR(PR)
     A  33                                  DSPATR(UL)
     A            SBB05          5  0B 11 67
     A                                      EDTCDE(4)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A N33                                  DSPATR(PR)
     A  33                                  DSPATR(UL)
     A            SBB02H    R        H      REFFLD(BB02 W2BBPF)
     A            SBB03H    R        H      REFFLD(BB03 W2BBPF)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A  32                                  CF04(04)
     A  32                                  CF05(05)
     A  85                                  CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'KIP002'
     A                                  1 26MSGCON(030 MSG9999 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A  86                              2 33'銷售訂單維護'
     A                                      DSPATR(HI)
     A  87                              2 33'預售訂單維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'專櫃代號:'
     A            DBA022    R        B  5 13REFFLD(BA02 W2BAPF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            D#A032    R        O  5 19REFFLD(#A03 PA#APF)
     A                                    +1'單別:'
     A            DBA06     R        O    +1REFFLD(BA06 W2BAPF)
     A            DBA06W        10O  O    +1
     A                                  6  1'訂單日期:'
     A            DBA01          6Y 0B  6 13EDTWRD('  /  /  ')
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6 22'(月/日/年)'
     A                                  6 40'訂單單號:'
     A            DBA03     R        O  6 52REFFLD(BA03 W2BAPF)
     A                                      DSPATR(HI)
     A                                  6 65'直客:'
     A                                      COLOR(PNK)
     A            DBA10     R        B    +1REFFLD(BA10 W2BAPF)
     A                                      VALUES(' ' 'V')
     A N32                                  DSPATR(PR)
     A  32                                  DSPATR(UL)
     A                                    +1'(V)'
     A                                  7  1'備註:'
     A            DBA07     R  -62   B    +1REFFLD(BA07 W2BAPF)
     A N32                                  DSPATR(PR)
     A  32                                  DSPATR(UL)
     A                                  8  1'總金額:'
     A            TOTAL         15  2O    +1EDTCDE(4)
     A                                 10  2'產品代號 產品名稱         銷+
     A                                      售數量  銷售金額(含稅) 搭+
     A                                      贈數量      '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                 22  1'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  32                                +1'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  32                                +1'F5 =參考資料'
     A                                      COLOR(BLU)
     A  85                                +1'F9 =確認資料'
     A                                      COLOR(BLU)
     A                                    +1'F12=回前畫面'
     A                                      COLOR(BLU)
     A*                                   +1'PAGE DOWN =下頁'
     A*                                     COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 MSG9996 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR3                    SFL
     A  54                                  SFLNXTCHG
     A            RRN3           4S 0H
     A            SBB03     R        B 11  2REFFLD(BB03 W2BBPF)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N33                                  DSPATR(PR)
     A  33N71                               DSPATR(UL)
     A  33N71                               DSPATR(HI)
     A            SMA03     R        O 11 12REFFLD(MA03 MTMAPF)
     A            SBB04          5Y 0B 11 36EDTCDE(4)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A N34                                  DSPATR(PR)
     A  34                                  DSPATR(UL)
     A  78                                  DSPATR(ND)
     A            SBB06          5Y 0B 11 46EDTCDE(4)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A N35                                  DSPATR(PR)
     A  35                                  DSPATR(UL)
     A  79                                  DSPATR(ND)
     A            SAA04     R        O 11 53REFFLD(AA04 W1AAPF)
     A                                      EDTCDE(Q)
     A  79                                  DSPATR(ND)
     A            SBB02H    R        H      REFFLD(BB02 W2BBPF)
     A            SBB03H    R        H      REFFLD(BB03 W2BBPF)
     A*----------------------------------------------------------------
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A  32                                  CF04(04)
     A  32                                  CF09(09)
     A  32                                  CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN3          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'KIP002'
     A                                  1 26MSGCON(030 MSG9999 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A  88                              2 33'試飲訂單維護'
     A                                      DSPATR(HI)
     A  89                              2 33'預收訂單維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'專櫃代號:'
     A            DBA022    R        B  5 13REFFLD(BA02 W2BAPF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            D#A032    R        O  5 19REFFLD(#A03 PA#APF)
     A                                    +1'單別:'
     A            DBA06     R        O    +1REFFLD(BA06 W2BAPF)
     A            DBA06W        10O  O    +1
     A                                  6  1'訂單日期:'
     A            DBA01          6Y 0B  6 13EDTWRD('  /  /  ')
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6 22'(月/日/年)'
     A                                  6 40'訂單單號:'
     A            DBA03     R        O  6 52REFFLD(BA03 W2BAPF)
     A                                      DSPATR(HI)
     A                                  6 65'直客:'
     A                                      COLOR(PNK)
     A            DBA10     R        B    +1REFFLD(BA10 W2BAPF)
     A                                      VALUES(' ' 'V')
     A N32                                  DSPATR(PR)
     A  32                                  DSPATR(UL)
     A                                    +1'(V)'
     A  88                              7  1'試飲代號:'
     A  88        DBA08     R        B    +1REFFLD(BA08 W2BAPF)
     A N32                                  DSPATR(PR)
     A  32N63                               DSPATR(UL)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  32N63                               DSPATR(HI)
     A            DBA08W    R        O    +1REFFLD(#A03 PA#APF)
     A                                  8  1'備註:'
     A            DBA07     R  -62   B    +1REFFLD(BA07 W2BAPF)
     A N32                                  DSPATR(PR)
     A  32                                  DSPATR(UL)
     A  88                             10  2'產品代號 產品名稱           -
     A                                            試飲數量     牌價   '
     A                                      DSPATR(UL)
     A  89                             10  2'產品代號 產品名稱         銷+
     A                                      售數量   '
     A                                      DSPATR(UL)
