     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : SOR079D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 10/07/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR079                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :試飲訂單分析明細表                        *
     A*===============================================================
     A* MODI CODE |  AUTHOR  |  DATE  | DESCRIPTION
     A*---------------------------------------------------------------
     A*  M003     | MICHELLE | 991228 |增加試飲原因選項及總計
     A*---------------------------------------------------------------
     A*  M004     | MICHELLE | 011116 |增加試飲客戶名稱
     A*===============================================================
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    62MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    65'試飲訂單分析／明細表'
     A                                      SPACEA(2)
     A                                     1'辦事處　:'
     A            COMPS          1A       13
     A                                    15'-'
     A            COMPE          1A       17
     A                                      SPACEA(1)
     A                                     1'業務員　:'
     A            DSC27S         6   O    13
     A                                    20'-'
     A            DSC27E         6   O    22
     A                                      SPACEA(1)
     A                                     1'費用單位:'
     A            DSC04S         5   O    13
     A                                    19'-'
     A            DSC04E         5   O    20
     A                                      SPACEA(1)
M004AA                                     1'客戶代號:'
M004AA            DSC16S         5   O    13
M004AA                                    19'-'
M004AA            DSC16E         5   O    20
M004AA                                      SPACEA(1)
     A                                     1'產品代號:'
     A            DSD03S         9   O    13
     A                                    +1'-'
     A            DSD03E         9   O    +1
     A                                   109'日期:'
     A            $EGMDY         6  0O   116EDTCDE(Y)
     A                                   126'頁次:'
     A                                   134PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'確認日期:'
     A            DSC39S         6  0O    13EDTCDE(Y)
     A                                    +1'-'
     A            DSC39E         6  0O    +1EDTCDE(Y)
     A                                   109'時間:'
     A                                   116TIME
M003AA                                      SPACEA(1)
M003AA                                     1'試飲原因:'
M003AA            DSC10S         2   O    13
M003AA                                    +1'-'
M003AA            DSC10E         2   O    +1
M003AA            DSC101        12O  O    +1
M003AA                                    +1'-'
M003AA            DSC102        12O  O    +1
     A                                    68'***送貨日期***'
     A                                   109' USER :'
     A            $USER         10   O   116
     A                                   127'<SOR079DP>'
     A                                      SPACEA(1)
     A                                     1'===================='
     A                                    21'===================='
     A                                    41'===================='
     A                                    61'===================='
     A                                    81'===================='
     A                                   101'===================='
     A                                   121'=========='
M004AA                                   131'===================='
M004AA                                   151'=========='
     A                                      SPACEA(1)
     A                                     1'送貨日期'
     A                                    12'產品代號／名稱'
     A                                    45'費用單位／簡稱'
     A                                    75'訂單號碼'
     A                                    88'試飲量'
     A                                    99'業務員／姓名'
     A                                   119'確認日期'
M004AA                                   129'客戶代號'
M004AA                                   142'客戶簡稱'
     A                                      SPACEA(1)
     A                                     1'--------------------'
     A                                    21'--------------------'
     A                                    41'--------------------'
     A                                    61'--------------------'
     A                                    81'--------------------'
     A                                   101'--------------------'
     A                                   121'----------'
M004AA                                   131'--------------------'
M004AA                                   151'----------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A  21                                 1' '
     A                                      SPACEA(1)
     A N30        DSC08          6  0      2EDTCDE(Y)
     A N31        SD03      R             13
     A N31        MA03      R             23
     A N32        SC04      R             43
     A N32        SC05      R             49
     A N32        ME04      R             54
     A            SC02      R             73
     A            SD45           9  0     86EDTCDE(J)
     A            SC27      R            100
     A            MC02      R            107
     A            DSC39          6  0O   120EDTCDE(Y)
M004AA            W1617          9       130
M004AA            WME04         18O      140
     A*----------------------------------------------------------------
     A          R PT1                       SPACEA(1)
     A                                    66'產品小計------->'
     A            TOT45          9  0     86EDTCDE(J)
     A*----------------------------------------------------------------
M003AA          R PT2                       SPACEA(1)
M003AA                                    66'總　　計------->'
M003AA            TOT            9  0     86EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PE1                       SPACEA(1)
     A                                     1'===================='
     A                                    21'===================='
     A                                    41'===================='
     A                                    61'===================='
     A                                    81'===================='
     A                                   101'===================='
     A                                   121'=========='
M004AA                                   131'===================='
M004AA                                   151'=========='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2                       SPACEA(1)
     A                                     1'===================='
     A                                    21'===================='
     A                                    41'===================='
     A                                    61'===================='
     A                                    81'===================='
     A                                   101'===================='
     A                                   121'=========='
M004AA                                   131'===================='
M004AA                                   151'=========='
     A                                      SPACEA(1)
     A                                     2'<<SOR079DP>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
