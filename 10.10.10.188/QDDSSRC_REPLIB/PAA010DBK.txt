     A*%%TS  SD  19920602  125519  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PAA010D                                    *
     A*    AUTHOR       : A1005  ROGER CHIEN                         *
     A*    CREATE DATE  : 80/11/20                                   *
     A*    UPDATE DATE  : 81/01/14 BY STEPHANIE                      *
     A*    PROGRAM NAME : PAA010                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料建檔子系統                        *
     A*    REMARK       :部門品牌對照資料 (A/U/D/I)                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.10.26 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M002      | 00.09.11 | A&P品牌延伸試飲處理
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19920530  082821  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
M002MA*           DOPT1          1A  B  9  4VALUES(' ' '2' '4' '5')
M002MA            DOPT1          1A  B  9  2VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
M002MA*           #301      R        O  9  8
M002MA*           ME04      R        O  9 16
M002MA*           #302      R        O  9 37
M002MA*           #A03      R        O  9 42REFFLD(#A03)
M002MA            #301      R        O  9  4
M002MA            ME04      R        O  9 10
M002MA            #302      R        O  9 29
M002AA            #303           6   O  9 33
M002AA            #304           2   O  9 42
M002MA            D#A03     R        O  9 48REFFLD(#A03)
M002MA          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920602  125519  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'PAA010'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 28'部門品牌對照資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  5'2=修改'
     A                                  5 15'4=刪除'
     A                                  5 25'5=查詢'
M002AA                                  6 33'產品 延伸'
M002MA*                                 7  4'A  部門   部門說明     -
M002MA*                                         品牌品牌說明         -
M002MA*                                     '
M002MA                                  7  2'A部門部門說明　　　品牌 前-
M002MA                                      六碼　品牌　-
M002MA                                      延伸品牌說明　　　　　　　　'
     A                                      DSPATR(UL)
M002MA*           DBGN1     R        B  8  8REFFLD(#301)
M002MA            DBGN1     R        B  8  4REFFLD(#301)
     A*           DBGN2     R        B  8 14REFFLD(#302)
M002AA            DBGN3          6   B  8 33
     A          R DSPC11
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A                                    +2'F9 =列印'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPC2
     A*%%TS  SD  19920127  161113  REPGMR      REL-V2R1M0  5738-PW1
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  1  2'PAA010'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 28'部門品牌對照資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'部門代號:'
     A            D#301     R        B  4 14REFFLD(#301)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            ME04      R        O  4 21REFFLD(ME04)
     A                                  5  2'品牌代號:'
     A            D#302     R        B  5 14REFFLD(#302)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            #A03      R        O  5 18REFFLD(#A03)
M002AA                                  6  2'產品前六碼:'
M002AA            D#303          6   B  6 16
M002AA  62                                  DSPATR(PC)
M002AA  62                                  DSPATR(RI)
M002AA  31                                  DSPATR(UL)
M002AA N31                                  DSPATR(PR)
M002AA                                  7  2'延伸品牌　:'
M002AA            D#304          2   B  7 16
M002AA  63                                  DSPATR(PC)
M002AA  63                                  DSPATR(RI)
M002AA  31                                  DSPATR(UL)
M002AA N31                                  DSPATR(PR)
M002AA            D#A03     R        O  7 20REFFLD(#A03)
     A*                                 7  2'型態說明:'
     A*           #A033     R        B  7 14REFFLD(#303)
     A* 62                                  DSPATR(PC)
     A* 62                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A                                 19  2'異動日期:'
M001MA*           D#3XX     R        O 19 14REFFLD(#3XX)
M001MA            D#3XX          6  0O 19 14
     A                                      EDTCDE(Y)
     A                                 19 23'(MM/DD/YY)'
     A                                 20  2'異動時間:'
     A            D#3YY     R        O 20 14REFFLD(#3YY)
     A                                      EDTWRD('  :  :  ')
     A                                 21  2'使用者　:'
     A            D#3ZZ     R        O 21 14REFFLD(#3ZZ)
     A                                 22  2'執行'
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F12=回前畫面'
     A  98        ERR1          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
