     A*%%TS  SD  20021003  182106  REPGMR      REL-V4R4M0  5769-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : SOA005D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/02/17                                   *
     A*    UPDATE DATE  : 81/04/17 PHILIP                            *
     A*    PROGRAM NAME : SOA005                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售訂單作業                              *
     A*    REMARK       :銷貨退回資料處理 (A/U/D/I)                *
     A*===============================================================
     A* CODE | AUTHOR |  DATE  | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001 |MICHELLE|98.11.11| MANUAL MODIFY FOR Y2K
     A*---------------------------------------------------------------
     A* M005 |MICHELLE|00.11.23|開放修正業務員代號
     A*---------------------------------------------------------------
     A* M004 |MICHELLE|00.05.15|菸酒稅法實施
     A*---------------------------------------------------------------
     A* M006 |MICHELLE|09.17.02|新增活動代碼
     A*---------------------------------------------------------------
     A* M007 |MICHELLE|06.13.03| CHECK專櫃可退貨庫存
     A*---------------------------------------------------------------
     A*================================================================
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19921021  090108  D910121     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            HSG16          1A  H
     A            SG14      R        H
     A            DOPT1          1A  B  9  3VALUES(' ' '2' '4' '5' '6')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
M001MA            DSG06          6Y 0O  9  5
     A                                      EDTWRD('  /  /  ')
     A            SG02      R        O  9 14
     A            SG15      R        O  9 28
     A            SG04      R        O  9 31
     A            SG05      R        O  9 37
     A            ME04      R        O  9 42
     A            SG08      R        O  9 61
M001MA            DSG26          6Y 0O  9 65
     A                                      EDTWRD('  /  /  ')
     A            SG03      R        H
     A            MARK           1   O  9 76
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19921021  090108  D910121     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      CF09(09)
     A                                      CF10(10)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'SOA005'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'銷貨退回處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  6'2=修改'
     A                                  5 16'4=刪除'
     A                                  5 26'5=查詢'
     A                                  5 36'6=退貨明細'
     A                                  6 27'地'
     A                                  6  5'退貨日期'
     A                                  7  3'A  MM/DD/YY退貨單號   區客戶+
     A                                      代號 客戶簡稱       原因 日+
     A                                      期  註記'
     A                                      DSPATR(UL)
M001MA*           DBGN1     R        B  8  5REFFLD(SG06)
M001MA            DBGN1          6Y 0B  8  5
     A                                      EDTWRD('  /  /  ')
     A            DBGN2     R        B  8 14REFFLD(SG02)
     A                                  6 66'折讓'
     A                                  6 59'退貨'
     A                                  6 74'跨折'
     A*------------------------------------------------------------
     A*
     A          R DSPC1
     A*%%TS  SD  19920929  114954  D910095     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F6 =新增'
     A  70                             24  2'退貨單號'
     A                                      DSPATR(HI)
     A  70        DSG02     R        O 24 13REFFLD(SG02)
     A                                      DSPATR(RI)
     A  70                             24 26'新增成功'
     A                                      DSPATR(HI)
     A  97        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR9          63A  O 24  2MSGID(URE 0018 REMF)
     A                                      DSPATR(HI)
     A  88        ERR11         63A  O 24  2MSGID(URE 0063 REMF)
     A                                      DSPATR(HI)
     A  87        ERR12         63A  O 24  2MSGID(URE 0021 REMF)
     A                                      DSPATR(HI)
     A  86        ERR13         63A  O 24  2MSGID(UPT 2023 PTMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 22 39'F9 =列印'
     A                                 22 50'F10=康齡退貨差異'
     A*------------------------------------------------------------
     A*
     A          R DSPD2
     A*%%TS  SD  19960426  141748  DERLERN     REL-V3R1M0  5763-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A            HSG07     R        H      REFFLD(SG07)
M007AA            HSG18     R        H      REFFLD(SG18)
     A                                  1  2'SOA005'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'銷貨退回處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號　:'
     A            DSG01     R        B  5 16REFFLD(SG01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            #B03      R        O  5 19
M006AA                                  5 47'活動代碼  :'
"    A            DSG27         12   B  5 61
"    A  31                                  DSPATR(UL)
"    A N31                                  DSPATR(PR)
     A                                  7  2'地區代號　:'
     A            DSG15     R        O  7 16REFFLD(SG15)
     A            #D02      R        O  7 19
     A                                  9  2'客戶代號　:'
     A                                      DSPATR(HI)
     A            DSG04     R        B  9 16REFFLD(SG04)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DSG05     R        B  9 22REFFLD(SG05)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DME04     R        O  9 27REFFLD(ME04)
     A                                  9 47'退貨原因　:'
     A                                      DSPATR(HI)
     A            DSG08     R        B  9 61REFFLD(SG08)
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11  2'退貨單號　:'
     A            DSG02     R        O 11 16REFFLD(SG02)
     A                                 13 48'業務員:'
M005MA                                      DSPATR(HI)
M005MA*           DSG11     R        O 13 58REFFLD(SG11)
M005MA            DSG11     R        B 13 58REFFLD(SG11)
M005AA N67                                  DSPATR(HI)
M005AA  67                                  DSPATR(PC)
M005AA  67                                  DSPATR(RI)
M005AA  31                                  DSPATR(UL)
M005AA N31                                  DSPATR(PR)
     A            DMC02     R        O 13 65REFFLD(MC02)
     A            DSG12     R        H      REFFLD(SG12)
     A                                 13  2'退貨日期　:'
M001MA            DSG06          6Y 0B 13 16
     A                                      EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 26'(MM/DD/YY)'
     A                                 15  2'退回物流點:'
     A                                      DSPATR(HI)
     A            DSG07     R        B 15 16REFFLD(SG07)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A031        22A  O 15 22
     A                                 17  2'專櫃代號　:'
     A            DSG18     R        B 17 16REFFLD(SG04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A032        22A  O 10 47
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A                                 22 58'F12=回前畫面'
     A  97        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0088 REMF)
     A                                      DSPATR(HI)
     A  87        ERR11         63A  O 24  2MSGID(URE 0064 REMF)
     A                                      DSPATR(HI)
     A  86        ERR12         63A  O 24  2MSGID(URE 0089 REMF)
     A                                      DSPATR(HI)
     A  85        ERR13         63A  O 24  2MSGID(URE 0111 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPC3
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F4 =視窗查詢'
     A                                    +2'F5 =參考資料'
     A                                    +2'F12=回前畫面'
     A  97        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         63A  O 24  2MSGID(URE 0059 REMF)
     A                                      DSPATR(HI)
     A  88        ERR12         63A  O 24  2MSGID(URE 0132 REMF)
     A                                      DSPATR(HI)
     A  87        ERR13         63A  O 24  2MSGID(UPT 2506 PTMF)
     A                                      DSPATR(HI)
     A  85        ERR14         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A*%%TS  SD  20021003  182106  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            HMA07          1A  H
     A            HQTY           7S 0H
     A            DOPT2          1A  B 13  3VALUES(' ' '1' '2' '4')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DSH03     R        B 13  6REFFLD(SH03)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            MA03      R        O 13 16
     A            MA13      R        O 13 35
M007AA            HSH04     R        H      REFFLD(SH04)
     A            DSH04     R        B 13 42REFFLD(SH04)
     A                                      EDTCDE(1)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
M007AA            HSH05     R        H      REFFLD(SH05)
     A            DSH05     R        B 13 49REFFLD(SH05)
     A                                      EDTCDE(1)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            SH07      R        O 13 55EDTCDE(3)
     A            S207           9Y 0O 13 68EDTCDE(3)
     A            DCHK           1A  H
     A*                                     'Y' -已存檔中--> UPD
     A*                                     ' ' -不存檔中--> ADD
     A*------------------------------------------------------------
     A*
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  20020925  112440  REPGMR      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0008)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOA005'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 33'銷貨退回處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A            DSG01     R        O  5 14REFFLD(SG01)
     A            #B03      R        O  5 17
     A                                  5 41'地區代號:'
     A            DSG15     R        O  5 53REFFLD(SG15)
     A            #D02      R        O  5 59
     A                                  6  2'客戶代號:'
     A            DSG04     R        O  6 14REFFLD(SG04)
     A            DSG05     R        O  6 20REFFLD(SG05)
     A            DME04     R        O  6 25REFFLD(ME04)
     A                                  7  2'退貨單號:'
     A            DSG02     R        O  7 14REFFLD(SG02)
     A                                  7 39'退回物流點:'
     A            DSG07     R        O  7 53REFFLD(SG07)
     A            D#A031        22A  O  7 59
     A                                  8  2'退貨日期:'
     A            DSG06          6Y 0O  8 14EDTWRD('  /  /  ')
     A                                  8 24'(MM/DD/YY)'
     A                                  8 41'退貨原因:'
     A            DSG08     R        O  8 53REFFLD(SG08)
     A            D#A032        22A  O  8 59
     A                                  9 39'銷退總金額:'
     A            SG10      R        O  9 56EDTCDE(J)
     A                                 10 41'銷退稅額:'
     A            SX10           9Y 0O 10 56EDTCDE(J)
     A                                 11 35'含稅銷退總金額:'
     A            SGX10          9Y 0O 11 56EDTCDE(J)
     A                                 10  5'1=新增'
     A                                 10 16'2=修改'
     A                                 10 27'4=刪除'
     A                                 12  3'A 產品代號／名稱              +
     A                                      單位　　數量 搭贈 銷退金額 +
     A                                      銷退稅額 '
     A                                      DSPATR(UL)
