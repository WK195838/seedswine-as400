     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : POI0271D                                   *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 01/19/94                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POI027                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :採購單／驗收單查詢                        *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            PC03      R        O 10  3
     A            MA03      R        O 10 13
     A            PC09      R        O 10 34
     A            PC06      R        O 10 43EDTCDE(J)
     A            PC11      R        O 10 53EDTCDE(J)
     A            WPC61     R        O 10 63EDTCDE(J)
     A                                      REFFLD(PC11)
     A            PC10      R        O 10 73EDTCDE(J)
     A*------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'POI027'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'採購單查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司:'
     A            PC01      R        O  5 10
     A            #B03      R        O  5 13
     A                                  5 40'供應商:'
     A            PC05      R        O  5 50
     A            MB03      R        O  5 56
     A                                  7  2'採購單號:'
     A            PC02      R        O  7 15
     A                                  7 40'採購日期:'
M001DA*           DPB04     R        O  7 52REFFLD(PB04)
M001AA            DPB04          6  0O  7 52
     A                                      EDTWRD('  /  /  ')
     A                                  9  2'產品代號／簡稱              +
     A                                      　單位   採購量  己收量+
     A                                      　　未收量轉換 '
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
