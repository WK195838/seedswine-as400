     A*%%TS  SD  19920609  111821  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : DPI021D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/04/13                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : DPI021                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :                                            *
     A*    REMARK       :成本調整狀況                              *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920528  115315  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  4VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            MA01      R        O 10  8
     A            MA03      R        O 10 18
     A            MA06      R        O 10 37
     A*--------------------------------------------------------------*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920609  111821  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'DPI021'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'成本調整狀況'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'輸入作業選擇:'
     A                                  6  4'5=查詢'
     A                                  8  4'A  產品代號 中文簡稱        +
     A                                      英文簡稱      '
     A                                      DSPATR(UL)
     A            DBGN1     R        B  9  8REFFLD(MA01)
     A*--------------------------------------------------------------*
     A          R DSPC1
     A*%%TS  SD  19920217  160736  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A          R SFLSR2                    SFL
     A*%%TS  SD  19920320  200738  REPGMRP     REL-V2R1M0  5738-PW1
     A            RRN2           4S 0H
M001MA*           DIB04     R        O  9  3REFFLD(IB04)
M001MA            DIB04          6  0O  9  3
     A                                      EDTWRD('  /  /  ')
     A            IB02      R        O  9 13
     A            IB03      R        O  9 16
     A            IB10      R        O  9 22EDTCDE(J)
     A            IB11      R        O  9 38EDTCDE(J)
     A            IB12      R        O  9 54EDTCDE(J)
     A            IB13      R        O  9 70EDTCDE(J)
     A*--------------------------------------------------------------*
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19920217  161554  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                 19  3'=======-
     A                                      ===================================-
     A                                      ==================================='
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A          R DSPC2
     A*%%TS  SD  19920217  161554  REPGMRP     REL-V2R1M0  5738-PW1
     A                                  1  2'DPI021'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'成本調整狀況'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'產品代號:'
     A            MA01      R        O  5 14
     A            MA03      R        O  5 24
     A                                  7  2'調整日期　物流點'
     A                                  7 30'FOB'
     A                                  7 46'FHI'
     A                                  7 61'DUTY'
     A                                  7 69'庫存數量'
     A                                  8  3'=======-
     A                                      ===================================-
     A                                      ==================================='
