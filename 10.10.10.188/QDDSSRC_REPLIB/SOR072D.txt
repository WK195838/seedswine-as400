     A*%%TS  SD  19930419  134023  D910091     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR072D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 04/19/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR072                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :普銷子系統                                *
     A*    REMARK       :普銷統一發票列印                          *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930419  134023  D910091     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR072'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'普銷統一發票列印'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'公司　　:'
     A            DSI01          2A  B  6 38
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                 10 26'發票日期:'
     A            DSI21S         6  0B 10 38
     A*           DSI21S    R        B 10 38REFFLD(SI06)
     A                                      EDTWRD('  /  /  ')
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                 10 47'-'
     A            DSI21E         6  0B 10 49
     A*           DSI21E    R        B 10 49REFFLD(SI06)
     A                                      EDTWRD('  /  /  ')
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                 10 59'(MM/DD/YY)'
     A                                  8 26'物流點　:'
     A            DSI05S    R        B  8 38REFFLD(SI05)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  8 44'-'
     A            DSI05E    R        B  8 46REFFLD(SI05)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 12 26'發票號碼:'
     A            DSI02S    R        B 12 38REFFLD(SI02)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 12 49'-'
     A            DSI02E    R        B 12 51REFFLD(SI02)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 31'***請準備套版紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(URE 0117 REMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR12         50A  O 24  2MSGID(URE 0099 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
