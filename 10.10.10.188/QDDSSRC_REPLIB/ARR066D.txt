     A*%%TS  SD  19921224  100015  D910091     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : ARR066D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 81/12/24                                   *
     A*    PROGRAM NAME : ARR066                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :發票作廢排行榜                            *
     /*=============================================================*/
     /* CODE   AUTHOR.     DATE    DESCRIPTION                   */
     /*-------------------------------------------------------------*/
     /* M001  MICHELLE   98.12.23  Y2K MODIFY                    */
     /*=============================================================*/
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921224  100015  D910091     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARR066'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'發票作廢排行榜'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 28'辦事處　:'
     A            COMP           1A  B  5 40VALUES(' ' '1' '2' '3')
     A                                  5 42'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  7 28'客戶　　:'
     A                                      DSPATR(HI)
     A            DSI08S    R        B  7 40REFFLD(SI08)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  7 46'-'
     A            DSI08E    R        B  7 48REFFLD(SI08)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8 28'業務員　:'
     A                                      DSPATR(HI)
     A            DSI31S    R        B  8 40REFFLD(SI31)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 48'-'
     A            DSI31E    R        B  8 50REFFLD(SI31)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 28'發票日期:'
M001MA*           DSI21S    R        B  9 40REFFLD(SI21)
M001MA            DSI21S         6  0B  9 40
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      EDTWRD('  /  /  ')
     A                                  9 49'-'
M001MA*           DSI21E    R        B  9 51REFFLD(SI21)
M001MA            DSI21E         6  0B  9 51
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                      EDTWRD('  /  /  ')
     A                                  9 60'(MM/DD/YY)'
     A                                 10 28'前幾名內:'
     A            DSORT          4Y 0B 10 40TEXT('前幾名內')
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                      EDTCDE(Z)
     A                                      RANGE(0 9999)
     A                                 12 28'選擇　　:'
     A            DSEL           1A  B 12 41VALUES('A' 'B')
     A                                 12 43'A－依業務員'
     A                                 13 43'B－依客戶'
     A                                 16 28'執行環境:'
     A            $EVR           1Y 0B 16 41VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 28'列印份數'
     A                                 17 39':'
     A            $CPY           3Y 0B 17 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 28'列表機'
     A                                      DSPATR(HI)
     A                                 18 39':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 41CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 29'***請準備８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
