     A*%%TS  SD  19920811  103855  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARA007D                                    *
     A*    AUTHOR       : A1139 JANE                                 *
     A*    CREATE DATE  : 81/03/26                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARA007                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款子系統                            *
     A*    REMARK       :價差折讓處理 (A/U/D/I)                    *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.10 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M004      | 00.04.11 | FOR DUTY菸酒稅法實施
     A*---------------------------------------------------------------
     A* M005      | 03.04.09 |發票勾稽原因碼80不扣抵發票未沖金額
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A*%%TS  SD  19920811  103855  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B 10  2VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            AF01      R        O 10  5
     A            AF02      R        O 10 17
     A            AF03      R        O 10 30
     A            AF04      R        O 10 36
M003MA*           AF07      R        O 10 41EDTCDE(N)
M003MA            W79            9  0O 10 41EDTCDE(N)
     A            AF06      R        O 10 54
M001MA*           DAF05     R        O 10  8REFFLD(AF05)
M001MA            DAF05          6  0O 10  8
     A                                      EDTWRD('  /  /  ')
     A            AF08      R        O 10 66REFFLD(RERFR/AF08 *LIBL/RERF)
     A            S#A031        12   O 10 69
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920611  084135  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'ARA007'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'價差折讓處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  6'2=修改'
     A                                  5 16'4=刪除'
     A                                  5 26'5=查詢'
     A            DBGN3     R        B  9 17REFFLD(AF02)
     A                                  8  2'A公司MM/DD/YY折讓單號  客戶+
M004MA*                                     代號    折讓金額發票號碼 +
M004MA                                      代號  折讓總金額發票號碼 +
M004MA                                        價差原因      '
     A                                      DSPATR(UL)
     A            DBGN1          2   B  9  5
     A            DBGN2          6Y 0B  9  8EDTCDE(Y)
     A*----------------------------------------------------------------
     A          R DSPC1
     A*%%TS  SD  19920320  104337  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          60A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          60A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          60A  O 24  2MSGID(UPT 0123 PTMF)
     A                                      DSPATR(HI)
     A  80                             24  2'折讓單號'
     A                                      DSPATR(HI)
     A  80        DAF02     R        O    +1REFFLD(AF02)
     A  80                                  DSPATR(RI)
     A  80                                +1'新增成功'
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A*%%TS  SD  19920810  183651  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CF03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'ARA007'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'價差折讓處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司　　:'
     A            DAF01     R        B  5 15REFFLD(AF01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#B03         16A  O  5 20
     A                                  7  2'客戶代號:'
     A                                      DSPATR(HI)
     A            HAF03     R        H      REFFLD(AF03)
     A            DAF03     R        B  7 15REFFLD(AF03)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DAF04     R        B  7 21REFFLD(AF04)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            ME04      R        O  7 27
     A                                  9  2'折讓單號:'
     A            DAF02     R        O  9 15REFFLD(AF02)
     A                                 11  2'折讓日期:'
M001MA*           DAF05     R        B 11 15REFFLD(AF05)
M001MA            DAF05          6  0B 11 15
     A                                      EDTWRD('  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 11 25'(MM/DD/YY)'
     A                                 13  2'發票號碼:'
     A                                      DSPATR(HI)
     A            HAF06     R        H      REFFLD(AF06)
     A            DAF06     R        B 13 15REFFLD(AF06)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A N65                                  DSPATR(HI)
M004AA                                 15  2'折讓總金額:'
M004AA            DAF10     R        B 15 16REFFLD(AF07)
M004AA                                      EDTCDE(J)
M004AA  66                                  DSPATR(PC)
M004AA  66                                  DSPATR(RI)
M004AA  30                                  DSPATR(UL)
M004AA N30                                  DSPATR(PR)
M004AA            HAF10     R        H      REFFLD(AF07)
M004MA*                                15  2'折讓金額:'
M004MA                                 16  4'未稅金額:'
     A            HAF07     R        H      REFFLD(AF07)
M004MA*           DAF07     R        B 15 15REFFLD(AF07)
M004MA            DAF07     R        O 16 16REFFLD(AF07)
     A                                      EDTCDE(J)
M004MA* 66                                  DSPATR(PC)
M004MA* 66                                  DSPATR(RI)
M004MA* 30                                  DSPATR(UL)
M004MA*N30                                  DSPATR(PR)
M004AA                                 17  4'折讓稅額:'
M004AA            HAF09          9  0H
M004AA            DAF09          9  0B 17 16
     A                                      EDTCDE(J)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
M004MA*                                17  2'價差原因:'
M004MA                                 19  4'價差原因:'
     A                                      DSPATR(HI)
M004MA*           DAF08     R        B 17 15REFFLD(AF08)
M004MA            DAF08     R        B 19 16REFFLD(AF08)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
M004MA*           D#A031        22A  O 17 19
M004MA            D#A031        22A  O 19 20
     A                                 15 36'未沖金額:'
     A            DSI30     R        O 15 48REFFLD(RERFR/SI30 *LIBL/RERF)
     A                                      EDTCDE(J)
M004AA                                 16 36'未沖稅額:'
M004AA            DSI24     R        O 16 48REFFLD(RERFR/SI30 *LIBL/RERF)
M004AA                                      EDTCDE(J)
M004MA*                                14 36'銷退金額:'
M004MA                                 14 32'含稅銷退金額:'
     A            DSI28     R        O 14 48REFFLD(RERFR/SI28 *LIBL/RERF)
     A                                      EDTCDE(J)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A                                 22 58'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          60A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          60A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          60A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         60A  O 24  2MSGID(URE 0052 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         60A  O 24  2MSGID(URE 0053 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         60A  O 24  2MSGID(URE 0016 REMF)
     A                                      DSPATR(HI)
     A  84        ERR13         60A  O 24  2MSGID(URE 0054 REMF)
     A                                      DSPATR(HI)
     A  83        ERR14         60A  O 24  2MSGID(UPT 5015 PTMF)
     A                                      DSPATR(HI)
     A  82        ERR15         60A  O 24  2MSGID(URE 0096 REMF)
     A                                      DSPATR(HI)
     A  81        ERR16         60A  O 24  2MSGID(URE 0097 REMF)
     A                                      DSPATR(HI)
     A  80        ERR17         60A  O 24  2MSGID(URE 0098 REMF)
     A                                      DSPATR(HI)
     A  79        ERR18         60A  O 24  2MSGID(URE 0100 REMF)
     A                                      DSPATR(HI)
M004AA  78        ERR19         60A  O 24  2MSGID(URE 0158 REMF)
M004AA                                      DSPATR(HI)
M004AA  77        ERR20         60A  O 24  2MSGID(URE 0159 REMF)
M004AA                                      DSPATR(HI)
M004AA  76        ERR21         60A  O 24  2MSGID(URE 0160 REMF)
M004AA                                      DSPATR(HI)
M005AA  75        ERR22         60A  O 24  2MSGID(URE 0165 REMF)
M005AA                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
