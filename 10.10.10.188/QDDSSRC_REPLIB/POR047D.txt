     A*%%TS  SD  19920817  162800  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : POR047D                                    *
     A*    AUTHOR       : A1118  PHILP                               *
     A*    CREATE DATE  : 81/01/17                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POR047                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購資料子系統                            *
     A*    REMARK       :國外採購到貨成本單                        *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.12.16 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19920817  162800  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'POR047'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'國外採購到貨成本單'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'公司別  :'
     A            DPD01S    R        B  6 38REFFLD(PD01)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  6 51'-'
     A            DPD01E    R        B  6 53REFFLD(PD01)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                 12 26'驗收單號:'
     A            DPD02S    R        B 12 38REFFLD(PD02)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                 12 51'-'
     A            DPD02E    R        B 12 53REFFLD(PD02)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備　８０格報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                  8 26'供應商　:'
     A                                 10 26'進貨日期:'
M001MA*           DPD04S    R        B 10 38REFFLD(RERFR/PD04 *LIBL/RERF)
M001MA            DPD04S         6  0B 10 38
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                      EDTCDE(Y)
     A                                  8 51'-'
     A                                 10 51'-'
M001MA*           DPD04E    R        B 10 53REFFLD(RERFR/PD04 *LIBL/RERF)
M001MA            DPD04E         6  0B 10 53
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                      EDTCDE(Y)
     A            DPD07S    R        B  8 38REFFLD(RERFR/PD07 *LIBL/RERF)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A            DPD07E    R        B  8 53REFFLD(RERFR/PD07 *LIBL/RERF)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
