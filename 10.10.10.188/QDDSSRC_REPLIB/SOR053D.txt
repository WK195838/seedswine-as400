     A*%%TS  SD  19941124  091738  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR053D                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/04/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR053                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :收款明細表                                *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19941124  091738  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SOR053'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 34'收款明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 25'公司　　　:'
     A                                      DSPATR(HI)
     A            DSI01S    R        B  4 39REFFLD(SI01)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A                                  4 42'-'
     A            DSI01E    R        B  4 44REFFLD(SI01)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                  6 25'通路　　　:'
     A                                      DSPATR(HI)
     A            DSI03S         1A  B  6 39DSPATR(HI)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A                                  6 42'-'
     A            DSI03E         1A  B  6 44DSPATR(HI)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                  7 25'型態　  :'
     A                                      DSPATR(HI)
     A            DSI04S         2A  B  7 39DSPATR(HI)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                  7 42'-'
     A            DSI04E         2A  B  7 44DSPATR(HI)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A                                  8 25'客戶代號　:'
     A                                      DSPATR(HI)
     A            CSI23S         9A  B  8 39
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  8 49'-'
     A            CSI23E         9A  B  8 51
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                 10 25'送貨日期　:'
     A            DSI06          6Y 0B 10 39EDTWRD('  /  /  ')
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A            DSI06S         6Y 0B 11 39EDTWRD('  /  /  ')
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                 11 49'-'
     A            DSI06E         6Y 0B 11 51EDTWRD('  /  /  ')
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                 13 25'選項　  :'
     A            DSEL           1A  B 13 39VALUES('A' 'B')
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                 13 42'(A-最新業務員)'
     A                                 14 42'(B-交易業務員)'
     A                                 16 26'執行環境　:'
     A            $EVR           1Y 0B 16 40VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數　:'
     A            $CPY           3Y 0B 17 40EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 40CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備套版報表紙***  '
     A                                 21 26'***僅含未沖帳完畢資料***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR8          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR9          50A  O 24  2MSGID(UPT 0131 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 10 49'止'
     A                                  9 25'業務員　　:'
     A                                      DSPATR(HI)
     A            DSI31S         6A  B  9 39
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A N72                                  DSPATR(HI)
     A                                  9 49'-'
     A            DSI31E         6A  B  9 51
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A N73                                  DSPATR(HI)
