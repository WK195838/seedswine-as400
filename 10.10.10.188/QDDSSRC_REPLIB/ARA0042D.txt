     A*%%TS  SD  20021008  173648  REPGMR      REL-V4R4M0  5769-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARA0042D                                   *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/03/24                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARA0042                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :發票明細查詢                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M004      | 00.05.04 |菸酒稅實施，修改應稅
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR                     SFL
     A*%%TS  SD  20021008  173648  REPGMR      REL-V4R4M0  5769-PW1
     A            RRN            4S 0H
     A            SF03      R        O 10  5
     A            MA03      R        O 10 15
     A            QTY            5Y 0O 10 35EDTCDE(M)
     A            DSF06     R        O 10 43REFFLD(SF06)
     A                                      EDTCDE(M)
     A            TOT            9Y 0O 10 55EDTCDE(M)
     A            DISC           9Y 0O 10 69EDTCDE(M)
     A*--------------------------------------------------------------*
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920122  174051  REPGMR      REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F12=回前畫面'
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A          R DSPC1
     A*%%TS  SD  19920810  101740  REPGMRP     REL-V2R1M0  5738-PW1
     A                                  1  2'ARA0042'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR005'
     A                                  2 32'發票明細查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A            SE01      R        O  4 14
     A            D#B03     R   16   O  4 17REFFLD(#B03)
     A                                  4 34'地區代號:'
     A            SE34      R        O  4 46
     A            #D02      R        O  4 49
     A                                  5  2'客戶代號:'
     A            SE04      R        O  5 14
     A            SE05      R        O  5 20
     A            ME04      R        O  5 25
     A                                  6  2'單據號碼:'
     A            SE02      R        O  6 14
     A                                  6 34'日　　期:'
M001MA*           DSE06     R        O  6 46REFFLD(SE06)
M001MA            DSE06          6  0O  6 46
     A                                      EDTWRD('  /  /  ')
     A                                  6 56'(MM/DD/YY)'
     A                                  7  2'發票日期:'
     A            YDATE          6Y 0O  7 14EDTWRD('  /  /  ')
M004AA                                  7 34'發票稅額:'
M004AA            SI24      R        O  7 46REFFLD(SI24)
M004AA                                      EDTCDE(2)
     A                                  9  4'產品代號／名稱　　　　　　　　　+
     A                                      數量   單價      金　額      -
     A                                      折　扣'
     A                                      DSPATR(UL)
     A                                  7 23'(MM/DD/YY)'
