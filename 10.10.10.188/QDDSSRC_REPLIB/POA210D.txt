     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : POA210D                                    *
     A*    AUTHOR       : A1034  STEPHANIE                           *
     A*    CREATE DATE  : 79/08/09                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POA210, POI210                             *
     A*    SYSTEM       :銷售系統                                  *
     A*    SUBSYSTEM    :車輛採購                                  *
     A*    REMARK       :車型請購單建立，查詢                      *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PORF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A                                  1  2'<POA210D>'
     A            CONAME        40A  O  1 20MSGID(US#9001 S#MF)
     A                                  1 64'日期:'
     A            $CHYMD         6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'車型請購單建立'
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A*
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                  1  2'<POI210D>'
     A            CONAME        40A  O  1 20MSGID(US#9001 S#MF)
     A                                  1 64'日期:'
     A            $CHYMD         6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'車型請購單查詢'
     A*----------------------------------------------------------------
     A          R DSPC3                     OVERLAY
     A  98        ERR1          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR6          50A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                    +3'PF12=回前畫面'
     A                                    +3'PF19=計劃變更'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC4                     OVERLAY
     A*
     A  96        ERR1          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          50A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          50A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          50A  O 22  2MSGID(UPO 0004 POMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR6          50A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          50A  O 22  2MSGID(UPO 0011 POMF)
     A                                      DSPATR(HI)
     A*
     A                                 23  2'PF1 =明細顯示'
     A                                 23 20'PF3 =回主畫面'
     A                                 23 38'PF5 =參考資料'
     A                                 23 56'PF12=回前畫面'
     A                                 24  2'PF19=計劃變更'
     A                                 24 20'PA1 =下一頁　'
     A                                 24 38'PA2 =上一頁　'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPC5                     OVERLAY
     A*
     A  96        ERR1          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          50A  O 22  2MSGID(UPT 2031 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          50A  O 22  2MSGID(UPT 2041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR5          50A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A*
     A                                 23  2'PF1 =明細顯示'
     A                                    +3'PF3 =回主畫面'
     A                                    +3'PF7 =上一筆　'
     A                                    +3'PF8 =下一筆　'
     A                                 24  2'PF12=回前畫面'
     A                                    +3'PF19=計劃變更'
     A                                    +3'PA1 =下一頁　'
     A                                    +3'PA2 =上一頁　'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<POA210D>'
     A            CONAME        40A  O  1 20MSGID(US#9001 S#MF)
     A                                  1 64'日期:'
     A            $CHYMD         6Y 0O  1 72EDTCDE(Y)
     A            $USERN        10A  O  2  2
     A                                  2 30'車型請購單建立'
     A                                  4  2'請購單號:'
     A            DZF01     R        B  4 14REFFLD(ZF01)
     A  60                                  DSPATR(PC RI)
     A                                  5  2'請購日期:'
     A            DZF021         2Y 0B  5 14
     A  61                                  DSPATR(PC RI)
     A                                      CHECK(RB)
     A                                      EDTWRD('  ')
     A                                    +1'/'
     A            DZF022         2Y 0B    +1
     A  61                                  DSPATR(PC RI)
     A                                      CHECK(RB)
     A                                      EDTWRD('  ')
     A                                    +1'/'
     A            DZF023         2Y 0B    +1
     A  61                                  DSPATR(PC RI)
     A                                      CHECK(RB)
     A                                      EDTWRD('  ')
     A                                  7 27'作業選擇:'
     A            DOPT           1A  B  7 39VALUES('1' '2' '4' '5')
     A                                  7 45'1.新增'
     A                                  8 45'2.修改'
     A                                  9 45'4.刪除'
     A                                 10 45'5.查詢'
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR6          50A  O 22  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR5          50A  O 22  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 20'PF18=全頁查詢'
     A                                 24 67'【泛太資訊】'
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF19(19)
     A                                  4  2'請購單號:'
     A            DZF01     R        O  4 14REFFLD(ZF01)
     A                                  4 49'請購部門:'
     A            DZF05     R        B  4 61REFFLD(ZF05)
     A  60                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A03         12A  O  4 68
     A                                  5  2'請購日期:'
     A            DZF021         2Y 0O  5 14EDTWRD('  ')
     A  61                                  DSPATR(PC RI)
     A                                  5 17'/'
     A            DZF022         2Y 0O  5 19
     A  61                                  DSPATR(PC RI)
     A                                      EDTWRD('  ')
     A                                  5 22'/'
     A            DZF023         2Y 0B  5 24
     A                                      CHECK(RB)
     A                                      EDTWRD('  ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  61                                  DSPATR(PC RI)
     A                                  5 28'需求日期:'
     A            DZF03     R        B  5 40REFFLD(ZF03)
     A                                      CHECK(RB)
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  5 49'請購人員:'
     A            DZF04     R        B  5 61REFFLD(ZF04)
     A  63                                  DSPATR(PC RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            #C02      R        O  5 68REFFLD(#C02 *LIBL/PTRF)
     A                                  6  2'功'
     A                                  6  8'車型編號'
     A                                  6 42'-------配　備--------請購　採-
     A                                      購　結'
     A                                  7  2'能NO車型名稱'
     A                                  7 25'車色'
     A                                  7 41'一　二　三　四　五　六　數量　數-
     A                                      量　案'
     A                                  8  2' -- -- --------------- ------------
     A                                      ----- --- --- --- --- --- --- ---- -
     A                                       ----  --'
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            DOP            1A  B  9  4VALUES(' ' '1' '2' '4')
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A  64                                  DSPATR(PC RI)
     A            RRN            4  0H
     A            DZG02          2Y 0O  9  6EDTCDE(4)
     A            DZG03     R        B  9  9REFFLD(ZG03)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  65                                  DSPATR(PC RI)
     A            DZG04     R        B  9 25REFFLD(ZG04)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  66                                  DSPATR(PC RI)
     A            D#A031        12   O  9 29
     A            DZG07     R        B  9 42REFFLD(ZG07)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  67                                  DSPATR(PC RI)
     A            DZG08     R        B  9 46REFFLD(ZG08)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  68                                  DSPATR(PC RI)
     A            DZG09     R        B  9 50REFFLD(ZG09)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  69                                  DSPATR(PC RI)
     A            DZG10     R        B  9 54REFFLD(ZG10)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(PC RI)
     A            DZG11     R        B  9 58REFFLD(ZG11)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  70                                  DSPATR(PC RI)
     A            DZG12     R        B  9 62REFFLD(ZG12)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  72                                  DSPATR(PC RI)
     A            DZG05          4  0B  9 66
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A  34
     AO 35                                  DSPATR(UL)
     A N34N35                               DSPATR(PR)
     A  80                                  DSPATR(PC RI)
     A            DZG06          4  0O  9 72
     A                                      EDTCDE(3)
     A            ZG19      R        O  9 78
     A            DZC02         22   O 10  9
     A            DZG13     R        B 10 42REFFLD(ZG13)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  73                                  DSPATR(PC RI)
     A            DZG14     R        B 10 46REFFLD(ZG14)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  74                                  DSPATR(PC RI)
     A            DZG15     R        B 10 50REFFLD(ZG15)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  75                                  DSPATR(PC RI)
     A            DZG16     R        B 10 54REFFLD(ZG16)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  76                                  DSPATR(PC RI)
     A            DZG17     R        B 10 58REFFLD(ZG17)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  77                                  DSPATR(PC RI)
     A            DZG18     R        B 10 62REFFLD(ZG18)
     A  35                                  DSPATR(UL)
     A N35                                  DSPATR(PR)
     A  78                                  DSPATR(PC RI)
     A            DCHK           1   H
     A*                                     'Y' -已存檔中--> UPD
     A*                                     ' ' -不存檔中--> ADD
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0006)
     A                                      SFLDROP(CF01)
     A                                      CA03(03)
     A N36                                  CF05(05)
     A  36                                  CF07(07)
     A  36                                  CF08(08)
     A                                      CF12(12)
     A                                      CF19(19)
     A                                      ROLLUP(25)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                 21  2'功能(1-新增2-修改4-刪除)'
     A  32                             21 46'車型明細:'
     A  32        DOP1      R        B 21 58REFFLD(RRN *SRC)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A  79                                  DSPATR(PC RI)
     A  29                                  DSPATR(UL)
     A N29                                  DSPATR(PR)
     A  33                             21 64'配備明細:'
     A  33        DZB01     R        B 21 76REFFLD(ZB01)
     A  29                                  DSPATR(UL)
     A N29                                  DSPATR(PR)
