     A*%%TS  SD  19930316  170842  D910074     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : SOR065D                                    *
     A*    AUTHOR       : TINA WHANG                                 *
     A*    CREATE DATE  : 82/03/15                                   *
     A*    PROGRAM NAME : SAR065                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :試飲原因統計表　　　　　　                *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930316  170842  D910074     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SAR065'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'試飲原因統計表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 23'辦事處　:'
     A            COMPS          1A  B  5 35VALUES(' ' '1' '2' '3')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5 37'-'
     A            COMPE          1A  B  5 39VALUES(' ' '1' '2' '3')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 42'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  7 23'試飲原因:'
     A                                      DSPATR(HI)
     A            DSC10S    R        B  7 35REFFLD(SC10)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  7 38'-'
     A            DSC10E    R        B  7 40REFFLD(SC10)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 23'通　　路:'
     A                                      DSPATR(HI)
     A            DSC33S    R        B  9 35REFFLD(SC33)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  9 37'-'
     A            DSC33E    R        B  9 39REFFLD(SC33)
     A N65                                  DSPATR(HI)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 11 23'產品代號:'
     A                                      DSPATR(HI)
     A            DSD03S    R        B 11 35REFFLD(SD03)
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                 11 45'-'
     A            DSD03E    R        B 11 47REFFLD(SD03)
     A N67                                  DSPATR(HI)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                 13 23'確認日期:'
M001DA*           DSC39S    R        B 13 35REFFLD(SC39)
M001AA            DSC39S         6  0B 13 35
     A                                      EDTWRD('  /  /  ')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                 13 45'-'
M001DA*           DSC39E    R        B 13 47REFFLD(SC39)
M001AA            DSC39E         6  0B 13 47
     A                                      EDTWRD('  /  /  ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                 13 58'(MM/DD/YY)'
     A                                 16 28'執行環境:'
     A            $EVR           1Y 0B 16 41VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 28'列印份數'
     A                                 17 39':'
     A            $CPY           3Y 0B 17 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 28'列表機'
     A                                      DSPATR(HI)
     A                                 18 39':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 41CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 29'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A  89        ERR12         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
