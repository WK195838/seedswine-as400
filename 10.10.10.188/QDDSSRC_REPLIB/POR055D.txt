     A*%%TS  SD  19930621  111802  D910091     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*    DSPF NAME    : POR055D                                    *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 05/11/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POR055                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :採購資料子系統                            *
     A*    REMARK       :供應商到貨完稅狀況表                      *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19930621  111802  D910091     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'POR055'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'供應商到貨完稅狀況表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 24'供應商代號:'
     A                                      DSPATR(HI)
     A            DPD07S    R        B  6 38REFFLD(PD07)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A N60                                  DSPATR(HI)
     A                                  6 44'-'
     A            DPD07E    R        B  6 46REFFLD(PD07)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A                                  8 26'到貨年月:'
     A            DPD04S         4Y 0B  8 38EDTWRD('  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  8 44'-'
     A            DPD04E         4Y 0B  8 46EDTWRD('  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  8 52'(MM/YY)'
     A                                 10 26'產品代號:'
     A                                      DSPATR(HI)
     A            DPE04S    R        B 10 38REFFLD(PE04)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64                                  DSPATR(HI)
     A                                 10 48'-'
     A            DPE04E    R        B 10 51REFFLD(PE04)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備１３２格報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行  F3 =回主畫面F4 =視窗查+
     A                                      詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
