     A*%%TS  SD  20021017  165132  REPGMR      REL-V4R4M0  5769-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARA0083D                                   *
     A*    AUTHOR       : A1084  JOYCE                               *
     A*    CREATE DATE  : 81/03/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARA008                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款作業                              *
     A*    REMARK       :銷貨折讓處理－發票分配查詢                *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.10 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M002      | 00.04.14 | FOR DUTY菸酒稅法實施
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  20021017  165132  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DAG04     R        O 13  4REFFLD(AG04)
     A            DSJ04A    R        O 13 15REFFLD(SJ04)
     A                                      EDTCDE(1)
     A            SJ13      R        O 13 22EDTCDE(1)
     A            DSJ06A    R        O 13 28REFFLD(SJ05)
     A                                      EDTCDE(3)
     A            DSJ06B    R        O 13 39REFFLD(SJ06)
     A                                      EDTCDE(3)
     A            DAG12     R        O 13 49REFFLD(AG12)
     A                                      EDTCDE(3)
     A            HAG12     R        H      REFFLD(AG12)
     A            DAG13     R        O 13 55REFFLD(AG13)
     A                                      EDTCDE(3)
     A            HAG13     R        H      REFFLD(AG13)
     A            DAG07     R        O 13 71REFFLD(AG07)
     A                                      EDTCDE(3)
     A            HAG05     R        H      REFFLD(AG05)
     A            HAG07     R        H      REFFLD(AG07)
     A            HAG21     R        H      REFFLD(AG07)
     A            HAG08     R        H      REFFLD(AG08)
     A            HAG14     R        H      REFFLD(AG14)
     A            HAG15     R        H      REFFLD(AG15)
     A            HAG16     R        H      REFFLD(AG16)
     A            HRCDN          4A  H
     A            HSJ08     R        H      REFFLD(SJ08)
     A            HSJ09     R        H      REFFLD(SJ09)
     A            HSJ14     R        H      REFFLD(SJ14)
     A            HSJ15     R        H      REFFLD(SJ15)
     A            DAG06     R        O 13 60REFFLD(AG06)
     A                                      EDTCDE(3)
     A            DSI21          6Y 0O 14  4EDTCDE(Y)
     A            DAG21          6Y 0O 14 60EDTCDE(1)
     A            DSI24          6Y 0O 14 71EDTCDE(J)
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920716  102623  REPGMRP     REL-V2R1M0  5738-PW1
M002MA*                                     SFLSIZ(0016)
M002MA*                                     SFLPAG(0008)
M002MA                                      SFLSIZ(0060)
M002MA                                      SFLPAG(0004)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DATA2          6Y 0H
     A                                  1  2'ARA008'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 33'銷貨折讓處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司　　:'
     A            SH01      R        O  4 14
     A            #B03      R        O  4 17
     A                                  4 36'客戶代號　:'
     A            SG04      R        O  4 50
     A            SG05      R        O  4 56
     A                                  5  2'產品代號:'
     A            SH03      R        O  5 14
     A            MA03      R        O  5 24
     A            ME04      R        O  4 61
     A                                  6  2'折讓單號:'
     A            SG09      R        O  6 14
     A                                  5 43'退貨日期　:'
M001MA*           DSG06     R        O  5 57REFFLD(SG06)
M001MA            DSG06          6  0O  5 57
     A                                      EDTWRD('  /  /  ')
     A                                  5 67'(MM/DD/YY)'
     A                                  6 43'折讓日期　:'
M001MA*           DSG26     R        O  6 57REFFLD(SG26)
M001MA            DSG26          6  0O  6 57
     A                                      EDTWRD('  /  /  ')
     A                                  6 67'(MM/DD/YY)'
     A                                  7  2'退貨單號:'
     A            SH02      R        O  7 14
     A*M002M                            7 43'折讓金額　:'
M002MA                                  7 39'含稅折讓金額　:'
     A            DSH07     R        O  7 57REFFLD(SH07)
     A                                      EDTCDE(1)
     A                                  8  2'訂購數量:'
     A            SH04      R        O  8 14EDTCDE(1)
     A                                  8 39'未分配訂購數量:'
     A            DSH041    R        O  8 57REFFLD(SH04)
     A                                      EDTCDE(1)
     A                                  9  2'搭贈數量:'
     A            SH05      R        O  9 15EDTCDE(1)
     A                                  9 39'未分配搭贈數量:'
     A            DSH051    R        O  9 58REFFLD(SH05)
     A                                      EDTCDE(1)
     A                                 11 15'-發票數量-'
     A*M002M                           11 47'-分配數量-'
M002MA                                 11 47'-分配數量- 單價　　銷退金額'
M002MA*                                12  2 發票金額 訂購搭贈單價+
     A                                 12  2'發票號碼   訂購搭贈  售價 -
M002MA                                       發票金額 訂購搭贈稅額+
M002MA*                                     　　銷退金額'
M002MA                                           　　未沖稅額'
     A                                      DSPATR(UL)
     A*
     A          R DSPC1
     A*%%TS  SD  19920521  133807  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0056 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0057 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0062 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
