     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : PIR042P                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/04/25                                   *
     A*    UPDATE DATE  : 12/01/92 JUDY                              *
     A*    PROGRAM NAME : PIR042                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :盤點系統                                  *
     A*    REMARK       :盤點清冊                                  *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    32MSGCON(030 URE9999 REMF)
     A                                    41'盤點清冊'
     A                                      SPACEB(1)
     A*                                    1'公司　:'
     A*                                     SPACEB(2)
     A*           DIO01     R        O    +1REFFLD(IO01)
     A*           #B03      R        O    17
     A                                    62'日期:'
     A                                      SPACEB(2)
     A            $EGMDY         6  0O    70EDTCDE(Y)
     A                                    79'頁次:'
     A                                    87PAGNBR EDTCDE(Z)
     A                                    62'時間:'
     A                                      SPACEB(1)
     A                                    70TIME
     A                                     1'物流點:'
     A                                      SPACEB(1)
     A            DIO02     R        O    +1REFFLD(IO02)
     A            DSTOR         22A  O    17
     A                                    62' USER :'
     A            $USER         10   O    70
     A                                    81'<PIR042>'
     A                                     1
     A                                      DFT('=======================-
     A                                      ===================================-
     A                                      ==================================')
     A                                      SPACEB(1)
     A                                     1'盤點單號　產品代號　產品簡稱'
     A                                      SPACEB(1)
     A                                    39'單位　良品數量　壞品數量　總數量'
     A                                    73'盤點日期'
     A                                    83'作廢註記'
     A                                     1
     A                                      DFT('------------------------
     A                                      ------------------------------------
     A                                      ----------------------------------')
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEB(1)
     A            IO03      R              2
     A            IO04      R             11
     A            MA03      R             20
     A            MA13      R             39
     A            IO06      R             47EDTCDE(J)
     A            IO07      R             57EDTCDE(J)
     A            IO05      R             65EDTCDE(J)
M001MA*           DIO08     R             74REFFLD(IO08)
M001MA            DIO08          6  0     74
     A                                      EDTWRD('  /  /  ')
     A            IO09      R             87
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      DFT('=======================-
     A                                      ===================================-
     A                                      ==================================')
     A                                      SPACEB(1)
     A                                     2'***續下頁***'
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      DFT('=======================-
     A                                      ===================================-
     A                                      ==================================')
     A                                      SPACEB(1)
     A                                     2'<<PIR042>>--->報表結束'
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      DFT('=======================-
     A                                      ===================================-
     A                                      ==================================')
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE9                       SPACEB(1)
     A                                     3'もも此範圍無資料もも'
