     A*%%TS  SD  19930423  164636  D910121     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : IMA011D                                    *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 04/16/93                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMA011                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :普銷領／繳貨處理                          *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19930423  144030  D910121     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  2
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      VALUES(' ' '2' '4' '5' '6' '7')
     A            PII05     R        H      REFFLD(II05)
     A            DII05     R        O  9  5REFFLD(II05)
     A                                      EDTWRD('  /  /  ')
     A            PII02     R        O  9 15REFFLD(II02)
     A            PII03     R        O  9 29REFFLD(II03)
     A            #A0311        18A  O  9 35
     A            PII04     R        O  9 54REFFLD(II04)
     A            PII06     R        H      REFFLD(II06)
     A            DII06     R        O  9 68REFFLD(II06)
     A                                      EDTWRD('  /  /  ')
     A            PII01     R        H      REFFLD(II01)
     A            PII09     R        H      REFFLD(II09)
     A            PII07     R        H      REFFLD(II07)
     A            PII08     R        H      REFFLD(II07)
      *
     A            XII02     R        O 10 15REFFLD(II02)
     A            XII03     R        O 10 29REFFLD(II03)
     A            XII04     R        O 10 54REFFLD(II04)
     A            XII05     R        H      REFFLD(II05)
     A            DII061    R        O 10 68REFFLD(II06)
     A                                      EDTWRD('  /  /  ')
     A            XII06     R        H      REFFLD(II06)
     A            XII07     R        H      REFFLD(II07)
     A            XII08     R        H      REFFLD(II07)
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19930423  144030  D910121     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0005)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'IMA011'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'普銷領／繳貨處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  6'2=修改'
     A                                  5 16'4=刪除'
     A                                  5 26'5=查詢'
     A                                  6  4'繳領日期'
     A                                  7  2'A  MM/DD/YY 單據號碼    出貨物+
     A                                      流點             收貨物流點 +
     A                                      實際入庫日'
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  5REFFLD(II05)
     A                                      EDTWRD('  /  /  ')
     A            DBGN2     R        B  8 15REFFLD(II02)
     A                                  5 37'6=領繳明細'
     A                                  5 51'7=繳貨作業'
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F6 =新增'
     A  70                             24  2'單據號碼'
     A                                      DSPATR(HI)
     A  70        DII02     R             +1REFFLD(II02)
     A                                      DSPATR(RI)
     A  70                                +1'新增成功'
     A                                      DSPATR(HI)
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0018 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0063 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0120 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0026 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R DSPD2
     A*%%TS  SD  19930423  164636  D910121     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A            HII03     R        H      REFFLD(II03)
     A                                  1  2'IMA011'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'普銷繳／領貨處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'出貨公司:'
     A            DII01     R        B  5 16REFFLD(II01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            #B031     R        O  5 19REFFLD(#B03)
     A                                  7  2'收貨公司　:'
     A            DII09     R        O  7 16REFFLD(II09)
     A            #B032     R        O  7 19REFFLD(#B03)
     A                                  9  2'單據號碼　:'
     A            DII02     R        O  9 16REFFLD(II02)
     A                                 11  2'出貨物流點:'
     A                                      DSPATR(HI)
     A            DII03     R        B 11 16REFFLD(II03)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            #A0312        22A  O 11 22
     A                                 13  2'收貨物流點:'
     A                                      DSPATR(HI)
     A            DII04     R        B 13 16REFFLD(II04)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            #A0322        22A  O 13 22
     A                                 15  2'領貨日期　:'
     A            DII05     R        B 15 16REFFLD(II05)
     A                                      EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 15 26'(MM/DD/YY)'
     A                                 17  2'實際入庫日:'
     A            DII06     R        O 17 16REFFLD(II06)
     A                                      EDTWRD('  /  /  ')
     A                                 17 26'(MM/DD/YY)'
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A                                 22 58'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0015 REMF)
     A                                      DSPATR(HI)
     A  86        ERR11         63A  O 24  2MSGID(URE 0016 REMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A  84        ERR13         63A  O 24  2MSGID(URE 0064 REMF)
     A                                      DSPATR(HI)
     A  83        ERR14         63A  O 24  2MSGID(URE 0099 REMF)
     A                                      DSPATR(HI)
     A  82        ERR15         63A  O 24  2MSGID(URE 0104 REMF)
     A                                      DSPATR(HI)
     A  81        ERR16         63A  O 24  2MSGID(URE 0119 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPC3
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F4 =視窗查詢'
     A                                    +2'F5 =參考資料'
     A                                    +2'F12=回前畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR9          63A  O 24  2MSGID(UPT 0080 PTMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A*%%TS  SD  19920717  210227  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 12  2
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                      VALUES(' ' '1' '2' '4' '5')
     A            DIJ03     R        B 12  5REFFLD(IJ03)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            MA03      R        O 12 15
     A            MA13      R        O 12 34
     A            HIJ04     R        H      REFFLD(IJ04)
     A            DIJ04     R        B 12 43REFFLD(IJ04)
     A                                      EDTCDE(1)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DCHK           1A  H
     A*                                     'Y' -已存檔中--> UPD
     A*                                     ' ' -不存檔中--> ADD
     A*------------------------------------------------------------
     A*
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19930423  101200  D910121     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0160)
     A                                      SFLPAG(0008)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'IMA011'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR003'
     A                                  2 32'普銷領貨處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'出貨公司　:'
     A            DII01     R        O  4 16REFFLD(II01)
     A            #B031     R        O  4 19REFFLD(#B03)
     A                                  5  2'收貨公司　:'
     A            DII09     R        O  5 16REFFLD(II09)
     A            #B032     R        O  5 19REFFLD(#B03)
     A                                  6  2'單據號碼　:'
     A            DII02     R        O  6 16REFFLD(II02)
     A                                  7  2'出貨物流點:'
     A            DII03     R        O  7 16REFFLD(II03)
     A            #A0312        22A  O  7 22
     A                                  7 46'領貨日期:'
     A            DII05     R        O  7 58REFFLD(II05)
     A                                      EDTWRD('  /  /  ')
     A                                  7 68'(MM/DD/YY)'
     A                                  8  2'收貨物流點:'
     A            DII04     R        O  8 16REFFLD(II04)
     A            #A0322        22A  O  8 22
     A                                  8 46'實際入庫:'
     A            DII06     R        O  8 58REFFLD(II06)
     A                                      EDTWRD('  /  /  ')
     A                                  8 68'(MM/DD/YY)'
     A                                  9  4'1=新增'
     A                                  9 16'2=修改'
     A                                  9 28'4=刪除'
     A                                  9 39'5=庫存查詢'
     A                                 11  2'A 產品代號／名稱              +
     A                                      單位　領貨數量'
     A                                      DSPATR(UL)
     A*------------------------------------------------------------
     A          R DSPC4
     A*
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F4 =視窗查詢'
     A                                    +2'F5 =參考資料'
     A                                    +2'F12=回前畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR9          63A  O 24  2MSGID(UPT 0080 PTMF)
     A                                      DSPATR(HI)
     A  85        ERR12         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR3                    SFL
     A*%%TS  SD  19930423  102217  D910121     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN3           4S 0H
     A            DIJ03     R        O 12  5REFFLD(IJ03)
     A            MA03      R        O 12 15
     A            MA13      R        O 12 34
     A            DIJ05     R        O 12 43REFFLD(IJ04)
     A                                      EDTCDE(1)
     A            DIJ045    R        B 12 57REFFLD(IJ04)
     A                                      EDTCDE(1)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            HIJ045    R        H      REFFLD(IJ04)
     A*------------------------------------------------------------
     A*
     A          R SFLCR3                    SFLCTL(SFLSR3)
     A*%%TS  SD  19930423  161748  D910121     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0160)
     A                                      SFLPAG(0008)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN3          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'IMA011'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR004'
     A                                  2 32'普銷繳貨處理'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'出貨公司　:'
     A            DII01     R        O  4 16REFFLD(II01)
     A            #B031     R        O  4 19REFFLD(#B03)
     A                                  5  2'收貨公司　:'
     A            DII09     R        O  5 16REFFLD(II09)
     A            #B032     R        O  5 19REFFLD(#B03)
     A                                  6  2'單據號碼　:'
     A            DII02     R        O  6 16REFFLD(II02)
     A                                  7  2'出貨物流點:'
     A            DII03     R        O  7 16REFFLD(II03)
     A            #A0312        22A  O  7 22
     A                                  7 46'繳貨日期:'
     A            DII05     R        O  7 58REFFLD(II05)
     A                                      EDTWRD('  /  /  ')
     A                                  7 68'(MM/DD/YY)'
     A                                  8  2'收貨物流點:'
     A            DII04     R        O  8 16REFFLD(II04)
     A            #A0322        22A  O  8 22
     A                                  8 46'實際入庫:'
     A            DII061    R        O  8 58REFFLD(II06)
     A                                      EDTWRD('  /  /  ')
     A                                  8 68'(MM/DD/YY)'
     A                                 11  2'  產品代號／名稱              +
     A                                      單位　領貨數量    繳貨數量'
     A                                      DSPATR(UL)
