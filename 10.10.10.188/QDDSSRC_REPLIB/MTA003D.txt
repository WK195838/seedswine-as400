     A*%%TS  SD  19951208  084445  D910341     REL-V3R1M0  5763-PW1
     A*  80/11/19  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : MTA003D                                    *
     A*    AUTHOR       : A1118  PHILIP TSAI                         *
     A*    CREATE DATE  : 81/01/29                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTA003                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :基本資料建檔子系統                        *
     A*    REMARK       :客戶基本資料維護 (A/U/D/I)                *
     A*===============================================================
     A* CODE | AUTHOR |  DATE  | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001 |MICHELLE|98.11.04| MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A* M002 |MICHELLE|03.05.28|新增統編查詢
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920629  091031  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  2
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      VALUES(' ' '2' '3' '4' '5')
     A            MD01      R        O  9  4
     A            ME04      R        O  9 10
     A            ME10      R        O  9 29
     A            ME12      R        O  9 42
     A            MD07      R        O  9 58
     A            MD08      R        O  9 60
     A            ME07      R        O  9 64
     A            ME20      R        O  9 67
     A            MC02      R        O  9 69
     A*  91/05/13  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920629  091031  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF06(06)
     A                                      CF08(08)
M002AA                                      CF09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'MTA003'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'客戶基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  4'2=修改'
     A                                  5 14'3=拷備'
     A                                  5 34'5=查詢'
     A                                  6  2' 客戶                      +
     A                                                              通+
     A                                      型　地狀'
     A                                  7  2'A代號　客戶簡稱　　　　　　+
     A                                      聯絡人    電話一        路+
     A                                      態　區態　業務員　'
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  4REFFLD(MD01)
     A                                      DSPATR(HI)
     A                                  5 24'4=刪除'
     A*  81/01/21  10:53:16    REPGMR      REL-R02M00  5728-PW1
     A          R DSPC1
     A*%%TS  SD  19920810  161516  REPGMRP     REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F6 =新增'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                 22 38'F4 =客戶簡稱查詢'
     A                                 22 58'F8 =客戶全稱查詢'
M002AA                                 23 10'F9 =客戶統編查詢'
     A          R DSPD2
     A*%%TS  SD  19920919  120728  D910121     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CA12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MTA003'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'客戶基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'客戶代號:'
     A            DMD01     R        B  4 14REFFLD(MD01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  6  2'客戶全稱:'
     A            DME03     R        B  6 14REFFLD(ME03)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7  2'客戶簡稱:'
     A            DME04     R        B  7 14REFFLD(ME04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8  2'客戶地址:'
     A            DME05     R        B  8 14REFFLD(ME05)
     A N70                                  DSPATR(HI)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DME06     R        B  8 47REFFLD(ME06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  2'通路　　:'
     A            DMD07     R        O  9 14REFFLD(MD07)
     A            D#A03         22A  O  9 17
     A                                  9 44'型態　　:'
     A                                      DSPATR(HI)
     A            DMD08     R        B  9 56REFFLD(MD08)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#C03     R        O  9 59REFFLD(#C03)
     A                                 10  2'地區　　:'
     A                                      DSPATR(HI)
     A            DME07     R        B 10 14REFFLD(ME07)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#D02     R        O 10 17REFFLD(#D02)
     A                                 10 44'區域　　:'
     A                                      DSPATR(HI)
     A            DME08     R        B 10 56REFFLD(ME08)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#E03     R        O 10 59REFFLD(#E03)
     A                                 11  2'負責人　:'
     A            DME09     R        B 11 14REFFLD(ME09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 11 44'聯絡人　:'
     A            DME10     R        B 11 56REFFLD(ME10)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12  2'收貨人　:'
     A            DME11     R        B 12 14REFFLD(ME11)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 12 44'集團代號:'
     A            DMD06     R        B 12 56REFFLD(MD06)
     A N72                                  DSPATR(HI)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#A031        18A  O 12 59
     A                                 13  2'電話一  :'
     A            DME12     R        B 13 14REFFLD(ME12)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13 44'電話二  :'
     A            DME13     R        B 13 56REFFLD(ME13)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  2'傳真機  :'
     A            DME14     R        B 14 14REFFLD(ME14)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 44'統一編號:'
     A            DMD17     R        B 14 56REFFLD(MD17)
     A N71                                  DSPATR(HI)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  2'發票聯數:'
     A            DMD18     R        B 15 14REFFLD(MD18)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                      EDTCDE(2)
     A                                 15 17'(2-二聯式, 3-三聯式)'
     A                                 15 44'業務人員:'
     A                                      DSPATR(HI)
     A            DME15     R        B 15 56REFFLD(ME15)
     A N66                                  DSPATR(HI)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMC02     R        O 15 63REFFLD(MC02)
     A                                 16  2'備註    :'
     A            DME16     R        B 16 14REFFLD(ME16)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DME17     R        B 16 47REFFLD(ME17)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  2'可寄賣放:'
     A            DMD20     R        B 17 14REFFLD(MD20)
     A                                      VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMD21     R        B 17 16REFFLD(MD21)
     A                                      VALUES('Y' ' ')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 18'(Y, )'
     A                                 17 43'信用額度:'
     A            DMD22     R        B 17 55REFFLD(MD22)
     A                                      EDTCDE(J)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17 70'仟'
     A            CMD361         6O  O    +1
     A            HMD361         1A  H
     A                                 18  2'信用條件:'
     A            DMD23     R        B 18 14REFFLD(MD23)
     A                                      VALUES('C' 'D' 'M')
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 16'(C-現金D-交貨M-月結)'
     A                                 18 43'期限天數:'
     A            DMD25     R        B 18 55REFFLD(MD25)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 18 59'日 '
     A                                 19  2'結算日期:'
     A            DMD24     R        B 19 14REFFLD(MD24)
     A                                      EDTCDE(3)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 19 43'收款日  :'
     A            DMD26     R        B 19 55REFFLD(MD26)
     A                                      EDTCDE(3)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20  2'使用額度:'
     A            DMD29     R        O 20 14REFFLD(MD29)
     A                                      EDTCDE(J)
     A                                 20 29'仟'
     A                                 21  2'最近交易:'
M001MA*           DME18     R        O 21 14REFFLD(ME18)
M001MA            DME18          6  0O 21 14
     A                                      EDTCDE(Y)
     A                                 21 44'建檔日期:'
M001MA*           DME19     R        O 21 56REFFLD(ME19)
M001MA            DME19          6  0O 21 56
     A                                      EDTCDE(Y)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A                                 22 58'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR8          50A  O 24  2MSGID(UPT 4003 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
