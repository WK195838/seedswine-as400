     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : MSOR045D                                   *
     A*    AUTHOR       : AN MING HSIA                               *
     A*    CREATE DATE  : 05/23/94                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MSOR045                                    *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :月客戶銷售（退）統計表                    *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MSOR045'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 28'月客戶銷售（退）統計表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 18'辦事處:'
     A            COMP           1A  B  4 30VALUES(' ' '1' '2' '3')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  4 32'( 1=北2=中3=南)'
     A                                  5 18'通路　　:'
     A                                      DSPATR(HI)
     A            DCD13S    R        B  5 30REFFLD(CD13)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A                                  5 40'-'
     A            DCD13E    R        B  5 42REFFLD(CD13)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A                                  6 18'型態　　:'
     A                                      DSPATR(HI)
     A            DCD14S    R        B  6 30REFFLD(CD14)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(HI)
     A                                  6 40'-'
     A            DCD14E    R        B  6 42REFFLD(CD14)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64                                  DSPATR(HI)
     A*                                 7 18'地區　　:'
     A*                                     DSPATR(HI)
     A*           DCD11S    R        B  7 30REFFLD(CD11)
     A* 65                                  DSPATR(PC)
     A* 65                                  DSPATR(RI)
     A*N65                                  DSPATR(HI)
     A*                                 7 40'-'
     A*           DCD11E    R        B  7 42REFFLD(CD11)
     A* 66                                  DSPATR(PC)
     A* 66                                  DSPATR(RI)
     A*N66                                  DSPATR(HI)
     A                                  7 18'客戶代號:'
     A                                      DSPATR(HI)
     A            DCD06S         9A  B  7 30
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A N67                                  DSPATR(HI)
     A                                  7 40'-'
     A            DCD06E         9A  B  7 42
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A N68                                  DSPATR(HI)
     A                                  8 18'產品組別:'
     A            DMA11          1A  B  8 30VALUES('1' '2' '3' ' ')
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A                                  8 32'(1-BRANDY 2-WHISKY 3-WINE " "-ALL)'
     A                                  9 18'產品代號:'
     A                                      DSPATR(HI)
     A            DCD04S    R        B  9 30REFFLD(CD04)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A N69                                  DSPATR(HI)
     A                                  9 40'-'
     A            DCD04E    R        B  9 42REFFLD(CD04)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N70                                  DSPATR(HI)
     A                                 10 16'業務員代號:'
     A                                      DSPATR(HI)
     A            DCD09S    R        B 10 30REFFLD(CD09)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A N74                                  DSPATR(HI)
     A                                 10 40'-'
     A            DCD09E    R        B 10 42REFFLD(CD09)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A N75                                  DSPATR(HI)
     A                                 11 18'確認年月:'
     A            DCD01          4Y 0B 11 30
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                      EDTWRD('  /  ')
     A                                 11 36'(MM/YY)'
     A                                 11 44'合計方式:'
     A            DSEL3          1   B 11 56VALUES('A' 'B')
     A                                 11 58'(A=總公司B=分公司)'
     A                                 12 18'分析方式:'
     A            DSEL           1A  B 12 30VALUES('A' 'B')
     A                                 12 32'( A-原包裝B-單瓶含拆解單瓶)'
     A                                 13 18'統計內容:'
     A            DSEL1          1A  B 13 30VALUES('4' '1' '2' '3')
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                 13 32'( 1=銷售（退）      2=銷售（退+
     A                                      ）含搭贈'
     A                                 14 32'  3=銷售（退）含試飲4=全部)'
     A                                 16 28'執行環境:'
     A            $EVR           1Y 0B 16 41VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 28'列印份數:'
     A            $CPY           3Y 0B 17 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 28'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 41CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 29'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
