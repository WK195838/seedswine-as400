     A*%%TS  SD  19960202  093933  D910341     REL-V3R1M0  5763-PW1
     A****************************************************************
     A*    DSPF NAME    : PIR048D                                    *
     A*    AUTHOR       : TEDDY                                      *
     A*    CREATE DATE  : 85/02/05                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PIR048                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :盤點資料子系統                            *
     A*    REMARK       :年終庫存物流彙總表                        *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PIR048'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'年終庫存物流彙總表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 26'年終年月:'
     A            DIY15          4Y 0B  6 38
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      EDTWRD('  /  ')
     A                                  6 44'(MM/YY)'
     A                                  8 26'組　　別:'
     A            DMA11          1A  B  8 38VALUES('1' '2' '3' ' ')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  8 40'(1-BRANDY 2-WHISKY 3-WINE " "-ALL)'
     A                                 10 26'品　　牌:'
     A            IY03MS         2   B 10 38
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 10 42'-'
     A            IY03ME         2   B 10 45
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 12 26'產品代號:'
     A            DIY03S    R    9A  B 12 38REFFLD(IY03)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64                                  DSPATR(HI)
     A                                 12 48'-'
     A            DIY03E    R    9A  B 12 52REFFLD(IY03)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
     A                                 16 26'執行環境:'
     A            $EVR           1Y 0B 16 38VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 26'列印份數:'
     A            $CPY           3Y 0B 17 38EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機　:'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 38DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTCDE(3)
     A                                 20 26'***請準備８０格報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行  F3 =回主畫面F4 =視窗查+
     A                                      詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
