     A*%%TS  SD  19920602  125944  REPGMRP     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : MTA002D                                    *
     A*    AUTHOR       : A1034  STEPHAIE                            *
     A*    CREATE DATE  : 81/01/13                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MTA002                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    REMARK       :供應商資料維護 (A/U/D/I)                  *
     A*00A需求單號: 1070818                                         *
     A*00A 2019/01/31 ADAM  EDI匯款                                 *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.11.04 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*%%TS  SD  19920528  083234  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B  9  5VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            MB01      R        O  9  9
     A            MB03      R        O  9 15
     A            MB12      R        O  9 35
     A            MB13      R        O  9 59
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  19920602  125944  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'MTA002'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'供應商基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'輸入作業選擇:'
     A                                  5  6'2=修改'
     A                                  5 16'4=刪除'
     A                                  5 26'5=查詢'
     A                                  7  5'A  供應商代號／簡稱         聯+
     A                                      絡人               電話        -
     A                                        '
     A                                      DSPATR(UL)
     A            DBGN1     R        B  8  9REFFLD(MB01)
     A                                      DSPATR(HI)
     A          R DSPC1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F4 =視窗查詢'
     A                                    +2'F6 =新增'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPD2
     A*%%TS  SD  19920127  154554  REPGMR      REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF05(05)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MTA002'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 30'供應商基本資料維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'供應商代號:'
     A            DMB01     R        B  4 16REFFLD(MB01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                  6  2'供應商全稱:'
     A            DMB02     R        B  6 16REFFLD(MB02)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7  2'供應商簡稱:'
     A            DMB03     R        B  7 16REFFLD(MB03)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  7 44'供應商別:'
     A            DMB24     R        O  7 56REFFLD(MB24)
     A                                  7 59'( F-國外 L-國內)'
     A                                  8  2'供應商國別:'
     A            DMB04     R        B  8 16REFFLD(MB04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  8 44'郵遞區號:'
     A            DMB05     R        B  8 56REFFLD(MB05)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  9  2'供應商地址:'
     A            DMB06     R        B  9 16REFFLD(MB06)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
 00A+A                                  9 50'收款銀行:'
 00A+A            DMBA02         7A  B  9 62
 00A+A  31                                  DSPATR(UL)
 00A+A N31                                  DSPATR(PR)
     A                                 10  2'            :'
     A            DMB07     R        B 10 16REFFLD(MB07)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
 00A+A                                 10 50'收款帳號:'
 00A+A            DMBA03        16A  B 10 62
 00A+A  31                                  DSPATR(UL)
 00A+A N31                                  DSPATR(PR)
     A                                 11  2'            :'
     A            DMB08     R        B 11 16REFFLD(MB08)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
 00A+A                                 11 50'統一編號:'
 00A+A*           DMBA04         8A  B 11 62
 00A+A            DMBA04        10A  B 11 62
 00A+A  31                                  DSPATR(UL)
 00A+A N31                                  DSPATR(PR)
     A                                 12  2'            :'
     A            DMB09     R        B 12 16REFFLD(MB09)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 13  2'            :'
     A            DMB10     R        B 13 16REFFLD(MB10)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14  2'負責人　　:'
     A            DMB11     R        B 14 16REFFLD(MB11)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 14 44'聯絡人　:'
     A            DMB12     R        B 14 56REFFLD(MB12)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15  2'電話一　　:'
     A            DMB13     R        B 15 16REFFLD(MB13)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 15 44'電話二　:'
     A            DMB14     R        B 15 56REFFLD(MB14)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16  2'傳真機　　:'
     A            DMB15     R        B 16 16REFFLD(MB15)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 16 44'電報機　:'
     A            DMB16     R        B 16 56REFFLD(MB16)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 17  2'幣別　　　:'
     A                                      DSPATR(HI)
     A            DMB17     R        B 17 16REFFLD(MB17)
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DCURR         22A  O 17 20
     A                                 17 44'付款方式:'
     A                                      DSPATR(HI)
     A            DMB18     R        B 17 56REFFLD(MB18)
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DPAY          22A  O 17 59
     A                                 18  2'採購人員　:'
     A                                      DSPATR(HI)
     A            DMB19     R        B 18 16REFFLD(MB19)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            MC02      R        O 18 23
     A                                 19  2'備註　　　:'
     A            DMB20     R        B 19 16REFFLD(MB20)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DMB21     R        B 19 49REFFLD(MB21)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                 20  2'最近交易　:'
M001MA*           DMB22     R        O 20 16REFFLD(MB23)
M001MA            DMB22          6  0O 20 16
     A                                      EDTCDE(Y)
     A                                 20 27'(MM/DD/YY)'
     A                                 20 44'建檔日期:'
M001MA*           DMB23     R        O 20 56REFFLD(MB23)
M001MA            DMB23          6  0O 20 56
     A                                      EDTWRD('  /  /  ')
     A                                 20 66'(MM/DD/YY)'
 00A+A                                 21  2'戶名:'
 00A+A            DMBA05        70O  B 21 10
 00A+A  31                                  DSPATR(UL)
 00A+A N31                                  DSPATR(PR)
     A                                 22  2'執行'
     A  28        DMSG          78A  O 22  2DSPATR(HI)
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A                                 22 42'F5 =參考資料'
     A                                 22 58'F12=回前畫面'
     A  98        ERR1          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          60A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A          R DSPD3
     A                                      CF14(14)
     A                                      CF12(12)
     A                                      OVERLAY  CLRL(*NO)
     A                                 10 26'                          '
     A                                      DSPATR(RI)
     A                                 11 26' '
     A                                      DSPATR(RI)
     A                                 11 28'                      '
     A                                 11 51' '
     A                                      DSPATR(RI)
     A                                 12 26' '
     A                                      DSPATR(RI)
     A                                 12 28'幣別更改！　請注意！'
     A                                 12 51' '
     A                                      DSPATR(RI)
     A                                 13 26' '
     A                                      DSPATR(RI)
     A                                 13 28'                      '
     A                                 13 51' '
     A                                      DSPATR(RI)
     A                                 14 26' '
     A                                      DSPATR(RI)
     A                                 14 28' 請按F14確認更新  '
     A                                 14 51' '
     A                                      DSPATR(RI)
     A                                 15 26' '
     A                                      DSPATR(RI)
     A                                 15 28' 或按F12回前畫面  '
     A                                 15 51' '
     A                                      DSPATR(RI)
     A                                 16 26' '
     A                                      DSPATR(RI)
     A                                 16 28'                      '
     A                                 16 51' '
     A                                      DSPATR(RI)
     A                                 17 26'                          '
     A                                      DSPATR(RI)
