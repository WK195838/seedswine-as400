     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:50422      *
     A*                                                              *
     A*    DSPF NAME    : SAR079D                                    *
     A*    AUTHOR       : MERCURY                                    *
     A*    CREATE DATE  : 83/05/12                                   *
     A*    PROGRAM NAME : SAR079                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :產品銷售分析週報表－依辦事處通路選擇      *
     /*=============================================================*/
     /* CODE   AUTHOR.     DATE    DESCRIPTION                   */
     /*-------------------------------------------------------------*/
     /* M001  MICHELLE   98.9.18  通路項目選擇                 */
     /*-------------------------------------------------------------*/
     /* M002  MICHELLE   98.12.18  Y2K MODIFY                    */
     /*-------------------------------------------------------------*/
     /* M003  MICHELLE   99.3.23  彙總，明細選擇               */
     /*=============================================================*/
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19931020  160917  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 '視窗查詢')
     A                                      CF05(05 '參考資料')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SAR079'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 25'產品銷售分析週報表－依辦事處'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 26'公司別'
     A                                  5 36':'
     A            DMA07          1A  B  5 38VALUES('R' 'C' 'W')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  5 41'(R-匯東 C-康齡 W-匯齡)'
     A                                  6 26'辦事處　:'
     A            COMP           1A  B  6 38VALUES(' ' '1' '2' '3')
     A                                  6 40'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  7 26'產品種類:'
     A            DMA11          1A  B  7 38VALUES('1' '2' '3')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  7 41'(1-白蘭地2-威士忌3-葡萄酒)'
     A                                  8 26'產品代號:'
     A                                      DSPATR(HI)
     A            DMA01S         9A  B  8 38
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N66                                  DSPATR(HI)
     A                                  8 48'-'
     A            DMA01E         9A  B  8 50
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A N67                                  DSPATR(HI)
     A                                  9 26'當期　　:'
M002MA*           DATES     R        B  9 38REFFLD(SC39)
M002MA            DATES          6  0B  9 38
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 48'-'
M002MA*           DATEE     R        B  9 50REFFLD(SC39)
M002MA            DATEE          6  0B  9 50
     A                                      EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 61'(MM/DD/YY)'
     A                                 10 26'當月　　:'
M002MA*           D01S      R        B 10 38REFFLD(SC39)
M002MA            D01S           6  0B 10 38
     A                                      EDTWRD('  /  /  ')
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                 10 48'-'
M002MA*           D01E      R        B 10 50REFFLD(SC39)
M002MA            D01E           6  0B 10 50
     A                                      EDTWRD('  /  /  ')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                 10 61'(MM/DD/YY)'
     A                                 11 26'當年　　:'
M002MA*           D02S      R        B 11 38REFFLD(SC39)
M002MA            D02S           6  0B 11 38
     A                                      EDTWRD('  /  /  ')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A                                 11 48'-'
M002MA*           D02E      R        B 11 50REFFLD(SC39)
M002MA            D02E           6  0B 11 50
     A                                      EDTWRD('  /  /  ')
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                 11 61'(MM/DD/YY)'
     A                                 12 26'去年　　:'
     A            D03S           4Y 0B 12 38EDTWRD('  /  ')
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                 12 48'-'
     A            D03E           4Y 0B 12 50EDTWRD('  /  ')
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                 12 61'(MM/YY)'
M001DA*                                12 26'內　　容:'
     A*           DSEL3          1A  B 12 38VALUES('A' 'B')
     A* 64                                  DSPATR(PC)
     A* 64                                  DSPATR(RI)
M001DA*                                12 41'(A-含專櫃 B-不含專櫃)'
     A                                 14 26'含試飲　:'
M001MA*           DSEL1          1A  B 14 38VALUES('N' 'Y')
M001MA            DSEL1          1A  O 14 38
M001DA* 64                                  DSPATR(PC)
M001DA* 64                                  DSPATR(RI)
     A                                 14 40'(N/Y)'
     A                                 15 24'　分析方式:'
     A            DSEL2          1A  B 15 38VALUES('A' 'B')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 15 41'A-禮盒不拆'
     A                                 16 41'B-禮盒拆單瓶'
M003AA                                 15 55'種類:'
M003AA            TYPEN          1A  B 15 63VALUES('1' '2')
M003AA                                 15 66'1-彙總'
M003AA                                 16 66'2-明細'
M001MA*                                13 26'通　　路:'
M001MA                                 13 26'通路選擇:'
@@   A*M001M      DSEL4          1A  B 13 38VALUES('A' 'B' 'C')
M001MA            DSEL4          1A  B 13 38VALUES('A' 'B' 'C' 'D' 'F' 'Z' ' ')
@@   A  66                                  DSPATR(PC)
@@   A  66                                  DSPATR(RI)
M001AA            DSEL5          1A  B 13 40VALUES('A' 'B' 'C' 'D' 'F' 'Z' ' ')
M001AA  74                                  DSPATR(PC)
M001AA  74                                  DSPATR(RI)
M001AA            DSEL6          1A  B 13 42VALUES('A' 'B' 'C' 'D' 'F' 'Z' ' ')
M001AA  74                                  DSPATR(PC)
M001AA  74                                  DSPATR(RI)
M001AA            DSEL7          1A  B 13 44VALUES('A' 'B' 'C' 'D' 'F' 'Z' ' ')
M001AA  74                                  DSPATR(PC)
M001AA  74                                  DSPATR(RI)
M001AA            DSEL8          1A  B 13 46VALUES('A' 'B' 'C' 'D' 'F' 'Z' ' ')
M001AA  74                                  DSPATR(PC)
M001AA  74                                  DSPATR(RI)
M001AA            DSEL9          1A  B 13 48VALUES('A' 'B' 'C' 'D' 'F' 'Z' ' ')
M001AA  74                                  DSPATR(PC)
M001AA  74                                  DSPATR(RI)
M001AA            DSEL10         1A  B 13 50VALUES('A' 'B' 'C' 'D' 'F' 'Z' ' ')
M001AA  74                                  DSPATR(PC)
M001AA  74                                  DSPATR(RI)
@@   A*M001M                           13 41'(A-含D.   B-不含D   C- ONLY D)'
M001MA                                 13 52'(A B C D F Z)'
     A                                 17 26'執行環境 :'
     A            $EVR           1Y 0B 17 39EDTCDE(3)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 41'(1-即時   2-批次)'
     A                                 18 26'列印份數'
     A                                 18 37':'
     A            $CPY           3Y 0B 18 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 26'列表機'
     A                                      DSPATR(HI)
     A                                 19 37':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 21 27'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3 =回主畫面'
     A                                 23 25'F4 =視窗查詢'
     A                                 23 40'F5 =參考資料'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A  89        ERR12         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR13         50A  O 24  2MSGID(URE 0128 REMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A  86        ERR14         50A  O 24  2MSGID(URE 0080 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
