     A*===============================================================
     F* MODI CODE |   DATE   |  DESCRIPTION
     F*================================================================
     F*  M001     | 98.10.21 | MANUAL MODIFY BY MICHELLE FOR Y2K
     F*----------------------------------------------------------------
     F*  M002     | 99.11.08 | ADD CUSGRP OPTION
     F*----------------------------------------------------------------
     F*  M003     | 00.11.08 |增加大區域選項
     F*================================================================
     A                                      REF(RERF)
     A          R PH1
     A                                      SKIPB(002)
     A                                    84
     A                                      MSGCON(030 URE9999 REMF)
     A                                      SPACEA(001)
     A                                    88
     A                                      '品牌銷售彙總表'
     A                                      SPACEA(001)
     A            GROUP         10   O    95
     A                                      SPACEA(001)
     A                                     1
     A                                      '辦事處　:'
     A            COMP           1A  O    +1
     A            COMPC          6O  O    +1
     A                                      SPACEA(001)
     A                                     1
     A                                      '業務人員:'
     A            DSC27S    R        O    15
     A                                      REFFLD(MC02)
     A                                      SPACEA(001)
     A                                     1
     A                                      '產品代號:'
     A            DSD03S         2A  O    15
     A                                    +1
     A                                      '-'
     A            DSD03E         2A  O    +1
     A                                      SPACEA(001)
     A                                     1
     A                                      '通　　路:'
     A            DSC33S    R        O    13
     A                                      REFFLD(SC33)
     A                                    15
     A                                      '-'
     A            DSC33E    R        O    17
     A                                      REFFLD(SC33)
     A                                    20
     A                                      '型  態:'
     A            DSC34S    R        O    33
     A                                      REFFLD(SC34)
     A                                    36
     A                                      '-'
     A            DSC34E    R        O    38
     A                                      REFFLD(SC34)
     A                                      SPACEA(001)
     A                                     1
     A                                      '客戶代號:'
     A            DSC45S         9   O    13
     A                                    23
     A                                      '-'
     A            DSC45E         9   O    25
     A                                      SPACEA(001)
     A                                     1
     A                                      '客戶名稱:'
     A            DSC46S        18O  O    13
     A                                   167
     A                                      '日期:'
     A            $EGMDY         6  0O   175
     A                                      EDTCDE(Y)
     A                                   184
     A                                      '頁次:'
     A                                   193
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A N20                                 1
     A                                      '送貨日期:'
     A  20                                 1
     A                                      '確認日期:'
M001MA*N20        DSC08S    R        O    13REFFLD(SC39)
     A N20        DSC08S         6  0O    13
     A                                      EDTWRD('  /  /  ')
M001MA* 20        DSC39S    R        O    13REFFLD(SC39)
     A  20        DSC39S         6  0O    13
     A                                      EDTWRD('  /  /  ')
     A                                    21
     A                                      '-'
M001MA*N20        DSC08E    R        O    22REFFLD(SC39)
     A N20        DSC08E         6  0O    22
     A                                      EDTWRD('  /  /  ')
M001MA* 20        DSC39E    R        O    22REFFLD(SC39)
     A  20        DSC39E         6  0O    22
     A                                      EDTWRD('  /  /  ')
     A                                   167
     A                                      '時間:'
     A                                   175
     A                                      TIME
     A                                      SPACEA(001)
     A                                     1
     A                                      '分析方式:'
     A            DSEL2          1A       +1
     A N22                                20
     A                                      '禮盒不拆'
     A  22                                20
     A                                      '禮盒拆單瓶'
     A                                   168
     A                                      'USER :'
     A            $USER         10   O   175
     A                                   186
     A                                      '<SOR160>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================'
     A                                      SPACEA(001)
     A                                    68
     A                                      '-----------'
     A                                    +0
     A                                      '銷售'
     A                                    +0
     A                                      '-----------'
     A                                    +2
     A                                      '-----------'
     A                                    +0
     A                                      '銷退'
     A                                    +0
     A                                      '-----------'
     A                                   129
     A                                      '-------'
     A                                   136
     A                                      '標準箱數'
     A                                   146
     A                                      '------'
     A                                      SPACEA(001)
     A  33                                 1
     A                                      '客戶／名稱'
     A N33                                 1
     A                                      '品牌／名稱'
     A N33                                39
     A                                      '客戶／名稱'
     A  33                                39
     A                                      '品牌／名稱'
     A                                    70
     A                                      '訂購'
     A                                    +3
     A                                      '搭贈'
     A                                    +3
     A                                      '試飲'
     A*                                   +6
     A*                                     '淨金額'
     A                                    +6
     A                                      '訂購'
     A                                    +3
     A                                      '搭贈'
     A                                    +3
     A                                      '試飲'
     A*                                   +4
     A*                                     '銷退金額'
     A                                   129
     A                                      '訂購'
     A                                   138
     A                                      '搭贈'
     A                                   146
     A                                      '試飲'
     A                                   157
     A                                      '銷售金額'
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------'
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEA(001)
     A  21                                 1
     A                                      ' '
     A                                      SPACEA(001)
     A*
     A N30N33     WF201     R        O     2
     A                                      REFFLD(WF11   *LIBL/REWF1)
     A N30N33     #A03      R        O    +1
     A N31N33     WF202     R        O    +1
     A                                      REFFLD(WF02 *LIBL/REWF1)
     A N31N33     WF203     R        O    +0
     A                                      REFFLD(WF021 *LIBL/REWF1)
     A N31N33     ME04      R        O    +0
     A*
     A N31 33     SWF202    R        O     2
     A                                      REFFLD(WF02 *LIBL/REWF1)
     A N31 33     SWF203    R        O    +0
     A                                      REFFLD(WF021 *LIBL/REWF1)
     A N31 33     SME04     R        O    +0REFFLD(ME04 *LIBL/MTMEPF)
     A N30 33     SWF201    R        O    +1
     A                                      REFFLD(WF11   *LIBL/REWF1)
     A N30 33     S#A03     R        O    +1REFFLD(#A03 *LIBL/PA#APF)
     A*
     A            WF204     R        O    +4
     A                                      REFFLD(WF03 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            WF205     R        O    +1
     A                                      REFFLD(WF04 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            WF206     R        O    +1
     A                                      REFFLD(WF05 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A*           PWF06     R        O    +0
     A*                                     REFFLD(WF06 *LIBL/REWF1)
     A*                                     EDTCDE(J)
     A            WF208     R        O    +4
     A                                      REFFLD(WF07 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            WF209     R        O    +1
     A                                      REFFLD(WF08 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            WF210     R        O    +1
     A                                      REFFLD(WF09 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A*           WF10      R        O    +0
     A*                                     REFFLD(WF10 *LIBL/REWF1)
     A*                                     EDTCDE(J)
     A            WF212          6  0O   128
     A                                      EDTCDE(J)
     A            WF213          6  0O   137
     A                                      EDTCDE(J)
     A            WF214          6  0O   146
     A                                      EDTCDE(J)
     A            WF220          9  0O   157
     A                                      EDTCDE(J)
     A*----------------------------------------------------------------
     A          R PT1
     A                                      SPACEA(001)
     A                                    20
     A                                      '品牌小計------------>'
     A            TWF204    R        O    69
     A                                      REFFLD(WF03 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            TWF205    R        O    +1
     A                                      REFFLD(WF04 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            TWF206    R        O    +1
     A                                      REFFLD(WF05 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A*           TWF06     R        O    +0
     A*                                     REFFLD(WF06 *LIBL/REWF1)
     A*                                     EDTCDE(J)
     A            TWF208    R        O    +4
     A                                      REFFLD(WF07 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            TWF209    R        O    +1
     A                                      REFFLD(WF08 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            TWF210    R        O    +1
     A                                      REFFLD(WF09 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            TWF212         6  0O   128
     A                                      EDTCDE(J)
     A            TWF213         6  0O   137
     A                                      EDTCDE(J)
     A            TWF214         6  0O   146
     A                                      EDTCDE(J)
     A            TWF220         9  0O   157
     A                                      EDTCDE(J)
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------'
     A*----------------------------------------------------------------
     A          R PT2
     A                                      SPACEA(001)
     A                                    20
     A                                      '總　　計------------>'
     A            WWF204    R        O    69
     A                                      REFFLD(WF03 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            WWF205    R        O    +1
     A                                      REFFLD(WF04 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            WWF206    R        O    +1
     A                                      REFFLD(WF05 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A*           WWF06     R        O    +0
     A*                                     REFFLD(WF06 *LIBL/REWF1)
     A*                                     EDTCDE(J)
     A            WWF208    R        O    +4
     A                                      REFFLD(WF07 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            WWF209    R        O    +1
     A                                      REFFLD(WF08 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A            WWF210    R        O    +1
     A                                      REFFLD(WF09 *LIBL/REWF1)
     A                                      EDTCDE(J)
     A*           WWF10     R        O    +0
     A*                                     REFFLD(WF10 *LIBL/REWF1)
     A*                                     EDTCDE(J)
     A            WWF212         6  0O   128
     A                                      EDTCDE(J)
     A            WWF213         6  0O   137
     A                                      EDTCDE(J)
     A            WWF214         6  0O   146
     A                                      EDTCDE(J)
     A            WWF220         9  0O   157
     A                                      EDTCDE(J)
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------'
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================'
     A                                      SPACEA(001)
     A                                     2
     A                                      '***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ========================'
     A                                      SPACEA(001)
     A                                     2
     A                                      '<<SOR160>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEA(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
