     A*%%TS  SD  19920608  180312  REPGMRP     REL-V2R1M0  5738-PW1
     A*  81/01/24  08:50:00    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : POI026D                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/01/24                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : POI026                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :供應商子系統                              *
     A*    REMARK       :供應商產品報價查詢                        *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19920528  131305  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  3VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            MB01      R        O 10  6
     A            MB02      R        O 10 13
     A*------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19920608  180312  REPGMRP     REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'POI026'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'供應商產品報價查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'輸入作業選擇:'
     A                                  6  5'5=查詢'
     A                                  8  3'A 供應商　供應商名稱            -
     A                                                        '
     A                                      DSPATR(UL)
     A            DBGN1     R        B  9  6REFFLD(MB01)
     A*------------------------------------------------------------
     A*
     A          R DSPC1
     A*%%TS  SD  19920127  163423  REPGMR      REL-V2R1M0  5738-PW1
     A                                 22  2'執行'
     A                                    +2'F3 =回主畫面'
     A  97        ERR1          50A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR3          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR4          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR5          50A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR7          50A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  80        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R SFLSR2                    SFL
     A            RRN2           4S 0H
     A            PA03      R        O  9  2
     A            MA06      R        O  9 12
     A            DOC            5A  O  9 28
     A            PA05      R        O  9 34
     A            PA10      R        O 10 35
     A            DPRICE        10A  O 10 38
     A            PA08      R        O  9 47
     A            DPA09          7  2O  9 51EDTCDE(1)
M001DA*           PA04      R        O  9 61EDTWRD('  /  /  ')
M001AA            DPA04          6  0O  9 61EDTWRD('  /  /  ')
M001DA*           PA06      R        O 10 61EDTWRD('  /  /  ')
M001AA            DPA06          6  0O 10 61EDTWRD('  /  /  ')
     A            PA02      R        O  9 70
     A            DPAY          10A  O 10 70
     A*--------------------------------------------------------------*
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0006)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      SFLDROP(CF01)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                 22  2'執行'
     A                                    +2'F1 =明細顯示'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A  96        ERR2          50A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A          R DSPC2
     A*%%TS  SD  19920127  163423  REPGMR      REL-V2R1M0  5738-PW1
     A                                  1  2'POI026'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 30'供應商產品報價查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'供應商代號:'
     A            MB01      R        O  4 16
     A            MB02      R        O  4 22
     A                                  6 60'(MM/DD/YY)'
     A                                  7  1'產品代號／名稱           單位+
     A                                      　供應商產品　幣別　單價　　生效+
     A                                      日期　報價單號'
     A                                  8  1'                                 -
     A                                      報價價格條件　　　　　　　截止日+
     A                                      期　付款條件'
     A                                      DSPATR(UL)
