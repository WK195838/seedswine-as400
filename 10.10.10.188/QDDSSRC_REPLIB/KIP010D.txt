     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIP010D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 98/04/16                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :茂世進銷存                                *
     A*    SUBSYSTEM    :                                          *
     A*    REMARK       : IP對照表維護                           *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  3VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SAB01     R        O 10  6REFFLD(AB01 W1ABPF)
     A            S#A03     R        O 10 18REFFLD(#A03 PA#APF)
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'KIP010'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33' IP對照表維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  6 17'2=修改'
     A                                      COLOR(BLU)
     A                                  6 26'4=刪除'
     A                                      COLOR(BLU)
     A                                  6 35'5=查詢'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN1     R        B  9  6REFFLD(AB01 W1ABPF)
     A                                  8  3'A 登入帳號   '
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 30'F6 =新增'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99N97     ERRMSG        60A  O 23  2MSGID(&ERRID &ERRF )
     A                                      DSPATR(HI)
     A                                 23 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A  99 97     @MSG          60A  O 24  2
     A                                      DSPATR(HI)
      *此燈號控制新增成功以及修改成功專用
     A  91        ERR0          63A  O 23  2MSGID(MSG 1000 R1MF)
     A                                      DSPATR(HI)
     A  92        ERR1          63A  O 23  2MSGID(MSG 1001 R1MF)
     A                                      DSPATR(HI)
     A  93        ERR2          63A  O 23  2MSGID(MSG 1002 R1MF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 23  2MSGID(MSG 1003 R1MF)
     A                                      DSPATR(HI)
     A  95        ERR4          63A  O 23  2MSGID(MSG 1004 R1MF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SAB02     R        B  8  2REFFLD(AB02 W1ABPF)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A N32                                  DSPATR(PR)
     A  32                                  DSPATR(UL)
     A            SAB02H    R        H      REFFLD(AB02 W1ABPF)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A  32                                  CF04(04)
     A  32                                  CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'KIP010'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33' IP對照表維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'登入帳號:'
     A            DAB01     R        B  5 14REFFLD(AB01 W1ABPF)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            D#A03     R        O    +1REFFLD(#A03 PA#APF)
     A                                  7  2'       可使用IP        '
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R DSPC2
     A*                                22  2'執行'
     A*                                     COLOR(BLU)
     A                                 22  1'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  32                                +1'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  32                                +1'F5 =參考資料'
     A                                      COLOR(BLU)
     A                                    +1'F12=回前畫面'
     A                                      COLOR(BLU)
     A                                    +1'PAGE DOWN =下頁'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 23  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
