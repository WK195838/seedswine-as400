     A*%%TS  SD  20020916  181359  REPGMR      REL-V4R4M0  5769-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARI022D                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/04/08                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARI022                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :產品銷售查詢 (I)                          *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR                     SFL
     A*%%TS  SD  20020916  181359  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            SJ03      R        O  8  2
     A            SJ11      R        O  8 12
     A            SJ12      R        O  8 18
     A            DSJ16          6Y 0O  8 23EDTWRD('  /  /  ')
     A            SJ02      R        O  8 32
     A            SI10      R        O  8 43REFFLD(RERFR/SI10 *LIBL/RERF)
     A            PRICE          9Y 0O  8 56EDTCDE(1)
     A            QTY            5Y 0O  8 68EDTCDE(1)
     A            SJ13           4Y 0O  8 75EDTCDE(1)
     A*------------------------------------------------------------
     A*
     A          R SFLCR                     SFLCTL(SFLSR)
     A*%%TS  SD  20020916  181359  REPGMR      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'ARI022'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 26'發票資料查詢（依產品客戶）'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A            DBGN2     R        B  7  2REFFLD(SJ03)
     A            DBGN3     R        B  7 12REFFLD(SJ11)
     A            DBGN4     R        B  7 18REFFLD(SJ12)
     A            DBGN5          6Y 0B  7 23EDTWRD('  /  /  ')
     A            DBGN6     R        B  7 32REFFLD(SJ02)
     A                                  6  2'產品代號客戶代號 發票日期 +
     A                                      發票號碼訂單號碼     淨售價 -
     A                                      訂贈　搭贈'
     A                                      DSPATR(UL)
     A                                  5 33'(MM/DD/YY)'
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
