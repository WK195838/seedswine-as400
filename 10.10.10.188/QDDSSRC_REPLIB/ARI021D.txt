     A*%%TS  SD  20020916  180815  REPGMR      REL-V4R4M0  5769-PW1
     A*  81/02/17  11:09:16    REPGMR      REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : ARI021D                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/03/25                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARI021                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :發票資料查詢 (I)                          *
     A*===============================================================
     A* MODI CODE |  DATE  |  AUTHOR  | DESCRIPTION
     A*---------------------------------------------------------------
     A*  M004     |00.10.16| MICHELLE |顯示正負數
     A*---------------------------------------------------------------
     A*  M005     |01.01.12| MICHELLE |菸酒稅修正
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A*%%TS  SD  20020916  180815  REPGMR      REL-V4R4M0  5769-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  2
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      VALUES(' ' '5' '6')
     A            SI12      R        O  9  4
     A            SI13      R        O  9 10
     A            DSI21          6Y 0O  9 15EDTCDE(Y)
     A            SI25      R        O  9 49EDTCDE(L)
     A            DTOT           9Y 0O  9 60EDTCDE(3)
     A            SI30      R        O  9 71EDTCDE(L)
     A            SI02      R        O  9 24
     A            SI10      R        O  9 35
     A            SI01      R        H
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  20020916  180815  REPGMR      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'ARI021'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'發票資料查詢（依客戶）'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  5'5=明細查詢'
     A            DBGN1     R        B  8  4REFFLD(SI12)
     A            DBGN2     R        B  8 10REFFLD(SI13)
     A            DBGN3          6Y 0B  8 15EDTCDE(Y)
     A                                  7  2'A客戶代號 發票日期發票號碼+
     A                                      訂單號碼      發票金額折讓金+
     A                                      額 未收金額'
     A                                      DSPATR(UL)
     A                                  5 21'6=沖帳查詢'
     A*------------------------------------------------------------
     A          R DSPC1
     A*
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A*%%TS  SD  19920514  110455  REPGMRP     REL-V2R1M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SJ03      R        O 12  3
     A            D#A       R        O 12 14REFFLD(SJ04)
M004MA*                                     EDTCDE(1)
M004MA                                      EDTCDE(3)
     A            SJ13           4Y 0O 12 22EDTCDE(1)
     A            PRICE          9  2O 12 28EDTCDE(3)
M004MA*           DSJ06          8Y 0O 12 36EDTCDE(1)
M004MA            DSJ06          9  0O 12 39EDTCDE(3)
     A            SJ14           5Y 0O 12 50EDTCDE(1)
     A            SJ15           4Y 0O 12 61EDTCDE(1)
     A*           SJ09           9Y 0O 12 67EDTCDE(1)
     A            SJ09      R        O 12 67REFFLD(SJ09)
     A                                      EDTCDE(1)
     A**60                                  DSPATR(PC)
     A**60                                  DSPATR(RI)
     A**31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A            MA03      R          13  2
     A*------------------------------------------------------------
     A*
     A**                                    CSRLOC(D#ROW      D#COL)
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19940527  161249  D910121     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0004)
     A                                      SFLDROP(CF01)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'ARI021'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'發票資料查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司　　:'
     A            SI01      R        O  4 14
     A            D#B03         16A  O  4 17
     A                                  4 39'客戶代號:'
     A            SI08      R        O  4 52
     A                                  4 58'-'
     A            SI09      R        O  4 60
     A                                  5  2'發票號碼:'
     A            SI02      R        O  5 14
     A            ME04      R        O  5 52
     A                                  6  2'發票金額:'
M004MA*           SI25      R        O  6 14EDTCDE(1)
M004MA            SI25      R        O  6 14EDTCDE(J)
     A                                  6 39'發票日期:'
M001DA*           DSI21     R        O  6 52REFFLD(SI21)
M001AA            DSI21          6  0O  6 52
     A                                      EDTWRD('  /  /  ')
     A                                  7  2'價差折讓:'
     A            SI27      R        O  7 14EDTCDE(1)
     A                                  7 39'送貨單號:'
     A            SI11      R        O  7 52
     A                                  8  2'銷退折讓:'
     A            SI28      R        O  8 14EDTCDE(1)
     A                                  8 39'未沖金額:'
     A                                 11  2'產品代號   數量 數量  售價-
M005MA*                                      發票金額訂購數量搭贈數量  +
M005MA                                       未稅金額訂購數量搭贈數量  +
     A                                      銷退金額'
     A                                      DSPATR(UL)
     A                                 10  2'             訂購 搭贈        -
     A                                                     銷退    銷退    -
M005MA*                                             '
M005MA                                        未稅'
     A            SI30      R        O  8 52REFFLD(RERFR/SI30 *LIBL/RERF)
M004MA*                                     EDTCDE(1)
M004MA                                      EDTCDE(J)
     A                                  6 62'(MM/DD/YY)'
     A                                  9  2'確認日期:'
M001DA*           DSI35     R        O  9 15REFFLD(SI35)
M001AA            DSI35          6  0O  9 15
     A                                      EDTWRD('  /  /  ')
     A                                  9 25'(MM/DD/YY)'
     A*------------------------------------------------------------
     A          R DSPC2
     A*
     A                                 22  2'執行'
     A                                    +2'F1 =產品說明'
     A                                    +2'F3 =回主畫面'
     A                                    +2'F12=回前畫面'
     A*
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR3          63A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR4          63A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          63A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR6          63A  O 24  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR7          63A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR8          63A  O 24  2MSGID(UPT 0030 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR9          63A  O 24  2MSGID(URE 0037 REMF)
     A                                      DSPATR(HI)
     A  87        ERR10         63A  O 24  2MSGID(URE 0025 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
