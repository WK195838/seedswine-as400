     A*%%TS  SD  19960920  163314  DERLERN     REL-V3R1M0  5763-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SAR078D                                    *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 82/09/21                                   *
     A*    PROGRAM NAME : SAR078                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :產品銷售分析週報表                        *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19960920  163314  DERLERN     REL-V3R1M0  5763-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 '視窗查詢')
     A                                      CF05(05 '參考資料')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SAR078'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 24'產品（客戶／通路）銷售分析報表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5 26'產品類別:'
     A            DMA11          1A  B  5 38VALUES('1' '2' '3')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  5 41'(1-白蘭地2-威士忌3-葡萄酒)'
     A                                  7 26'產品代號:'
     A                                      DSPATR(HI)
     A            DMA01S         9A  B  7 38
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                  7 48'-'
     A            DMA01E         9A  B  7 50
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                  9 26'確認日期:'
M001MA*           DATES     R        B  9 38REFFLD(SC39)
M001MA            DATES          6  0B  9 38
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  9 48'-'
M001MA*           DATEE     R        B  9 50REFFLD(SC39)
M001MA            DATEE          6  0B  9 50
     A                                      EDTWRD('  /  /  ')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  9 61'(MM/DD/YY)'
     A                                 11 26'通　　路:'
     A            DMD07S    R        B 11 38REFFLD(MD07)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                 11 48'-'
     A            DMD07E    R        B 11 50REFFLD(MD07)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                 13 24'　分析方式:'
     A            DSEL2          1A  B 13 38VALUES('A' 'B')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 13 41'A-禮盒不拆'
     A                                 14 41'B-禮盒拆單瓶'
     A                                 16 26'執行環境 :'
     A            $EVR           1Y 0B 16 39EDTCDE(3)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 41'(1-即時   2-批次)'
     A                                 17 26'列印份數'
     A                                 17 37':'
     A            $CPY           3Y 0B 17 39EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 26'列表機'
     A                                      DSPATR(HI)
     A                                 18 37':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 39CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 27'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 25'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR11         50A  O 24  2MSGID(URE 0081 REMF)
     A                                      DSPATR(HI)
     A  89        ERR12         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR13         50A  O 24  2MSGID(URE 0128 REMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A  86        ERR14         50A  O 24  2MSGID(URE 0080 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
