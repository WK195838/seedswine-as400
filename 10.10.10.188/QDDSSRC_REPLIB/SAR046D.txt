     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SAR046D                                    *
     A*    AUTHOR       : A1084  CHARLES                             *
     A*    CREATE DATE  : 81/05/04                                   *
     A*    UPDATE DATE  : 05/23/94  AN MING HSIA                     *
     A*    PROGRAM NAME : SAR046                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售分析系統                              *
     A*    REMARK       :客戶銷售統計表                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'SAR046'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'客戶銷售統計表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 18'辦事處　:'
     A            COMP           1A  B  4 30VALUES(' ' '1' '2' '3')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  4 32'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  5 18'通路　　:'
     A                                      DSPATR(HI)
     A            DSC33S    R        B  5 30REFFLD(SC33)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A                                  5 40'-'
     A            DSC33E    R        B  5 42REFFLD(SC33)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A                                  6 18'型態　　:'
     A                                      DSPATR(HI)
     A            DSC34S    R        B  6 30REFFLD(SC34)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(HI)
     A                                  6 40'-'
     A            DSC34E    R        B  6 42REFFLD(SC34)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A N64                                  DSPATR(HI)
     A                                  7 18'地區　　:'
     A                                      DSPATR(HI)
     A            DSC31S    R        B  7 30REFFLD(SC31)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A N65                                  DSPATR(HI)
     A                                  7 40'-'
     A            DSC31E    R        B  7 42REFFLD(SC31)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A N66                                  DSPATR(HI)
     A                                  8 18'客戶代號:'
     A                                      DSPATR(HI)
     A            DSC04S         9A  B  8 30
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A N67                                  DSPATR(HI)
     A                                  8 40'-'
     A            DSC04E         9A  B  8 42
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A N68                                  DSPATR(HI)
     A                                  9 18'產品代號:'
     A                                      DSPATR(HI)
     A            DSD03S    R        B  9 30REFFLD(SD03)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A N69                                  DSPATR(HI)
     A                                  9 40'-'
     A            DSD03E    R        B  9 42REFFLD(SD03)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N70                                  DSPATR(HI)
     A                                 10 16'業務員代號:'
     A                                      DSPATR(HI)
     A            DSC27S    R        B 10 30REFFLD(SC27)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
     A N74                                  DSPATR(HI)
     A                                 10 40'-'
     A            DSC27E    R        B 10 42REFFLD(SC27)
     A  75                                  DSPATR(PC)
     A  75                                  DSPATR(RI)
     A N75                                  DSPATR(HI)
     A                                 11 18'送貨年月:'
     A            DSC08          4Y 0B 11 30
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A                                      EDTWRD('  /  ')
     A                                 11 36'(MM/YY)'
     A                                 12 18'確認年月:'
     A            DSC39          4Y 0B 12 30
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                      EDTWRD('  /  ')
     A                                 12 36'(MM/YY)'
     A                                 13 18'分析方式:'
     A            DSEL           1A  B 13 30VALUES('A' 'B')
     A                                 13 32'( A-禮盒不拆B-禮盒拆單瓶)'
     A                                 16 28'執行環境:'
     A                                 14 18'統計內容:'
     A            DSEL1          1A  B 14 30VALUES('4' '1' '2' '3')
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                 14 32'( 1=銷售2=銷售含搭贈3=銷售含+
     A                                      試飲4=全部)'
     A            $EVR           1Y 0B 16 41VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 43'(1-即時   2-批次)'
     A                                 17 28'列印份數'
     A                                 17 39':'
     A            $CPY           3Y 0B 17 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 28'列表機'
     A                                      DSPATR(HI)
     A                                 18 39':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 41CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 29'***請準備１３２行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A  87        ERR8          63A  O 24  2MSGID(URE 0051 REMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
