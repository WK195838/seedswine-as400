     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOA012P                                    *
     A*    AUTHOR       : A1034 STEPHANIE                            *
     A*    CREATE DATE  : 81/06/03                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOA012                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售訂單系統                              *
     A*    REMARK       :訂單批次處理錯誤報表列印                  *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    27MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    27'專櫃單批次處理錯誤報表列印'
     A                                      SPACEA(2)
     A                                     1'公司　　　:'
     A            DSC01     R        O    15REFFLD(SC01)
     A            D#B03         16   O    19
     A                                      SPACEA(1)
     A                                     1'訂單日期　:'
     A            DSC07S         6  0O    15
     A                                      EDTCDE(Y)
     A                                    +1'-'
     A            DSC07E         6  0O    +1
     A                                      EDTCDE(Y)
     A                                      SPACEA(1)
     A                                     1'客戶代號　:'
     A            DSC04S    R        O    15REFFLD(SC04)
     A                                    +1'-'
     A            DSC04E    R        O    +1REFFLD(SC04)
     A                                    51'日期:'
     A            $EGMDY         6  0O    59EDTCDE(Y)
     A                                    68'頁次:'
     A                                    76PAGNBR EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     1'訂單號碼　:'
     A            DSC02S    R        O    15REFFLD(SC02)
     A                                    +1'-'
     A            DSC02E    R        O    +1REFFLD(SC02)
     A                                    51'時間:'
     A                                    59TIME
     A                                    70'<SOA012>'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     1'訂單號碼'
     A                                    15'送貨單號'
     A                                    31'發票號碼'
     A                                    44'客戶代號／簡稱'
     A                                      SPACEA(1)
     A                                     1'--------------------+
     A                                       --------------------+
     A                                       --------------------+
     A                                       --------------------'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            SC02      R              2
     A            SC29      R             16
     A            SC30      R             32
     A            SC04      R             45
     A                                    51'-'
     A            SC05      R             53
     A            ME04      R             57
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'<<SOA012>>--->報表結束'
