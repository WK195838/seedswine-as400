     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : ARR057P                                    *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/05/01                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARR057                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :應收帳款系統                              *
     A*    REMARK       :折讓單                                    *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(1)
     A                                    45MSGCON(030 URE9999 REMF)
     A                                      SPACEA(001)
     A                                    42'營業人進（銷）貨退出或折讓證明表-
     A                                      '
     A                                      SPACEA(002)
     A                                     1'辦事處:'
     A            COMP           1   O    +1
     A            TEXT           6   O    +1
     A                                    91'日期:'
     A            $EGMDY         6  0O    99EDTCDE(Y)
     A                                   109'頁次:'
     A                                   116PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'公司別:'
     A            DSG01S         2   O    +1
     A                                    +1'-'
     A            DSG01E         2   O    +1
     A                                    91'時間:'
     A                                    99TIME
     A                                      SPACEA(001)
     A                                     1'折讓日期:'
     A            DSG26S         6  0O    +1EDTCDE(Y)
     A                                    +1'-'
     A            DSG26E         6  0O    +1EDTCDE(Y)
     A                                    91' USER :'
     A            $USER         10A  O    99
     A                                   109'<ARR074AP>'
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEA(001)
     A                                     1'聯式'
     A                                     8'年'
     A                                    13'月'
     A                                    18'日'
     A                                    23'字軌'
     A                                    30'號碼'
     A                                    71'金額（不含稅之進貨額）'
     A                                    95'營業稅額'
     A                                   107'折讓單號'
     A                                      SPACEA(001)
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ----------------'
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A            SI17      R              4
     A            PSIYY          2         9
     A            PSIMM          2        14
     A            PSIDD          2        19
     A            PSI02F         2        25
     A            PSI02B         8        29
     A            CHAR           8O       39
     A            DATA1         10  0     79EDTCDE(1)
     A            DATA2          5  0     96EDTCDE(1)
     A            AF02      R        O   106
     A*----------------------------------------------------------------
     A          R PT1                       SPACEA(1)
     A                                    57'每頁小計------>'
     A            T1TOT1        11  0O    78
     A                                      EDTCDE(1)
     A            T1TOT2         6  0O    95
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R PT2                       SPACEA(1)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEA(001)
     A                                    57
     A                                      '總　　計------>'
     A            TOTA          11  0O    78
     A                                      EDTCDE(1)
     A            TOTB           6  0O    95
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R PE1
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEA(001)
     A                                     2
     A                                      '***續下頁***'
     A*----------------------------------------------------------------
     A          R PE2
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ================'
     A                                      SPACEA(001)
     A                                     2
     A                                      '<<ARR074AP>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE3
     A                                      SPACEA(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
