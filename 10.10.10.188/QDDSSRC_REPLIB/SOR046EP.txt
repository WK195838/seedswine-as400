     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : SOR046EP                                   *
     A*    AUTHOR       : A1087  JOYCE                               *
     A*    CREATE DATE  : 81/02/21                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : SOR046                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :銷售訂單系統                              *
     A*    REMARK       :核准簽章明細表                            *
     A****************************************************************
     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    65MSGCON(030 URE9999 REMF)
     A                                      SPACEA(1)
     A                                    71'核准簽章明細表'
     A                                      SPACEA(1)
     A  20                                74'(非普銷)'
     A N20                                74'(普　銷)'
     A                                     1' '
     A                                      SPACEA(1)
     A                                    75'(換貨)'
     A                                      SPACEA(1)
     A                                     1'地　　區:'
     A                                      SPACEB(2)
     A            DSC33S    R        O    14REFFLD(SC33)
     A                                    17'-'
     A            DSC33E    R        O    19REFFLD(SC33)
     A                                   148'日期:'
     A            $EGMDY         6  0O   156EDTCDE(Y)
     A                                   164'頁次:'
     A                                   173PAGNBR EDTCDE(Z)
     A                                     1'通　　路:'
     A                                      SPACEB(1)
     A            DSC31S    R        O    14REFFLD(SC31)
     A                                    17'-'
     A            DSC31E    R        O    19REFFLD(SC31)
     A                                   148'時間:'
     A                                   156TIME
     A                                   164'<SOR046E>'
     A                                     1'截止日期:'
     A                                      SPACEB(1)
     A            DSC08T    R        O    14REFFLD(SC08)
     A                                      EDTWRD('  /  /  ')
     A                                   148' USER:'
     A            $USER         10A  O   156
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======'                    )
     A                                      SPACEB(1)
     A                                    29'送　　貨'
     A                                      SPACEB(1)
     A                                   110'單　額　逾　搭　試　換'
     A                                     1'客戶代號'
     A                                      SPACEB(1)
     A                                    12'客戶簡稱'
     A                                    29'日　　期'
     A                                    38'單據號碼'
     A                                    51'業務員'
     A                                    69'產品代號'
     A                                    79'產品名稱'
     A                                    95'單位'
     A                                   102'數量'
     A                                   110'價　度　期　贈　飲　貨'
     A                                   134'備註'
     A                                   159'簽核'
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======'                    )
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEB(1)
     A  30        DSC04     R              2REFFLD(SC04)
     A  30        DSC05     R              8REFFLD(SC05)
     A  30        ME04      R             12
     A  30        DSC08     R             30REFFLD(SC08)
     A                                      EDTWRD('  /  /  ')
     A  30        DSC02     R             39REFFLD(SC02)
     A  30        DSC27     R             52REFFLD(SC27)
     A  30        DMC02         12A       58
     A            DSD03     R             69REFFLD(SD03)
     A            MA03      R             78
     A            MA13      R             95
     A            DSD04     R            101REFFLD(SD04)
     A                                      EDTCDE(J)
     A*
     A            DSC22     R            112REFFLD(SC22)
     A            DSC23     R            116REFFLD(SC23)
     A            DSC24     R            120REFFLD(SC24)
     A            DSC25     R            124REFFLD(SC25)
     A            DSC26     R            128REFFLD(SC26)
     A            DSE19     R            132REFFLD(SE19)
     A            DSC21     R            134REFFLD(SC21)
     A  30                               160'_________________'
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======'                    )
     A                                      SPACEB(1)
     A                                     2'***續下頁***'
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      SPACEB(1)
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ====='                     )
     A                                     2'<<SOR046E>>--->報表結束'
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      DFT('==============================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======='                   )
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A          R PE9                       SPACEB(1)
     A                                     3'もも此範圍無資料もも'
