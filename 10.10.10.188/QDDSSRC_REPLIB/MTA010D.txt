     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            S501           5   O  7  2
     A            DS502          6  0B  7 18EDTWRD('  /  /  ')
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A            DS503         11  2B  7 30EDTCDE(K)
     A                                      CHECK(RB)
     A            DS504         11  2B  7 46EDTCDE(K)
     A                                      CHECK(RB)
     A            DS506          6   O  7 66
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(25 '下一頁')
     A                                      CF03(03)
     A*                                     CA09(09)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'MTA010'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'產品最低售價維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A            DBGN1          5   B  6  2
     A                                  5  1'產品分類代碼　售價有效日期 +
     A                                      有效日期前售價 +
     A                                      有效日期後售價'
     A                                      DSPATR(UL)
                                        5 64'建立人員'
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A          R DSPC11
     A                                 22  2'執行'
     A                                 22 12'F3 =回主畫面'
     A*                                22 40'F9 =列印'
     A  98        ERR1          60A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR2          60A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          60A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          60A  O 24  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR4          60A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  28        DMSG          78A  O 24  2DSPATR(HI)
     A                                 24 64MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
