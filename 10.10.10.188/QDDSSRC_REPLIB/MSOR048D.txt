     A*%%TS  SD  19941216  115735  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*   人頭馬匯東洋酒股份有限公司－版權所有      TEL:5042266    *
     A*                                                              *
     A*    DSPF NAME    : MSOR048D                                   *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 84/04/01                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : MSOR048D                                   *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :訂單系統                                  *
     A*    REMARK       :通路產品銷售平均單價表                    *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19941216  115735  D910074     REL-V2R2M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'MSOR048'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 29'通路產品銷售平均單價表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4 25'辦事處　:'
     A            COMP           1A  B  4 37VALUES(' ' '1' '2' '3')
     A                                  4 40'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                  5 25'通路　　:'
     A                                      DSPATR(HI)
     A            DCD13S    R        B  5 37REFFLD(CD13)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  5 47'-'
     A            DCD13E    R        B  5 49REFFLD(CD13)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  7 25'地區　　:'
     A                                      DSPATR(HI)
     A            DCD11S    R        B  7 37REFFLD(CD11)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                  7 47'-'
     A            DCD11E    R        B  7 49REFFLD(CD11)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A                                  8 25'組別　　:'
     A                                      DSPATR(HI)
     A            DCD10S    R        B  8 37REFFLD(CD10)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A N65                                  DSPATR(HI)
     A                                  8 47'-'
     A            DCD10E    R        B  8 49REFFLD(CD10)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A N66                                  DSPATR(HI)
     A                                  9 25'業務員　:'
     A                                      DSPATR(HI)
     A            DCD09S    R        B  9 37REFFLD(CD09)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(HI)
     A                                  9 47'-'
     A            DCD09E    R        B  9 49REFFLD(CD09)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A N68                                  DSPATR(HI)
     A                                 10 25'產品種類:'
     A            DMA11          1A  B 10 37VALUES('1' '2' '3' ' ')
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A                                 10 40'(1-BRANDY 2-WHISKY 3-WINE " "-ALL)'
     A                                 11 25'產品代號:'
     A                                      DSPATR(HI)
     A            DCD04S    R        B 11 37REFFLD(CD04)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A N69                                  DSPATR(HI)
     A                                 11 47'-'
     A            DCD04E    R        B 11 49REFFLD(CD04)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A N70                                  DSPATR(HI)
     A                                 12 25'客戶代號:'
     A                                      DSPATR(HI)
     A            DCD06S         9A  B 12 37
     A  74                                  DSPATR(RI)
     A  74                                  DSPATR(PC)
     A N74                                  DSPATR(HI)
     A                                 12 47'-'
     A            DCD06E         9A  B 12 49
     A  75                                  DSPATR(RI)
     A  75                                  DSPATR(PC)
     A N75                                  DSPATR(HI)
     A                                 13 25'確認年月:'
M001DA*           DCD01S    R        B 13 37REFFLD(CD01)
M001AA            DCD01S         4  0B 13 37
     A                                      EDTWRD('  /  ')
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                 13 47'-'
M001DA*           DCD01E    R        B 13 49REFFLD(CD01)
M001AA            DCD01E         4  0B 13 49
     A                                      EDTWRD('  /  ')
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A                                 13 56'(MM/YY)'
     A                                 14 25'分析方式:'
     A            DSEL           1A  B 14 37
     A  73                                  DSPATR(PC)
     A  73                                  DSPATR(RI)
     A                                      VALUES('A' 'B')
     A                                 14 40'A-原包裝'
     A                                 15 40'B-單瓶含拆解之單瓶'
     A                                 17 25'執行環境:'
     A            $EVR           1Y 0B 17 37VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 17 42'(1-即時   2-批次)'
     A                                 18 25'列印份數:'
     A            $CPY           3Y 0B 18 37EDTCDE(3)
     A                                      CHECK(RB)
     A                                 19 25'列表機  :'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 19 37CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 21 26'***請準備８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A  89        ERR11         50A  O 24  2MSGID(UPT 0011 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                  6 25'型態　　:'
     A                                      DSPATR(HI)
     A            DCD14S    R        B  6 37REFFLD(CD14)
     A  76                                  DSPATR(RI)
     A  76                                  DSPATR(PC)
     A N76                                  DSPATR(HI)
     A                                  6 47'-'
     A            DCD14E    R        B  6 49REFFLD(CD14)
     A  77                                  DSPATR(RI)
     A  77                                  DSPATR(PC)
     A N77                                  DSPATR(HI)
