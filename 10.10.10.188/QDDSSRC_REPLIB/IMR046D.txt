     A*%%TS  SD  19921201  164656  D910121     REL-V2R1M0  5738-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : IMR046D                                    *
     A*    AUTHOR       : A1139  JANE                                *
     A*    CREATE DATE  : 81/02/27                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : IMR046                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :庫存系統                                  *
     A*    REMARK       :客戶送貨明細表                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A* M001      | 98.12.10 | MANUAL MODIFY BY MICHELLE FOR Y2K
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R DSPD1
     A*%%TS  SD  19921201  164656  D910121     REL-V2R1M0  5738-PW1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A                                      CA03(03 '結束執行')
     A                                      CF04(04 'F4 HELP')
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'IMR046'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'客戶送貨明細表'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6 39':'
     A                                  7 28'通路　　'
     A                                      DSPATR(HI)
     A                                  7 39':'
     A            DSE27S    R        B  7 41REFFLD(SE27)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  DSPATR(HI)
     A                                  7 50'-'
     A            DSE27E    R        B  7 52REFFLD(SE27)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A N62                                  DSPATR(HI)
     A                                  8 28'型態　　'
     A                                      DSPATR(HI)
     A                                  8 39':'
     A            DSE28S    R        B  8 41REFFLD(SE28)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A N63                                  DSPATR(HI)
     A                                  8 50'-'
     A            DSE28E    R        B  8 52REFFLD(SE28)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A N64                                  DSPATR(HI)
     A                                  9 28'物流點　'
     A                                  9 39':'
     A            DSE07S    R        B  9 41REFFLD(SE07)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A                                  9 50'-'
     A            DSE07E    R        B  9 52REFFLD(SE07)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A                                 10 28'客戶代號:'
     A                                      DSPATR(HI)
     A            DSE04S         9A  B 10 41
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N67                                  DSPATR(HI)
     A                                 10 50'-'
     A            DSE04E         9A  B 10 52
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A N68                                  DSPATR(HI)
     A                                 13 28'送貨日期'
     A                                 13 39':'
M001MA*           DSE21S    R        B 13 41REFFLD(SE21)
M001MA            DSE21S         6  0B 13 41
     A                                      EDTWRD('  /  /  ')
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A                                 13 50'-'
M001MA*           DSE21E    R        B 13 52REFFLD(SE21)
M001MA            DSE21E         6  0B 13 52
     A                                      EDTWRD('  /  /  ')
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A                                 13 62'(MM/DD/YY)'
     A                                 16 28'執行環境'
     A                                 16 39':'
     A            $EVR           1Y 0B 16 41VALUES(1 2)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                 16 44'(1-即時   2-批次)'
     A                                 17 28'列印份數'
     A                                 17 39':'
     A            $CPY           3Y 0B 17 41EDTCDE(3)
     A                                      CHECK(RB)
     A                                 18 28'列表機'
     A                                      DSPATR(HI)
     A                                 18 39':'
     A                                      DSPATR(HI)
     A            $PRTID         2Y 0B 18 41CHECK(RB)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 29'***請準備８０行報表紙***'
     A                                      DSPATR(HI)
     A                                 22  2'執行'
     A                                 22 10'F3 =回主畫面'
     A                                 22 26'F4 =視窗查詢'
     A  98        ERR1          50A  O 24  2MSGID(UPT 2140 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 24  2MSGID(UPT 2110 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 24  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 24  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 24  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 24  2MSGID(UPT 1011 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR10         50A  O 24  2MSGID(UPT 0041 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A                                  6 28'辦事處　'
     A            COMP           1A  B  6 41VALUES(' ' '1' '2' '3')
     A                                  6 43'( 1=台北2=台中3=高雄" "=全+
     A                                      部)'
     A                                 14 28'確認日期 :'
M001MA*           DSE24S    R        B 14 41REFFLD(RERFR/SE24 *LIBL/RERF)
M001MA            DSE24S         6  0B 14 41
     A                                      EDTWRD('  /  /  ')
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A                                 14 50'-'
M001MA*           DSE24E    R        B 14 52REFFLD(RERFR/SE24 *LIBL/RERF)
M001MA            DSE24E         6  0B 14 52
     A                                      EDTWRD('  /  /  ')
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A                                 11 28'業務員　 :'
     A            DSE32S    R        B 11 41REFFLD(RERFR/SE32 *LIBL/RERF)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A                                 11 50'-'
     A            DSE32E         6A  B 11 52
     A  74                                  DSPATR(RI)
     A  74                                  DSPATR(PC)
