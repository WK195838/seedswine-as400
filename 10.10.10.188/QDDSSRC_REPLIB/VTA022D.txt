     A*%%TS  SD  19940307  114607  D910121     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*                                                              *
     A*    DSPF NAME    : VTA022D                                    *
     A*    AUTHOR       : MICHELLE                                   *
     A*    CREATE DATE  : 04/25/02                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : VTA022                                     *
     A*    SYSTEM       :寰盛洋酒                                  *
     A*    SUBSYSTEM    :媒體申報                                  *
     A*    REMARK       :銷項憑證轉檔－銷退折讓                    *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A          R SFLSR                     SFL
     A            RRN            3Y 0H
     A            SG02      R        H      REFFLD(SG02)
     A            SG09      R        H      REFFLD(SG09)
     A            SG26      R        H      REFFLD(SG26)
     A            AF01      R        H      REFFLD(AF01)
     A            AF02      R        H      REFFLD(AF02)
     A            AF05      R        H      REFFLD(AF05)
     A            AF06      R        H      REFFLD(AF06)
     A            AF07      R        H      REFFLD(AF07)
     A            AF09           9  0H
     A            DOPT           1A  B  9  2DSPATR(CS)
     A                                      VALUES('X' ' ')
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            SG26N          6Y 0O  9  4EDTCDE(Y)
     A            DSG02         12   O  9 13
     A            DSG04          5   O  9 26
     A            DSG05          4   O  9 32
     A            SG04N         18A  O  9 37
     A            DSG10          9  0O  9 56EDTCDE(2)
     A            DSX10          9  0O  9 68EDTCDE(2)
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0052)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      CF14(14)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  96                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                 21 47'合計:'
     A            DSUM           9  0O 21 56EDTCDE(2)
     A            DSUM2          9  0O 21 68EDTCDE(2)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        70A  O 22  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                 23 10'F3=回主畫面'
     A                                 23 25'F12=回前畫面'
     A                                 23 41'F14=確認'
     A                                 23 53'PA1=下一頁'
     A                                 23 67'PA2=上一頁'
     A*                                24 67'【寰盛洋酒】'
     A                                 24 64MSGCON(016 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A          R DSPC1
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      OVERLAY
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'VTA022.2'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'銷退折讓轉檔作業'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'公司別　:'
     A            DVP91          1   O    +2
     A            CONAME         6   O    +2
     A                                  5  1'分配年月:'
     A*           DVP04S         4  0O    +2
     A*                                     EDTWRD('  /  ')
     A*                                   +2'-'
     A            DVP04E         4  0O    +2
     A                                      EDTWRD('  /  ')
     A                                    +2'(MM/YY)'
     A                                  7  2'X=不轉檔'
     A                                  8  2'X'
     A                                      DSPATR(UL)
     A                                  8  5'日期'
     A                                      DSPATR(UL)
     A                                  8 13'折讓單號'
     A                                      DSPATR(UL)
     A                                  8 25'客戶代號'
     A                                      DSPATR(UL)
     A                                  8 38'客戶簡稱'
     A                                      DSPATR(UL)
     A                                  8 56'未稅金額'
     A                                      DSPATR(UL)
     A                                  8 72'稅　額'
     A                                      DSPATR(UL)
