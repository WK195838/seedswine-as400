     A                                      REF(RERF)
     A          R PH1                       SKIPB(2)
     A                                    26MSGCON(030 URE9999 *LIBL/REMF)
     A                                    62'日期:'
     A            $EGMDY         6  0     70EDTCDE(Y)
     A                                      SPACEA(1)
     A                                     2'SOR065'
     A                                    33'銷售訂單列印'
     A                                    62'時間:'
     A                                    70TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    62' USER :'
     A            $USER         10A       70
     A                                      SPACEA(2)
     A                                     2'公司代號  :'
     A            D#B031    R             15REFFLD(#B03)
     A                                    47'地區:'
     A            #D02      R             +0
     A                                      SPACEA(1)
     A                                     2'客戶名稱  :'
     A            DME04     R             +0REFFLD(ME04)
     A                                    47'單別:'
     A            #203      R             +0
     A                                      SPACEA(1)
     A                                     2'訂單號碼  :'
     A            SC02      R             16
     A                                    39'客戶訂單號碼:'
     A            SC06      R             55
     A                                      SPACEA(1)
     A                                     2'訂單日期  :'
     A            DSC07     R             16REFFLD(SC07)
     A                                      EDTWRD('  /  /  ')
     A                                    43'送貨日期:'
     A            DSC08     R             55REFFLD(SC08)
     A                                      EDTWRD('  /  /  ')
     A                                      SPACEA(1)
     A                                     2'發票聯數  :'
     A            SC09      R             16REFFLD(SC09)
     A                                      EDTCDE(Z)
     A                                    19'(2.二聯 3.三聯)'
     A                                    43'試飲原因:'
     A            D#A03         22A       +1
     A                                      SPACEA(1)
     A                                     2'出貨物流點:'
     A            DSC11C        22O       +0
     A                                    45'業務員:'
     A            MC02      R             +0REFFLD(MC0/MC02 *LIBL/MTMCPF)
     A                                      SPACEA(1)
     A                                     2'訂單狀態  :'
     A            DSC13C        10O       +0
     A                                    45'總金額:'
     A            SC12      R             55
     A                                      EDTCDE(2)
     A                                      SPACEA(1)
     A                                     2'課稅別    :'
     A            SC37      R             16REFFLD(SC37)
     A                                    18'(D-應稅B-免稅Z-零稅)'
     A                                    43'統一編號:'
     A            MD17      R             55
     A                                      SPACEA(1)
     A                                     2'發票地址  :'
     A            ME031     R             +0REFFLD(ME03)
     A                                      SPACEA(1)
     A            ME051     R             15REFFLD(ME05)
     A            ME061     R             48REFFLD(ME06)
     A                                      SPACEA(1)
     A                                     2'送貨地址  :'
     A            ME032     R             +0REFFLD(ME03)
     A                                      SPACEA(1)
     A            ME052     R             15REFFLD(ME05)
     A            ME062     R             48REFFLD(ME06)
     A                                      SPACEA(1)
     A                                     2'訂單附註: 1.'
     A            SC18      R             17
     A                                      SPACEA(1)
     A                                    14'2.'
     A            SC19      R             17
     A                                      SPACEA(1)
     A                                    14'3.'
     A            SC20      R             17
     A                                      SPACEA(1)
     A                                     2'核準備註  :'
     A            SC21      R             16
     A                                      SPACEA(1)
     A                                    43'確認日期:'
     A            DSC39     R             55REFFLD(SC0/SC07 *LIBL/SOSCPF)
     A                                      EDTWRD('  /  /  ')
     A                                      SPACEA(1)
     A                                     2'送貨單號  :'
     A            SC29      R             16REFFLD(SC0/SC29 *LIBL/SOSCPF)
     A                                    43'發票號碼:'
     A            SC30      R             55REFFLD(SC0/SC30 *LIBL/SOSCPF)
     A                                      SPACEA(3)
     A*----------------------------------------------------------------
     A          R PD1                       SPACEA(1)
     A                                     2'もも待核準項目もも:'
     A                                      SPACEA(1)
     A  31                                 6'單價'
     A                                      SPACEA(1)
     A  32                                 6'額度'
     A                                      SPACEA(1)
     A  33                                 6'逾期'
     A                                      SPACEA(1)
     A  34                                 6'搭贈'
     A                                      SPACEA(1)
     A  35                                 6'試飲'
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PH2                       SPACEA(1)
     A                                    32'－－訂貨－－'
     A                                    48'－－搭贈－－'
     A                                      SPACEA(1)
     A                                     2'產品代號／名稱'
     A                                    33'箱'
     A                                    41'瓶'
     A                                    49'箱'
     A                                    57'瓶'
     A                                    64'單價'
     A                                    76'金額'
     A                                      SPACEA(1)
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A*----------------------------------------------------------------
     A          R PD2                       SPACEA(1)
     A            SD03      R              2
     A            MA03      R             12
     A            SD041          4  0     31EDTCDE(2)
     A            SD042          4  0     39EDTCDE(1)
     A            SD051          4  0     47EDTCDE(2)
     A            SD052          4  0     55EDTCDE(1)
     A            SD06      R             60EDTCDE(3)
     A            SD07      R             70EDTCDE(3)
     A*----------------------------------------------------------------
     A          R PE1                       SKIPB(58)
     A                                     2'簽核:'
     A                                      SPACEA(1)
     A                                     5'填表人'
     A                                    16'業務主管'
     A                                    27'核準主管'
     A                                    38'核準主管'
     A                                    49'核準主管'
     A                                    60'財務主管'
     A                                    71'副　　總'
     A                                      SPACEA(2)
     A            UN1            8         6UNDERLINE
     A            UN2            8        17UNDERLINE
     A            UN3            8        28UNDERLINE
     A            UN4            8        39UNDERLINE
     A            UN5            8        50UNDERLINE
     A            UN6            8        61UNDERLINE
     A            UN7            8        72UNDERLINE
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ===================='
     A                                      SPACEA(1)
     A                                     2'<<SOR063>>--->報表結束'
     A*----------------------------------------------------------------
     A          R PE9                       SPACEA(1)
     A                                     3'もも此範圍無資料もも'
