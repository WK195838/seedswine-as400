     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIS010D                                    *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 98/03/24                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :茂世進銷存                                *
     A*    SUBSYSTEM    :                                          *
     A*    REMARK       :採購訂單驗收                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '5' '6')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SBG01          6Y 0O  9  6EDTWRD('  /  /  ')
     A            SBG01H    R        H      REFFLD(BG01 W2BGPF)
     A            SBG03     R        O  9 17REFFLD(BG03 W2BGPF)
     A            STATUS        10A  O  9 32
     A            SBG02H    R        H      REFFLD(BG02 W2BGPF)
     A            SBG06H    R        H      REFFLD(BG06 W2BGPF)
     A            SBG04H    R        H      REFFLD(BG04 W2BGPF)
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A                                  1  2'KIS010'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'採購訂單驗收'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  1'專櫃代號:'
     A            DBG02     R        B    +1REFFLD(BG02 W2BGPF)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  62                                  DSPATR(PC RI)
     A            D#A03     R        O    +1REFFLD(#A03 PA#APF)
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                    +2'5=查詢'
     A                                      COLOR(BLU)
     A                                    +2'6=確認'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN1          6Y 0B  8  6EDTWRD('  /  /  ')
     A            DBGN2         12A  B  8 17
     A                                  6  3'(月/日/年)'
     A                                  7  3'A 訂單日期  驗收單號       +
     A                                      狀態   '
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SBH02     R        O  9  3REFFLD(BH02 W2BHPF)
     A            SMA03     R        O  9 13REFFLD(MA03 MTMAPF)
     A            SBF03     R        O  9 32REFFLD(BF03 W2BFPF)
     A                                      EDTCDE(3)
     A            SBH03          5  0B  9 43
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A  61                                  DSPATR(PC RI)
     A                                      EDTCDE(4)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF05(05)
     A  34                                  CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'KIS010'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'採購訂單驗收'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'專櫃代號:'
     A            DBG022    R        O    +1REFFLD(BG02 W2BGPF)
     A            D#A032    R        O    +1REFFLD(#A03 PA#APF)
     A                                  6  1'訂單日期:'
     A            DBG01          6Y 0O    +1EDTWRD('  /  /  ')
     A                                    +1'採購單號:'
     A            DBG06     R        O    +1REFFLD(BG06 W2BGPF)
     A                                  8  2'產品代號                  -
     A                                      訂貨量 驗收量'
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  34                                +2'F5 =參考資料'
     A                                      COLOR(BLU)
     A  34                                +2'F9 =確認'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 URE9996 *LIBL/REMF)
     A                                      DSPATR(HI)
