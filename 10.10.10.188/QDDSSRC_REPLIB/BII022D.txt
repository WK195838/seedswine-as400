     A*%%TS  SD  19940307  113029  D910074     REL-V2R2M0  5738-PW1
     A****************************************************************
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PII001D                                    *
     A*    AUTHOR       : TINA                                       *
     A*    CREATE DATE  : 83/03/07                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PII001                                     *
     A*    SYSTEM       :人頭馬匯東                                *
     A*    SUBSYSTEM    :盤點子系統                                *
     A*    REMARK       :年終成本數量查詢                          *
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RERF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*--------------------------------------------------------------*
     A          R SFLSR1                    SFL
     A*%%TS  SD  19940307  112228  D910074     REL-V2R2M0  5738-PW1
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  4VALUES(' ' '5')
     A            DIX11          4Y 0O 10  9EDTWRD('  /  ')
     A            IX01      R        O 10 18
     A            MA03      R        O 10 29
     A            IX06      R        O 10 52EDTCDE(2)
     A            IX07      R        O 10 69EDTCDE(2)
     A            IX08      R        O 11 29EDTCDE(2)
     A            IX09      R        O 11 48EDTCDE(2)
     A            IX10      R        O 11 65EDTCDE(2)
     A*--------------------------------------------------------------*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A*%%TS  SD  19940307  113029  D910074     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0005)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'PII022.1'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'年終成本數量查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'輸入作業選擇:'
     A                                  6  6'5=各物流點數量查詢'
     A                                  8  4'A  年終年月　產品／簡稱'
     A                                  8 52'保稅量'
     A                                  8 69'完稅量'
     A            DBGN11    R        B  9  9REFFLD(IX11)
     A                                      EDTWRD('  /  ')
     A            DBGN12    R        B  9 18REFFLD(IX01)
     A                                  9 33'FOB-TOT'
     A                                  9 52'FHI-TOT'
     A                                  9 68'DUTY-TOT'
     A*--------------------------------------------------------------*
     A          R DSPC1
     A                                 22  2'執行'
     A                                 22 11'F3 =回主畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
     A*--------------------------------------------------------------*
     A          R SFLSR2                    SFL
     A*%%TS  SD  19940307  104238  D910074     REL-V2R2M0  5738-PW1
     A            RRN2           4S 0H
     A            IY02      R        O 10  3
     A            #A03      R        O 10  9
     A            IY06      R        O 10 42EDTCDE(2)
     A            IY07      R       0O 10 50EDTCDE(2)
     A            IY08      R       0O 10 58EDTCDE(2)
     A            IY09      R       0O 10 66EDTCDE(2)
     A            IY10      R       0O 10 74EDTCDE(2)
     A*--------------------------------------------------------------*
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A*%%TS  SD  19940307  113029  D910074     REL-V2R2M0  5738-PW1
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A                                  1  2'PII022'
     A                                  1 26MSGCON(030 URE9999 *LIBL/REMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002  '
     A                                  2 31'年終成本數量查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'年終年月:'
     A            DIX11          4Y 0O  4 14EDTWRD('  /  ')
     A                                  4 46'完稅量:'
     A            IX07      R        O  4 56EDTCDE(2)
     A                                  5  2'產品代號:'
     A            IX01      R        O  5 14
     A            MA03      R        O  5 26
     A                                  5 46'保稅量:'
     A            IX06      R        O  5 56EDTCDE(2)
     A                                  6  3'FOB-TOT:'
     A            IX08      R        O  6 12EDTCDE(2)
     A                                  6 29'FHI-TOT:'
     A            IX09      R        O  6 38EDTCDE(2)
     A                                  6 56'DUTY-TOT:'
     A            IX10      R        O  6 66EDTCDE(2)
     A                                  8  2'物流點／名稱　             　+
     A                                      　 　　庫存量　良品量　壞品量　+
     A                                      保留量　可用量'
     A                                      DSPATR(UL)
     A*--------------------------------------------------------------*
     A          R DSPC2
     A                                 22  2'執行'
     A                                 22 11'F3 =回主畫面'
     A                                 22 27'F12=回前畫面'
     A  97        DMSG          78A  O 24  2DSPATR(HI)
     A  96        ERR1          63A  O 24  2MSGID(UPT 2142 PTMF)
     A                                      DSPATR(HI)
     A  90        ERR10         63A  O 24  2MSGID(UPT 2150 PTMF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 UPT9999 *LIBL/PTMF)
     A                                      DSPATR(HI)
