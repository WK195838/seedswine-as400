     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : KIP005D                                    *
     A*    AUTHOR       : A1492  TERRY                               *
     A*    CREATE DATE  : 98/04/15                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME :                                            *
     A*    SYSTEM       :茂世進銷存                                *
     A*    REMARK       :產品牌價維護                              *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B  9  3VALUES(' ' '2' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            AA01      R        O  9  7REFFLD(AA01 W1AAPF)
     A*----------------------------------------------------------------
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(MSG1006 *LIBL/R1MF)
     A                                  1  2'KIP005'
     A                                  1 26MSGCON(030 MSG9999 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'產品牌價維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  1'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 17'2=修改'
     A                                      COLOR(BLU)
     A                                  5 26'5=查詢'
     A                                      COLOR(BLU)
     A            DRRN1          4S 0H      SFLRCDNBR
     A            DBGN3     R        B  8  7REFFLD(AA01 W1AAPF)
     A                                  7  3'A   牌價序號 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                 22 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 22 26'F6 =新增'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 MSG9996 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            SAA02     R        B  9  6REFFLD(AA02 W1AAPF)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A  33N61                               DSPATR(HI)
     A            SMA03     R        O  9 16REFFLD(MA03 MTMAPF)
     A            SAA03          6  0B  9 35EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            SAA04     R        B  9 45REFFLD(AA04 W1AAPF)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            SAA02H    R        H      REFFLD(AA02 W1AAPF)
     A            SAA03H         6  0H
     A            SAA04H    R        H      REFFLD(AA04 W1AAPF)
     A*----------------------------------------------------------------
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A  33                                  CF04(04)
     A  33                                  CF05(05)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  98                                  SFLMSGID(MSG1006 *LIBL/R1MF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'KIP005'
     A                                  1 26MSGCON(030 MSG9999 *LIBL/R1MF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 33'產品牌價維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6  1'牌價序號:'
     A                                      COLOR(BLU)
     A            DAA01     R        O    +1REFFLD(AA01 W1AAPF)
     A                                      COLOR(BLU)
     A                                  8  5'產品代號／說明             -
     A                                      有效起日      牌價 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R DSPC3
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  33                                +2'F4 =視窗查詢'
     A                                      COLOR(BLU)
     A  33                                +2'F5 =參考資料'
     A                                      COLOR(BLU)
     A                                    +2'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 MSG9996 *LIBL/R1MF)
     A                                      DSPATR(HI)
