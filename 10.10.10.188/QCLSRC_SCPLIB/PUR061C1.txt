     /***************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250   */
     /*                                                             */
     /*    AUTHOR       : A1492  TERRY                              */
     /*    CREATE DATE  : 101/01/10                                 */
     /*    UPDATE DATE  :                                           */
     /*    PROGRAM NAME : PUR061C1                                  */
     /*    SYSTEM       :                                           */
     /*    SUBSYSTEM    :採購系統                                 */
     /*    REMARK       :採購單彙總表                             */
     /*                                                             */
     /***************************************************************/
             PGM

             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)

             DCL        VAR(&L)      TYPE(*CHAR) LEN(10)
             DCL        VAR(&F)      TYPE(*CHAR) LEN(10)

             DCL        VAR(&DOWNL)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&DCH01)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&DCH02S) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DCH02E) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DCH07S) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DCH07E) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DCH06S) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DCH06E) TYPE(*CHAR) LEN(5)
             DCL        VAR(&DCI03S) TYPE(*CHAR) LEN(9)
             DCL        VAR(&DCI03E) TYPE(*CHAR) LEN(9)
             DCL        VAR(&DCD03S) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DCD03E) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DCH08S) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DCH08E) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DPRTTP) TYPE(*CHAR) LEN(8)

             CHGVAR     VAR(&DOWNL)  VALUE(%SST(*LDA 600 1))
             CHGVAR     VAR(&DCH01)  VALUE(%SST(*LDA 601 2))
             CHGVAR     VAR(&DCH02S) VALUE(%SST(*LDA 603 2))
             CHGVAR     VAR(&DCH02E) VALUE(%SST(*LDA 605 2))
             CHGVAR     VAR(&DCH07S) VALUE(%SST(*LDA 607 5))
             CHGVAR     VAR(&DCH07E) VALUE(%SST(*LDA 612 5))
             CHGVAR     VAR(&DCH06S) VALUE(%SST(*LDA 617 5))
             CHGVAR     VAR(&DCH06E) VALUE(%SST(*LDA 622 5))
             CHGVAR     VAR(&DCI03S) VALUE(%SST(*LDA 627 9))
             CHGVAR     VAR(&DCI03E) VALUE(%SST(*LDA 636 9))
             CHGVAR     VAR(&DCD03S) VALUE(%SST(*LDA 670 8))
             CHGVAR     VAR(&DCD03E) VALUE(%SST(*LDA 678 8))
             CHGVAR     VAR(&DCH08S) VALUE(%SST(*LDA 686 8))
             CHGVAR     VAR(&DCH08E) VALUE(%SST(*LDA 694 8))
             CHGVAR     VAR(&DPRTTP) VALUE(%SST(*LDA 669 1))

             OVRDBF     FILE(SCWK251) SHARE(*YES)
             CLRPFM     FILE(SCWK251)
             OVRDBF     FILE(SCWK252) SHARE(*YES)
             CLRPFM     FILE(SCWK252)

             OVRDBF     FILE(POCHPF) TOFILE(POCHPF) SHARE(*YES)
             OPNQRYF    FILE(POCHPF) OPTION(*ALL) QRYSLT('            +
                             CH01  =       ("' || &DCH01  || '")      +
                           & CH02  = %RANGE("' || &DCH02S || '"       +
                                            "' || &DCH02E || '")      +
                           & CH07  = %RANGE("' || &DCH07S || '"       +
                                            "' || &DCH07E || '")      +
                           & CH06  = %RANGE("' || &DCH06S || '"       +
                                            "' || &DCH06E || '")      +
                           & CH08C = %RANGE("' || &DCH08S || '"       +
                                            "' || &DCH08E || '")')    +
                        KEYFLD((*FILE))  MAPFLD((CH08C CH08 *CHAR 8))
 /* WRITE WORKFILE */
             IF         COND(&DPRTTP *EQ '1') THEN(DO)
             CALL       PGM(PUR0611)
             ENDDO
             ELSE       CMD(DO)
             CALL       PGM(PUR0613)
             ENDDO

             CLOF       OPNID(POCHPF)

             IF         COND(&DOWNL *EQ '1') THEN(DO)
             GOTO       DOWNLOAD
             ENDDO
             ELSE       CMD(DO)
             GOTO       PRINT
             ENDDO
 DOWNLOAD:
             IF         COND(&DPRTTP *EQ '1') THEN(DO)
             CHGVAR     VAR(&L) VALUE('SCFLIB')
             CHGVAR     VAR(&F) VALUE('SCWK251')
             ENDDO
             ELSE       CMD(DO)
             CHGVAR     VAR(&L) VALUE('SCFLIB')
             CHGVAR     VAR(&F) VALUE('SCWK252')
             ENDDO
             CALL       PGM(ECL100C) PARM(&L &F)
             GOTO       END
 PRINT:
             IF         COND(&DPRTTP *EQ '1') THEN(DO)
             OVRPRTF    FILE(PUR061P1) PAGESIZE(*N 132) +
                          USRDTA('採購總表')
             CALL       PGM(PUR0612)
             ENDDO
             ELSE       CMD(DO)
             OVRPRTF    FILE(PUR061P2) PAGESIZE(*N 157) +
                          USRDTA('採購明細')
             CALL       PGM(PUR0614)
             ENDDO

             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)

             IF         COND(&DPRTTP *EQ '1') THEN(DO)
             CHGVAR     VAR(&P6601I) VALUE('PUR061P1')
             ENDDO

             IF         COND(&DPRTTP *EQ '2') THEN(DO)
             CHGVAR     VAR(&P6601I) VALUE('PUR061P2')
             ENDDO

             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)

 END:
             DLTOVR     FILE(*ALL)
             ENDPGM
