             PGM

             DCL        VAR(&DEVD)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&LCK)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN96)   TYPE(*LGL)  LEN(1)
             DCL        VAR(&WEH01)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&DATFM)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&DATTO)  TYPE(*CHAR) LEN(8)
             RTVDTAARA  DTAARA(*LDA (163  10)) RTNVAR(&DEVD)
             RTVDTAARA  DTAARA(*LDA (602  2)) RTNVAR(&WEH01)
             RTVDTAARA  DTAARA(*LDA (604  8)) RTNVAR(&DATFM)
             RTVDTAARA  DTAARA(*LDA (612  8)) RTNVAR(&DATTO)

             /* LOCK CHECK */
             CHGVAR     VAR(&LCK) VALUE('1')
             CALL       PGM(INT006C2) PARM(&LCK &IN96)
             IF         COND(&IN96 *EQ '1') THEN( GOTO CMDLBL(ERR) )

             CLRPFM     FILE(INTNWF)
             CLRPFM     FILE(INTPWF)

             OVRPRTF    FILE(INT006P1) USRDTA('未入庫表')
             OVRPRTF    FILE(INT006P2) USRDTA('負庫存表')
             OVRDBF     FILE(SEHALF01) SHARE(*YES)
             OPNQRYF    FILE(SEHALF01) OPTION(*ALL) QRYSLT('      +
                        HA01 = "' || &WEH01 || '"   &           +
                        HA03C = %RANGE("' || &DATFM || '"       +
                                       "' || &DATTO || '") ')   +
                        KEYFLD(*FILE) MAPFLD((HA03C HA03 *CHAR 8))
             OVRDBF     FILE(INTPPF)   SHARE(*YES)
             OPNQRYF    FILE(INTPPF)   OPTION(*ALL) QRYSLT('      +
                        TP01 = "' || &WEH01 || '"   &           +
                        TP03C = %RANGE("' || &DATFM || '"       +
                                       "' || &DATTO || '") ')   +
                        KEYFLD(*FILE) MAPFLD((TP03C TP03 *CHAR 8))
             OVRDBF     FILE(INTQPF)   SHARE(*YES)
             OPNQRYF    FILE(INTQPF)   OPTION(*ALL) QRYSLT('    +
                        TQ01 = "' || &WEH01 || '"   &           +
                        TQ03C = %RANGE("' || &DATFM || '"       +
                                       "' || &DATTO || '") ')   +
                        KEYFLD(*FILE) MAPFLD((TQ03C TQ03 *CHAR 8))
             CALL       PGM(INT0061)
             CLOF       OPNID(SEHALF01)
             CLOF       OPNID(INTPPF)
             CLOF       OPNID(INTQPF)
             DLTOVR     FILE(*ALL)

             /* 執行完成,變更狀態 */
             CHGDTAARA  DTAARA(SCDA01 (2   1)) VALUE('0')
             SNDBRKMSG  MSG('月結處理完成！') TOMSGQ(&DEVD)
             GOTO CMDLBL(RLSOBJ)

ERR:        SNDBRKMSG  MSG('檔案仍被其它人使用中，無法執行月結處理!  +
                       ') TOMSGQ(&DEVD)
RLSOBJ:      /* DLCOBJ */
             CHGVAR     VAR(&LCK) VALUE(' ')
             CALL       PGM(INT006C2) PARM(&LCK &IN96)

             ENDPGM
