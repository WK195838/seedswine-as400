     /**************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250  */
     /*                                                            */
     /*    PROGRAM ID   : VOU002C1                                 */
     /*    PROGRAM NAME :採購傳票批次                          */
     /*    AUTHOR       : A1492 TERRY                              */
     /*    CREATE DATE  : 2009.02.24                               */
     /*    UPDATE DATE  :                                          */
     /*    SYSTEM       : 進銷存收TO總帳                       */
     /**************************************************************/
             PGM

             DCL        VAR(&D01)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&MBR)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&F01)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&F02)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)

             CHGVAR     VAR(&D01)   VALUE(%SST(*LDA 625 1))
             CHGVAR     VAR(&F01)   VALUE(%SST(*LDA 601 8))
             CHGVAR     VAR(&F02)   VALUE(%SST(*LDA 609 8))
             CHGVAR     VAR(&MBR)   VALUE('M' *CAT %SST(&F02 1 6))

             RMVLIBLE   LIB(QTEMP)
             MONMSG   MSGID(CPF2104)
             ADDLIBLE LIB(QTEMP) POSITION(*LAST)

     /**** WFJR10PF ****/

             CHKOBJ     OBJ(*LIBL/WFJR10PF) OBJTYPE(*FILE) MBR(&MBR)
             MONMSG     MSGID(CPF9815) EXEC(ADDPFM FILE(WFJR10PF) +
                          MBR(&MBR))
             OVRDBF     FILE(WFJR1022) TOFILE(*LIBL/WFJR10PF) MBR(&MBR)

             CPYF       FROMFILE(WFJR10PF) TOFILE(QTEMP/WFJR10PF) +
                          CRTFILE(*YES)
             MONMSG   MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/WFJR10PF))
             OVRDBF   FILE(WFJR10PF) TOFILE(QTEMP/WFJR10PF) SHARE(*YES)

     /**** WFJR10LF ****/
             CRTDUPOBJ  OBJ(WFJR10LF) FROMLIB(SCFLIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPD2104 CPF2130)
             OVRDBF   FILE(WFJR10LF) TOFILE(QTEMP/WFJR10LF) SHARE(*YES)

     /**** WFJR13PF ****/
             CHKOBJ     OBJ(*LIBL/WFJR13PF) OBJTYPE(*FILE) MBR(&MBR)
             MONMSG     MSGID(CPF9815) EXEC(ADDPFM FILE(WFJR13PF) +
                          MBR(&MBR))
  /*         CLRPFM     FILE(*LIBL/WFJR13PF) MBR(&MBR)    */
             OVRDBF     FILE(WFJR1322) TOFILE(*LIBL/WFJR13PF) MBR(&MBR)

           CPYF       FROMFILE(WFJR13PF) TOFILE(QTEMP/WFJR13PF) +
                        CRTFILE(*YES)
           MONMSG   MSGID(CPF2817) EXEC(CLRPFM FILE(QTEMP/WFJR13PF))
           OVRDBF   FILE(WFJR13PF) TOFILE(QTEMP/WFJR13PF) SHARE(*YES)

   /* 驗收單明細檔*/
             OVRDBF     FILE(SCWK04) TOFILE(POCMPF) SHARE(*YES)
             OPNQRYF    FILE((POCMPF) (POCLPF)) FORMAT(SCWK04) +
                          QRYSLT('CL01L *EQ "' *CAT &D01 *CAT '" & +
                          CL07 *EQ %RANGE(' *CAT &F01 *CAT ' ' *CAT +
                          &F02 *CAT ') & CM15="P"  & ( CL06="C1" *OR +
                          CL06="C2" ) ')  KEYFLD(CL01 CL02 CM01) +
                          JFLD((CM01 CL05)) MAPFLD((CL01L           +
                          '%SST(CL01 1 1)' *CHAR 1))

   /*產生暫存檔及傳票*/
             CALL       VOU0022

             CLOF       OPNID(POCMPF)

    /*把本次執行結果 COPY 到月份 MEMBER   */
             CPYF       FROMFILE(QTEMP/WFJR13PF) TOFILE(WFJR1322) +
                          TOMBR(&MBR) MBROPT(*ADD) FMTOPT(*NOCHK)
   /*報表*/
             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)

  /*         OVRPRTF    FILE(VOU0022P) PAGESIZE(*N 198) CPI(15) +
                          USRDTA('採購成本')                    */
             OVRPRTF    FILE(VOU0022P) PAGESIZE(*N 132)         +
                          USRDTA('採購成本')
             CALL       VOU0023
             CHGVAR     VAR(&P6601I) VALUE('VOU0022P')
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)

             DLTOVR     FILE(*ALL)

             ENDPGM
