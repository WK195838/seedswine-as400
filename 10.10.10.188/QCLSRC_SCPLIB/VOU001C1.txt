     /**************************************************************/
     /*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250  */
     /*                                                            */
     /*    PROGRAM ID   : VOU001C1                                 */
     /*    PROGRAM NAME : 銷售傳票批次                           */
     /*    AUTHOR       : A1492 TERRY                              */
     /*    CREATE DATE  : 2009.01.17                               */
     /*    UPDATE DATE  :                                          */
     /*    SYSTEM       : 進銷存收TO總帳                       */
     /**************************************************************/
             PGM

             DCL        VAR(&D01)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&MBR)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&F01)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&F02)   TYPE(*CHAR) LEN(8)
             DCL        VAR(&$EVR)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&$CPY)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&$PRTID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&P6601I) TYPE(*CHAR) LEN(10)
             DCL        VAR(&P6602I) TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&P6603I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6604I) TYPE(*DEC)  LEN(2 0)

             CHGVAR     VAR(&D01)   VALUE(%SST(*LDA 625 1))
             CHGVAR     VAR(&F01)   VALUE(%SST(*LDA 601 8))
             CHGVAR     VAR(&F02)   VALUE(%SST(*LDA 609 8))
             CHGVAR     VAR(&MBR)   VALUE('M' *CAT %SST(&F02 1 6))
   /*        RTVDTAARA  DTAARA(*LDA (625 1)) RTNVAR(&D01)    */

             RMVLIBLE   LIB(QTEMP)
             MONMSG   MSGID(CPF2104)
             ADDLIBLE LIB(QTEMP) POSITION(*LAST)

     /**** WFJR10PF ****/
             CHKOBJ     OBJ(*LIBL/WFJR10PF) OBJTYPE(*FILE) MBR(&MBR)
             MONMSG     MSGID(CPF9815) EXEC(ADDPFM FILE(WFJR10PF) +
                          MBR(&MBR))
             OVRDBF     FILE(WFJR1012) TOFILE(*LIBL/WFJR10PF) MBR(&MBR)

             CPYF       FROMFILE(WFJR10PF) TOFILE(QTEMP/WFJR10PF) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM +
                          FILE(QTEMP/WFJR10PF))
             OVRDBF     FILE(WFJR10PF) TOFILE(QTEMP/WFJR10PF) +
                          SHARE(*YES)

     /**** WFJR10LF ****/
             CRTDUPOBJ  OBJ(WFJR10LF) FROMLIB(SCFLIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPD2104 CPF2130)
             OVRDBF   FILE(WFJR10LF) TOFILE(QTEMP/WFJR10LF) SHARE(*YES)

     /**** WFJR11PF ****/
             CHKOBJ     OBJ(*LIBL/WFJR11PF) OBJTYPE(*FILE) MBR(&MBR)
             MONMSG     MSGID(CPF9815) EXEC(ADDPFM FILE(WFJR11PF) +
                          MBR(&MBR))
             OVRDBF     FILE(WFJR1112) TOFILE(*LIBL/WFJR11PF) MBR(&MBR)

             CPYF       FROMFILE(WFJR11PF) TOFILE(QTEMP/WFJR11PF) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM +
                          FILE(QTEMP/WFJR11PF))
             OVRDBF     FILE(WFJR11PF) TOFILE(QTEMP/WFJR11PF) +
                          SHARE(*YES)

     /**** WFJR12PF ****/
             CHKOBJ     OBJ(*LIBL/WFJR12PF) OBJTYPE(*FILE) MBR(&MBR)
             MONMSG     MSGID(CPF9815) EXEC(ADDPFM FILE(WFJR12PF) +
                          MBR(&MBR))
             OVRDBF     FILE(WFJR1212) TOFILE(*LIBL/WFJR12PF) MBR(&MBR)

             CPYF       FROMFILE(WFJR12PF) TOFILE(QTEMP/WFJR12PF) +
                          CRTFILE(*YES)
             MONMSG     MSGID(CPF2817) EXEC(CLRPFM +
                          FILE(QTEMP/WFJR12PF))
             OVRDBF     FILE(WFJR12PF) TOFILE(QTEMP/WFJR12PF) +
                          SHARE(*YES)

     /*家樂福銷售資料檔*/
             OVRDBF     FILE(SEHAPF) SHARE(*YES)
             OPNQRYF    FILE((SEHAPF)) QRYSLT('HA01L *EQ  "' *CAT +
                          &D01 *CAT '" & HA03 *EQ %RANGE(' *CAT +
        &F01 *CAT ' ' *CAT &F02 *CAT ') *AND    HA10="P"        ') +
                          KEYFLD(*FILE) MAPFLD((HA01L '%SST(HA01 1 +
                          1)' *CHAR 1))

     /*產生暫存檔及傳票*/
             CALL       PGM(VOU0012)

             CLOF       OPNID(SEHAPF)

    /*把本次執行結果 COPY 到 月份 MEMBER   */
             CPYF       FROMFILE(QTEMP/WFJR11PF) TOFILE(WFJR1112) +
                          TOMBR(&MBR) MBROPT(*ADD) FMTOPT(*NOCHK)
             CPYF       FROMFILE(QTEMP/WFJR12PF) TOFILE(WFJR1212) +
                          TOMBR(&MBR) MBROPT(*ADD) FMTOPT(*NOCHK)
   /*銷貨收入明細報表*/
             RTVDTAARA  DTAARA(*LDA (162 1)) RTNVAR(&$EVR)
             RTVDTAARA  DTAARA(*LDA (125 3)) RTNVAR(&$CPY)
             RTVDTAARA  DTAARA(*LDA (598 2)) RTNVAR(&$PRTID)

   /*報表*/
   /*        OVRPRTF    FILE(VOU0011P) PAGESIZE(*N 198) CPI(15) +
                          USRDTA('收入明細')         */
             OVRPRTF    FILE(VOU0011P) PAGESIZE(*N 132) +
                          USRDTA('收入明細')
             CALL       VOU0013
             CHGVAR     VAR(&P6601I) VALUE('VOU0011P')
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)

             OVRPRTF    FILE(VOU0012P) PAGESIZE(*N 198) CPI(15) +
                          USRDTA('成本彙總')
             CALL       VOU0014
             CHGVAR     VAR(&P6601I) VALUE('VOU0012P')
             CHGVAR     VAR(&P6602I) VALUE(&$EVR)
             CHGVAR     VAR(&P6603I) VALUE(&$CPY)
             CHGVAR     VAR(&P6604I) VALUE(&$PRTID)
             CALL       PGM(P66) PARM(&P6601I &P6602I &P6603I &P6604I)
             DLTOVR     FILE(*ALL)

             ENDPGM
