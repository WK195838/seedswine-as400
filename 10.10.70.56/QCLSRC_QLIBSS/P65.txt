             PGM PARM(&P6501I &P6502I &P6511O &P6512O)
             DCL        VAR(&P6501I) TYPE(*CHAR) LEN(80)
             DCL        VAR(&TOTLEN) TYPE(*DEC) LEN(3 0) VALUE(80)
             DCL        VAR(&P6502I) TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&P6511O) TYPE(*CHAR) LEN(80)
             DCL        VAR(&P6512O) TYPE(*CHAR) LEN(80)
             DCL        VAR(&POS) TYPE(*DEC) LEN(3 0) VALUE(1)
             DCL        VAR(&POS2) TYPE(*DEC) LEN(3 0) VALUE(1)
             DCL        VAR(&LEN1) TYPE(*DEC) LEN(3 0) VALUE(0)
             DCL        VAR(&LEN2) TYPE(*DEC) LEN(3 0) VALUE(0)
             DCL        VAR(&DBCS) TYPE(*DEC) LEN(1 0) VALUE(0)
             DCL        VAR(&BYTE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SISO) TYPE(*CHAR) LEN(2) VALUE('')
             DCL        VAR(&SI) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SO) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FLAG) TYPE(*CHAR) LEN(8) VALUE('GIANT/PT')
     /***********************************************************/
     /*      SEP. 23, 1989;      BY  HENRY SHAO.                */
     /*   &POS   : POSITION ON BOTH IN-STRING   &P6501I,    1   */
     /*                             OUT-STRING1 &P6511O.        */
     /*   &POS2  : POSITION ON      OUT-STRING2 &P6512O.    1   */
     /*   &LEN1  : BYTES PUT ON     OUT-STRING1 &P6511O.    0   */
     /*   &LEN2  : BYTES PUT ON     OUT-STRING2 &P6512O.    0   */
     /*   &BYTE  : CONTENTS OF THE BYTE TO BE PROCESSED.        */
     /*   &DBCS  : INDECATION OF LAST BYTE -                0   */
     /*            0 = SBCS OR SO                               */
     /*            1 = SI   OR 2ND BYTE OF DBCS                 */
     /*            2 = 1ST BYTE OF DBCS.                        */
     /***********************************************************/
 /**/
             CALL       PGM(R@S005) PARM(&FLAG)
 /**/
 CHECK:      IF         COND(&P6502I > &TOTLEN - 1) THEN(DO)
                        GOTO EOJ
                        ENDDO
             IF         COND(&P6502I < 2) THEN(DO)
                        GOTO EOJ
                        ENDDO
 START:      CHGVAR     VAR(&SI) VALUE(%SST(&SISO 1 1))
             CHGVAR     VAR(&SO) VALUE(%SST(&SISO 2 1))
             CHGVAR     VAR(&P6511O) VALUE(' ')
             CHGVAR     VAR(&P6512O) VALUE(' ')
 /**/
 LOOP1:
             CHGVAR     VAR(&BYTE) VALUE(%SST(&P6501I &POS 1))
             IF         COND(&POS = &P6502I) THEN(DO)
                        GOTO LAST
                        ENDDO
             CHGVAR     VAR(%SST(&P6511O &POS 1)) VALUE(&BYTE)
             CHGVAR     VAR(&POS) VALUE(&POS + 1)
             CHGVAR     VAR(&LEN1) VALUE(&LEN1 + 1)
             IF         COND(&BYTE = &SI) THEN(DO)
                        CHGVAR  VAR(&DBCS) VALUE(1)
                        ENDDO
             ELSE IF    COND(&BYTE = &SO) THEN(DO)
                        CHGVAR  VAR(&DBCS) VALUE(0)
                        ENDDO
             ELSE IF    COND(&DBCS = 1) THEN(DO)
                        CHGVAR  VAR(&DBCS) VALUE(2)
                        ENDDO
             ELSE IF    COND(&DBCS = 2) THEN(DO)
                        CHGVAR  VAR(&DBCS) VALUE(1)
                        ENDDO
         /*  ELSE       CHGVAR  VAR(&DBCS) VALUE(0) */

                         /*  &DBCS HAS BEEN SET FOR NEXT &BYTE    */
             GOTO LOOP1
 /**/
 LAST:
             IF         COND(&DBCS = 2) THEN(DO)
                        CHGVAR VAR(%SST(&P6511O &POS 1)) VALUE(&BYTE)
                        CHGVAR VAR(&LEN1) VALUE(&LEN1 + 1)
                        CHGVAR VAR(&POS) VALUE(&POS + 1)
                        CHGVAR VAR(%SST(&P6511O &POS 1)) VALUE(&SO)
                        CHGVAR VAR(&LEN1) VALUE(&LEN1 + 1)
                        CHGVAR VAR(%SST(&P6512O &POS2 1)) VALUE(&SI)
                        CHGVAR VAR(&LEN2) VALUE(&LEN2 + 1)
                        CHGVAR VAR(&POS2) VALUE(&POS2 + 1)
                        GOTO SECOND1
                        ENDDO

             IF         COND(&DBCS = 1) THEN(DO)
                        CHGVAR VAR(%SST(&P6511O &POS 1)) VALUE(&SO)
                        CHGVAR VAR(&LEN1) VALUE(&LEN1 + 1)
                        CHGVAR VAR(%SST(&P6512O &POS2 1)) VALUE(&SI)
                        CHGVAR VAR(&LEN2) VALUE(&LEN2 + 1)
                        CHGVAR VAR(&POS2) VALUE(&POS2 + 1)
                        GOTO SECOND1
                        ENDDO

             IF         COND(&DBCS = 0) THEN(DO)
               IF       COND(&BYTE = &SI) THEN(DO)
                        GOTO SECOND1
                        ENDDO
               ELSE     CMD(DO)
                        CHGVAR VAR(%SST(&P6511O &POS 1)) VALUE(&BYTE)
                        CHGVAR VAR(&LEN1) VALUE(&LEN1 + 1)
                        CHGVAR VAR(&POS) VALUE(&POS + 1)
                        ENDDO
                        ENDDO

 SECOND1:
             IF         COND(&POS > &TOTLEN) THEN(DO)
                        GOTO EOJ
                        ENDDO
             CHGVAR     VAR(&BYTE) VALUE(%SST(&P6501I &POS 1))
             CHGVAR     VAR(%SST(&P6512O &POS2 1)) VALUE(&BYTE)
             CHGVAR     VAR(&POS) VALUE(&POS + 1)
             CHGVAR     VAR(&POS2) VALUE(&POS2 + 1)
             CHGVAR     VAR(&LEN2) VALUE(&LEN2 + 1)
             GOTO SECOND1

 EOJ:
             ENDPGM
