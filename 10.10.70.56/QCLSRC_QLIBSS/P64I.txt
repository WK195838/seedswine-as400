     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : P64I                                       *
     H*    PROGRAM NAME :印表機選擇                                *
     H*    AUTHOR       : OMF                                        *
     H*    CREATE DATE  : 920923                                     *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       : PANTECH                                    *
     F****************************************************************
     FP64F    IF  E           K        DISK
     FP64ID   CF  E                    WORKSTN
     E*----------------------------------------------------------------
     E                    @SL         8  1               資料選擇
     E                    @CD         8  2               代碼
     E                    @NM         8 12               說明
     E*----------------------------------------------------------------
     I*OLD STARE KEY (FOR COMPARE START KEY CHANGE)
     I            DS
     I                                        1   2 @K64O
     I*NEW STARE KEY (FOR COMPARE START KEY CHANGE)
     I            DS
     I                                        1   2 @K64N
     I                                        1   2 D6401
     I*SUBFILE TOP KEY
     I            DS
     I                                        1   2 @K64T
     I                                        1   2 K6401T
     I*SUBFILE BOTTOM KEY
     I            DS
     I                                        1   2 @K64B
     I                                        1   2 K6401B
     I*FILE KEY (FOR COMPARE KEY TOP)
     I            DS
     I                                        1   2 @K64
     I                                        1   2 P6401
     I*DEFINE SCREEN FIELD
     I            DS
@    I                                        1   8 @SL
     I                                        1   1 DSEL1
     I                                        2   2 DSEL2
     I                                        3   3 DSEL3
     I                                        4   4 DSEL4
     I                                        5   5 DSEL5
     I                                        6   6 DSEL6
     I                                        7   7 DSEL7
     I                                        8   8 DSEL8
     I            DS
@    I                                        1  16 @CD
     I                                        1   2 DCDE1
     I                                        3   4 DCDE2
     I                                        5   6 DCDE3
     I                                        7   8 DCDE4
     I                                        9  10 DCDE5
     I                                       11  12 DCDE6
     I                                       13  14 DCDE7
     I                                       15  16 DCDE8
     I            DS
@    I                                        1  96 @NM
     I                                        1  12 DNAM1
     I                                       13  24 DNAM2
     I                                       25  36 DNAM3
     I                                       37  48 DNAM4
     I                                       49  60 DNAM5
     I                                       61  72 DNAM6
     I                                       73  84 DNAM7
     I                                       85  96 DNAM8
     I*==============================================================*
     I*                    DEFN
     I*==============================================================*
     I              '** EOF **   '        C         @EMSG
     C*==============================================================*
     C*                    *ENTRY PLIST
     C*==============================================================*
     C           *ENTRY    PLIST
     C           D6401     PARM           P64II1  2         印表機碼
     C*==============================================================*
     C*                    KEY LIST
     C*==============================================================*
     C           KEY64T    KLIST
     C                     KFLD           K6401T           *TOP
     C           KEY64B    KLIST
     C                     KFLD           K6401B           *BOTTOM
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR R0100                      *INIT
     C*
     C           @RTNC     DOWEQ*BLANK
     C                     EXSR R1000                      *SCR PROCESS
     C                     END
     C*
     C                     SETON                     LR
     C*==============================================================*
     C*          R0100 ....INITIAL VALUE
     C*==============================================================*
     C           R0100     BEGSR
     C*
     C* SET INIT VALUE *---------------------------------------------*
     C                     MOVEL*BLANK    @RTNC   2        *OUT PARM
@   C                     Z-ADD8         @PRCD   20       *PAGE RCD
     C                     Z-ADD60        @INFR   20       *INDICATOR FROM
     C*--------------------------------------------------------------*
     C                     EXSR R1A00                      .SFL INIT
     C*
     C                     ENDSR
     C*==============================================================*
     C*          R1000 ....SCREEN  PROCESS
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C                     EXFMTDSPD1
     C*
     C           *IN03     IFEQ '1'                        *END
     C                     MOVEL'03'      @RTNC
     C                     GOTO E1000
     C                     END
     C*
     C           *IN12     IFEQ '1'                        *PREVIOUS
     C                     MOVEL'12'      @RTNC
     C                     GOTO E1000
     C                     END
     C*
     C           *IN25     IFEQ '1'                        *NEXT PAGE
     C           KEY64B    SETGTP64R
     C                     EXSR R1N00
     C                     GOTO E1000
     C                     END
     C*
     C           *IN26     IFEQ '1'                        *PREV PAGE
     C           KEY64T    SETLLP64R
     C                     EXSR R1P00
     C                     GOTO E1000
     C                     END
     C*
     C           @K64O     IFNE @K64N                      *START KEY
     C                     EXSR R1A00                      *SFL INIT
     C                     GOTO E1000
     C                     END
     C*
     C                     EXSR R1C00                      *OPTION
     C*
     C           E1000     ENDSR
     C*=============================================================*
     C*          R1A00 ....SUBFILE INIT PREPARE
     C*=============================================================*
     C           R1A00     BEGSR
     C*
     C                     MOVEL@K64N     @K64O            *KEY SAVE
     C                     MOVEL@K64N     @K64T            *KEY TOP
     C                     MOVEL@K64B     @K64B            *KEY BOTTOM
     C*
     C           KEY64T    SETLLP64R
     C                     EXSR R1N00                      *ADD SFL DTA
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1C00 ....挑選資料
     C*=============================================================*
     C           R1C00     BEGSR
     C*
     C                     Z-ADD1         @R
     C           @R        DOWLE@PRCD
     C*
     C           @SL,@R    IFNE *BLANK
     C* <<SET OUTPUT PARM>>++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVEL@CD,@R    P64II1
     C*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     C                     MOVEL'00'      @RTNC
     C                     Z-ADD@PRCD     @R
     C                     END
     C*
     C                     ADD  1         @R
     C                     END
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1N00 .... PREPARE NEXT PAGE
     C*=============================================================*
     C           R1N00     BEGSR
     C*
     C                     READ P64R                     46
     C           *IN46     IFEQ '0'
     C                     EXSR R1Z03                      *KEY TOP
     C                     ELSE                            *LAST PAGE
     C                     GOTO E1N00
     C                     END
     C*
     C* CLEAR SUBFILE
     C                     MOVEA*ALL'0'   *IN,60
     C                     MOVEL*BLANK    @SL
     C                     MOVEL*BLANK    @CD
     C                     MOVEL*BLANK    @NM
     C* PREPARE SUBFILE
     C                     Z-ADD0         @R      20
     C           @R        DOWLT@PRCD
     C           *IN46     ANDEQ'0'
     C*
     C                     ADD  1         @R
     C                     EXSR R1Z01                      *ADD SFL
     C                     EXSR R1Z02                      *KEY BOTTOM
     C*
     C                     READ P64R                     46
     C                     END
     C*
     C           @R        IFLT @PRCD
     C                     ADD  1         @R
     C                     MOVEL@EMSG     @NM,@R
     C                     END
     C*
     C           E1N00     ENDSR
     C*=============================================================*
     C*          R1P00 .... PREPARE PREVIOUS  PAGE
     C*=============================================================*
     C           R1P00     BEGSR
     C*
     C                     READPP64R                     46
     C           *IN46     IFEQ '0'
     C           @K64      ANDGE@K64N
     C                     EXSR R1Z02                      *KEY BOTTOM
     C                     ELSE                            *FIRST PAGE
     C                     GOTO E1P00
     C                     END
     C*
     C           @PRCD     ADD  1         @R
     C           @R        DOWGT1
     C           *IN46     ANDEQ'0'
     C           @K64      ANDGE@K64N
     C*
     C                     SUB  1         @R
     C                     EXSR R1Z01                      *ADD SFL
     C                     EXSR R1Z03                      *KEY TOP
     C*
     C                     READPP64R                     46
     C                     END
     C*
     C           @R        IFGT 1
     C                     MOVEL@K64T     @K64B
     C           KEY64T    SETLLP64R
     C                     EXSR R1N00
     C                     END
     C*
     C           E1P00     ENDSR
     C*=============================================================*
     C*          R1Z01 .... SUBFILE DATA SET
     C*=============================================================*
     C           R1Z01     BEGSR
     C*
     C           @R        ADD  @INFR     @I      20
     C                     MOVEL'1'       *IN,@I
     C                     MOVEL*BLANK    @SL,@R
     C                     MOVELP6401     @CD,@R
     C                     MOVELP6404     @NM,@R
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1Z02 .... SET KEY BOTTOM
     C*=============================================================*
     C           R1Z02     BEGSR
     C*
     C                     MOVELP6401     K6401B
     C*
     C                     ENDSR
     C*=============================================================*
     C*          R1Z03 .... SET KEY TOP
     C*=============================================================*
     C           R1Z03     BEGSR
     C*
     C                     MOVELP6401     K6401T
     C*
     C                     ENDSR
