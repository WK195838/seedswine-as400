     **  1. AUTHOR      : SANDY_LAIW                                 *
     **  2. CREATE DATE : 04/10/1990                                 *
     **  3. FUNCTION    : TRANSLATE REPORT TO PRTF                   *
     **--------------------------------------------------------------*
     F*--------------------------------------------------------------*
     F*  FILE  SPECIFICATIONS                                        *
     F*--------------------------------------------------------------*
     FXIGC1   IF  E           K        DISK
     F            XIGC1                             KRENAMEXIGC1R
     FXK09P   O   E           K        DISK
     E*--------------------------------------------------------------*
     E*  ARRAY & TABLE                                               *
     E*--------------------------------------------------------------*
     E                    ARY1      198  1
     E                    ARY2    1   4 70
     E                    ARY3       36  1
     E                    ARY4    1   1  1
     E                    ARY5    1   4 14
     I*--------------------------------------------------------------*
     I*  DATA  STRUCTURE                                             *
     I*--------------------------------------------------------------*
     IDS1         DS
     I                                        6   6 CARD
     I                                        9  10 INDCOR
     I                                       17  17 REC
     I                                       19  28 NAME
     I                                       30  34 LEN
     I                                       35  35 TYPE
     I                                       36  37 DEC
     I                                       38  38 USAGE
     I                                       42  44 POS
     I                                       45  80 FUN
     I*--------------------------------------------------------------*
     I            DS
     I                                        1   4 RNAME1
     I                                        5   5 RNAME2
     I                                        1   5 RNAME
     I*--------------------------------------------------------------*
     I            DS
     I                                        1   1 $DEC1
     I                                        2   2 $DEC2
     I                                        1   20$DEC
     I*--------------------------------------------------------------*
     I            DS
     I                                        1   1 $LEN1
     I                                        2   2 $LEN2
     I                                        1   20$LEN
     I*--------------------------------------------------------------*
     I            DS
     I                                        2   3 $POS1
     I                                        3   3 $POS2
     I                                        1   30$POS
     I*--------------------------------------------------------------*
     I            DS
     I                                        1   7 SKIPB1
     I                                        8   80SKIPB2
     I                                        9   9 SKIPB3
     I                                        1   9 SKIPB
     I*--------------------------------------------------------------*
     I            DS
     I                                        1   7 SPCEA1
     I                                        8   80SPCEA2
     I                                        9   9 SPCEA3
     I                                        1   9 SPACEA
     I*--------------------------------------------------------------*
     IOEOF        DS
     I                                        1   1 OE
     I                                        2   2 OF
     C*--------------------------------------------------------------*
     C*  MAIN  SECTION                                               *
     C*--------------------------------------------------------------*
     C                     EXSR SR000
     C*
     C                     READ XIGC1R                   46
     C           *IN46     DOWEQ'0'
     C                     ADD  1         CTLINE  30
     C           SRCDTA    IFEQ *BLANK
     C                     ADD  1         CTBLNK  20
     C                     ELSE
     C                     EXSR SR010
     C                     END
     C                     READ XIGC1R                   46
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
     C*--------------------------------------------------------------*
     C*  SR000   MOVE COMMON SECTION TO XK09P FILE                   *
     C*--------------------------------------------------------------*
     C           SR000     BEGSR
     C*
     C           1         DO   3         I       20
     C                     MOVELARY2,I    XK0901
     C* ERR                MOVEAARY2,I    XK0901
     C                     WRITEXK09PR
     C                     END
     C*
oeof C                     MOVE ''      OEOF    2
MUST C                     MOVE 'FIRST'   $SKIPB  5
MUST C                     MOVE 'FIRST'   $CHFST  5
MUST C                     MOVE 'FIRST'   $@FIST  5
MUST C                     MOVE 'FIRST'   $XFIST  5
MUST C                     MOVE 'FIRST'   $9FIST  5
MUST C                     Z-ADD0         $LEN    20
MUST C                     Z-ADD0         $DEC    20
     C*
     C           ED000     ENDSR
     C*--------------------------------------------------------------*
     C*  SR010   CHECK CHARACTER = ?                                 *
     C*--------------------------------------------------------------*
     C           SR010     BEGSR
     C*
     C                     MOVEASRCDTA    ARY1
     C           1         DO   198       Y       30
     C                     MOVEAARY1,Y    $CHAR   1
     C*
     C           Y         CASEQ1         SR100
     C*
     C           $CHAR     CASEQ' '       SR200
     C*
     C           $CHAR     CASEQ'9'       SR300
     C*
     C           $CHAR     CASEQ'/'       SR350
     C           $CHAR     CASEQ','       SR400
     C           $CHAR     CASEQ'.'       SR500
     C*
     C           $CHAR     CASEQ'X'       SR600
     C           $CHAR     CASEQ'x'       SR600
     C*
     C           $CHAR     CASEQ'_'       SR700
     C           $CHAR     CASEQ'-'       SR710
     C*
     C           $CHAR     CASEQ'@'       SR800
     C*
     C           $CHAR     CASEQ'D'       SR900
     C           $CHAR     CASEQ'd'       SR900
     C*
     C           $CHAR     CASEQ'T'       SR1000
     C           $CHAR     CASEQ't'       SR1000
     C*
     C           $CHAR     CASEQ'P'       SR1100
     C           $CHAR     CASEQ'p'       SR1100
     C*
     C           $CHAR     CASEQOE        SR1200
     C           $CHAR     CASEQOF        SR1200
     C*
     C           $CHAR     CASEQARY4,1    SR1300
     C*
     C                     CAS            SR1400
     C                     END
0530 C           Y         IFEQ 198
0530 C           $CHAR     ANDNE*BLANK
0530 C                     EXSR SR200
0530 C                     END
     C*
     C                     END
     C*
     C           ED010     ENDSR
     C*--------------------------------------------------------------*
     C*  SR100   IF  Y = 1                                           *
     C*--------------------------------------------------------------*
     C           SR100     BEGSR
     C*
     C                     MOVE 'A'       CARD
     C*--------------------------------------------------------------*
     C           $CHAR     IFEQ *BLANK
     C                     EXSR SR110
     C                     GOTO ED100
     C                     END
     C*--------------------------------------------------------------*
     C           $CHAR     IFEQ 'H'
     C           $CHAR     OREQ 'h'
     C*
     C           $SKIPB    IFEQ 'FIRST'
     C                     EXSR SR120
     C                     MOVE *BLANK    $SKIPB
     C                     ELSE
     C                     EXSR SR110
     C*
     C                     ADD  1         CTHEAD  10
     C                     MOVEL'H'       $HNAME  2
     C                     MOVE CTHEAD    $HNAME
     C                     MOVEL$HNAME    NAME
     C                     EXSR SR130
     C                     END
     C*
     C                     GOTO ED100
     C                     END
     C*--------------------------------------------------------------*
     C           $CHAR     IFEQ 'D'
     C           $CHAR     OREQ 'd'
     C*
     C                     EXSR SR110
     C*
     C                     ADD  1         CTDETL  10
     C                     MOVEL'D'       $DNAME  2
     C                     MOVE CTDETL    $DNAME
     C                     MOVEL$DNAME    NAME
     C                     EXSR SR130
     C*
     C                     GOTO ED100
     C                     END
     C*--------------------------------------------------------------*
     C           $CHAR     IFEQ 'T'
     C           $CHAR     OREQ 't'
     C*
     C                     EXSR SR110
     C*
     C                     ADD  1         CTTAIL  10
     C                     MOVEL'T'       $TNAME  2
     C                     MOVE CTTAIL    $TNAME
     C                     MOVEL$TNAME    NAME
     C                     EXSR SR130
     C*
     C                     END
     C*
     C           ED100     ENDSR
     C*--------------------------------------------------------------*
     C*  SR110   WRITE SPACEA(?) & ARY4,3                            *
     C*--------------------------------------------------------------*
     C           SR110     BEGSR
     C*
     C                     MOVE 'SPACEA(' SPCEA1
     C                     ADD  1         CTBLNK
     C                     MOVE CTBLNK    SPCEA2
     C                     MOVE ')'       SPCEA3
     C                     MOVELSPACEA    FUN
     C                     MOVELDS1       XK0901
     C                     WRITEXK09PR
     C                     MOVE *BLANK    DS1    80
     C*
     C                     Z-ADD0         CTBLNK
     C*--------------------------------------------------------------*
     C                     MOVELARY2,3    XK0901
     C                     WRITEXK09PR
     C                     MOVE *BLANK    XK0901
     C*
     C           ED110     ENDSR
     C*--------------------------------------------------------------*
     C*  SR120   WRITE H1 & SKIPB(?) & ARY2,4                        *
     C*--------------------------------------------------------------*
     C           SR120     BEGSR
     C*
     C                     MOVE 'R'       REC
     C                     ADD  1         CTHEAD  10
     C                     MOVEL'H'       $HNAME  2
     C                     MOVE CTHEAD    $HNAME
     C                     MOVEL$HNAME    NAME
     C*
     C                     MOVE 'SKIPB('  SKIPB1
     C                     ADD  1         CTBLNK
     C                     MOVE CTBLNK    SKIPB2
     C                     MOVE ')'       SKIPB3
     C                     MOVELSKIPB     FUN
     C*
     C                     MOVELDS1       XK0901
     C                     WRITEXK09PR
     C                     MOVE *BLANK    DS1    80
     C*
     C                     Z-ADD0         CTBLNK
     C*
     C                     MOVELARY2,4    XK0901
     C                     WRITEXK09PR
     C                     MOVE *BLANK    XK0901
     C*
     C           ED120     ENDSR
     C*--------------------------------------------------------------*
     C*  SR130   WRITE REC-NAME(H/D/T)                               *
     C*--------------------------------------------------------------*
     C           SR130     BEGSR
     C*
     C                     MOVE 'A'       CARD
     C                     MOVE 'R'       REC
     C                     MOVELDS1       XK0901
     C                     WRITEXK09PR
     C                     MOVE *BLANK    DS1    80
     C*
     C           ED130     ENDSR
     C*--------------------------------------------------------------*
     C*  SR200   IF CHARACTER = *BLANK                               *
     C*--------------------------------------------------------------*
     C           SR200     BEGSR
     C*
     C*--------------------------------------------------------------*
     C           $DATE     IFEQ 'YES'
     C                     MOVELARY5,1    $FUN
     C                     EXSR SR210
     C                     MOVE 'FIRST'   $@FIST  5
     C                     MOVE *BLANK    $@CH
     C                     MOVE *BLANK    $DATE   3
     C                     EXSR SR220
     C                     END
     C*--------------------------------------------------------------*
     C           $TIME     IFEQ 'YES'
     C                     MOVEL'TIME'    $FUN
     C                     EXSR SR210
     C                     MOVE 'FIRST'   $@FIST  5
     C                     MOVE *BLANK    $@CH
     C                     MOVE *BLANK    $TIME   3
     C                     EXSR SR220
     C                     END
     C*--------------------------------------------------------------*
     C           $PAGE     IFEQ 'YES'
     C                     MOVEL'PAGNBR'  $FUN
     C                     EXSR SR210
     C                     MOVE 'FIRST'   $@FIST  5
     C                     MOVE *BLANK    $@CH
     C                     MOVE *BLANK    $PAGE   3
     C                     EXSR SR220
     C                     END
     C*--------------------------------------------------------------*
     C           $VAR      IFEQ 'YES'
     C                     EXSR SR210
     C                     MOVE 'FIRST'   $XFIST  5
     C                     MOVE 'FIRST'   $9FIST  5
     C                     MOVE *BLANK    $CH9    3
     C                     MOVE *BLANK    $SLASH  3
     C                     MOVE *BLANK    $COMMA  3
     C                     MOVE *BLANK    $DECIM
     C                     MOVE *BLANK    $VAR    3
     C                     EXSR SR220
     C                     END
     C*--------------------------------------------------------------*
     C           $CONST    IFEQ 'YES'
     C           $OE       IFEQ 'YES'
0614 C*          $OF       ANDNE'YES'
     C                     EXSR SR410
     C                     ELSE
     C                     ADD  1         Z       20
     C                     MOVELARY4,1    ARY3,Z
     C                     MOVEAARY3      $FUN
     C                     EXSR SR210
     C                     MOVEA*BLANK    ARY3
     C                     Z-ADD0         Z
MUST C                     Z-ADD0         CT1200
     C                     MOVE 'FIRST'   $CHFST
     C                     MOVE *BLANK    $OE     3
0614 C*                    MOVE *BLANK    $OF     3
     C                     MOVE *BLANK    $APOST  3
     C                     MOVE *BLANK    $CONST  3
     C                     EXSR SR220
     C                     END
     C                     END
     C*
     C           ED200     ENDSR
     C*--------------------------------------------------------------*
     C*  SR210   MOVE DATA TO DATA BASE FILE                         *
     C*--------------------------------------------------------------*
     C           SR210     BEGSR
     C*
     C                     MOVE 'A'       CARD
     C*
     C           $LEN      IFNE 0
     C                     ADD  1         CTVAR   20
     C                     MOVEL'#'       $NAME1  3
     C                     MOVE CTVAR     $NAME1
     C                     MOVEL$NAME1    NAME
     C                     END
     C*
     C           $LEN      IFEQ 0
     C                     MOVE *BLANK    LEN
     C                     ELSE
     C           $LEN      IFLE 9
     C                     MOVE $LEN2     LEN
     C                     ELSE
     C                     MOVE $LEN      LEN
     C                     END
     C                     END
     C*
     C                     MOVE $TYPE     TYPE
     C*
     C           $TYPE     IFEQ 'A'
     C           $TYPE     OREQ 'O'
     C           $LEN      OREQ 0
     C                     MOVE *BLANK    DEC
     C                     ELSE
     C           $DEC      IFLE 9
     C                     MOVE $DEC2     DEC
     C                     ELSE
     C                     MOVE $DEC      DEC
     C                     END
     C                     END
     C*
     C           $POS      IFEQ 0
     C                     MOVE *BLANK    POS
     C                     ELSE
     C           $POS      IFLE 9
     C                     MOVE $POS2     POS
     C                     ELSE
     C           $POS      IFLE 99
     C                     MOVE $POS1     POS
     C                     ELSE
     C                     MOVE $POS      POS
     C                     END
     C                     END
     C                     END
     C*
     C           $SLASH    IFEQ 'YES'
     C                     MOVELARY5,4    $FUN
     C                     GOTO TG210
     C                     END
     C*
     C           $COMMA    IFEQ 'YES'
     C                     MOVELARY5,2    $FUN
     C                     GOTO TG210
     C                     END
     C*
     C           $TYPE     IFNE 'A'
     C           $TYPE     ANDNE'O'
     C           $LEN      ANDNE0
     C                     MOVELARY5,3    $FUN
     C                     END
     C*
     C           TG210     TAG
     C*
     C                     MOVE $FUN      FUN
     C*
     C                     MOVE DS1       XK0901
     C                     WRITEXK09PR
     C                     MOVEL*BLANK    DS1
     C*
     C           ED210     ENDSR
     C*--------------------------------------------------------------*
     C*  SR220   INIT. FIELDS OF DATA                                *
     C*--------------------------------------------------------------*
     C           SR220     BEGSR
     C*
     C                     MOVE *BLANK    DS1
     C*
     C                     MOVE *BLANK    $NAME   6
     C                     Z-ADD0         $LEN    20
     C                     MOVE *BLANK    $TYPE   1
     C                     Z-ADD0         $DEC    20
     C*                    MOVE *BLANK    $USAGE  1
     C*                    Z-ADD0         $LINE   20
     C                     Z-ADD0         $POS    30
     C                     MOVE *BLANK    $FUN   36
     C*
     C           ED220     ENDSR
     C*--------------------------------------------------------------*
     C*  SR300   IF CHARACTER = '9'                                  *
     C*--------------------------------------------------------------*
     C           SR300     BEGSR
     C*
MUST C           $OE       IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED300
     C                     END
     C*
     C           $CONST    IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED300
     C                     END
     C*
     C           $9FIST    IFEQ 'FIRST'
     C                     MOVE 'YES'     $VAR
     C                     MOVE 'YES'     $CH9    3
     C                     Z-ADDY         $POS
     C                     MOVE *BLANK    $9FIST  5
     C                     ADD  1         $LEN
     C                     GOTO ED300
     C                     END
     C*
     C                     ADD  1         $LEN
     C           $DECIM    IFEQ 'YES'
     C                     ADD  1         $DEC
     C                     END
     C*
     C           ED300     ENDSR
     C*--------------------------------------------------------------*
     C*  SR350   IF CHARACTER = '/'                                  *
     C*--------------------------------------------------------------*
     C           SR350     BEGSR
     C*
MUST C           $OE       IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED350
     C                     END
     C*
     C           $CH9      IFEQ 'YES'
     C                     MOVE 'YES'     $SLASH  3
     C                     ELSE
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR410
     C                     END
     C*
     C           ED350     ENDSR
     C*--------------------------------------------------------------*
     C*  SR400   IF CHARACTER = ','                                  *
     C*--------------------------------------------------------------*
     C           SR400     BEGSR
     C*
MUST C           $OE       IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED400
     C                     END
     C*
     C           $CH9      IFEQ 'YES'
     C                     MOVE 'YES'     $COMMA  3
     C                     ELSE
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR410
     C                     END
     C*
     C           ED400     ENDSR
     C*--------------------------------------------------------------*
     C*  SR410   IF $CONST = 'YES'                                   *
     C*--------------------------------------------------------------*
     C           SR410     BEGSR
     C*
     C           $CHFST    IFEQ 'FIRST'
NO   C*                    Z-ADDX         $LINE
     C                     Z-ADDY         $POS
     C                     Z-ADD1         Z
     C                     MOVEAARY4,1    ARY3,Z
     C                     MOVE *BLANK    $CHFST
     C                     END
     C*
MUST C           $OE       IFEQ 'YES'
0614 C*          $OF       ANDNE'YES'
     C                     ADD  1         CT1200
     C                     END
     C*
     C           Z         IFEQ 33
0614 C           $OE       ANDEQ'YES'
     C           CT1200    IFEQ 1
     C                     ADD  1         Z
0614 C                     EXSR SR410C
0614 C                     GOTO ED410
     C                     ELSE
     C                     EXSR SR410A
ERR  C**                   GOTO ED410
     C                     END
     C                     END
     C*
     C           Z         IFEQ 34
0614 C           $OE       ANDEQ'YES'
0614 C           CT1200    IFEQ 1
0614 C                     ADD  1         Z
0614 C                     EXSR SR410C
0614 C                     GOTO ED410
0614 C                     ELSE
     C                     EXSR SR410B
ERR  C**                   GOTO ED410
     C                     END
     C                     END
     C*
0614 C           Z         IFEQ 35
0614 C           CT1200    ANDEQ1
     C                     ADD  1         Z
0614 C                     EXSR SR410C
0614 C                     GOTO ED410
0614 C                     END
     C*
     C                     ADD  1         Z
     C*
     C           Z         IFEQ 36
     C                     EXSR SR410C
     C                     GOTO ED410
     C                     END
     C*
     C           $APOST    IFEQ 'YES'
     C                     MOVEA$CHAR     ARY3,Z
     C                     ADD  1         Z
     C           Z         IFEQ 36
     C                     EXSR SR410C
     C                     ELSE
     C                     MOVEA$CHAR     ARY3,Z
MUST C                     MOVE *BLANK    $APOST
     C                     END
     C                     ELSE
     C                     MOVEA$CHAR     ARY3,Z
     C                     END
     C*
     C           ED410     ENDSR
     C*--------------------------------------------------------------*
     C*  SR410A  IF ARY3'S INDEX = 33  & CT1200 = 0                 *
     C*--------------------------------------------------------------*
     C           SR410A    BEGSR
     C*
     C           CT1200    DIV  2         $DIV    20
     C                     MVR            $REMER  20
     C*
     C           $REMER    IFEQ 0
     C                     ADD  1         Z
     C                     MOVELOF        ARY3,Z
     C                     ADD  1         Z
     C                     MOVEL'-'       ARY3,Z
     C                     MOVEAARY3      $FUN
     C                     EXSR SR210
     C                     EXSR SR220
     C                     MOVEA*BLANK    ARY3
     C                     Z-ADD1         Z
     C                     MOVELOE        ARY3,Z
MUST C                     Z-ADD0         CT1200
     C                     END
     C*
     C           ED410A    ENDSR
     C*--------------------------------------------------------------*
     C*  SR410B  IF ARY3'S INDEX = 34  & CT1200 = 0                 *
     C*--------------------------------------------------------------*
     C           SR410B    BEGSR
     C*
     C                     EXSR SR410A
     C*
     C           ED410B    ENDSR
     C*--------------------------------------------------------------*
     C*  SR410C  IF ARY3'S INDEX = 36                                *
     C*--------------------------------------------------------------*
     C           SR410C    BEGSR
     C*
     C                     MOVEL'-'       ARY3,Z
     C                     MOVEAARY3      $FUN
     C                     EXSR SR210
     C                     EXSR SR220
     C                     MOVEA*BLANK    ARY3
     C                     Z-ADD1         Z
     C           $APOST    IFEQ 'YES'
     C                     MOVEAARY4,1    ARY3,Z
MUST C                     MOVE *BLANK    $APOST
     C                     ELSE
     C                     MOVEL$CHAR     ARY3,Z
     C                     END
     C*
     C           ED410C    ENDSR
     C*--------------------------------------------------------------*
     C*  SR500   IF CHARACTER = '.'                                  *
     C*--------------------------------------------------------------*
     C           SR500     BEGSR
     C*
MUST C           $OE       IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED500
     C                     END
     C*
     C           $VAR      IFEQ 'YES'
     C                     MOVE 'YES'     $DECIM  3
     C                     Z-ADD0         $DEC
     C                     ELSE
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR410
     C                     END
     C*
     C           ED500     ENDSR
     C*--------------------------------------------------------------*
     C*  SR600   IF CHARACTER = 'X' or 'x'                           *
     C*--------------------------------------------------------------*
     C           SR600     BEGSR
     C*
MUST C           $OE       IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED600
     C                     END
     C*
     C           $CONST    IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED600
     C                     END
     C*
     C           $XFIST    IFEQ 'FIRST'
     C                     MOVE 'YES'     $VAR
     C                     ADD  1         $LEN
     C                     Z-ADDY         $POS
     C                     MOVE *BLANK    $XFIST  5
     C                     MOVE 'A'       $TYPE
     C                     GOTO ED600
     C                     END
     C*
     C                     ADD  1         $LEN
     C*
     C           ED600     ENDSR
     C*--------------------------------------------------------------*
     C*  SR700   IF CHARACTER = '_'                                  *
     C*--------------------------------------------------------------*
     C           SR700     BEGSR
     C*
     C*  IF CANCEL THE '_' BETWEEN oeof,WILL HAPPEN TRANSLATE-ERR
0727 C           $OE       IFEQ 'YES'
0727 C                     EXSR SR410
0727 C                     GOTO ED700
0727 C                     END
     C*
     C                     MOVE 'YES'     $CONST
     C                     ADD  1         Z
     C           Z         IFEQ 36
     C                     EXSR SR410C
     C                     END
     C                     MOVEL' '       ARY3,Z
     C*
     C           ED700     ENDSR
     C*--------------------------------------------------------------*
     C*  SR710   IF CHARACTER = '-'                                  *
     C*--------------------------------------------------------------*
     C           SR710     BEGSR
     C*
MUST C           $OE       IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED710
     C                     END
     C*
     C           $VAR      IFNE 'YES'
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR410
     C                     END
     C*
     C           ED710     ENDSR
     C*--------------------------------------------------------------*
     C*  SR800   IF CHARACTER = '@'                                  *
     C*--------------------------------------------------------------*
     C           SR800     BEGSR
     C*
MUST C           $OE       IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED800
     C                     END
     C*
     C           $@FIST    IFEQ 'FIRST'
     C                     MOVE 'YES'     $@CH    3
     C                     Z-ADDY         $POS
     C                     MOVE *BLANK    $@FIST  5
     C                     ELSE
     C                     MOVE 'YES'     $VAR
     C                     MOVE 'O'       $TYPE
     C                     END
     C*
     C                     ADD  1         $LEN
     C*
     C           ED800     ENDSR
     C*--------------------------------------------------------------*
     C*  SR900   IF CHARACTER = 'D'  or  'd'                         *
     C*--------------------------------------------------------------*
     C           SR900     BEGSR
     C*
MUST C           $OE       IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED900
     C                     END
     C*
     C           $@CH      IFEQ 'YES'
     C                     MOVE 'YES'     $DATE   3
     C                     Z-ADD0         $LEN
     C                     Z-ADDY         $POS
     C                     ELSE
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR410
     C                     END
     C*
     C           ED900     ENDSR
     C*--------------------------------------------------------------*
     C*  SR1000  IF CHARACTER = 'T'  or  't'                         *
     C*--------------------------------------------------------------*
     C           SR1000    BEGSR
     C*
MUST C           $OE       IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED1000
     C                     END
     C*
     C           $@CH      IFEQ 'YES'
     C                     MOVE 'YES'     $TIME   3
     C                     Z-ADD0         $LEN
     C                     Z-ADDY         $POS
     C                     ELSE
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR410
     C                     END
     C*
     C           ED1000    ENDSR
     C*--------------------------------------------------------------*
     C*  SR1100  IF CHARACTER = 'P'  or  'p'                         *
     C*--------------------------------------------------------------*
     C           SR1100    BEGSR
     C*
MUST C           $OE       IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED1100
     C                     END
     C*
     C           $@CH      IFEQ 'YES'
     C                     MOVE 'YES'     $PAGE   3
     C                     Z-ADD0         $LEN
     C                     Z-ADDY         $POS
     C                     ELSE
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR410
     C                     END
     C*
     C           ED1100    ENDSR
     C*--------------------------------------------------------------*
     C*  SR1200  IF CHARACTER = OE  or  OF                           *
     C*--------------------------------------------------------------*
     C           SR1200    BEGSR
     C*
     C           $CHAR     IFEQ OE
     C                     Z-ADD0         CT1200  20
     C                     MOVE 'YES'     $OE     3
     C                     ELSE
     C                     MOVEL*BLANK    $OE
MUST C                     Z-ADD0         CT1200  20
     C                     END
     C*
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR410
     C*
     C           ED1200    ENDSR
     C*--------------------------------------------------------------*
     C*  SR1300  IF CHARACTER = '''                                  *
     C*--------------------------------------------------------------*
     C           SR1300    BEGSR
     C*
     C           $OE       IFEQ 'YES'
     C                     EXSR SR410
     C                     GOTO ED1300
     C                     END
     C*
     C                     MOVE 'YES'     $APOST  3
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR410
     C*
     C           ED1300    ENDSR
     C*--------------------------------------------------------------*
     C*  SR1400  IF CHARACTER = OTHERS                               *
     C*--------------------------------------------------------------*
     C           SR1400    BEGSR
     C*
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR410
     C*
     C           ED1400    ENDSR
**     ARY2
     A*--------------------------------------------------------------*
     A* ™â™ã Computer  Center  of  Lucoral  Company                *
     A*--------------------------------------------------------------*
     A*
**     ARY4
'
**     ARY5
DATE EDTCDE(Y)
EDTCDE(J)
EDTCDE(L)
EDTCDE(Y)
