             PGM
             DCL        VAR(&WRKSTN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CHYMD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&A8YMD) TYPE(*CHAR) LEN(8) /*西元八位*/
             DCL        VAR(&C8YMD) TYPE(*CHAR) LEN(8) /*中國八位*/
             DCL        VAR(&ADMDY) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ADDMY) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ADYMD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&JOBNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&QYY) TYPE(*CHAR) LEN(2)   /*系統年*/
             DCL        VAR(&QMM) TYPE(*CHAR) LEN(2)   /*系統月*/
             DCL        VAR(&QDD) TYPE(*CHAR) LEN(2)   /*系統日*/
             DCL        VAR(&PYY) TYPE(*DEC) LEN(4 0)  /* TEMP FIELD */
             DCL        VAR(&QYY4C) TYPE(*CHAR) LEN(4) /*四位中國年*/
             DCL        VAR(&QYY4A) TYPE(*CHAR) LEN(4) /*四位西元年*/
             DCL        VAR(&HYY) TYPE(*CHAR) LEN(2)   /*世紀*/
             DCL        VAR(&QLP) TYPE(*DEC) LEN(1 0)  /*系統曆別*/
             DCL        VAR(&PTN01) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ETTIME) TYPE(*CHAR) LEN(9)
             DCL        VAR(&PTN02) TYPE(*CHAR) LEN(4)
             RTVJOBA    JOB(&WRKSTN) USER(&USER) NBR(&JOBNBR)
             CHGDTAARA  DTAARA(*LDA (163 10)) VALUE(&WRKSTN)
             CHGDTAARA  DTAARA(*LDA (101 10)) VALUE(&USER)
             CHGDTAARA  DTAARA(*LDA (193 6)) VALUE(&JOBNBR)
             RTVSYSVAL  SYSVAL(QYEAR) RTNVAR(&QYY)
             RTVSYSVAL  SYSVAL(QMONTH) RTNVAR(&QMM)
             RTVSYSVAL  SYSVAL(QDAY) RTNVAR(&QDD)
             RTVSYSVAL  SYSVAL(QLEAPADJ) RTNVAR(&QLP)
 /*系統日期為西曆*/
             IF (&QLP *EQ 0) DO
             IF (&QYY *LE '50') CHGVAR VAR(&HYY) VALUE('20')
             ELSE               CHGVAR VAR(&HYY) VALUE('19')
             CHGVAR     VAR(&ADMDY) VALUE(&QMM *CAT &QDD *CAT &QYY)
             CHGDTAARA  DTAARA(*LDA (119 6)) VALUE(&ADMDY)
             CHGVAR     VAR(&ADDMY) VALUE(&QDD *CAT &QMM *CAT &QYY)
             CHGDTAARA  DTAARA(*LDA (129 6)) VALUE(&ADDMY)
             CHGVAR     VAR(&ADYMD) VALUE(&QYY *CAT &QMM *CAT &QDD)
             CHGDTAARA  DTAARA(*LDA (187 6)) VALUE(&ADYMD)
             CHGVAR     VAR(&QYY4A) VALUE(&HYY *CAT &QYY)
             CHGVAR     VAR(&PYY)   VALUE(&QYY4A)
             CHGVAR     VAR(&PYY)   VALUE(&PYY - 1911)
             CHGVAR     VAR(&QYY4C) VALUE(&PYY)
             CHGVAR     VAR(&QYY) VALUE(%SST(&QYY4C 3 2))
             CHGVAR     VAR(&CHYMD) VALUE(&QYY *CAT &QMM *CAT &QDD)
             CHGDTAARA  DTAARA(*LDA (111 6)) VALUE(&CHYMD)
             CHGVAR     VAR(&A8YMD) VALUE(&QYY4A *CAT &QMM *CAT &QDD)
             CHGDTAARA  DTAARA(*LDA (201 8)) VALUE(&A8YMD)
             CHGVAR     VAR(&C8YMD) VALUE(&QYY4C *CAT &QMM *CAT &QDD)
             CHGDTAARA  DTAARA(*LDA (209 8)) VALUE(&C8YMD)
             ENDDO
 /*系統日期為國曆*/
             ELSE DO
             CHGVAR     VAR(&CHYMD) VALUE(&QYY *CAT &QMM *CAT &QDD)
             CHGDTAARA  DTAARA(*LDA (111 6)) VALUE(&CHYMD)
             CHGVAR     VAR(&QYY4C) VALUE('00' *CAT &QYY)
             CHGVAR     VAR(&PYY)   VALUE(&QYY4C)
             CHGVAR     VAR(&PYY)   VALUE(&PYY + 1911)
             CHGVAR     VAR(&QYY4A) VALUE(&PYY)
             CHGVAR     VAR(&QYY)   VALUE(%SST(&QYY4A 3 2))
             CHGVAR     VAR(&ADMDY) VALUE(&QMM *CAT &QDD *CAT &QYY)
             CHGDTAARA  DTAARA(*LDA (119 6)) VALUE(&ADMDY)
             CHGVAR     VAR(&ADDMY) VALUE(&QDD *CAT &QMM *CAT &QYY)
             CHGDTAARA  DTAARA(*LDA (129 6)) VALUE(&ADDMY)
             CHGVAR     VAR(&ADYMD) VALUE(&QYY *CAT &QMM *CAT &QDD)
             CHGDTAARA  DTAARA(*LDA (187 6)) VALUE(&ADYMD)
             CHGVAR     VAR(&A8YMD) VALUE(&QYY4A *CAT &QMM *CAT &QDD)
             CHGDTAARA  DTAARA(*LDA (201 8)) VALUE(&A8YMD)
             CHGVAR     VAR(&C8YMD) VALUE(&QYY4C *CAT &QMM *CAT &QDD)
             CHGDTAARA  DTAARA(*LDA (209 8)) VALUE(&C8YMD)
             ENDDO
 /*數值起始值*/
             CHGDTAARA  DTAARA(*LDA (125 3)) VALUE('000')
             CHGDTAARA  DTAARA(*LDA (128 1)) VALUE('0')
             CHGDTAARA  DTAARA(*LDA (162 1)) VALUE('0')
 /*版權系統相關資料*/
             RTVDTAARA  DTAARA(R@DA01 (102 7)) RTNVAR(&PTN01)
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&ETTIME)
             CALL       R@S002 PARM(&PTN01 &ETTIME &PTN02)
             CHGDTAARA  DTAARA(*LDA (487 4)) VALUE(&PTN02)
             CHGDTAARA  DTAARA(*LDA (491 9)) VALUE(&ETTIME)
             ENDPGM
