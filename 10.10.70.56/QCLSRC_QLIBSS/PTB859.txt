             PGM        PARM(&NEWOWN)
             DCL        VAR(&TOTNBR) TYPE(*DEC) LEN(8) VALUE(0)
             DCL        VAR(&NBR) TYPE(*CHAR) LEN(8)
             DCL        VAR(&NEWOWN) TYPE(*CHAR) LEN(10)
             DCLF       FILE(PTFLIB/PF859)
     /***********************************************************/
     /*      NOV. 14, 1989;      BY  HENRY SHAO.                */
     /***********************************************************/
             ?          DSPOBJD ?-DETAIL(*FULL) ?-OUTPUT(*OUTFILE) +
                          ?-OUTFILE(QTEMP/OWN1) ?-OUTMBR(*FIRST +
                          *REPLACE)
 /* 錯誤訊息*/
             MONMSG     MSGID(CPF6801) EXEC(DO)
             SNDPGMMSG  MSGID(CPF6801) MSGF(QCPFMSG)
             GOTO       EOJ
             ENDDO
 /**/
             MONMSG     MSGID(CPF2105) EXEC(DO)
             SNDPGMMSG  MSG('OBJECT不存在')
             GOTO       EOJ
             ENDDO
 /**/
             MONMSG     MSGID(CPF2110) EXEC(DO)
             SNDPGMMSG  MSG('LIBRARY不存在')
             GOTO       EOJ
             ENDDO
 /*************/
             CRTPF      FILE(QTEMP/PF859) SRCFILE(PTFLIB/QDDSSRC) +
                          OPTION(*NOSRC *NOLIST *NOSECLVL)
             MONMSG     MSGID(CPF7302)     /* FILE "PF859" ALREADY   */
                                           /* EXISTED, IT IS OKEY.  */
             CPYF       FROMFILE(QTEMP/OWN1) TOFILE(QTEMP/PF859) +
                          MBROPT(*REPLACE) INCREL((*IF ODOBOW *NE +
                          &NEWOWN)) FMTOPT(*MAP *DROP)
             DLTF       FILE(QTEMP/OWN1)
             OVRDBF     FILE(PF859) TOFILE(QTEMP/PF859) +
                          POSITION(*START) SHARE(*YES)
             OPNDBF     FILE(QTEMP/PF859) OPTION(*INP) +
                          OPNID(PF859)
 RCVF:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
             GOTO       EOF
             ENDDO
 /**************************************************************/
             CHGOBJOWN  OBJ(&ODLBNM/&ODOBNM) OBJTYPE(&ODOBTP) +
                          NEWOWN(&NEWOWN) CUROWNAUT(*SAME)
 /* 錯誤訊息*/
             MONMSG     MSGID(CPF2226 CPF2232 CPF2217) EXEC(DO)
             SNDPGMMSG  MSG('沒有權利將OBJECT從USRPRF' *BCAT +
                          &ODOBOW *BCAT '轉給USRPRF' *BCAT &NEWOWN)
             GOTO       EOF
             ENDDO
 /**/
             MONMSG     MSGID(CPF2233) EXEC(DO)
             SNDPGMMSG  MSG('沒有權利使用USRPRF' *BCAT +
                          &ODOBOW)
             GOTO       EOF
             ENDDO
 /**/
             MONMSG     MSGID(CPF2204) EXEC(DO)
             SNDPGMMSG  MSG('USRPRF' *BCAT &NEWOWN *BCAT '不存在')
             GOTO       EOF
             ENDDO
 /**/
             MONMSG     MSGID(CPF22BE) EXEC(DO)
             SNDPGMMSG  MSG('USRPRF' *BCAT &NEWOWN *BCAT +
                          '不存在或者已被隱藏起來')
             GOTO       EOF
             ENDDO
 /**/
             MONMSG     MSGID(CPF0001) EXEC(DO)
             SNDPGMMSG  MSG('USRPRF' *BCAT &NEWOWN *BCAT '錯誤')
             GOTO       EOF
             ENDDO
 /**************/
             CHGVAR     VAR(&TOTNBR) VALUE(1 + &TOTNBR)
             GOTO  RCVF
 /**************************************************************/
 EOF:
             CLOF       OPNID(PF859)
             CLRPFM     FILE(PF859)
             DLTOVR     FILE(PF859)
 SEND:
             CHGVAR     VAR(&NBR) VALUE(&TOTNBR)
             SNDPGMMSG  MSG('TOTAL ' *CAT &NBR *CAT ' OBJESTS +
                          CHANGED.')
 EOJ:
             ENDPGM
