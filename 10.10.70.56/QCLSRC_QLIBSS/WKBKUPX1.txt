/*-------------------------------------------------------------------*/
/*           1. AUTHOR     : JULIA                                   */
/*           2. CREAT DATE : 80.2.8                                  */
/*           3. FUNCTION   : FOR WEEKLY BACK UP                      */
/*  DESCRIPTION:                                                     */
/*                                                                   */
/*  PARMATERS:                                                       */
/*                                                                   */
/*-------------------------------------------------------------------*/
             PGM
             DCL        VAR(&IN03)   TYPE(*LGL)  LEN(1)
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(10) VALUE(' ')
             DCL        VAR(&N) TYPE(*DEC) LEN(2) VALUE(1)
             DCL        VAR(&A) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&NUM) TYPE(*DEC) LEN(2)
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(80) VALUE(' ')
             DCL        VAR(&PLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&XXPGMR) TYPE(*CHAR) LEN(10)
             DCLF       FILE(WKBKUP)
             MONMSG     MSGID(MCH0000 CPF0000 CPF9999)
                           /*EXECEPTION HANDER */
             CHGMSGQ    MSGQ(*USRPRF) DLVRY(*NOTIFY)

             CALL       PGM(SET) PARM(&IN03)
             IF         COND(&IN03 *EQ '0') THEN(DO)
FIRFIL:      RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(ENDPGM)) +
                        /*EXIT OF FILE REACHED */
             IF         COND(&LIB *EQ '          ') THEN(GOTO +
                          CMDLBL(FIRFIL))
             SNDBRKMSG  MSG('此LIB: ' *CAT &LIB *CAT +
                          '馬上要做WEEKLY BACKUP +
                          請使用此LIBRARY之同仁立即...SIGNOFF... +
                          謝謝您的合作!!!') TOMSGQ(*ALLWS)
             DLYJOB     DLY(30)
             CHKOBJ     OBJ(QSYS/&LIB) OBJTYPE(*LIB) /*CHECK IF FILE +
                          EXISTS */
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(FIRFIL))
             CHGVAR     VAR(&PLIB) VALUE(&LIB)

RCVFIL:      RCVF
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(LASTLIB)) +
                        /*EXIT OF FILE REACHED */
             IF         COND(&LIB *EQ '          ') THEN(GOTO +
                          CMDLBL(RCVFIL))
             CHKOBJ     OBJ(QSYS/&LIB) OBJTYPE(*LIB) /*CHECK IF FILE +
                          EXISTS */
             MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(RCVFIL))
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(RCVFIL))
             CHGVAR     VAR(&XXPGMR) VALUE(%SST(&LIB 1 2) *CAT +
                          'PGMR')
             SBMJOB     CMD(CALL PGM(SNDMSGSTN) PARM(&LIB &XXPGMR))
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +
                          MSGDTA('此LIB: ' *CAT &PLIB *CAT +
                          '正在做WEEKLY BACKUP') TOPGMQ(*EXT) +
                          MSGTYPE(*STATUS)
             DLYJOB     DLY(50)
             SAVLIB     LIB(&PLIB) DEV(TAP01) SEQNBR(&N) +
                          EXPDATE(090189) ENDOPT(*LEAVE) +
                          TGTRLS(*CURRENT)
             CHGVAR     VAR(&N)       VALUE(&N + 1)
             PRINT      LINE(&PLIB)
             CHGVAR     VAR(&PLIB) VALUE(&LIB)

             GOTO       RCVFIL

 LASTLIB:    SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('此LIB: +
                          ' *CAT &PLIB *CAT '正在做WEEKLY BACKUP') +
                          TOPGMQ(*EXT) MSGTYPE(*STATUS)
             SAVLIB     LIB(&PLIB) DEV(TAP01) SEQNBR(&N) +
                          EXPDATE(090189) ENDOPT(*LEAVE) +
                          TGTRLS(*CURRENT)

             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)
             CHGVAR     VAR(&TIME) VALUE(%SST(&TIME 1 2) *CAT ':' +
                          *CAT %SST(&TIME 3 2) *CAT ':' *CAT +
                          %SST(&TIME 5 2))
             PRINT      LINE('REPORT END RUN TIME :' *CAT &TIME)
             PRINT      ACTION(*CLO)
             CLOF       OPNID(WKBKUP)
             CHKTAP     DEV(TAP01) ENDOPT(*UNLOAD)
             CHGMSGQ    MSGQ(*USRPRF) DLVRY(*BREAK)
             ENDDO
ENDPGM:      ENDPGM
