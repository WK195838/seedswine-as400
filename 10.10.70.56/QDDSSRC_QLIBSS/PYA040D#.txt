     A****************************************************************
     A*    DSPF NAME    : PYA040D$                                   *
     A*    AUTHOR       : A1034  STEPHANIE                           *
     A*    CREATE DATE  : 78/10/17                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PYA040$                                    *
     A*    SYSTEM       :財務系統                                  *
     A*    SUBSYSTEM    :人事薪資系統                              *
     A*    REMARK       :人事基本資料建立                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R DSPC1
     A                                      CA03(03)
     A                                  1  2'<PYA040D>'
     A                                  1 21'***    人事基本資料建立    ***'
     A                                  1 63'日期:'
     A            DDATE          6  0O  1 71EDTCDE(Y)
     A                                  8 24'員工編號:'
     A            DEM01          5   B  8 37
     A  60                                  DSPATR(PC RI)
     A                                 10 24'項目選擇:'
     A            DITEM          1   B 10 37
     A  61                                  DSPATR(PC RI)
     A                                 10 47'1.人事'
     A                                 11 47'2.薪資'
     A                                 12 47'3.履歷'
     A                                 14 24'作業選擇:'
     A            DOPT           1   B 14 37
     A  63                                  DSPATR(PC RI)
     A                                 14 47'1.新增'
     A                                 15 47'2.修改'
     A                                 16 47'4.刪除'
     A                                 17 47'5.查詢'
     A  76        ERR1          50   O 22 02MSGID(UGL0001 FIMF)
     A                                      DSPATR(HI)
     A  78        ERR2          50   O 22 02MSGID(UGL0003 FIMF)
     A                                      DSPATR(HI)
     A  79        ERR3          50   O 22 02MSGID(UGL0005 FIMF)
     A                                      DSPATR(HI)
     A  80        ERR4          50   O 22 02MSGID(UPY0001 FIMF)
     A                                      DSPATR(HI)
     A  81        ERR5          50   O 22 02MSGID(UPY0008 FIMF)
     A                                      DSPATR(HI)
     A  82        ERR6          50   O 22 02MSGID(UGL0002 FIMF)
     A                                      DSPATR(HI)
     A  83        ERR7          50   O 22 02MSGID(UGL0018 FIMF)
     A                                      DSPATR(HI)
     A                                 23 31'PF3= 回主畫面'
     A*----------------------------------------------------------------
     A          R DSPC2
     A                                      CA03(03)
     A                                      CF12(12)
     A                                  1  2'<PYA040D>'
     A                                  1 21'***    人事基本資料建立    ***'
     A                                  1 63'日期:'
     A            DDATE          6  0O  1 71EDTCDE(Y)
     A                                  2 63'作業:'
     A            DFUN           6   O  2 71DSPATR(RI)
     A                                  3 26'<<< 人事基本資料 >>>'
     A                                  5  2'員工編號 :'
     A            DEM01          5   O  5 15
     A                                      DSPATR(UL)
     A                                  5 44'員工姓名 :'
     A            DEM02         12O  B  5 57
     A N31                                  DSPATR(PR)
     A  60                                  DSPATR(PC RI)
     A                                  6  2'公司別   :'
     A            DEM03          1   B  6 15
     A N31                                  DSPATR(PR)
     A  63                                  DSPATR(PC RI)
     A            DCM03         12   O  6 20
     A                                  6 44'部門代號 :'
     A            DEM04          5   B  6 57
     A N31                                  DSPATR(PR)
     A  61                                  DSPATR(PC RI)
     A            D1NM03        14   O  6 65
     A                                  7  2'職稱代號 :'
     A            DEM05          3   B  7 15
     A N31                                  DSPATR(PR)
     A  62                                  DSPATR(PC RI)
     A            D2NM03        22   O  7 20
     A                                  7 44'編制別   :'
     A            DEM11          1   B  7 57
     A N31                                  DSPATR(PR)
     A  66                                  DSPATR(PC RI)
     A                                  7 59'( 0-試用, 1-正式)'
     A                                  8  2'身份証號 :'
     A            DEM06         10   B  8 15
     A N31                                  DSPATR(PR)
     A  64                                  DSPATR(PC RI)
     A                                  8 44'性　　別 :'
     A            DEM07          1   O  8 57
     A                                  8 59'( F-女 , M-男)'
     A                                  9  2'出生日期 :'
     A            DEM08          6  0B  9 15EDTCDE(Y)
     A N31                                  DSPATR(PR)
     A  73                                  DSPATR(PC RI)
     A                                  9 44'籍　　貫 :'
     A            DEM09         22O  B  9 57
     A N31                                  DSPATR(PR)
     A                                 10  2'血　　型 :'
     A            DEM10          2   B 10 15
     A N31                                  DSPATR(PR)
     A                                 10 44'兵　　役 :'
     A            DEM12          1   B 10 57
     A N31                                  DSPATR(PR)
     A                                 11  2'通訊地址 :'
     A            DEM13         62O  B 11 15
     A N31                                  DSPATR(PR)
     A                                 12  2'戶籍地址 :'
     A            DEM14         62O  B 12 15
     A N31                                  DSPATR(PR)
     A  72                                  DSPATR(PC RI)
     A                                 13  2'通訊電話 :'
     A            DEM15         15   B 13 15
     A N31                                  DSPATR(PR)
     A                                 13 44'戶籍電話 :'
     A            DEM16         15   B 13 57
     A N31                                  DSPATR(PR)
     A                                 14  2'婚姻狀況 :'
     A            DEM17          1   B 14 15
     A N31                                  DSPATR(PR)
     A  65                                  DSPATR(PC RI)
     A                                 14 18'( S-未婚, M-巳婚)'
     A                                 14 44'扶養人數 :'
     A            DEM18          2  0B 14 57EDTCDE(J)
     A N31                                  DSPATR(PR)
     A                                 15  2'到職日期 :'
     A            DEM19          6  0B 15 15EDTCDE(Y)
     A N31                                  DSPATR(PR)
     A  67                                  DSPATR(PC RI)
     A                                 15 44'離職日期 :'
     A            DEM20          6  0B 15 57EDTCDE(Y)
     A N31                                  DSPATR(PR)
     A  68                                  DSPATR(PC RI)
     A                                 16  2'復職日期 :'
     A            DEM21          6  0B 16 15EDTCDE(Y)
     A N31                                  DSPATR(PR)
     A  69                                  DSPATR(PC RI)
     A                                 16 44'投保日期 :'
     A            DEM22          6  0B 16 57EDTCDE(Y)
     A N31                                  DSPATR(PR)
     A  69                                  DSPATR(PC RI)
     A                                 17  2'加保日期 :'
     A            DEM23          6  0B 17 15EDTCDE(Y)
     A N31                                  DSPATR(PR)
     A  70                                  DSPATR(PC RI)
     A                                 17 44'退保日期 :'
     A            DEM24          6  0B 17 57EDTCDE(Y)
     A N31                                  DSPATR(PR)
     A  71                                  DSPATR(PC RI)
     A                                 18  2'投保金額 :'
     A            DEM25          6  0B 18 15EDTCDE(J)
     A N31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                 18 44'勞保費   :'
     A            DEM43          4Y 0B 18 57EDTCDE(J)
     A N31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                 19  2'異動日期 :'
     A            DEM98          6  0O 19 15EDTCDE(Y)
     A                                      DSPATR(UL)
     A                                 19 40'最近調薪日期 :'
     A            DEM29          6  0B 19 57EDTCDE(Y)
     A N31                                  DSPATR(PR)
     A                                 20  2'可休年假 :'
     A            DEM27          2  0B 20 15EDTCDE(J)
     A N31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                 20 44'巳休年假 :'
     A            DEM28          2  0B 20 57EDTCDE(J)
     A N31                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                 21  2'年　　資 :'
     A            DYEAR          3  1O 21 15EDTCDE(J)
     A  74        ERR1          70   O 22 02MSGID(UPY0010 FIMF)
     A                                      DSPATR(HI)
     A  76        ERR3          50   O 22 02MSGID(UGL0001 FIMF)
     A                                      DSPATR(HI)
     A  77        ERR4          50   O 22 02MSGID(UGL0005 FIMF)
     A                                      DSPATR(HI)
     A  78        ERR5          50   O 22 02MSGID(UPY0001 FIMF)
     A                                      DSPATR(HI)
     A  79        ERR6          50   O 22 02MSGID(UPY0002 FIMF)
     A                                      DSPATR(HI)
     A                                 23 12'PF3= 回主畫面'
     A                                 23 53'PF12= 回前畫面'
     A*----------------------------------------------------------------
     A          R DSPC3                     CHGINPDFT(UL)
     A                                      CA03(03)
     A                                      CF12(12)
     A                                  1  2'<PYA040D>'
     A                                  1 21'***    人事基本資料建立    ***'
     A                                  1 63'日期:'
     A            DDATE          6  0O  1 71EDTCDE(Y)
     A                                  2 63'作業:'
     A            DFUN           6   O  2 71DSPATR(RI)
     A                                  3 26'<<< 薪資基本資料 >>>'
     A                                  5 12'員工編號:'
     A            DEM01          5   O  5 24
     A                                  5 44'員工姓名:'
     A            DEM02         12   O  5 56
     A                                  7 12'日　　薪:'
     A            DEM30          4Y 0B  7 24EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A  60                                  DSPATR(PC RI)
     A                                  8 10'加班鐘點費:'
     A            DEM31          4Y 0B  8 24EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A  61                                  DSPATR(PC RI)
     A                                  9 12'加班獎金:'
     A            DEM32          4Y 0B  9 24EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 10 12'伙食津貼:'
     A            DEM33          4Y 0B 10 24EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 11 12'責任津貼:'
     A            DEM34          4Y 0B 11 24EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 12 12'例假津貼:'
     A            DEM35          4Y 0B 12 24EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 13 12'交通津貼:'
     A            DEM36          4Y 0B 13 24EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 14 12'房屋津貼:'
     A            DEM37          4Y 0B 14 24EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 15 12'生產津貼:'
     A            DEM38          4Y 0B 15 24EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 16 12'職務津貼:'
     A            DEM39          4Y 0B 16 24EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 17 12'全勤加給:'
     A            DEM40          4Y 0B 17 24EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                 18  8'加班調整基數:'
     A            DEM41          4Y 0O 18 24EDTCDE(J)
     A                                  7 44'伙食費  :'
     A            DEM42          4Y 0B  7 56EDTCDE(J)
     A                                      CHECK(RB)
     A N31                                  DSPATR(PR)
     A  31                                  DSPATR(UL)
     A                                  8 44'勞保費  :'
     A            DEM43          4  0O  8 56EDTCDE(J)
     A  76        ERR1          50   O 22 02MSGID(UPY0002 FIMF)
     A                                      DSPATR(HI)
     A                                 23 12'PF3= 回主畫面'
     A                                 23 53'PF12= 回前畫面'
     A*----------------------------------------------------------------
     A          R DSPC4
     A                                      CA03(03)
     A                                      CF12(12)
     A                                  1  2'<PYA040D>'
     A                                  1 21'***    人事基本資料建立    ***'
     A                                  1 63'日期:'
     A            DDATE          6  0O  1 71EDTCDE(Y)
     A                                  2 63'作業:'
     A            DFUN           6   O  2 71DSPATR(RI)
     A                                  3 27'<<< 學經歷資料 >>>'
     A                                  5  2'員工編號:'
     A            DEM01          5   O  5 14
     A            DEM02         12   O  5 21
     A                                  7  2'專　　長:'
     A            DEM45         24O  B  7 14
     A N31                                  DSPATR(PR)
     A                                  9  2'興　　趣:'
     A            DEM46         24O  B  9 14
     A N31                                  DSPATR(PR)
     A                                 11  2'學歷－１:'
     A            DEM47         40O  B 11 14
     A N31                                  DSPATR(PR)
     A                                 12  2'    －２:'
     A            DEM48         40O  B 12 14
     A N31                                  DSPATR(PR)
     A                                 14  2'經歷－１:'
     A            DEM49         40O  B 14 14
     A N31                                  DSPATR(PR)
     A                                 15  2'    －２:'
     A            DEM50         40O  B 15 14
     A N31                                  DSPATR(PR)
     A                                 16  2'    －３:'
     A            DEM51         40O  B 16 14
     A N31                                  DSPATR(PR)
     A                                 17  2'    －４:'
     A            DEM52         40O  B 17 14
     A N31                                  DSPATR(PR)
     A                                 18  2'    －５:'
     A            DEM53         40O  B 18 14
     A N31                                  DSPATR(PR)
     A                                 19  2'    －６:'
     A            DEM54         40O  B 19 14
     A N31                                  DSPATR(PR)
     A                                 23 12'PF3= 回主畫面'
     A                                 23 53'PF12= 回前畫面'
