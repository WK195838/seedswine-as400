     A*  90/03/08  11:33:03    QSECOFR     REL-R01M02  5728-PW1
     A****************************************************************
     A*    DSPF NAME    : S@A14D                                     *
     A*    AUTHOR       : A1038  CATHY SUN                           *
     A*    CREATE DATE  : 79/02/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : S@A14                                      *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :安全管制系統                              *
     A*    REMARK       :              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S@TXPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R SFLSR                     SFL
     A*  90/03/08  11:33:03    QSECOFR     REL-R01M02  5728-PW1
     A  54                                  SFLNXTCHG
     A            RRN            3S 0O  8  2
     A            DOPT           1A  B  8  8VALUES('Y' 'N')
     A            ODOBNM    R        O  8 12
     A            DDOBTX        40O  O  8 23
     A            ODLBNM    R        O  8 64
     A            SUBSYS         2A  O  8 76
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  90/03/08  11:33:03    QSECOFR     REL-R01M02  5728-PW1
     A                                      SFLSIZ(0999)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      ROLLUP(25 '下一頁')
     A                                      ROLLDOWN(26 '前一頁')
     A                                      CA03(03)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(UPT2030 *LIBL/PTMF)
     A  97                                  SFLMSGID(UPT2040 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<S@A14D>'
     A                                  1 22'***'
     A                                  1 28'程式代碼建立（選擇）'
     A                                  1 51'***'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<S@A14.1>'
     A                                  4  2'OP ='
     A                                  4 23'Y=建立'
     A                                  4 41'N=不建立'
     A                                  5  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  6  1'序號'
     A                                  6  8'OP'
     A                                  6 11'程式代號'
     A                                  6 22'程式說明'
     A                                  6 64'LIBRARY'
     A                                  6 73'子系統'
     A                                  7  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A          R DSPC11
     A                                 23  2'PF3=回主畫面'
     A                                    +2'PA1=下一頁'
     A                                    +2'PA2=上一頁'
     A  92        ERR5          70A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
