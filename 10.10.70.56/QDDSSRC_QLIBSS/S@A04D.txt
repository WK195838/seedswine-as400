     A*  90/03/08  13:58:13    QSECOFR     REL-R01M02  5728-PW1
     A****************************************************************
     A*    DSPF NAME    : S@A04D                                     *
     A*    AUTHOR       : A1046  JOHNSON CHEN                        *
     A*    CREATE DATE  : 78/11/29                                   *
     A*    UPDATE DATE  : 79/02/09                                   *
     A*    PROGRAM NAME : S@A04                                      *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :安全管制系統                              *
     A*    REMARK       :程式使用權設定（依使用者別）              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S@RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A*  90/02/09  10:32:19    A1046       REL-R01M02  5728-PW1
     A                                      CF03(03 '結束執行')
     A                                      CF09(09 '自動設定所有程式使用權')
     A                                  1  2'<S@A04D>'
     A                                  1 18'***'
     A                                  1 24'程式使用權設定（依使用者別）'
     A                                  1 57'***'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<S@A04.1>'
     A                                  7 24'使用者代號 :'
     A            DSU01     R        B  7 39REFFLD(SU01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  9 24'程式代號   :'
     A            DSU02     R        B  9 39REFFLD(SU02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                 11 24'所在LIBRARY:'
     A            DSU03     R        B 11 39REFFLD(SU03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 13 24'所屬子系統 :'
     A            DSP04     R        B 13 39REFFLD(SP04)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 19'PF9 =自動設定所有程式使用權'
     A                                 23 51'執行＝程式使用權設定'
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 2022 PTMF)
     A                                      DSPATR(HI)
     A  80        ERR3          50A  O 22  2MSGID(UPT 2023 PTMF)
     A                                      DSPATR(HI)
     A          R SFLSR                     SFL
     A*  90/03/08  13:58:13    QSECOFR     REL-R01M02  5728-PW1
     A  54                                  SFLNXTCHG
     A            RRN            3S 0O  8  2
     A            DOPT      R        B  8  8REFFLD(SU04)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                      VALUES('Y' 'N')
     A            SP01      R        O  8 12REFFLD(SP01)
     A  64                                  DSPATR(RI)
     A            SP02      R        O  8 23REFFLD(SP02)
     A            SP03      R        O  8 64REFFLD(SP03)
     A            SP04      R        O  8 76REFFLD(SP04)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  90/03/08  13:58:13    QSECOFR     REL-R01M02  5728-PW1
     A                                      SFLSIZ(0999)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      ROLLUP(25 '下一頁')
     A                                      ROLLDOWN(26 '前一頁')
     A                                      CF03(03)
     A                                      CF12(12)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(UPT2030 *LIBL/PTMF)
     A  97                                  SFLMSGID(UPT2040 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<S@A04D>'
     A                                  1 18'***'
     A                                  1 24'程式使用權設定（依使用者別）'
     A                                  1 56'***'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<S@A04.2>'
     A                                  3  1'使用者代號 :'
     A            DSU01     R        O  3 16REFFLD(SU01)
     A                                  4  2'OP =使用權設定'
     A                                  4 23'Y=有使用權'
     A                                  4 41'N=無使用權'
     A                                  5  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  6  1'序號'
     A                                  6  8'OP'
     A                                  6 11'程式代號'
     A                                  6 22'程式說明'
     A                                  6 64'LIBRARY'
     A                                  6 73'子系統'
     A                                  7  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A          R DSPC11
     A                                 23  2'PF3=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 39'PA1=下一頁'
     A                                 23 55'PA2=上一頁'
     A  92        ERR5          70A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  80        ERR3          50A  O 22  2MSGID(UPT 2023 PTMF)
     A                                      DSPATR(HI)
