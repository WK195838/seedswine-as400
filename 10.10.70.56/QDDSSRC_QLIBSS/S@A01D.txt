     A*  90/02/08  11:53:05    A1046       REL-R01M02  5728-PW1
     A****************************************************************
     A*    DSPF NAME    : S@A01D                                     *
     A*    AUTHOR       : A1046  JOHNSON CHEN                        *
     A*    CREATE DATE  : 78/11/29                                   *
     A*    UPDATE DATE  : 79/02/08                                   *
     A*    PROGRAM NAME : S@A01                                      *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :安全管制系統                              *
     A*    REMARK       :程式代號維護                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S@RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A*  90/02/08  11:53:05    A1046       REL-R01M02  5728-PW1
     A                                      CF03(03)
     A                                      CF18(18)
     A                                  1  2'<S@A01D>'
     A                                  1 27'***   程式代號維護   ***'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<S@A01.1>'
     A                                  7 27'程式代號:'
     A            DSP01     R        B  7 39REFFLD(SP01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  9 27'作業選擇:'
     A            DOPT           1A  B  9 39VALUES(' ' '1' '2' '4' '5')
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  9 42'1.新增'
     A                                 10 42'2.修改'
     A                                 11 42'4.刪除'
     A                                 12 42'5.查詢'
     A                                 23  2'PF3 =回主畫面'
     A                                 23 18'PF18 =全頁查詢'
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22  2MSGID(UPT 2020 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A          R DSPC2
     A*  90/02/08  11:53:05    A1046       REL-R01M02  5728-PW1
     A                                      CHGINPDFT(UL)
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  1  2'<S@A01D>'
     A                                  1 27'***   程式代號維護   ***'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<S@A01.2>'
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  7 24'程式代號   :'
     A            WSP01     R        O  7 39REFFLD(SP01)
     A  60                                  DSPATR(RI)
     A                                  9 24'程式說明   :'
     A            DSP02     R        B  9 39REFFLD(SP02)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                 11 24'所在LIBRARY:'
     A            DSP03     R        B 11 39REFFLD(SP03)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                 13 24'所在子系統 :'
     A            DSP04     R        B 13 39REFFLD(SP04)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 23  2'PF3 =回主畫面   PF12 =回前畫面-
     A                                      '
     A  96        ERR3          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  80        ERR4          50A  O 22  2MSGID(UPT 2023 PTMF)
     A                                      DSPATR(HI)
     A  81        ERR5          50A  O 22  2MSGID(UPT 2026 PTMF)
     A                                      DSPATR(HI)
     A  82        ERR6          50A  O 22  2MSGID(UPT 2025 PTMF)
     A                                      DSPATR(HI)
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A*  90/02/08  11:53:05    A1046       REL-R01M02  5728-PW1
     A            RRN            2S 0O  7  3
     A            DOP            1A  B  7  7VALUES(' ' '2' '4' '5')
     A            SP01      R        O  7 10
     A            SP02      R        O  7 22
     A            SP03      R        O  7 64
     A            SP04      R        O  7 76
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  89/11/29  11:47:29    A1046       REL-R01M02  5728-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      ROLLUP(25 '下一頁')
     A                                      ROLLDOWN(26 '前一頁')
     A                                      CF03(03)
     A                                      CF12(12)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(UPT2030 *LIBL/PTMF)
     A  97                                  SFLMSGID(UPT2040 *LIBL/PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A                                  1  2'<S@A01.3>'
     A                                  1 27'***   程式代號維護   ***'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  3  4'OP='
     A                                  3 20'2:修改'
     A                                  3 40'4:刪除'
     A                                  3 60'5:單筆查詢'
     A                                  4  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  5  1'序號OP'
     A                                  5 10'程式代號'
     A                                  5 21'程式說明'
     A                                  5 64'LIBRARY'
     A                                  5 73'子系統'
     A                                  6  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A          R DSPC11                    OVERLAY
     A                                 21  2'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       =================='
     A  93        ERR1          50A  O 22 02MSGID(UPT2050 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 39'PA1=下一頁'
     A                                 23 55'PA2=上一頁'
