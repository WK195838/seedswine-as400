     A*  90/02/09  10:36:58    A1046       REL-R01M02  5728-PW1
     A****************************************************************
     A*    DSPF NAME    : S@A12D                                     *
     A*    AUTHOR       : A1046  JOHNSON CHEN                        *
     A*    CREATE DATE  : 79/02/28                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : S@A12                                      *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :安全管制系統                              *
     A*    REMARK       :程式使用權隱藏                            *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S@RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A*  90/02/09  10:32:19    A1046       REL-R01M02  5728-PW1
     A                                      CF03(03 '結束執行')
     A                                      CF09(09 '自動隱藏所有程式使用權')
     A                                  1  2'<S@A12D>'
     A                                  1 25'***'
     A                                  1 31'程式使用權隱藏'
     A                                  1 50'***'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<S@A12.1>'
     A                                  9 24'程式代號   :'
     A            DSP01     R        B  9 39REFFLD(SP01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                 13 24'所屬子系統 :'
     A            DSP04     R        B 13 39REFFLD(SP04)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 19'PF9 =自動隱藏所有程式使用權'
     A                                 23 51'執行＝程式使用權隱藏設定'
     A  80        ERR1          70A  O 22  2MSGID(UPT 2023 PTMF)
     A                                      DSPATR(HI)
     A          R SFLSR                     SFL
     A*  90/02/09  10:36:58    A1046       REL-R01M02  5728-PW1
     A  54                                  SFLNXTCHG
     A            RRN            3S 0O  8  2
     A            DSP05     R        B  8  8REFFLD(SP05)
     A  62                                  DSPATR(PC RI)
     A                                      VALUES('Y' 'N')
     A            SP01      R        O  8 12REFFLD(SP01)
     A  62                                  DSPATR(RI)
     A            SP02      R        O  8 23REFFLD(SP02)
     A            SP03      R        O  8 64REFFLD(SP03)
     A            SP04      R        O  8 75REFFLD(SP04)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  89/11/29  15:01:13    A1046       REL-R01M02  5728-PW1
     A                                      OVERLAY
     A                                      ROLLUP(25 '下一頁')
     A                                      ROLLDOWN(26 '前一頁')
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      SFLSIZ(0999)
     A                                      SFLPAG(0014)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(UPT2030 *LIBL/PTMF)
     A  97                                  SFLMSGID(UPT2040 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<S@A12D>'
     A                                  1 25'***'
     A                                  1 31'程式使用權隱藏'
     A                                  1 50'***'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<S@A12.2>'
     A                                  4  2'OP =使用權隱藏設定'
     A                                  4 23'Y=隱藏'
     A                                  4 41'N=不隱藏'
     A                                  5  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  6  1'序號'
     A                                  6  8'OP'
     A                                  6 11'程式代號'
     A                                  6 22'程式說明'
     A                                  6 64'LIBRARY'
     A                                  6 73'子系統'
     A                                  7  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A          R DSPC11
     A                                 23  2'PF3=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 39'PA1=下一頁'
     A                                 23 55'PA2=上一頁'
     A  96        ERR5          70A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  80        ERR1          70A  O 22  2MSGID(UPT 2023 PTMF)
     A                                      DSPATR(HI)
