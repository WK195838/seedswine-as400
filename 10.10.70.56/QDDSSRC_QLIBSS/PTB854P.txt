     A****************************************************************
     A*    PRTF NAME    : PTB854P                                    *
     A*    PROGRAM NAME : PTB854                                     *
     A*    AUTHOR       : A1046  JOHNSON CHEN                        *
     A*    CREATE DATE  : 78/11/11                                   *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :泛太公用系統                              *
     A*    SUBSYSTEM    :應用系統文件                              *
     A*    REMARK       :檔案被程式使用的狀況列印                  *
     A****************************************************************
     A                                      REF(FLPF)
     A          R PH1                       SKIPB(2)
     A                                     1'泛太資訊科技開發股份有限公司'
     A                                    49'日期:'
     A            DDATE          6  0     57EDTCDE(Y)
     A                                    67'頁次:'
     A                                    75PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(1)
     A                                    49'時間:'
     A                                    57TIME
     A                                    68'<PTB851>'
     A                                      SPACEA(2)
     A                                     1'檔案被程式使用的狀況列印'
     A                                      SPACEA(1)
     A                                     4'條件:  FILES:'
     A            FI            10        20
     A                                    35'LIBRARY:'
     A            LI            10        44
     A                                    59'PROGRAMS:'
     A            PR            10        69
     A                                      SPACEA(2)
     A                                     1'==================================+
     A                                       ==================================+
     A                                       ============'
     A                                      SPACEA(1)
     A                                     1'FILE-NAME'
     A                                    13'LIB-NAME'
     A                                    25'TYPE  N-DEP.'
     A                                    +2'TEXT'
     A                                      SPACEA(1)
     A                                     3'PGM-NAME'
     A                                    15'PGM-LIB'
     A                                    26'USG TEXT'
     A                                    36'(USG: 1=IN,2=OUT,4=UPDATE,+
     A                                      6=ADD,8=NOT SPEC.)'
     A                                      SPACEA(1)
     A                                     1'==================================+
     A                                       ==================================+
     A                                       ============'
     A                                      SPACEA(1)
     A*
     A          R PD1                       SPACEA(1)
     A            WHRFI     R              1REFFLD(WHRFI FLPF)
     A            WHRLI     R             13REFFLD(WHRLI FLPF)
     A            WHRTYP    R             26REFFLD(WHRTYP FLPF)
     A            WHNO      R             31REFFLD(WHNO FLPF)
     A            PTEXT         40        +2
     A*
     A          R PD2                       SPACEA(1)
     A            WHPNAM    R              3REFFLD(WHPNAM PGPF)
     A            WHLIB     R             15REFFLD(WHLIB  PGPF)
     A            WHFUSG    R             27REFFLD(WHFUSG PGPF)
     A            WHTEXT    R             30REFFLD(WHTEXT PGPF)
     A*
     A          R PL1                       SPACEA(1)
     A                                     1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ------------'
     A*
     A          R PE2                       SPACEA(1)
     A                                     1'==================================+
     A                                       ==================================+
     A                                       ============'
     A                                      SPACEA(1)
     A                                    40'***報表結束***'
     A*
     A          R PE1                       SPACEA(1)
     A                                    40'*   接下頁   *'
