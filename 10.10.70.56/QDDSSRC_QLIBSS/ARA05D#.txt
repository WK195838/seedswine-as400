     A****************************************************************
     A*    DSPF NAME    : FIS210DA                                   *
     A*    AUTHOR       : A1038  CATHY SUN                           *
     A*    CREATE DATE  : 78/10/12                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : FIS210A                                    *
     A*    SYSTEM       :財務系統                                  *
     A*    SUBSYSTEM    :總帳系統                                  *
     A*    REMARK       :銷貨退回折讓資料輸入                      *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      MSGLOC(22)
     A*===============================================================*
     A*           SFLSR1                                              *
     A*===============================================================*
     A          R SFLSR1                    SFL
     A            RRN            2  0H
     A            D04            1   B  7  4
     A  63                                  DSPATR(PC RI)
     A            D05           10   B  7  8
     A  64                                  DSPATR(PC RI)
     A            D06       R        B  7 19REFFLD(RC12 RCPF)
     A  65                                  DSPATR(PC RI)
     A            D07       R        B  7 32REFFLD(RC13 RCPF)
     A                                      EDTCDE(4)
     A            D08       R        B  7 42REFFLD(RC15 RCPF)
     A                                      EDTCDE(4)
     A  68                                  DSPATR(PC RI)
     A            D09       R        B  7 53REFFLD(RC16 RCPF)
     A                                      EDTCDE(4)
     A            D10            9   B  7 64
     A  66                                  DSPATR(PC RI)
     A            D11            1   B  7 77
     A  67                                  DSPATR(PC RI)
     A*===============================================================*
     A*           SFLCR1                                              *
     A*===============================================================*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(91)
     A                                      SFLPAG(13)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A N50                                  SFLDSP
     A N50                                  SFLDSPCTL
     A  50                                  SFLCLR
     A  51                                  SFLEND
     A  87                                  SFLMSGID(UPT2030 PTMF)
     A  86                                  SFLMSGID(UPT2040 PTMF)
     A            DSPREC         2  0H      SFLRCDNBR
     A                                  1  2'<ARA05D>'
     A                                  1 20'ももも　出貨退回折讓資料輸入+
                                            ももも'
     A                                  1 64'日期:'
     A            DDAT           6  0O  1 72EDTCDE(Y)
     A                                  3 02'公司別   :'
     A            D01       R        B  3 15REFFLD(RC01 RCPF)
     A  61                                  DSPATR(PC RI)
     A            CM03      R        O  3 19REFFLD(CM03 CMPF)
     A                                  3 44'退回折讓日期:'
     A            D03       R        B  3 60REFFLD(RC06 RCPF)
     A                                      EDTCDE(4)
     A  62                                  DSPATR(PC RI)
     A                                  5  1'類別'
     A                                  5  8'發票號碼'
     A                                  5 19'品號'
     A                                  5 31'退量'
     A                                  5 41'退折金額'
     A                                  5 52'退折稅額'
     A                                  5 63'出貨單號'
     A                                  5 74'入帳'
     A                                  6  2'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ==================='
     A*===============================================================*
     A*           DSPC1                                               *
     A*===============================================================*
     A          R DSPC1                     OVERLAY
     A                                 20  2'====================+
     A                                       ====================+
     A                                       ====================+
     A                                       ==================='
     A  98        ERR1          70   O 22  2MSGID(UAR0007 FIMF)
     A                                      DSPATR(HI)
     A  97        ERR2          70   O 22  2MSGID(UAR0025 FIMF)
     A                                      DSPATR(HI)
     A  96        ERR3          70   O 22  2MSGID(UAR0005 FIMF)
     A                                      DSPATR(HI)
     A  95        ERR4          70   O 22  2MSGID(UAR0004 FIMF)
     A                                      DSPATR(HI)
     A  94        ERR7          70   O 22  2MSGID(UAR0028 FIMF)
     A                                      DSPATR(HI)
     A  93        ERR5          70   O 22  2MSGID(UAR0045 FIMF)
     A                                      DSPATR(HI)
     A  92        ERR6          70   O 22  2MSGID(UAR0023 FIMF)
     A                                      DSPATR(HI)
     A  91        ERR8          70   O 22  2MSGID(UAR0037 FIMF)
     A                                      DSPATR(HI)
     A  90        ERR9          70   O 22  2MSGID(UAR0052 FIMF)
     A                                      DSPATR(HI)
     A  89        ERR10         70   O 22  2MSGID(UAR0053 FIMF)
     A                                      DSPATR(HI)
     A  88        ERR11         70   O 22  2MSGID(UAR0056 FIMF)
     A                                      DSPATR(HI)
     A*
     A                                 23  2'PF3 :回主畫面'
     A                                    +1'PF5 :參考資料'
     A                                    +1'PA1 :下一頁'
     A                                    +1'PA2 :上一頁'
