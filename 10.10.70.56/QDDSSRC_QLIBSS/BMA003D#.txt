     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : BMA003D                                    *
     A*    AUTHOR       : A1046  JOHNSON CHEN                        *
     A*    CREATE DATE  : 78/12/06                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : BMA003                                     *
     A*    SYSTEM       :製造系統                                  *
     A*    SUBSYSTEM    :ＢＯＭ系統                                *
     A*    REMARK       :銷退折讓類別維護                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(22)
     A                                      REF(BMSTRF)
     A                                      PRINT
     A*===============================================================*
     A*  SC01   : DSPC1                    * FOR MAIN SCREEN          *
     A*===============================================================*
     A          R DSPC1
     A                                      CA03(03)
     A                                  1  2'<BMA003>'
     A                                  1 31'材料結構資料維護'
     A                                  1 64'日期:'
     A            DDATE          6  0O  1 72EDTCDE(Y)
     A                                  2  2'<BOM0060>'
     A                                  7 27'產品料號:'
     A            DBS01     R        B  7 39REFFLD(BS01)
     A  60                                  DSPATR(PC RI)
     A                                  9 27'作業選擇:'
     A            DOPT           1A  B  9 39
     A  61                                  DSPATR(PC RI)
     A                                  9 42'1.新增'
     A                                 10 42'2.修改'
     A                                 11 42'4.刪除'
     A                                 12 42'5.查詢'
     A                                 15 28'COPY FROM:'
     A            DBSCPY    R        B 15 39REFFLD(BS01)
     A  62                                  DSPATR(PC RI)
     A                                 23  2'PF3 =回主畫面'
     A  92        ERR1          50A  O 22 02MSGID(UPT0010 PTMF)
     A                                      DSPATR(HI)
     A  91        ERR2          50A  O 22 02MSGID(UPT0050 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22 02MSGID(UBM0030 BMMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22 02MSGID(UPT2020 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 22 02MSGID(UPT2010 PTMF)
     A                                      DSPATR(HI)
     A  93        ERR6          50A  O 22 02MSGID(UPT0100 PTMF)
     A                                      DSPATR(HI)
     A*===============================================================*
     A*  SC02   : DSPC2                    * FOR ADD & UPD            *
     A*===============================================================*
     A          R DSPC2
     A                                  1  2'<BMA003>'
     A                                  1 31'材料結構資料維護'
     A                                  1 64'日期:'
     A            DDATE          6  0O  1 72EDTCDE(Y)
     A                                  2  2'<BOM0070>'
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  3  2'產品料號:'
     A            DBS01     R        O  3 14REFFLD(BS01)
     A                                  5 20'序號'
     A                                  5 32'代碼'
     A                                  5 40'結構代碼名稱'
     A                                  5 58'說　　　　　　明'
     A                                  6 21'===='
     A                                  6 33'===='
     A                                  6 41'============'
     A                                  6 58'==================='
     A          R SFLSR1                    SFL
     A            DSEQS          2  0O  7 22EDTCDE(Z)
     A            DBS03S    R        O  7 34REFFLD(BS03)
     A            DNM031        12A  O  7 41
     A            DNM032        19A  O  7 58
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SFLSIZ(60)
     A                                      SFLPAG(13)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(UPT2030 PTMF)
     A  97                                  SFLMSGID(UPT2040 PTMF)
     A            DRRN           2S 0H      SFLRCDNBR
     A  31                             21  1'功能:'
     A  31        DPRO           1   B 21  9
     A  63                                  DSPATR(PC RI)
     A  31                             21 13'序號:'
     A  31        DSEQ           2  0B 21 21EDTCDE(Z)
     A                                      CHECK(RB)
     A  31                                  DSPATR(CS)
     A  65                                  DSPATR(PC RI)
     A  31                             21 26'代碼:'
     A  31        DBS03          2   B 21 34
     A  64                                  DSPATR(PC RI)
     A                                 23  2'PF03 =回主畫面'
     A                                    +1'PF12 =回前畫面'
     A                                    +1'PA1 =下一頁'
     A                                    +1'PA2 =上一頁'
     A  90        ERR0          50A    22 02MSGID(UBM0050 BMMF)
     A                                      DSPATR(HI)
     A  89        ERR1          50A    22 02MSGID(UPT0010 PTMF)
     A                                      DSPATR(HI)
     A  88        ERR2          50A    22 02MSGID(UBM0040 BMMF)
     A                                      DSPATR(HI)
     A  87        ERR3          50A    22 02MSGID(UPT2020 PTMF)
     A                                      DSPATR(HI)
     A  86        ERR4          50A    22 02MSGID(UPT0030 PTMF)
     A                                      DSPATR(HI)
