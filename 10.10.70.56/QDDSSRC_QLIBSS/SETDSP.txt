     A*  90/12/20  09:24:12    QSECOFR     REL-R02M00  5728-PW1
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : CHGVAL                                     *
     A*    AUTHOR       : A1084  CHARLES                             *
     A*    CREATE DATE  : 79/11/20                                   *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A*  90/12/20  09:24:12    QSECOFR     REL-R02M00  5728-PW1
     A                                      CA03(03 '結束執行')
     A                                  1  2'<SETDATE>'
     A                                  1 25'***   設定開機日期   ***'
     A                                      DSPATR(HI)
     A                                  1 60'日期:'
     A            $CHYMD         6  0O  1 68EDTCDE(Y)
     A                                  9 22'開機日期:'
     A            DDATE          6Y 0B  9 34EDTCDE(Y)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                  9 43'(MM/DD/YY)'
     A                                 11 22'開機時間:'
     A            DTME           6Y 0B 11 34EDTWRD('  :  :  ')
     A                                      CHECK(RB)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                 11 43'(HH:MM:SS)'
     A                                 14 17'SELECT LIBRARY SAVE:'
     A            OPTLIB         1   B 14 39VALUES('1' '2' '*')
     A                                 14 43'(1.USE 2.OTHER 3.*ALL)'
     A                                 17 17'END SUBSYSTEM QINTER:'
     A            ENDINT         1   B 17 39VALUES('Y' 'N')
     A                                 17 43'(Y/N)'
     A  98        ERR1          50A  O 22  2MSGID(UPT 0040 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 3006 PTMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22  2MSGID(UPT 3007 PTMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22  2MSGID(UPT 3008 PTMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 22  2MSGID(UPT 3009 PTMF)
     A                                      DSPATR(HI)
     A                                 23  2'PF3 =結束作業'
     A                                 23 19'執行=設定'
