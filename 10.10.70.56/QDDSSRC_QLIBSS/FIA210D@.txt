     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    DSPF NAME    : FIA210D@                                   *
     A*    AUTHOR       : A1040  JUSTIN                              *
     A*    CREATE DATE  : 78/10/06                                   *
     A*    UPDATE DATE  : 78/11/29                                   *
     A*    PROGRAM NAME : FIA210D@                                   *
     A*    SYSTEM       :財務系統                                  *
     A*    SUBSYSTEM    :總帳系統                                  *
     A*    REMARK       :銷退折讓類別維護                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*===============================================================*
     A*  SC01   : DSPC1                    * FOR MAIN SCREEN          *
     A*===============================================================*
     A          R DSPC1
     A                                      CA03(03)
     A                                      CF18(18)
     A                                  1  2'<FIA210>'
     A                                  1 22'***     銷退折讓類別維護    ***'
     A                                  1 64'日期:'
     A            DDATE          6  0O  1 72EDTCDE(Y)
     A                                  7 27'銷退折讓代碼:'
     A            DNM02          1A  B  7 43
     A  60                                  DSPATR(PC RI)
     A                                  9 27'作業選擇    :'
     A            DOPT           1A  B  9 43
     A  61                                  DSPATR(PC RI)
     A                                  9 46'1.新增'
     A                                 10 46'2.修改'
     A                                 11 46'4.刪除'
     A                                 12 46'5.查詢'
     A                                 23  2'PF3 =回主畫面'
     A                                    +1'PF18 =銷退折讓類別查詢'
     A  98        ERR1          50A  O 22 02MSGID(UAR0001 FIMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22 02MSGID(UAR0002 FIMF)
     A                                      DSPATR(HI)
     A  96        ERR3          50A  O 22 02MSGID(UAR0003 FIMF)
     A                                      DSPATR(HI)
     A  95        ERR4          50A  O 22 02MSGID(UAR0004 FIMF)
     A                                      DSPATR(HI)
     A  94        ERR5          50A  O 22 02MSGID(UPT0100 PTMF)
     A                                      DSPATR(HI)
     A*===============================================================*
     A*  SC02   : DSPC2                    * FOR ADD & UPD            *
     A*===============================================================*
     A          R DSPC2                     CHGINPDFT(UL)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                  1  2'<FIA210>'
     A                                  1 22'***     銷退折讓類別維護    ***'
     A                                  1 64'日期:'
     A            DDATE          6  0O  1 72EDTCDE(Y)
     A                                  2 64'作業:'
     A            DFUN           6O  O  2 72DSPATR(RI)
     A                                  7 27'銷退折讓代碼:'
     A            DNM02          1   O  7 43
     A                                  9 27'說明    :'
     A            DNM031        14   B  9 43
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  62                                  DSPATR(PC RI)
     A                                 11 27'類別   :'
     A            DNM032         1   B 11 43
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A  63                                  DSPATR(PC RI)
     A                                 11 47'1.銷貨退回'
     A                                 12 47'2.銷貨折讓'
     A                                 23  2'PF3 =回主畫面   PF12 =回前畫面'
     A  93        ERR6          50A  O 22 02MSGID(UAR0005 FIMF)
     A                                      DSPATR(HI)
     A  92        ERR7          50A  O 22 02MSGID(UAR0006 FIMF)
     A                                      DSPATR(HI)
