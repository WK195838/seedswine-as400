     A****************************************************************
     A*    DISPLAY FILE ID - ARA01D#                                 *
     A*    DISPLAY FILE NAME - ARA01D#                               *
     A*    WRITTEN BY - JAMMY                                        *
     A*    DATE WRITTEN - 89/10/19                                   *
     A****************************************************************
     A                                      PRINT
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(22)
     A*==============================================================*
     A*           SFLSR1                                             *
     A*==============================================================*
     A          R SFLSR1                    SFL
     A            RRN1           1  0O 10 02
     A            D12       R        B 10 05REFFLD(RX12 RXPF)
     A  69                                  DSPATR(RI PC)
     A            D13       R        O 10 18REFFLD(PN02 PNPF)
     A            D14       R        B 10 53REFFLD(RX13 RXPF)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A  70                                  DSPATR(RI PC)
     A            D15       R        B 10 62REFFLD(RX14 RXPF)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A  71                                  DSPATR(RI PC)
     A            D16       R        B 10 71REFFLD(RX15 RXPF)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A  72                                  DSPATR(RI PC)
     A*==============================================================*
     A*           SFLCR1                                             *
     A*==============================================================*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      SFLSIZ(08)
     A                                      SFLPAG(08)
     A N50                                  SFLDSP
     A N50                                  SFLDSPCTL
     A  50                                  SFLCLR
     A  51                                  SFLEND
     A                                  1  2'<ARA01D>'
     A                                  1 24'ももも　出貨單資料輸入　ももも'
     A                                  1 64'日期:'
     A            DDAT           6  0O  1 72EDTCDE(Y)
     A                                  3 02'公司別　:'
     A            D01       R        B  3 14REFFLD(RX01 RXPF)
     A  61                                  DSPATR(RI PC)
     A            D02       R        O  3 16REFFLD(CM03 CMPF)
     A                                  3 38'客戶代號:'
     A            D03       R        B  3 50REFFLD(RX07 RXPF)
     A  62                                  DSPATR(RI PC)
     A            D04       R        O  3 57REFFLD(CU04 CUPF)
     A                                  4 02'出貨日期:'
     A            D05       R        B  4 14REFFLD(RX06 RXPF)
     A                                      EDTCDE(4)
     A  63                                  DSPATR(RI PC)
     A                                  4 38'帳款類別:'
     A            D06       R        B  4 50REFFLD(RX10 RXPF)
     A  64                                  DSPATR(RI PC)
     A            D07           22   O  4 57
     A                                  5 02'發票號碼:'
     A            D08       R        B  5 14REFFLD(RX19 RXPF)
     A  65                                  DSPATR(RI PC)
     A                                  5 38'發票日期:'
     A            D09       R        B  5 50REFFLD(RC06 RCPF)
     A                                      EDTCDE(4)
     A  66                                  DSPATR(RI PC)
     A                                  6 02'業務員　:'
     A            D10       R        B  6 14REFFLD(RX11 RXPF)
     A  67                                  DSPATR(RI PC)
     A            D11       R        O  6 20REFFLD(EM02 EMPF)
     A                                  6 38'請款月份:'
     A            D30       R        B  6 50REFFLD(RX08 RXPF)
     A                                      EDTCDE(4)
     A  68                                  DSPATR(RI PC)
     A                                  8 04'品　號'
     A                                  8 17'品　名'
     A                                  8 56'數量'
     A                                  8 65'單價　銷貨金額'
     A                                  9 02'==================================+
     A                                      ===================================+
     A                                      =========='
     A*==============================================================*
     A*           DSPC1                                              *
     A*==============================================================*
     A          R DSPC1                     OVERLAY
     A                                 18 02'==================================+
     A                                      ===================================+
     A                                      =========='
     A  98        ERR00         50A  O 22 02MSGID(UGL0001 FIMF)
     A                                      DSPATR(HI)
     A  97        ERR01         50A  O 22 02MSGID(UPY0002 FIMF)
     A                                      DSPATR(HI)
     A  96        ERR02         50A  O 22 02MSGID(UAR0007 FIMF)
     A                                      DSPATR(HI)
     A  95        ERR03         50A  O 22 02MSGID(UAR0017 FIMF)
     A                                      DSPATR(HI)
     A  94        ERR04         50A  O 22 02MSGID(UAR0021 FIMF)
     A                                      DSPATR(HI)
     A  93        ERR05         50A  O 22 02MSGID(UAR0024 FIMF)
     A                                      DSPATR(HI)
     A  92        ERR06         50A  O 22 02MSGID(UAR0025 FIMF)
     A                                      DSPATR(HI)
     A  91        ERR07         50A  O 22 02MSGID(UAR0026 FIMF)
     A                                      DSPATR(HI)
     A  90        ERR08         50A  O 22 02MSGID(UAR0027 FIMF)
     A                                      DSPATR(HI)
     A  89        ERR09         50A  O 22 02MSGID(UAR0028 FIMF)
     A                                      DSPATR(HI)
     A  88        ERR10         50A  O 22 02MSGID(UAR0029 FIMF)
     A                                      DSPATR(HI)
     A  87        ERR11         50A  O 22 02MSGID(UAR0030 FIMF)
     A                                      DSPATR(HI)
     A  86        ERR12         50A  O 22 02MSGID(UAR0031 FIMF)
     A                                      DSPATR(HI)
     A  85        ERR13         50A  O 22 02MSGID(UAR0042 FIMF)
     A                                      DSPATR(HI)
     A  84        ERR14         50A  O 22 02MSGID(UAR0049 FIMF)
     A                                      DSPATR(HI)
     A*
     A                                 23 02'PF3=回主畫面'
     A                                    +1'PF5=參考資料'
     A*==============================================================*
     A*           SFLSR2                                             *
     A*==============================================================*
     A          R SFLSR2                    SFL
     A            RRN2           1  0O 10 02
     A            D19       R        B 10 05REFFLD(RC12 RCPF)
     A  64                                  DSPATR(RI PC)
     A            D20       R        O 10 18REFFLD(PN02 PNPF)
     A            D21       R        B 10 53REFFLD(RC13 RCPF)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A  65                                  DSPATR(RI PC)
     A            D22       R        B 10 62REFFLD(RC14 RCPF)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A  66                                  DSPATR(RI PC)
     A            D23       R        B 10 71REFFLD(RC15 RCPF)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A  67                                  DSPATR(RI PC)
     A*==============================================================*
     A*           SFLCR2                                             *
     A*==============================================================*
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      SFLSIZ(08)
     A                                      SFLPAG(08)
     A N50                                  SFLDSP
     A N50                                  SFLDSPCTL
     A  50                                  SFLCLR
     A  51                                  SFLEND
     A                                  1  2'<ARA01D>'
     A                                  1 25'ももも　發票資料輸入　ももも'
     A                                  1 64'日期:'
     A            DDAT           6  0O  1 72EDTCDE(Y)
     A                                  3 02'公司別　:'
     A            D01       R        O  3 14REFFLD(RC01 RCPF)
     A            D02       R        O  3 16REFFLD(CM03 CMPF)
     A                                  3 38'客戶代號:'
     A            D24       R        B  3 50REFFLD(RC07 RCPF)
     A  61                                  DSPATR(RI PC)
     A            D25       R        O  3 57REFFLD(CU04 CUPF)
     A                                  4 02'出貨日期:'
     A            D05       R        O  4 14REFFLD(RX06 RXPF)
     A                                      EDTCDE(4)
     A                                  4 38'帳款類別:'
     A            D06       R        O  4 50REFFLD(RC10 RCPF)
     A            D07           22   O  4 57
     A                                  5 02'發票號碼:'
     A            D08       R        O  5 14REFFLD(RC09 RCPF)
     A                                  5 38'發票日期:'
     A            D09       R        O  5 50REFFLD(RC06 RCPF)
     A                                      EDTCDE(4)
     A                                  6 02'稅別　　:'
     A            D17       R        B  6 14REFFLD(RC17 RCPF)
     A  62                                  DSPATR(RI PC)
     A            D26            8   O  6 16
     A                                  6 38'銷項稅額:'
     A            D18       R        B  6 50REFFLD(RC16 RCPF)
     A                                      EDTCDE(3)
     A                                      CHECK(RB)
     A  63                                  DSPATR(RI PC)
     A                                  8 04'品　號'
     A                                  8 17'品　名'
     A                                  8 56'數量'
     A                                  8 65'單價　發票金額'
     A                                  9 02'==================================+
     A                                      ===================================+
     A                                      =========='
     A*==============================================================*
     A*           DSPC2                                              *
     A*==============================================================*
     A          R DSPC2                     OVERLAY
     A                                 18 02'==================================+
     A                                      ===================================+
     A                                      =========='
     A  98        ERR00         50A  O 22 02MSGID(UGL0001 FIMF)
     A                                      DSPATR(HI)
     A  97        ERR01         50A  O 22 02MSGID(UPY0002 FIMF)
     A                                      DSPATR(HI)
     A  96        ERR02         50A  O 22 02MSGID(UAR0005 FIMF)
     A                                      DSPATR(HI)
     A  95        ERR03         50A  O 22 02MSGID(UAR0024 FIMF)
     A                                      DSPATR(HI)
     A  94        ERR04         50A  O 22 02MSGID(UAR0029 FIMF)
     A                                      DSPATR(HI)
     A  93        ERR05         50A  O 22 02MSGID(UAR0030 FIMF)
     A                                      DSPATR(HI)
     A  92        ERR06         50A  O 22 02MSGID(UAR0031 FIMF)
     A                                      DSPATR(HI)
     A  91        ERR07         50A  O 22 02MSGID(UAR0032 FIMF)
     A                                      DSPATR(HI)
     A  90        ERR08         50A  O 22 02MSGID(UAR0033 FIMF)
     A                                      DSPATR(HI)
     A  89        ERR09         50A  O 22 02MSGID(UAR0034 FIMF)
     A                                      DSPATR(HI)
     A  88        ERR10         50A  O 22 02MSGID(UAR0035 FIMF)
     A                                      DSPATR(HI)
     A*
     A                                 23 02'PF3=回主畫面'
     A                                    +1'PF5=參考資料'
     A                                    +1'PF12=回前畫面'
     A*==============================================================*
     A*           DSPC3                                              *
     A*==============================================================*
     A          R DSPC3                     OVERLAY
     A                                      CA03(03)
     A                                      CF12(12)
     A                                  1  2'<ARA01D>'
     A                                  1 23'ももも　出貨單／發票列印　ももも'
     A                                  1 64'日期:'
     A            DDAT           6  0O  1 72EDTCDE(Y)
     A            D27       R        O  8 29REFFLD(RX04 RXPF)
     A                                  8 40'出貨單列印'
     A            D28       R        O 10 29REFFLD(RC04 RCPF)
     A                                 10 40'發票列印'
     A                                 13 28'印表機代號:'
     A            D29            5   B 13 42
     A  61                                  DSPATR(RI PC)
     A*
     A  98        ERR00         50A  O 22 02MSGID(UAR0036 FIMF)
     A                                      DSPATR(HI)
     A                                 23 02'PF3=回主畫面'
     A                                    +1'PF12=取消列印'
