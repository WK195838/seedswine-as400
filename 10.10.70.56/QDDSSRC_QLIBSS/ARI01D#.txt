     A****************************************************************
     A*    DSPF NAME    : ARI01D#                                    *
     A*    AUTHOR       : A1055  SUNNY                               *
     A*    CREATE DATE  : 78/10/12                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : ARI01D#                                    *
     A*    SYSTEM       :財務系統                                  *
     A*    SUBSYSTEM    :應收系統                                  *
     A*    REMARK       :客戶銷貨明細查詢                          *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RCPF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A*===============================================================*
     A*  SC01   : DSPC1                    * FOR MAIN SCREEN          *
     A*===============================================================*
     A          R DSPC1
     A                                      OVERLAY
     A                                      CA03(03)
     A                                  1  3'<ARI01D>'
     A                                  1 23'* * *'
     A                                  1 30'客戶銷貨明細查詢'
     A                                  1 49'* * *'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  7 27'公司別　:'
     A            DRC01     R        B  7 39REFFLD(RC01 RCPF)
     A  61                                  DSPATR(PC RI)
     A                                  9 27'客戶代號:'
     A            DRC07     R        B  9 39REFFLD(RC07 RCPF)
     A  62                                  DSPATR(PC RI)
     A                                 11 27'單據日期:'
     A            DRC06A    R        B 11 39REFFLD(RC06 RCPF)
     A                                      EDTCDE(Y)
     A  63                                  DSPATR(PC RI)
     A                                 11 48'-'
     A            DRC06B    R        B 11 50REFFLD(RC06 RCPF)
     A                                      EDTCDE(Y)
     A  64                                  DSPATR(PC RI)
     A                                 13 27'帳款種類:'
     A            DCM04     R        B 13 39REFFLD(CM04 CMPF)
     A  65                                  DSPATR(PC RI)
     A                                 13 42'(1.出貨單  2.發票)'
     A                                 23  2'PF3=回主畫面'
     A  71        ERR1          50A  O 22  2MSGID(UAR0005 FIMF)
     A                                      DSPATR(HI)
     A  72        ERR2          50A  O 22  2MSGID(UAR0007 FIMF)
     A                                      DSPATR(HI)
     A  73        ERR3          50A  O 22  2MSGID(UAR0025 FIMF)
     A                                      DSPATR(HI)
     A  74        ERR4          50A  O 22  2MSGID(UAR0063 FIMF)
     A                                      DSPATR(HI)
     A  75        ERR5          50A  O 22  2MSGID(UAR0054 FIMF)
     A                                      DSPATR(HI)
     A  76        ERR6          50A  O 22  2MSGID(UAR0064 FIMF)
     A                                      DSPATR(HI)
     A*===============================================================*
     A*  SC02   : DSPC2S (SUBFILE)         * FOR INQUIRY              *
     A*===============================================================*
     A          R DSPC2S                    SFL
     A            RRN            4S 0H
     A            RX06      R        O  8  2REFFLD(RX06 RXPF)
     A                                      EDTCDE(Y)
     A            RX04      R        O  8 12REFFLD(RX04 RXPF)
     A            DPN02X        26A  O  8 23
     A            RX13      R        O  8 50REFFLD(RX13 RXPF)
     A                                      EDTCDE(3)
     A            RX14      R        O  8 59REFFLD(RX14 RXPF)
     A                                      EDTCDE(3)
     A            RX15      R        O  8 68REFFLD(RX15 RXPF)
     A                                      EDTCDE(3)
     A*===============================================================*
     A*  SC02   : DSPC2  (SUBFILE CONTROL) * FOR INQUIRY              *
     A*===============================================================*
     A          R DSPC2                     SFLCTL(DSPC2S)
     A                                      OVERLAY
     A                                      SFLSIZ(0240)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      ROLLDOWN(26 '前一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(UPT2030 PTMF)
     A  97                                  SFLMSGID(UPT2040 PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  3'<ARI01D>'
     A                                  1 19'* * *'
     A                                  1 26'客戶銷貨明細查詢(內)'
     A                                  1 51'* * *'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  3  1'公司別　:'
     A            DRC01     R        O  3 14REFFLD(RC01 RCPF)
     A            CM03      R        O  3 16REFFLD(CM03 CMPF)
     A                                  3 47'客戶代號:'
     A            DRC07     R        O  3 59REFFLD(RC07 RCPF)
     A            CU04      R        O  3 66REFFLD(CU04 CUPF)
     A                                  4  1'出貨日期:'
     A            DRC06A    R        O  4 14REFFLD(RC06 RCPF)
     A                                      EDTCDE(Y)
     A                                  4 23'-'
     A            DRC06B    R        O  4 25REFFLD(RC06 RCPF)
     A                                      EDTCDE(Y)
     A                                  6  1'出貨日期　出貨單號　　品　　　名'
     A                                  6 51'數量'
     A                                  6 60'單價'
     A                                  6 69'金　額'
     A                                  7  2'==================================-
     A                                      ===================================-
     A                                      =========='
     A*===============================================================*
     A*  SC03   : DSPC3S (SUBFILE)         * FOR INQUIRY              *
     A*===============================================================*
     A          R DSPC3S                    SFL
     A            RRN            4S 0H
     A            RC06      R        O  8  2REFFLD(RC06 RCPF)
     A                                      EDTCDE(Y)
     A            RC04      R        O  8 12REFFLD(RC04 RCPF)
     A            DPN02C        26A  O  8 23
     A            RC13      R        O  8 50REFFLD(RC13 RCPF)
     A                                      EDTCDE(3)
     A            RC15      R        O  8 59REFFLD(RC15 RCPF)
     A                                      EDTCDE(3)
     A            RC20      R        O  8 69REFFLD(RC20 RCPF)
     A*===============================================================*
     A*  SC03   : DSPC3  (SUBFILE CONTROL) * FOR INQUIRY              *
     A*===============================================================*
     A          R DSPC3                     OVERLAY
     A                                      SFLCTL(DSPC3S)
     A                                      SFLSIZ(0240)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(25 '下一頁')
     A                                      ROLLDOWN(26 '前一頁')
     A                                      CA03(03)
     A                                      CA12(12)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(UPT2030 PTMF)
     A  97                                  SFLMSGID(UPT2040 PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  3'<ARI01D>'
     A                                  1 23'* * *'
     A                                  1 30'客戶銷貨明細查詢'
     A                                  1 49'* * *'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  3  1'公司別　:'
     A            DRC01     R        O  3 14REFFLD(RC01 RCPF)
     A            CM03      R        O  3 16REFFLD(CM03 CMPF)
     A                                  3 47'客戶代號:'
     A            DRC07     R        O  3 59REFFLD(RC07 RCPF)
     A            CU04      R        O  3 66REFFLD(CU04 CUPF)
     A                                  4  1'發票日期:'
     A            DRC06A    R        O  4 14REFFLD(RC06 RCPF)
     A                                      EDTCDE(Y)
     A                                  4 23'-'
     A            DRC06B    R        O  4 25REFFLD(RC06 RCPF)
     A                                      EDTCDE(Y)
     A                                  6  1'發票日期　發票號碼　　品　　　名'
     A                                  6 51'數量'
     A                                  6 60'金　額　出貨單號'
     A                                  7  2'==================================-
     A                                      ===================================-
     A                                      =========='
     A          R DSPC11                    OVERLAY
     A                                 20  2'==================================-
     A                                      ===================================-
     A                                      =========='
     A                                 23  2'PF3=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 39'PA1=下一頁'
     A                                 23 55'PA2=上一頁'
