     A*  90/02/10  10:50:28    A1046       REL-R01M02  5728-PW1
     A****************************************************************
     A*    DSPF NAME    : S@A05D                                     *
     A*    AUTHOR       : A1046  JOHNSON CHEN                        *
     A*    CREATE DATE  : 78/11/29                                   *
     A*    UPDATE DATE  : 79/02/10                                   *
     A*    PROGRAM NAME : S@A05                                      *
     A*    SYSTEM       :                                            *
     A*    SUBSYSTEM    :安全管制系統                              *
     A*    REMARK       :程式使用權設定（依程式別）                *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/S@RF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPC1
     A*  90/02/10  10:50:28    A1046       REL-R01M02  5728-PW1
     A                                      CF03(03)
     A                                  1  2'<S@A05D>'
     A                                  1 19'***'
     A                                  1 25'程式使用權設定（依程式別）'
     A                                  1 56'***'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<S@A05.1>'
     A                                  9 24'程式代號   :'
     A            DSU02     R        B  9 39REFFLD(SU02)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                 11 24'所在LIBRARY:'
     A            DSU03     R        B 11 39REFFLD(SU03)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                 23  2'PF3 =回主畫面'
     A                                 23 19'執行＝程式使用權設定'
     A  98        ERR1          50A  O 22  2MSGID(UPT 0010 PTMF)
     A                                      DSPATR(HI)
     A  97        ERR2          50A  O 22  2MSGID(UPT 2010 PTMF)
     A                                      DSPATR(HI)
     A* 80        ERR3          50A  O 22  2MSGID(UPT 2023 PTMF)
     A*                                     DSPATR(HI)
     A          R SFLSR                     SFL
     A*  90/02/10  10:50:28    A1046       REL-R01M02  5728-PW1
     A  54                                  SFLNXTCHG
     A            RRN            3S 0O 10  2
     A            DOPT           1A  B 10  8VALUES('Y' 'N')
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A            DSU01     R        B 10 12REFFLD(SU01)
     A  29                                  DSPATR(PR)
     A N29                                  DSPATR(UL)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A          R SFLCR                     SFLCTL(SFLSR)
     A*  90/02/10  10:50:28    A1046       REL-R01M02  5728-PW1
     A                                      ROLLUP(25 '下一頁')
     A                                      ROLLDOWN(26 '前一頁')
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0036)
     A                                      SFLLIN(0007)
     A  98                                  SFLMSGID(UPT2030 *LIBL/PTMF)
     A  97                                  SFLMSGID(UPT2040 *LIBL/PTMF)
     A            DRRN           3S 0H      SFLRCDNBR
     A                                  1  2'<S@A05D>'
     A                                  1 19'***'
     A                                  1 25'程式使用權設定（依程式別）'
     A                                  1 56'***'
     A                                  1 64'日期:'
     A            DDATE          6Y 0O  1 72EDTCDE(Y)
     A                                  2  2'<S@A05.2>'
     A                                  3  2'程式代號   :'
     A            DSU02     R        O  3 17REFFLD(SU02)
     A  64                                  DSPATR(RI)
     A                                  4  2'程式說明   :'
     A            DSP02     R        O  4 17REFFLD(SP02)
     A                                  5  3'LIBRARY'
     A                                  5 15':'
     A            DSP03     R        O  5 17REFFLD(SP03)
     A                                  6  2'子系統代號 :'
     A            DSP04     R        O  6 17REFFLD(SP04)
     A                                  6 35'OP =使用權設定'
     A                                  6 52'(Y=可 N=不可)'
     A                                  7  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  8  1'序號'
     A                                  8  8'OP'
     A                                  8 11'使用者名稱'
     A                                  8 28'序號'
     A                                  8 35'OP'
     A                                  8 38'使用者名稱'
     A                                  8 55'序號'
     A                                  8 62'OP'
     A                                  8 65'使用者名稱'
     A                                  9  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A          R DSPC11                    OVERLAY
     A                                 23  2'PF3=回主畫面'
     A                                 23 20'PF12=回前畫面'
     A                                 23 39'PA1=下一頁'
     A                                 23 55'PA2=上一頁'
     A  96        ERR3          70A  O 22  2MSGID(UPT 2022 PTMF)
     A                                      DSPATR(HI)
     A  92        ERR5          70A  O 22  2MSGID(UPT 2050 PTMF)
     A                                      DSPATR(HI)
     A  80        ERR1          70A  O 22  2MSGID(UPT 2023 PTMF)
     A                                      DSPATR(HI)
