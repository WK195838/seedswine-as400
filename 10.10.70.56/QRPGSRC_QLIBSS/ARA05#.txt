     H****************************************************************
     H*    PROGRAM NAME : ARA05                                      *
     H*    AUTHER       : A1038 CATHY SUN                            *
     H*    CREATE DATE  : 78/10/03                                   *
     H*    UPDATE DATE  : 78/10/13                                   *
     F*    SYSTEM       :財務系統                                  *
     F*    SUBSYSTEM    :人事薪資系統                              *
     H*    PROCEDURE    :                                            *
     H*    PURPOSE      : 銷貨退回折讓資料輸入                     *
     H****************************************************************
     FCMPF    IF  E           K        DISK
     FRCPF    UF  E           K        DISK                      A
     FRXPF    UF  E           K        DISK                      A
     FRAPF    O   E           K        DISK
     FNMPF    IF  E           K        DISK
     FPNPF    IF  E           K        DISK
     FARA05D# CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR1
     E*
     E                    WX1         9  9 2
     E                    WC1         9  9 2
     E                    WX2         9  7 2
     E                    WC2         9  7 2
     I*
     I            DS
     I                                        1   2 WTYPE
     I                                        1   1 WTYPE1
     I                                        2   2 WTYPE2
     I           UDS
     I                                      101 110 WUSER
     I                                      111 1160DDAT
     C*
     C           KEYCM     KLIST
     C                     KFLD           CM01    1
     C*
     C           KEYRC     KLIST
     C                     KFLD           D01
     C                     KFLD           WRC03
     C                     KFLD           D05
     C                     KFLD           WRC05
     C*
     C           KEYRX     KLIST
     C                     KFLD           D01
     C                     KFLD           WRX03
     C                     KFLD           W10
     C                     KFLD           WRX05
     C*
     C           KEYNM     KLIST
     C                     KFLD           WNM01
     C                     KFLD           WNM02
     C*
     C           KEYPN     KLIST
     C                     KFLD           PN01   12
     C*=========================================================*
     C*                    MAIN ROUTINE
     C*=========================================================*
     C                     EXSR RTN100                     *INIT KEY
     C                     EXSR RTN200                     *INIT SCREEN
     C*
     C                     WRITEDSPC1
     C                     EXFMTSFLCR1
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     DOWEQ'0'
     C*
     C           *IN25     IFEQ '0'
     C           *IN26     ANDEQ'0'
     C                     EXSR RTN400                     *CLR SFL *IN
     C*
     C           *IN05     IFEQ '1'
     C                     EXSR RTN300                     *GET REF DTAA
     C                     EXSR RTN500                     *CHK SCREEN
     C                     ELSE
     C                     EXSR RTN500                     *CHK SCREEN
     C*
     C           *IN99     IFEQ '0'
     C                     EXSR RTN600                     *SAVE DATA
     C                     END
     C*
     C                     END
     C*
     C                     ELSE
     C           *IN25     IFEQ '1'                        *ROLLUP
     C                     SETON                     86    *LST PAG
     C                     Z-ADD91        DSPREC
     C                     END
     C           *IN26     IFEQ '1'                        *ROLLDOWN
     C                     SETON                     87    *1ST PAG
     C                     Z-ADD1         DSPREC
     C                     END
     C*
     C                     END
     C*
     C                     WRITEDSPC1
     C                     EXFMTSFLCR1
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C                     END                             *END IF
     C*
     C                     SETON                     LR
     C*=========================================================*
     C*          RTN100....INITIAL KEY VALUE
     C*=========================================================*
     C           RTN100    BEGSR
     C*
     C                     MOVE '11'      WRC03   2
     C                     Z-ADD01        WRC05   20
     C                     MOVE '11'      WRX03   2
     C                     Z-ADD01        WRX05   20
     C                     MOVE '21'      WNM01   2
     C                     MOVE *BLANK    WNM02  15
     C                     MOVE *BLANK    W10    10
     C*
     C                     ENDSR
     C*=========================================================*
     C*          RTN200...INITIAL SCREEN VALUE
     C*=========================================================*
     C           RTN200    BEGSR
     C*: INITAL HEADING VALUE :
     C           *IN43     IFEQ '0'
     C                     MOVE *BLANK    D01
     C                     MOVE *BLANK    CM03
     C                     Z-ADD0         D03
     C                     SETON                     43    *SFLCLR
     C                     END
     C*: CLEAR SUBFILE :
     C                     SETON                     50    *SFLCLR
     C                     WRITESFLCR1
     C                     SETOF                     50
     C*: INTIAL SUBFILE VALUE :
     C                     Z-ADD0         RRN
     C                     SETOF                     51    *SFLEND
     C           *IN51     DOWEQ'0'
     C                     ADD  1         RRN
     C                     MOVE *BLANK    D04
     C                     MOVE *BLANK    D05
     C                     MOVE *BLANK    D06
     C                     MOVE *BLANK    D10
     C                     MOVE *BLANK    D11
     C                     Z-ADD0         D07
     C                     Z-ADD0         D08
     C                     Z-ADD0         D09
     C                     WRITESFLSR1                   51*SFLEND
     C                     END
     C*: SHOW SUBFILE 1ST PAGE :
     C                     Z-ADD1         DSPREC
     C                     ENDSR
     C*=========================================================*
     C*          RTN300....GET REFERENCE DATA
     C*=========================================================*
     C           RTN300    BEGSR
     C*
     C*                    MOVE D01       CM01
     C*          KEYCM     CHAINCMPF                 40    *NF
     C*          *LOVAL    SETLLCMPF
     C*
     C                     Z-ADD0         RRN
     C                     SETOF                     40
     C           *IN40     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSFLSR1               40    *NF
     C           *IN40     IFEQ '0'
     C*
     C           D05       IFNE *BLANK
     C           KEYRC     CHAINRCPF                 42    *NF
     C           *IN42     IFEQ '0'
     C                     MOVE *BLANK    D10
     C                     MOVELRC20      D10
     C                     UPDATSFLSR1
     C                     END
     C                     END
     C*
     C                     END
     C                     END
     C*
     C           *LOVAL    SETLLRCPF
     C*
     C                     ENDSR
     C*=========================================================*
     C*          RTN400....CLEAR SUBFILE INDICATOR
     C*=========================================================*
     C           RTN400    BEGSR
     C                     Z-ADD0         RRN
     C                     SETOF                     40
     C           *IN40     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSFLSR1               40
     C           *IN40     IFEQ '0'
     C                     UPDATSFLSR1
     C                     END
     C                     END
     C                     ENDSR
     C*=========================================================*
     C*          RTN500....CHECK SCREEN
     C*=========================================================*
     C           RTN500    BEGSR
     C*: CHECK COMPANY ID :
     C           D01       IFEQ *BLANK                     *CHK BLANK
     C                     SETON                     619899
     C                     END
     C   99                GOTO END500
     C*
     C                     MOVE D01       CM01             *CHK VALUE
     C           KEYCM     CHAINCMPF                 40
     C           *IN40     IFEQ '0'
     C           CM04      IFEQ *BLANK
     C                     SETON                     619899
     C                     MOVE *BLANK    CM03
     C                     END
     C                     END
     C           *IN40     IFEQ '1'
     C                     SETON                     619899
     C                     END
     C           *LOVAL    SETLLCMPF
     C   99                GOTO END500
     C*: CHECK DATE :
     C                     CALL 'P04'
     C                     PARM           D03
     C           WCHKD     PARM           WCHKD   1
     C           WCHKD     IFEQ 'N'
     C                     SETON                     629799
     C                     END
     C   99                GOTO END500
     C*: CHECK SUBFILE :
     C                     Z-ADD0         RRN              *CHK BLANK
     C                     SETOF                     40    *NF
     C           *IN40     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSFLSR1               40    *NF
     C           *IN40     IFEQ '0'
     C*
     C           D04       IFNE *BLANK
     C           D05       ORNE *BLANK
     C           D06       ORNE *BLANK
     C           D10       ORNE *BLANK
     C           D11       ORNE *BLANK
     C           D07       ORNE 0
     C           D08       ORNE 0
     C           D09       ORNE 0
     C                     SETON                     40    *EXIT
     C                     END
     C*
     C                     ELSE
     C           1         CHAINSFLSR1               49    *NF
     C                     SETON                     639699
     C                     UPDATSFLSR1
     C                     Z-ADD1         DSPREC
     C                     END
     C                     END
     C                     SETOF                     4049
     C   99                GOTO END500
     C*                                                   *CHK VALUE
     C                     SETOF                     40
     C                     Z-ADD0         RRN
     C           *IN40     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSFLSR1               40
     C*
     C           *IN40     IFEQ '0'
     C                     EXSR RTN510                     *CHK DTA
     C           *IN99     IFEQ '1'
     C                     UPDATSFLSR1
     C                     SETON                     40    *EXIT
     C                     Z-ADDRRN       DSPREC
     C                     END
     C                     END
     C*
     C                     END
     C                     SETOF                     40
     C   99                GOTO END500
     C*
     C           END500    ENDSR
     C*=========================================================*
     C*          RTN510....CHECK SUBFILE DATA VALUE
     C*=========================================================*
     C           RTN510    BEGSR
     C*
     C           D04       IFEQ *BLANK
     C           D05       IFNE *BLANK
     C           D06       ORNE *BLANK
     C           D10       ORNE *BLANK
     C           D11       ORNE *BLANK
     C           D07       ORNE 0
     C           D08       ORNE 0
     C           D09       ORNE 0
     C                     SETON                     639699
     C                     END
     C                     END
     C   99                GOTO END510
     C*
     C           D04       IFNE *BLANK
     C                     MOVELD04       WNM02
     C           KEYNM     CHAINNMPF                 42    *NF
     C           *IN42     IFEQ '1'
     C                     SETON                     639599
     C                     END
     C                     END
     C           *LOVAL    SETLLNMPF
     C   99                GOTO END510
     C*
     C           D04       IFNE *BLANK
     C*
     C           D05       IFEQ *BLANK
     C           D10       ANDEQ*BLANK
     C                     SETON                     9699
     C                     SETON                     6466
     C                     END
     C           D08       IFEQ 0
     C                     SETON                     689699
     C                     END
     C*
     C                     END
     C   99                GOTO END510
     C*
     C           D05       IFNE *BLANK
     C           D10       ANDEQ*BLANK
     C*
     C           KEYRC     CHAINRCPF                 42
     C           *IN42     IFEQ '1'
     C                     SETON                     649499
     C                     END
     C*
     C           *IN42     IFEQ '0'
     C           RC97      IFNE *BLANK
     C           RC97      IFEQ 'C'
     C                     SETON                     649399
     C                     ELSE
     C                     SETON                     649499
     C                     END
     C                     ELSE
     C                     MOVE *BLANK    D10
     C                     MOVELRC20      D10
     C                     END
     C                     END
     C*
     C                     END
     C           *LOVAL    SETLLRCPF
     C   99                GOTO END510
     C*
     C           D06       IFNE *BLANK
     C                     MOVELD06       PN01
     C           KEYPN     CHAINPNPF                 42    *NF
     C           *IN42     IFEQ '1'
     C                     SETON                     659299
     C                     END
     C                     END
     C           *LOVAL    SETLLPNPF
     C   99                GOTO END510
     C*
     C           D10       IFNE *BLANK
     C*
     C                     MOVELD10       W10
     C           KEYRX     CHAINRXPF                 42
     C           *IN42     IFEQ '1'
     C                     SETON                     669199
     C                     END
     C           *IN42     IFEQ '0'
     C           RX97      IFNE *BLANK
     C                     SETON                     669199
     C                     END
     C                     END
     C*
     C                     END
     C           *LOVAL    SETLLRXPF
     C   99                GOTO END510
     C*
     C           D04       IFNE *BLANK
     C           D10       IFEQ *BLANK
     C                     SETON                     669699
     C                     END
     C                     END
     C   99                GOTO END510
     C*
     C           D04       IFNE *BLANK
     C           D11       IFNE '1'
     C           D11       ANDNE'2'
     C           D11       ANDNE'3'
     C                     SETON                     679099
     C                     END
     C                     END
     C   99                GOTO END510
     C*
     C           D04       IFNE *BLANK
     C           D05       IFEQ *BLANK
     C           D11       IFNE '1'
     C                     SETON                     678999
     C                     END
     C                     END
     C                     END
     C   99                GOTO END510
     C*
     C           D11       IFEQ '1'
     C           D11       OREQ '3'
     C           D03       IFLT RX06
     C                     SETON                     628899
     C                     END
     C                     END
     C   99                GOTO END510
     C*
     C           D11       IFEQ '2'
     C           D11       OREQ '3'
     C           D03       IFLT RC06
     C                     SETON                     628899
     C                     END
     C                     END
     C   99                GOTO END510
     C*
     C           END510    ENDSR
     C*=========================================================*
     C*          RTN600....SAVE DATA TO FILE
     C*=========================================================*
     C           RTN600    BEGSR
     C*
     C                     MOVE D01       S04I1   1
     C                     MOVELD03       S04I2   6
     C                     MOVE '3'       S04I3   1
     C                     CALL 'FIS04'
     C                     PARM           S04I1
     C                     PARM           S04I2
     C                     PARM           S04I3
     C                     PARM           S04O1   9
     C*
     C                     Z-ADD0         RRN
     C                     Z-ADD0         CNTER   20
     C                     Z-ADD0         WI      20
     C           *IN40     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSFLSR1               40    *NF
     C           *IN40     IFEQ '0'
     C           D04       ANDNE*BLANK
     C                     ADD  1         CNTER
     C*
     C           D11       IFEQ '1'
     C           D11       OREQ '3'
     C                     EXSR RTN610                     *SAVE RXPF(做內帳)
     C                     END
     C           D11       IFEQ '2'
     C           D11       OREQ '3'
     C                     EXSR RTN620                     *SAVE RCPF(做外帳)
     C                     END
     C*
     C                     END
     C                     END
     C*
     C                     EXSR RTN630                     *SAVE RAPF
     C                     EXSR RTN200                     *INIT SCREEN
     C*
     C                     Z-ADD1         DSPREC
     C                     ENDSR
     C*=========================================================*
     C*          RTN610....SAVE DATA TO RXPF(出貨單資料檔)
     C*=========================================================*
     C           RTN610    BEGSR
     C*: 出貨單號必須存在:
     C           D10       IFNE *BLANK
     C                     MOVELD10       W10
     C           KEYRX     CHAINRXPF                 42
     C           *IN42     IFEQ '0'
     C*
     C                     MOVE '2'       WTYPE1
     C                     MOVE D04       WTYPE2
     C                     MOVE D01       RX01
     C                     MOVE '00000'   RX02
     C                     MOVE WTYPE     RX03
     C                     MOVELS04O1     RX04
     C                     Z-ADDD03       RX06
     C*
     C           D07       IFEQ 0
     C                     Z-ADD0         WRX14   72
     C                     ELSE
     C           D08       DIV  D07       WRX14   72H
     C                     END
     C                     Z-ADDWRX14     RX14
     C                     MOVE D06       RX12
     C                     Z-ADDD07       RX13
     C                     Z-ADDD08       RX15
     C                     Z-ADDD09       RX16
     C                     Z-ADDCNTER     RX05
     C                     MOVE '1'       RX17
     C           D05       IFNE *BLANK
     C                     MOVE D05       RX19
     C                     END
     C                     MOVE *BLANK    RX97
     C                     Z-ADDDDAT      RX98
     C                     MOVE WUSER     RX99
     C                     WRITERX0
     C*:累加金額及稅額:
     C                     Z-ADD0         WI
     C                     MOVE D04       WI
     C                     ADD  D08       WX1,WI
     C                     ADD  D09       WX2,WI
     C*
     C                     END
     C                     END
     C                     ENDSR
     C*=========================================================*
     C*          RTN620....SAVE DATA TO RCPF(發票資料檔)
     C*=========================================================*
     C           RTN620    BEGSR
     C*: 發票號碼必須存在:
     C           D05       IFNE *BLANK
     C           KEYRC     CHAINRCPF                 42
     C           *IN42     IFEQ '0'
     C*
     C                     MOVE '2'       WTYPE1
     C                     MOVE D04       WTYPE2
     C                     MOVE D01       RC01
     C                     MOVE '00000'   RC02
     C                     MOVE WTYPE     RC03
     C                     MOVELS04O1     RC04
     C                     Z-ADDD03       RC06
     C*
     C           D07       IFEQ 0
     C                     Z-ADD0         WRC14   72
     C                     ELSE
     C           D08       DIV  D07       WRC14   72H
     C                     END
     C                     Z-ADDWRC14     RC14
     C                     MOVE D06       RC12
     C                     Z-ADDD07       RC13
     C                     Z-ADDD08       RC15
     C                     Z-ADDD09       RC16
     C                     Z-ADD0         RC19
     C                     Z-ADDCNTER     RC05
     C                     MOVE *BLANK    RC97
     C                     Z-ADDDDAT      RC98
     C                     MOVE WUSER     RC99
     C                     WRITERC0
     C*:累加金額及稅額:
     C                     Z-ADD0         WI
     C                     MOVE D04       WI
     C                     ADD  D08       WC1,WI
     C                     ADD  D09       WC2,WI
     C*
     C                     END
     C                     END
     C           ENDRC     ENDSR
     C*=========================================================*
     C*          RTN630....SAVE DATA TO RAPF(應收帳款交易檔)
     C*=========================================================*
     C           RTN630    BEGSR
     C*
     C                     Z-ADD1         WI
     C           KEYCM     CHAINCMPF                 40
     C*
     C           WI        DOUGT9
     C*
     C           WX1,WI    ADD  WX2,WI    WX     102
     C           WC1,WI    ADD  WC2,WI    WC     102
     C*
     C           WX        IFNE 0
     C                     MOVE CM04      RA01
     C                     Z-ADDWX1,WI    RA07
     C                     Z-ADDWX2,WI    RA08
     C                     EXSR RTN631                     *MOVE DATA
     C                     WRITERA0                         TO RAPF
     C                     END
     C           WC        IFNE 0
     C                     MOVE D01       RA01
     C                     Z-ADDWC1,WI    RA07
     C                     Z-ADDWC2,WI    RA08
     C                     EXSR RTN631                     *MOVE DATA
     C                     WRITERA0                         TO RAPF
     C                     END
     C*
     C                     ADD  1         WI
     C*
     C                     END                             *END DO
     C*
     C                     ENDSR
     C*=========================================================*
     C*          RTN631....MOVE DATA TO RAPF
     C*=========================================================*
     C           RTN631    BEGSR
     C*
     C                     MOVE '00000'   RA02
     C                     MOVE '2'       WTYPE1
     C                     MOVE WI        WTYPE2
     C                     MOVE WTYPE     RA03
     C                     MOVELS04O1     RA04
     C                     Z-ADDD03       RA05
     C                     MOVE *BLANK    RA06
     C                     MOVE '2'       RA09
     C                     MOVE *BLANK    RA10
     C                     MOVE *BLANK    RA11
     C                     Z-ADDDDAT      RA96
     C                     Z-ADDDDAT      RA98
     C                     MOVE WUSER     RA99
     C*
     C                     ENDSR
