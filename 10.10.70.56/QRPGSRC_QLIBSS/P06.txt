      ****************************************************************
      * PROGRAM ID : P06                                             *
      * SYSTEM ID  : PTPLIB                                          *
      * FUNTCTION  :貨櫃編號                                       *
      *                                                              *
      * PARAMETER : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             01   P0601I    I   *CHAR  11   待驗貨櫃編號    *
      *             02   P0611O    O   *CHAR   1   驗証結果...     *
      *                                             Y=OK , N =ERROR  *
      * W-VARABLE : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             01   P0603     N   *CHAR  26   英文字母        *
      *             02   P0604     N   *DEC   52 0 加權數          *
      *             03   P0605     N   *CHAR  10   數字編號        *
      *             04   P0606     N   *DEC   10   數字　          *
      *             05   P6A       N   *DEC   12 0 各數位之成積    *
      *             06   A1        N   *DEC   10 0 商數            *
      *             07   A2        N   *DEC   10 0 餘數暨檢查碼    *
      *                                                              *
      * CTNR-NO CHECK FLOW.....                                      *
      *                    (Y)                                       *
      *                     +- P0611O='Y'                            *
      *                     |                                        *
      *       %SST(P0601I,11,1)=BLANK   (Y)                          *
      *  CHECK CODE --------+ CHECK 11TH +-- P0611O='Y'              *
      *                     | CODE       |  SUM MVD 11 = A2          *
      *                     +------------+  IF A2=11TH CODE          *
      *                    (N)           |       ?                   *
      *                                  |                           *
      *                                  +-- P0611O='N'              *
      *                                 (N)                          *
      *                                                              *
      *                                                              *
      ****************************************************************
     E                    P0603  26  26  1               CHECK CHAR
     E                    P0604  26  26  2 0
     E                    P0605  10  10  1               CHECK CHAR    R
     E                    P0606  10  10  1 0
     E                    P6A        11 12 0
     IP0601I      DS
     I                                        1   1 P06011
     I                                        2   2 P06012
     I                                        3   3 P06013
     I                                        4   4 P06014
     I                                        5   5 P06015
     I                                        6   6 P06016
     I                                        7   7 P06017
     I                                        8   8 P06018
     I                                        9   9 P06019
     I                                       10  10 P0601A
     I                                       11  11 P0601B
      *--------------------------------------------------------------*
      *                    MAIN PROGRAM                              *
      *--------------------------------------------------------------*
     C           *ENTRY    PLIST
     C                     PARM           P0601I 11
     C                     PARM           P0611O  1
      *
     C                     CALL 'R@S005'
     C                     PARM 'GIANT/PT'S0051I  8
      *
     C                     SETOF                     9940
     C           P0601B    IFNE *BLANK
     C                     EXSR CHKCN1
     C           *IN99     IFEQ '0'
     C                     EXSR CHKCN2
     C                     END
     C                     END
     C*
     C           *IN99     IFEQ '1'
     C                     MOVE 'N'       P0611O
     C                     ELSE
     C                     MOVE 'Y'       P0611O
     C                     END
      *
     C                     RETRN
      /EJECT
      *--------------------------------------------------------------*
      *          CHKCN1....CHECK CHOP-NUMBER DIGIT                   *
      *--------------------------------------------------------------*
     C           CHKCN1    BEGSR
     C*
     C                     Z-ADD1         I       20
     C           P06011    LOKUPP0603,I                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     99
     C                     ELSE
     C           P0604,I   MULT 1         P6A,1
     C                     END
     C*
     C   99                GOTO ENDCK1
     C*
     C                     Z-ADD1         I       20
     C           P06012    LOKUPP0603,I                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     99
     C                     ELSE
     C           P0604,I   MULT 2         P6A,2
     C                     END
     C*
     C   99                GOTO ENDCK1
     C*
     C                     Z-ADD1         I       20
     C           P06013    LOKUPP0603,I                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     99
     C                     ELSE
     C           P0604,I   MULT 4         P6A,3
     C                     END
     C*
     C   99                GOTO ENDCK1
     C*
     C                     Z-ADD1         I       20
     C           P06014    LOKUPP0603,I                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     99
     C                     ELSE
     C           P0604,I   MULT 8         P6A,4
     C                     END
     C*
     C   99                GOTO ENDCK1
     C*
     C                     Z-ADD1         I       20
     C           P06015    LOKUPP0605,I                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     99
     C                     ELSE
     C           P0606,I   MULT 16        P6A,5
     C                     END
     C*
     C   99                GOTO ENDCK1
     C*
     C                     Z-ADD1         I       20
     C           P06016    LOKUPP0605,I                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     99
     C                     ELSE
     C           P0606,I   MULT 32        P6A,6
     C                     END
     C*
     C   99                GOTO ENDCK1
     C*
     C                     Z-ADD1         I       20
     C           P06017    LOKUPP0605,I                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     99
     C                     ELSE
     C           P0606,I   MULT 64        P6A,7
     C                     END
     C*
     C   99                GOTO ENDCK1
     C*
     C                     Z-ADD1         I       20
     C           P06018    LOKUPP0605,I                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     99
     C                     ELSE
     C           P0606,I   MULT 128       P6A,8
     C                     END
     C*
     C   99                GOTO ENDCK1
     C*
     C                     Z-ADD1         I       20
     C           P06019    LOKUPP0605,I                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     99
     C                     ELSE
     C           P0606,I   MULT 256       P6A,9
     C                     END
     C*
     C   99                GOTO ENDCK1
     C*
     C                     Z-ADD1         I       20
     C           P0601A    LOKUPP0605,I                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     99
     C                     ELSE
     C           P0606,I   MULT 512       P6A,10
     C                     END
     C*
     C   99                GOTO ENDCK1
     C*
     C           ENDCK1    ENDSR
      *--------------------------------------------------------------*
      *          CHKCN2....CHECK CHOP-NUMBER                         *
      *--------------------------------------------------------------*
     C           CHKCN2    BEGSR
     C                     Z-ADD0         P6A,11
     C                     XFOOTP6A       P6A,11
     C*
     C           P6A,11    DIV  11        A1     100
     C                     MVR            A2      20
     C           A2        DIV  10        A1
     C                     MVR            A2
     C*
     C                     Z-ADD1         I       20
     C           P0601B    LOKUPP0605,I                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     99
     C                     ELSE
     C           P0606,I   IFNE A2
     C                     SETON                     99
     C                     END
     C                     END
     C*
     C                     ENDSR
**
ABCDEFGHIJKLMNOPQRSTUVWXYZ
**
1012131415161718192021232425262728293031323435363738
**
0123456789
**
0123456789
