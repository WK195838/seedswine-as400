      ****************************************************************
      ****************************************************************
      *泛太資訊科技開發股份有限公司－版權所有    TEL:7313250       *
      *                                                              *
      * PROGRAM ID : P41                                             *
      * SYSTEM ID  : PTPLIB                                          *
      * UPDATE DATE: 79/06/22     A1062  JEANNY LIN                  *
      *                                                              *
      * FUN CTION  : CONVERT A NUMBER INTO CHINSE DBCS WITH UNIT     *
      *                                                              *
      * PARAMETER : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             01   P0401I    I   *DEC    9 0 待轉金額        *
      *             02   P0411O    O   *CHAR  72   中文金額        *
      *                                                              *
      * 共19字 : Ｘ億Ｘ仟Ｘ佰Ｘ拾Ｘ萬Ｘ仟Ｘ佰Ｘ拾Ｘ圓整        *
      *                                                              *
      ****************************************************************
     E                    P4101  10  10  4               數字ARRAY
     E                    P4102  10  10  4               單位ARRAY
     E                    P413       19  4               數字中文(*) R
     E                    P414        9  1               待轉金額
     E                    P415       19  4               數字中文
     E                    P416       72  1               中文金額
     E                    OEOF    2   2  1               OE/OF字元
     I            DS
     I                                        1   90P41N
     I                                        1   9 P41C
     I            DS
     I                                        1   4 CHAR
     I                                        1   1 OE
     I                                        2   2 CHAR1
     I                                        3   3 CHAR2
     I                                        4   4 OF
     C*--------------------------------------------------------------*
     C*                    PROGRAM PARAMETERS                        *
     C*--------------------------------------------------------------*
     C           *ENTRY    PLIST
     C           P41N      PARM           P4101I  90
     C                     PARM           P4111O 72
     C*--------------------------------------------------------------*
     C*                    MAIN PROGRAM                              *
     C*--------------------------------------------------------------*
     C*
     C                     CALL 'R@S005'
     C                     PARM 'GIANT/PT'S0051I  8
     C*
     C                     MOVE *BLANK    P4111O
     C                     MOVEAP41C      P414
     C                     EXSR RTN100
     C                     EXSR RTN200
     C                     EXSR RTN300
     C                     MOVEAP416      P4111O
     C                     SETON                     LR
     C                     RETRN
     C*--------------------------------------------------------------*
     C*          RTN100....CONVERT NUMBER INTO CHINESE DBCS          *
     C*--------------------------------------------------------------*
     C           RTN100    BEGSR
     C                     Z-ADD0         J       20
     C                     Z-ADD1         K       20
     C*將數字前之零轉成'*'
     C           P414,K    DOWEQ'0'
     C           K         ANDLE8
     C                     MOVE '*'       P414,K
     C                     ADD  1         K
     C                     END
     C*將數字轉成も及中文放入P413 ARRAY
     C** I :處理數字之所在位數(TOTAL : 09)
     C** J :處理文字之所在位數(TOTAL : 19)
     C                     Z-ADD1         I       20
     C                     DO   9         I       20
     C                     ADD  1         J
     C*1.數字轉為中文
     C           P414,I    IFEQ '*'
     C                     MOVE '****'    P413,J
     C                     ELSE
     C                     MOVE P414,I    X       10
     C           X         IFEQ 0
     C                     MOVE P4101,10  P413,J
     C                     ELSE
     C                     MOVE P4101,X   P413,J
     C                     END
     C                     END
     C*2.產生數字單位
     C           P413,J    IFNE '****'
     C                     ADD  1         J
     C                     MOVE P4102,I   P413,J
     C                     ELSE
     C                     ADD  1         J
     C                     MOVE '****'    P413,J
     C                     END
     C                     END
     C                     ADD  1         J
     C                     MOVE P4102,10  P413,J
     C                     ENDSR
     C*--------------------------------------------------------------*
     C*          RTN200    P413 -> P415 (去除P413 ARRAY之'****') *
     C*--------------------------------------------------------------*
     C           RTN200    BEGSR
     C                     MOVEA*BLANK    P415
     C                     Z-ADD1         I       20
     C                     Z-ADD1         J       20
     C                     DO   19        I       20
     C           P413,I    IFNE '****'
     C                     MOVE P413,I    P415,J
     C                     ADD  1         J
     C                     END
     C                     END
     C                     ENDSR
     C*--------------------------------------------------------------*
     C*          RTN300    P415 -> P416 (去除字元間之OE/OF記號)  *
     C*--------------------------------------------------------------*
     C           RTN300    BEGSR
     C                     MOVEA*BLANK    P416
     C                     MOVE OEOF,1    P416,1
     C                     Z-ADD1         I       20
     C                     Z-ADD2         J       20
     C                     DO   19        I       20
     C                     MOVE P415,I    CHAR
     C                     MOVE CHAR1     P416,J
     C                     ADD  1         J
     C                     MOVE CHAR2     P416,J
     C                     ADD  1         J
     C                     END
     C                     MOVE OEOF,2    P416,J
     C                     ENDSR
**
壹貳參肆伍陸柒捌玖零
**
億仟佰拾萬仟佰拾元整
**

