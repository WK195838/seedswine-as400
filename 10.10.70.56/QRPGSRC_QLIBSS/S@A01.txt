      ****************************************************************
      *    PROGRAM NAME : S@A01                                      *
      *    AUTHOR       : A1046  JOHNSON CHEN                        *
      *    CREATE DATE  : 78/11/29                                   *
      *    UPDATE DATE  : 79/02/12                                   *
      *    SYSTEM       :                                            *
      *    SUBSYSTEM    :安全管制系統                              *
      *    PURPOSE      :程式代號建立    (A,U,D,I)                 *
      ****************************************************************
     FS@SPPF  UF  E           K        DISK                      A
     FS@SULF01UF  E           K        DISK
     FS@A01D  CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
      *: FUNCTION DATA :
     E                    WFUN    5   5  6
      *
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
     C           KEYT      KLIST                           *S@SPPF
     C                     KFLD           SP01T  10
     C           KEYB      KLIST                           *S@SPPF
     C                     KFLD           SP01B  10
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C                     EXSR RTN010                     *INITIAL
     C           *IN03     DOWEQ'0'
      *
     C           SCID      CASEQ'SC01'    RTN100
     C           SCID      CASEQ'SC02'    RTN200
     C           SCID      CASEQ'SC03'    RTN300
     C                     END
      *
     C                     END
     C                     SETON                     LR
     C                     RETRN
      *==============================================================*
      *          RTN010....INITIAL VALUE
      *==============================================================*
     C           RTN010    BEGSR
     C                     MOVE 'SC01'    SCID    4
     C                     MOVE *BLANK    DSP01
     C                     MOVE 'N'       SP05
     C                     MOVE '1'       DOPT
     C                     ENDSR
      *==============================================================*
      *          RTN100....'SC01' SCREEN MAIN ROUTINE
      *==============================================================*
     C           RTN100    BEGSR
     C                     EXFMTDSPC1
      *: CLEAR ALL ERROR USED INDICATOR :
     C                     MOVEA*ALL'0'   *IN,60
      *
     C           *IN03     IFEQ '0'
      *
     C           *IN18     IFEQ '0'
      *: SCREEN CHECK CORRECT :
     C                     EXSR RTN110                     *SCR CHK
     C           *IN99     IFEQ '0'
     C                     MOVE 'SC02'    SCID
     C                     END
      *
     C                     ELSE
     C                     MOVE 'SC03'    SCID
     C                     END
      *
     C                     END
     C                     ENDSR
      *==============================================================*
      *          RTN110....SCREEN CHECK
      *==============================================================*
     C           RTN110    BEGSR
      *: CHECK BLANK :
     C           DSP01     IFEQ *BLANK
     C                     SETON                     609899
     C                     END
     C   99                GOTO END110
     C           DOPT      IFEQ *BLANK
     C                     SETON                     619899
     C                     END
     C   99                GOTO END110
      *: CHECK KEY :
     C           DSP01     CHAINS@SPPF               40    *NF
     C           DOPT      IFEQ '1'                        *ADD
     C           *IN40     ANDEQ'0'
     C                     SETON                     6061
     C                     SETON                     9599
     C                     END
     C   99                GOTO END110
      *
     C           DOPT      IFNE '1'                        *UPD,DEL,INQ
     C           *IN40     ANDEQ'1'
     C                     SETON                     6061
     C                     SETON                     9499
     C                     END
      *
     C                     MOVELDOPT      TEMP    1
     C                     MOVELDSP01     WSP01
     C           END110    ENDSR
      *==============================================================*
      *          RTN200....'SC02' SCREEN MAIN ROUTINE
      *==============================================================*
     C           RTN200    BEGSR
      *: MOVE FUNTION CHAR TO 'SC02' SCREEN :
     C                     MOVE TEMP      WNUM    10
     C                     MOVE WFUN,WNUM DFUN
      *
     C           TEMP      CASEQ'1'       RTN210           *ADD
     C           TEMP      CASEQ'2'       RTN220           *UPD
     C           TEMP      CASEQ'4'       RTN230           *DEL
     C           TEMP      CASEQ'5'       RTN240           *INQ
     C                     END
     C                     ENDSR
      *==============================================================*
      *          RTN210....ADD DATA
      *==============================================================*
     C           RTN210    BEGSR
      *: CLEAR SCREEN FIELD :
     C           *IN33     DOWEQ'0'                        *CTL LOOP
      *
     C                     SETON                     31    *CAN INPUT
     C                     EXFMTDSPC2
      *: CLEAR ALL ERROR USED INDICATOR :
     C                     MOVEA*ALL'0'   *IN,60
      *
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C                     EXSR RTN810                     *CHK SCR
      *
     C           *IN99     IFEQ '0'
      *: WRITE DATA TO FILE :
     C                     MOVELWSP01     SP01
     C                     MOVELDSP02     SP02
     C                     MOVELDSP03     SP03
     C                     MOVELDSP04     SP04
     C                     Z-ADDDDATE     SPYY
     C                     MOVELUSER      SPZZ
     C                     WRITESP0
      *: RETURN PREVIOUS :
     C                     MOVE 'SC01'    SCID
     C                     SETON                     33
     C                     END
      *
     C                     ELSE
      *: RETURN PREVIOUS :
     C                     MOVE 'SC01'    SCID
     C                     SETON                     33
     C                     END
     C                     ELSE
      *: RETURN :
     C                     SETON                     33
     C                     END
      *
     C                     END
     C                     SETOF                     33
     C                     ENDSR
      *==============================================================*
      *          RTN220....UPD DATA
      *==============================================================*
     C           RTN220    BEGSR
     C                     EXSR RTN820                     *MOV DTA
     C           *IN33     DOWEQ'0'                        *CTL LOOP
      *
     C                     SETON                     31    *CAN INPUT
     C                     EXFMTDSPC2
      *: CLEAR ALL ERROR USED INDICATOR :
     C                     MOVEA*ALL'0'   *IN,60
      *
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C                     EXSR RTN810                     *CHK SCR
      *
     C           *IN99     IFEQ '0'
      *: UPDATE FILE RECORD :
     C           WSP01     CHAINSP0                  40
     C                     MOVELDSP02     SP02
     C                     MOVELDSP03     SP03
     C                     MOVELDSP04     SP04
     C                     Z-ADDDDATE     SPYY
     C                     MOVELUSER      SPZZ
     C                     UPDATSP0
     C           WSP01     SETLLSUL1                 44
     C  N44      WSP01     READESUL1                     44
     C           *IN44     DOWEQ'0'
     C                     MOVELDSP03     SU03
     C                     Z-ADDDDATE     SUYY
     C                     MOVELUSER      SUZZ
     C                     UPDATSUL1
     C           WSP01     READESUL1                     44
     C                     END
     C                     MOVE 'SC01'    SCID
     C                     SETON                     33
     C                     END
     C                     ELSE
      *: RETURN PREVIOUS :
     C                     MOVE 'SC01'    SCID
     C                     SETON                     33
     C                     END
     C                     ELSE
      *: RETURN :
     C                     SETON                     33
     C                     END
      *
     C                     END
     C                     SETOF                     33
     C                     ENDSR
      *==============================================================*
      *          RTN230....DEL DATA
      *==============================================================*
     C           RTN230    BEGSR
     C                     EXSR RTN820                     *MOV DTA
     C                     SETOF                     31    *NO INPUT
     C           T230      TAG
     C                     EXFMTDSPC2
     C                     MOVEA*ALL'0'   *IN,60
     C           *IN03     IFEQ '0'
      *
     C           *IN12     IFEQ '0'
      *: DELETE FILE RECORD :
     C           WSP01     CHAINSP0                  40
     C           WSP01     SETLLSUL1                 44
     C  N44      WSP01     READESUL1                     44
     C           *IN44     DOWEQ'0'
     C                     CALL 'S@S15C'
     C                     PARM           SU01
     C                     PARM           SU02
     C                     PARM           SU03
     C                     PARM 'N'       PUB     1
     C                     PARM           COR     1
     C           COR       IFEQ 'Y'
     C           COR       OREQ 'O'
     C                     DELETSUL1
     C                     ELSE
     C                     SETON                     8060
     C                     GOTO T230
     C                     END
     C           WSP01     READESUL1                     44
     C                     END
     C                     CALL 'S@S15C'
     C                     PARM *BLANK    I      10
     C                     PARM           SP01
     C                     PARM           SP03
     C                     PARM 'Y'       PUB     1
     C                     PARM           COR     1
     C           COR       IFEQ 'Y'
     C           COR       OREQ 'O'
     C                     DELETSP0
     C                     ELSE
     C                     SETON                     8060
     C                     GOTO T230
     C                     END
     C                     MOVE 'SC01'    SCID
     C                     ELSE
      *: RETURN PREVIOUS :
     C                     MOVE 'SC01'    SCID
     C                     END
      *: RETURN PREVIOUS :
     C                     ELSE
     C                     MOVE 'SC01'    SCID
     C                     END
     C           END230    ENDSR
      *==============================================================*
      *          RTN240....INQ DATA
      *==============================================================*
     C           RTN240    BEGSR
     C                     EXSR RTN820                     *MOV DTA
     C                     SETOF                     31    *NO INPUT
     C                     EXFMTDSPC2
      *
     C           *IN03     IFEQ '0'
      *: RETURN PREVIOUS :
     C                     MOVE 'SC01'    SCID
     C                     END
     C                     ENDSR
      *==============================================================*
      *          RTN810....CHECK 'SC02' SCREEN
      *==============================================================*
     C           RTN810    BEGSR
      *: CHECK BLANK :
     C           DSP02     IFEQ *BLANK
     C                     SETON                     629699
     C                     END
     C   99                GOTO END810
      *
     C           DSP03     IFEQ *BLANK
     C                     SETON                     639699
     C                     ELSE
     C                     CALL 'S@S16C'
     C                     PARM DSP03     I1     10
     C                     PARM WSP01     I2     10
     C                     PARM           O1      1
     C           O1        IFEQ 'L'
     C                     SETON                     638199
     C                     ELSE
     C           O1        IFEQ 'P'
     C                     SETON                     606382
     C                     SETON                     99
     C                     END
     C                     END
     C                     END
     C   99                GOTO END810
      *
     C           DSP04     IFEQ *BLANK
     C                     SETON                     649699
     C                     END
     C           END810    ENDSR
      *==============================================================*
      *          RTN820....MOVE DATA TO 'SC02' SCREEN
      *==============================================================*
     C           RTN820    BEGSR
      *:MOVE DATA TO SCREEN :
     C                     MOVELSP02      DSP02
     C                     MOVELSP03      DSP03
     C                     MOVELSP04      DSP04
      *: RELEASE RECORD LOCK :
     C           SCID      IFEQ 'SC02'
     C           TEMP      ANDEQ'5'
     C                     UPDATSP0
     C                     END
     C                     ENDSR
      *=============================================================*
      *          RTN300 :  SUBFILE ROUTINE                          *
      *=============================================================*
     C           RTN300    BEGSR
     C                     EXSR RTN310                     *INIT VARI
     C                     EXSR RTN320                     *READ NEXT
     C                     EXSR RTN330                     *EXFMT SFL
      *
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
      *
     C           *IN25     IFEQ '0'
     C           *IN26     ANDEQ'0'
     C           WRRN      ANDNE0
     C                     EXSR RTN360                     *ONE PROCESS
     C           *IN03     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     GOTO END300
     C                     END
     C                     EXSR RTN370                     *REWTITE SFL
      *
     C                     ELSE
      *
     C           *IN25     IFEQ '1'
     C           WRRN      ANDNE0
     C                     SETOF                     53
     C           KEYB      SETGTSP0                        *AFTER 1 SETLL
     C                     EXSR RTN320                     *READ NEXT
     C                     END
      *
     C           *IN26     IFEQ '1'                        *ROLLDOWN
     C           KEYT      SETLLSP0
     C                     EXSR RTN321                     *READ PREV
     C                     END
     C                     END
      *
     C                     EXSR RTN330                     *EXFMT SFL
      *
     C                     END
      *
     C           END300    TAG
     C                     MOVE 'SC01'    SCID
     C                     ENDSR
      *=========================================================*
      *          RTN310 :  INITIAL                              *
      *=========================================================*
     C           RTN310    BEGSR
      *: INITIAL VALUE :
     C                     Z-ADD0         RRN     20
     C                     Z-ADD0         WRRN    20
     C                     Z-ADD1         DRRN    20
     C                     MOVE *BLANK    DOP
     C                     MOVELDSP01     SP01T
     C                     MOVELDSP01     SP01B
     C                     MOVELDSP01     WSP01
     C           KEYT      SETLLSP0                  40
      *
     C                     ENDSR
      *=========================================================*
      *          RTN320 :  READ NEXT
      *=========================================================*
     C           RTN320    BEGSR
     C                     Z-ADD0         RRN
      *
     C           RRN       DOWLT14
     C                     READ SP0                      40*EOF
      *
     C           *IN40     IFEQ '0'
     C                     ADD  1         RRN
      *
     C           RRN       IFEQ 1
     C                     EXSR RTN380                     *GET KEY TOP
     C                     END
      *
     C                     MOVELSP01      SP01B
     C                     SETOF                     53
     C                     WRITESFLSR
     C                     Z-ADDRRN       WRRN
     C                     ELSE
     C                     SETON                     53    *LST PAGE
     C  N98                SETON                     97    *LST PAGE
     C                     GOTO END320
     C                     END
      *
     C                     END                             *END DO
      *
     C           END320    ENDSR
      *==============================================================*
      *          RTN321 :  READ PREVIOUS
     C*==============================================================*
     C           RTN321    BEGSR
     C                     Z-ADD15        RRN
      *
     C           RRN       DOWGT1
     C                     READPSP0                      40*EOF
      *
     C           *IN40     IFEQ '0'
     C                     SUB  1         RRN
      *
     C           RRN       IFEQ 14
     C                     EXSR RTN380                     *GET KEY BOTTOM
     C                     END
     C                     MOVELSP01      SP01T
     C                     SETOF                     53    *DSP '+'
     C                     WRITESFLSR
     C                     Z-ADDRRN       WRRN
     C                     ELSE
     C                     SETON                     98    *FST PAGE
     C           KEYT      SETLLSP0                  40
     C                     EXSR RTN320
     C                     GOTO END321
     C                     END
      *
     C                     END
      *
     C           END321    ENDSR
      *==============================================================*
      *          RTN330... EXFMT SFL                                 *
      *==============================================================*
     C           RTN330    BEGSR
      *: CHECK SFL DATA EXIST :
     C           WRRN      IFEQ *ZEROS
     C                     SETON                     93    *NO DATA
     C                     ELSE
     C                     SETON                     50    *SFLDSP
     C                     END
      *: SHOW SCREEN :
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC11
     C                     EXFMTSFLCR
     C                     SETOF                     5051
     C                     MOVEA*ALL'0'   *IN,60
      *
     C                     ENDSR
      *==============================================================*
      *          RTN360....SINGLE RECORD MODIFY
      *==============================================================*
     C           RTN360    BEGSR
      *
     C                     READCSFLSR                    40
     C           *IN40     DOWEQ'0'
     C           DOP       IFNE *BLANK
     C                     MOVELSP01      WSP01
     C                     MOVELSP02      DSP02
     C                     MOVELSP03      DSP03
     C                     MOVELSP04      DSP04
     C                     MOVELDOP       TEMP
     C                     MOVE *BLANK    DOP
     C                     SETOF                     54
     C                     UPDATSFLSR
     C                     EXSR RTN200
     C                     END
     C                     READCSFLSR                    40
     C           *IN03     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     SETON                     40
     C                     END
     C                     END
      *
     C                     ENDSR
      *==============================================================*
      *          RTN370 :  REWRITE SUBFILE
      *==============================================================*
     C           RTN370    BEGSR
      *
     C                     SETOF                     53    *DSP '+'
     C                     Z-ADD0         WRRN
     C           KEYT      SETLLSP0
     C                     EXSR RTN320                     *READ NEXT
     C                     Z-ADDRRN       WRRN
      *
     C                     ENDSR
      *==============================================================*
      *          RTN380 :  SUBFILE CLEAR & INIT
      *==============================================================*
     C           RTN380    BEGSR
      *: CLEAR SFL DATA :
     C                     SETON                     52
     C                     WRITESFLCR
     C                     SETOF                     5254
      *: SET TOP VALUE :
     C                     MOVELSP01      SP01T
     C                     MOVELSP01      SP01B
     C                     MOVE *BLANK    DOP
     C                     ENDSR
**
新增修改      刪除查詢
