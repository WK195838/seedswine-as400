      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      *    PROGRAM NAME : BMA003                                     *
      *    AUTHOR       : A1046  JOHNSON CHEN                        *
      *    CREATE DATE  : 78/12/06                                   *
      *    UPDATE DATE  :                                            *
      *    SYSTEM       :製造系統                                  *
      *    SUBSYSTEM    :ＢＯＭ系統                                *
      *    PROCEDURE    :                                            *
      *    PURPOSE      :材料結構資料維護(A,U,D,I)                 *
      *    INDICATORS   :                                            *
      *    *IN31        : CAN INPUT                                  *
      *    *IN33        : CONTROL DO LOOP                            *
      *    *IN50        : SUBFILE DISPLAY                            *
      *    *IN51        : SUBFILE CONTROL DISPLAY                    *
      *    *IN52        : SUBFILE CLEAR                              *
      *    *IN53        : SUBFILE END                                *
      *    *IN60-*IN65  : FIELD ERROR PC & RI                        *
      *    *IN98-*IN94  : ERROR MESSAGE                              *
      *    *IN90-*IN86  : ERROR MESSAGE                              *
      *    *IN99        : SUBROUTINE ERROR DETECT & SKIP CHECK       *
      ****************************************************************
     FBMBSPF  UF  E           K        DISK                      A
     FBMNMPF  IF  E           K        DISK
     FBMPBPF  IF  E           K        DISK
     FBMA003D CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR1
      *: FUNCTION DATA :
     E                    WFUN    5   5  6
      *: DATA STRUCTURE FOR NM03 :
     I            DS
     I                                        1  72 NM03
     I                                        1  12 DNM031
     I                                       15  33 DNM032
      *: DEFINE KEY STRING FOR DPACK USE :
     I            DS
     I                                        1  60 WKEYS
     I                                        1  14 DBS01
     I                                       15  60 WBLANK
      *: LOCAL DATA AREA :
     I           UDS
     I                                      101 110 WUSER
     I                                      111 1160DDATE
      *: KEY LIST :
     C           KEYNM     KLIST                           *BMNMPF
     C                     KFLD           WNM01   2
     C                     KFLD           WNM02   6
     C           KEYBS     KLIST                           *BMBSPF
     C                     KFLD           WBS01  14
     C                     KFLD           WBS03   2
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C                     EXSR RTN010                     *INITIAL
     C           *IN03     DOWEQ'0'
      *
     C           SCID      CASEQ'SC01'    RTN100           *1ST SCREEN
     C           SCID      CASEQ'SC02'    RTN200           *2ND SCREEN
     C                     END
      *
     C                     END
     C                     SETON                     LR
      *==============================================================*
      *          RTN010....INITIAL VALUE
      *==============================================================*
     C           RTN010    BEGSR
     C                     MOVE 'SC01'    SCID    4
     C                     MOVE *BLANK    DBS01
     C                     MOVE *BLANK    DBSCPY
     C                     MOVE *BLANK    DOPT
     C                     MOVE 'DM'      WNM01
     C                     ENDSR
      *==============================================================*
      *          RTN100....'SC01' SCREEN MAIN ROUTINE
      *==============================================================*
     C           RTN100    BEGSR
     C                     EXFMTDSPC1                      *1ST SCREEN
      *: CLEAR ALL ERROR USED INDICATOR :
     C                     MOVEA*ALL'0'   *IN,60
      *****
     C           *IN03     IFEQ '0'
     C                     EXSR RTN110                     *SCR CHK
      *: SCREEN CHECK CORRECT :
     C           *IN99     IFEQ '0'                        *NO ERROR
     C                     MOVE 'SC02'    SCID
     C                     END
      *
     C                     END
      *****
     C                     ENDSR
      *==============================================================*
      *          RTN110....SCREEN CHECK
      *==============================================================*
     C           RTN110    BEGSR
      *: CHECK BLANK :
     C           DBS01     IFEQ *BLANK
     C                     SETON                     609899
     C                     END
     C   99                GOTO END110
      *
     C           DOPT      IFEQ *BLANK
     C                     SETON                     619899
     C                     END
     C   99                GOTO END110
      *: CHECK VALUE :
     C           DOPT      IFNE '1'
     C           DOPT      ANDNE'2'
     C           DOPT      ANDNE'4'
     C           DOPT      ANDNE'5'
     C                     SETON                     619799
     C                     END
     C   99                GOTO END110
      *: CHECK KEY :
     C           DOPT      IFEQ '1'                        *ADD
     C           DBS01     SETLLPB0                      40*EQ
     C           *IN40     IFEQ '0'
     C                     SETON                     609699
     C                     END
     C                     END
     C   99                GOTO END110
      ***********
     C                     MOVELDBS01     WBS01
     C                     MOVE *LOVAL    WBS03
     C           KEYBS     SETLLBS0                  40    *NF
     C                     READ BS0                      40*EOF
      *ADD
     C           DOPT      IFEQ '1'
     C           *IN40     IFEQ '0'
     C           BS01      IFEQ DBS01
     C                     SETON                     609599
     C                     END
     C                     END
     C                     ELSE
      *UPD,DEL,INQ
     C           *IN40     IFEQ '1'
     C           BS01      ORNE DBS01
     C                     SETON                     609499
     C                     END
     C                     END
     C   99                GOTO END110
      ***********
     C           DOPT      IFEQ '1'
     C           DBSCPY    IFNE *BLANK
     C                     MOVELDBSCPY    WBS01
     C                     MOVE *LOVAL    WBS03
     C           KEYBS     SETLLBS0                  40    *NF
     C                     READ BS0                      40*EOF
     C           *IN40     IFEQ '1'
     C           BS01      ORNE DBSCPY
     C                     SETON                     629499
     C                     END
     C                     END
     C                     END
     C   99                GOTO END110
     C*: CHECK DPACT :
     C           DOPT      IFEQ '2'
     C           DOPT      OREQ '4'
     C                     MOVEL'BMBSPF'  P6701I 10
     C                     MOVE *BLANK    WBLANK
     C                     MOVE WKEYS     P6702I 60
     C                     MOVE 'W'       P6703I  1
     C                     MOVE *BLANK    P6711O  1
     C                     CALL 'P67'
     C                     PARM           P6701I
     C                     PARM           P6702I
     C                     PARM           P6703I
     C           P6711O    PARM           P6711O
     C           P6711O    IFEQ 'N'
     C                     SETON                     609399
     C                     END
     C                     END
     C   99                GOTO END110
      *
     C           END110    ENDSR
      *==============================================================*
      *          RTN200....'SC02' SCREEN MAIN ROUTINE
      *==============================================================*
     C           RTN200    BEGSR
      *: INITIAL 2ND SCREEN'S INPUT FIELDS! :
     C                     MOVE 'A'       DPRO
     C                     Z-ADD0         DSEQ
     C                     MOVE *BLANK    DBS03
      *: MOVE FUNTION CHAR TO 'SC02' SCREEN :
     C                     MOVE DOPT      WNUM    10
     C                     MOVE WFUN,WNUM DFUN
      *
     C           DOPT      CASEQ'1'       RTN210           *ADD
     C           DOPT      CASEQ'2'       RTN220           *UPD
     C           DOPT      CASEQ'4'       RTN230           *DEL
     C           DOPT      CASEQ'5'       RTN240           *INQ
     C                     END
      *
     C                     ENDSR
      *==============================================================*
      *          RTN210....ADD DATA
      *==============================================================*
     C           RTN210    BEGSR
     C                     SETON                     31    *CAN INPUT
      *
     C           DBSCPY    IFNE *BLANK
     C                     EXSR RTN810                     *COPY
     C                     END
      *
     C                     EXSR RTN800                     *WRITE SFL
     C                     Z-ADD1         DRRN
      ************************************
     C           *IN33     DOWEQ'0'                        *CTL LOOP
      *
     C           RRN       IFGT *ZEROS
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETOF                     50
     C                     END
      *
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC2
     C                     EXFMTSFLCR1
      *: CLEAR ALL ERROR USED INDICATOR :
     C                     SETOF                     5051
     C                     MOVEA*ALL'0'   *IN,60
      **************************
     C           *IN03     IFEQ '0'
      *********************
     C           *IN12     IFEQ '0'
      ***********
     C           *IN25     IFEQ '1'                        *ROLLUP
     C                     SETON                     97    *LAST PAGE
     C                     Z-ADDRRN       DRRN
     C                     ELSE
      *******
     C           *IN26     IFEQ '1'                        *ROLLDOWN
     C                     SETON                     98    *FIRST PAGE
     C                     Z-ADD1         DRRN
     C                     ELSE
     C                     EXSR RTN830                     *CHK SCR
      *
     C           *IN99     IFEQ '0'
     C                     EXSR RTN211                     *PROCESS
     C                     EXSR RTN800                     *REWRT SFL
     C                     END
      *
     C                     END
      *******
     C                     END
      ***********
     C                     ELSE
     C                     MOVE 'SC01'    SCID             *GO 1ST SCR
     C                     SETON                     33    *RETURN
     C                     END
      *********************
     C                     ELSE
     C                     SETON                     33    *RETURN
     C                     END
      **************************
     C                     END
      ************************************
     C                     SETOF                     33
     C                     ENDSR
      *==============================================================*
      *          RTN220....UPD DATA
      *==============================================================*
     C           RTN220    BEGSR
     C                     SETON                     31    *CAN INPUT
     C                     EXSR RTN800                     *WRITE SFL
     C                     Z-ADD1         DRRN
      ************************************
     C           *IN33     DOWEQ'0'                        *CTL LOOP
      *
     C           RRN       IFGT *ZEROS
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETOF                     50
     C                     END
      *
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC2
     C                     EXFMTSFLCR1
      *: CLEAR ALL ERROR USED INDICATOR :
     C                     SETOF                     5051
     C                     MOVEA*ALL'0'   *IN,60
      **************************
     C           *IN03     IFEQ '0'
      *********************
     C           *IN12     IFEQ '0'
      ***********
     C           *IN25     IFEQ '1'                        *ROLLUP
     C                     SETON                     97    *LAST PAGE
     C                     Z-ADDRRN       DRRN
     C                     ELSE
      ******
     C           *IN26     IFEQ '1'                        *ROLLDOWN
     C                     SETON                     98    *FIRST PAGE
     C                     Z-ADD1         DRRN
     C                     ELSE
     C                     EXSR RTN830                     *CHK SCR
      *
     C           *IN99     IFEQ '0'
     C                     EXSR RTN211                     *PROCESS
     C                     EXSR RTN800                     *REWRT SFL
     C                     END
      *
     C                     END
      ******
     C                     END
      ***********
     C                     ELSE
     C                     MOVE 'SC01'    SCID             *GO 1ST SCR
     C                     SETON                     33    *RETURN
     C                     END
      *********************
     C                     ELSE
     C                     SETON                     33    *RETURN
     C                     END
      **************************
     C                     END
      ************************************
     C                     SETOF                     33
     C                     EXSR RTN820                     *CALL P67
     C                     ENDSR
      *==============================================================*
      *          RTN230....DEL DATA
      *==============================================================*
     C           RTN230    BEGSR
     C                     SETOF                     31    *NO INPUT
     C                     EXSR RTN800                     *WRITE SFL
     C                     Z-ADD1         DRRN
      ************************************
     C           *IN33     DOWEQ'0'                        *CTL LOOP
      *
     C           RRN       IFGT *ZEROS
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETOF                     50
     C                     END
      *
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC2
     C                     EXFMTSFLCR1
     C                     SETOF                     5051
      **************************
     C           *IN03     IFEQ '0'
      *********************
     C           *IN12     IFEQ '0'
      ***********
     C           *IN25     IFEQ '1'                        *ROLLUP
     C                     SETON                     97    *LAST PAGE
     C                     Z-ADDRRN       DRRN
     C                     ELSE
      ******
     C           *IN26     IFEQ '1'                        *ROLLDOWN
     C                     SETON                     98    *FIRST PAGE
     C                     Z-ADD1         DRRN
     C                     ELSE
      *: DELETE FILE RECORD :
     C           DBS01     SETLLBS0                  44
     C           DBS01     READEBS0                      44*READ EQ F1
      **
     C           *IN44     DOWEQ'0'                        *SFL FULL
     C                     DELETBS0
     C           DBS01     READEBS0                      44*READ EQ F1
     C                     END
      **
     C                     MOVE 'SC01'    SCID             *GO 1ST SCR
     C                     SETON                     33    *RETURN
     C                     END
      ******
     C                     END
      ***********
     C                     ELSE
     C                     MOVE 'SC01'    SCID             *GO 1ST SCR
     C                     SETON                     33    *RETURN
     C                     END
      *********************
     C                     ELSE
     C                     SETON                     33    *RETURN
     C                     END
      **************************
     C                     END
      ************************************
     C                     SETOF                     33
     C                     EXSR RTN820                     *CALL P67
     C                     ENDSR
      *==============================================================*
      *          RTN240....INQ DATA
      *==============================================================*
     C           RTN240    BEGSR
     C                     SETOF                     31    *NO INPUT
     C                     EXSR RTN800                     *WRITE SFL
     C                     Z-ADD1         DRRN
      ************************************
     C           *IN33     DOWEQ'0'                        *CTL LOOP
      *
     C           RRN       IFGT *ZEROS
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETOF                     50
     C                     END
      *
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC2
     C                     EXFMTSFLCR1
     C                     SETOF                     5051
      **************************
     C           *IN03     IFEQ '0'
      *********************
     C           *IN12     IFEQ '0'
      ***********
     C           *IN25     IFEQ '1'                        *ROLLUP
     C                     SETON                     97    *LAST PAGE
     C                     Z-ADDRRN       DRRN
     C                     ELSE
      ******
     C           *IN26     IFEQ '1'                        *ROLLDOWN
     C                     SETON                     98    *FIRST PAGE
     C                     Z-ADD1         DRRN
     C                     ELSE
     C                     MOVE 'SC01'    SCID             *GO 1ST SCR
     C                     SETON                     33    *RETURN
     C                     END
      ******
     C                     END
      ***********
     C                     ELSE
     C                     MOVE 'SC01'    SCID             *GO 1ST SCR
     C                     SETON                     33    *RETURN
     C                     END
      *********************
     C                     ELSE
     C                     SETON                     33    *RETURN
     C                     END
      **************************
     C                     END
      ************************************
     C                     SETOF                     33
     C                     ENDSR
      *==============================================================*
      *   RTN211 : PROCESS DATA
      *==============================================================*
     C           RTN211    BEGSR
      *********************
     C           DPRO      IFEQ 'A'                        *ADD
     C                     MOVELDBS01     BS01
     C                     MOVELDBS03     BS03
     C                     WRITEBS0
     C                     ELSE
     C           DSEQ      CHAINSFLSR1               40
     C                     MOVELDBS01     WBS01
     C                     MOVELDBS03S    WBS03
     C           KEYBS     CHAINBS0                  40
      ***********
     C           DPRO      IFEQ 'U'                        *UPDAT
     C                     MOVELDBS03     BS03
     C                     UPDATBS0
     C                     ELSE
     C                     DELETBS0                        *DELET
     C                     END
      ***********
     C                     END
      *********************
     C                     ENDSR
      *==============================================================*
      *   RTN800 : WRITE SUBFILE
      *==============================================================*
     C           RTN800    BEGSR
      *INITIAL VALUES
     C                     Z-ADD*ZEROS    RRN     20
     C                     Z-ADD*ZEROS    DRRN
      *SFL CLEAR
     C                     SETON                     52
     C                     WRITESFLCR1
     C                     SETOF                     5253
     C           DBS01     SETLLBS0                  53
      *********************
     C           *IN53     DOWEQ'0'                        *SFL FULL
     C           DBS01     READEBS0                      44*READ EQ F1
      ***********
     C           *IN44     IFEQ '0'                        *EOF
     C                     MOVELBS03      WNM02
     C           KEYNM     CHAINNM0                  40
      ****
     C           *IN40     IFEQ '1'
     C                     MOVE *BLANK    NM03
     C                     END
      ****
     C                     ADD  1         RRN
     C                     Z-ADDRRN       DSEQS
     C                     MOVELBS03      DBS03S
     C                     WRITESFLSR1                   53*SFLEND
     C                     ELSE
     C                     SETON                     53    *SFLEND
     C                     END
      ***********
     C                     END
      *********************
     C                     Z-ADDRRN       DRRN
     C                     ENDSR
      *==============================================================*
      *   RTN810 : COPY FROM DBSCPY TO DBS01
      *==============================================================*
     C           RTN810    BEGSR
     C           DBSCPY    SETLLBS0                  44
     C           DBSCPY    READEBS0                      44*READ EQ F1
      ***********
     C           *IN44     DOWEQ'0'                        *SFL FULL
     C                     MOVELDBS01     BS01
     C                     WRITEBS0
     C           DBSCPY    READEBS0                      44*READ EQ F1
     C                     END
      ***********
     C                     ENDSR
      *==============================================================*
      *   RTN820 : CALL 'P67'
      *==============================================================*
     C           RTN820    BEGSR
      *: RELEASE LOCK :*
     C                     MOVEL'BMBSPF'  P6701I 10
     C                     MOVE *BLANK    WBLANK
     C                     MOVE WKEYS     P6702I 60
     C                     MOVE 'D'       P6703I  1
     C                     MOVE *BLANK    P6711O  1
     C                     CALL 'P67'
     C                     PARM           P6701I
     C                     PARM           P6702I
     C                     PARM           P6703I
     C           P6711O    PARM           P6711O
      *
     C                     ENDSR
      *==============================================================*
      *          RTN830....CHECK 'SC02' SCREEN
      *==============================================================*
     C           RTN830    BEGSR
      *: CHECK BLANK :
     C           DPRO      IFEQ *BLANK
     C                     SETON                     638999
     C                     END
     C   99                GOTO END830
      *: CHECK VALUE :
     C           DPRO      IFNE 'A'
     C           DPRO      ANDNE'U'
     C           DPRO      ANDNE'D'
     C                     SETON                     639099
     C                     END
     C   99                GOTO END830
      *
     C           DPRO      IFEQ 'A'
     C           DPRO      OREQ 'U'
     C           DBS03     IFEQ *BLANK
     C                     SETON                     648999
     C                     END
     C                     END
     C   99                GOTO END830
      *
     C           DPRO      IFEQ 'U'
     C           DPRO      OREQ 'D'
     C           DSEQ      IFEQ 0
     C                     SETON                     658999
     C                     END
     C                     END
     C   99                GOTO END830
      *: DPRO = 'A' CHECK
     C           DPRO      IFEQ 'A'
     C                     MOVELDBS03     WNM02
     C           KEYNM     CHAINNM0                  40
     C           *IN40     IFEQ '1'
     C                     SETON                     648899
     C                     ELSE
     C                     MOVELDBS01     WBS01
     C                     MOVELDBS03     WBS03
     C           KEYBS     CHAINBS0                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     648799
     C                     END
     C                     END
     C                     END
     C   99                GOTO END830
      *: DPRO = 'U' CHECK
     C           DPRO      IFEQ 'U'
     C           DSEQ      CHAINSFLSR1               40
     C           *IN40     IFEQ '1'
     C                     SETON                     658699
     C                     ELSE
     C                     MOVELDBS01     WBS01
     C                     MOVELDBS03     WBS03
     C           KEYBS     CHAINBS0                  40
     C           *IN40     IFEQ '0'
     C                     SETON                     648799
     C                     ELSE
     C                     MOVELDBS03     WNM02
     C           KEYNM     CHAINNM0                  40
     C           *IN40     IFEQ '1'
     C                     SETON                     648899
     C                     END
     C                     END
     C                     END
     C                     END
     C   99                GOTO END830
      *: DPRO = 'D' CHECK
     C           DPRO      IFEQ 'D'
     C           DSEQ      CHAINSFLSR1               40
     C           *IN40     IFEQ '1'
     C                     SETON                     658699
     C                     END
     C                     END
     C   99                GOTO END830
      *
     C           END830    ENDSR
**
新增修改      刪除查詢
