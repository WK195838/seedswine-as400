     F****************************************************************
     F*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     F*                                                              *
     F*    PROGRAM NAME : FIA210#                                    *
     F*    AUTHOR       : A1040  JUSTIN                              *
     F*    CREATE DATE  : 78/10/06                                   *
     F*    UPDATE DATE  : 78/11/29 JAMMY                             *
     F*    SYSTEM       :財務系統                                  *
     F*    SUBSYSTEM    :人事薪資系統                              *
     F*    PROCEDURE    :                                            *
     F*    PURPOSE      :銷退折讓類別維護(A,U,D,I)                 *
     F****************************************************************
     FNMPF    UF  E           K        DISK                      A
     FFIA210D@CF  E                    WORKSTN
     E*: FUNCTION DATA :
     E                    WFUN    5   5  6
     I*
     I            DS
     I                                        1  14 DNM031
     I                                       15  15 DNM032
     I                                        1  15 WNM03
     I*: DEFINE KEY STRING FOR DPACK USE :
     I            DS
     I                                        1  60 WKEYS
     I                                        1   2 WNM01
     I                                        3  17 WNM02
     I                                       18  60 WBLANK
     I*: DEFINE KEY STRING FOR DPACK USE :
     I           UDS
     I                                      101 110 WUSER
     I                                      111 1160DDATE
     C           KEYNM     KLIST                           *NMPF
     C                     KFLD           WNM01
     C                     KFLD           WNM02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR RTN010                     *INITIAL
     C           *IN03     DOWEQ'0'
     C*
     C           SCID      CASEQ'SC01'    RTN100
     C           SCID      CASEQ'SC02'    RTN200
     C                     END
     C*
     C                     END
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE
     C*==============================================================*
     C           RTN010    BEGSR
     C                     MOVE 'SC01'    SCID    4
     C                     MOVE *BLANK    DNM02
     C                     MOVE *BLANK    DOPT
     C                     ENDSR
     C*==============================================================*
     C*          RTN100....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           RTN100    BEGSR
     C                     EXFMTDSPC1
     C*: CLEAR ALL ERROR USED INDICATOR :
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C*
     C           *IN18     IFEQ '0'
     C                     EXSR RTN110                     *SCR CHK
     C*: SCREEN CHECK CORRECT :
     C           *IN99     IFEQ '0'
     C                     MOVE 'SC02'    SCID
     C                     END
     C*
     C                     ELSE
     C                     CALL 'FIS210#'
     C                     PARM DNM02     S21I1   1
     C           *IN03     PARM *IN03     S21O1   1
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*==============================================================*
     C*          RTN110....SCREEN CHECK
     C*==============================================================*
     C           RTN110    BEGSR
     C*: CHECK BLANK :
     C           DNM02     IFEQ *BLANK
     C                     SETON                     609899
     C                     END
     C   99                GOTO END110
     C*: CHECK VALUE :
     C           DOPT      IFNE '1'
     C           DOPT      ANDNE'2'
     C           DOPT      ANDNE'4'
     C           DOPT      ANDNE'5'
     C                     SETON                     619799
     C                     END
     C   99                GOTO END110
     C*: CHECK KEY :
     C                     MOVE '21'      WNM01
     C                     MOVELDNM02     WNM02
     C*
     C           DOPT      IFEQ '1'                        *ADD
     C           KEYNM     SETLLNM0                      40*EQ
     C           *IN40     IFEQ '1'
     C                     SETON                     609699
     C                     END
     C                     END
     C   99                GOTO END110
     C*
     C           DOPT      IFNE '1'                        *UPD,DEL,INQ
     C           KEYNM     SETLLNM0                      40*EQ
     C           *IN40     IFEQ '0'
     C                     SETON                     609599
     C                     END
     C                     END
     C   99                GOTO END110
     C*: CHECK DPACT :*
     C           DOPT      IFEQ '2'
     C           DOPT      OREQ '4'
     C                     MOVE 'NMPF'    P6701I 10
     C                     MOVE '21'      WNM01
     C                     MOVELDNM02     WNM02
     C                     MOVE *BLANK    WBLANK
     C                     MOVE WKEYS     P6702I 60
     C                     MOVE 'W'       P6703I  1
     C                     MOVE *BLANK    P6711O  1
     C                     CALL 'P67'
     C                     PARM           P6701I
     C                     PARM           P6702I
     C                     PARM           P6703I
     C           P6711O    PARM           P6711O
     C           P6711O    IFEQ 'N'
     C                     SETON                     609499
     C                     END
     C                     END
     C   99                GOTO END110
     C*
     C           END110    ENDSR
     C*==============================================================*
     C*          RTN200....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           RTN200    BEGSR
     C*: MOVE FUNTION CHAR TO 'SC02' SCREEN :
     C                     MOVE DOPT      WNUM    10
     C                     MOVE WFUN,WNUM DFUN
     C*
     C           DOPT      CASEQ'1'       RTN210           *ADD
     C           DOPT      CASEQ'2'       RTN220           *UPD
     C           DOPT      CASEQ'4'       RTN230           *DEL
     C           DOPT      CASEQ'5'       RTN240           *INQ
     C                     END
     C                     ENDSR
     C*==============================================================*
     C*          RTN210....ADD DATA
     C*==============================================================*
     C           RTN210    BEGSR
     C*: CLEAR SCREEN FIELD :
     C                     MOVE *BLANK    DNM031
     C                     MOVE *BLANK    DNM032
     C           *IN33     DOWEQ'0'                        *CTL LOOP
     C*
     C                     SETON                     31    *CAN INPUT
     C                     EXFMTDSPC2
     C*: CLEAR ALL ERROR USED INDICATOR :
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C                     EXSR RTN810                     *CHK SCR
     C*
     C           *IN99     IFEQ '0'
     C*: WRITE DATA TO FILE :
     C                     MOVE *BLANK    NM02
     C                     MOVE *BLANK    NM03
     C                     MOVE '21'      NM01
     C                     MOVELDNM02     NM02
     C                     MOVELWNM03     NM03
     C                     Z-ADDDDATE     NM98
     C                     MOVE WUSER     NM99
     C                     WRITENM0
     C*: RETURN PREVIOUS :
     C                     MOVE 'SC01'    SCID
     C                     SETON                     33
     C                     END
     C*
     C                     ELSE
     C*: RETURN PREVIOUS :
     C                     MOVE 'SC01'    SCID
     C                     SETON                     33
     C                     END
     C                     ELSE
     C*: RETURN :
     C                     SETON                     33
     C                     END
     C*
     C                     END
     C                     SETOF                     33
     C                     ENDSR
     C*==============================================================*
     C*          RTN220....UPD DATA
     C*==============================================================*
     C           RTN220    BEGSR
     C                     EXSR RTN820                     *MOV DTA
     C           *IN33     DOWEQ'0'                        *CTL LOOP
     C*
     C                     SETON                     31    *CAN INPUT
     C                     EXFMTDSPC2
     C*: CLEAR ALL ERROR USED INDICATOR :
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C                     EXSR RTN810                     *CHK SCR
     C*
     C           *IN99     IFEQ '0'
     C*: UPDATE FILE RECORD :
     C           KEYNM     CHAINNM0                  40    *NF
     C                     MOVE *BLANK    NM03
     C                     MOVELWNM03     NM03
     C                     Z-ADDDDATE     NM98
     C                     MOVE WUSER     NM99
     C                     UPDATNM0
     C*: RELEASE LOCK :*
     C                     MOVE 'NMPF'    P6701I 10
     C                     MOVE '21'      WNM01
     C                     MOVELDNM02     WNM02
     C                     MOVE *BLANK    WBLANK
     C                     MOVE WKEYS     P6702I 60
     C                     MOVE 'D'       P6703I  1
     C                     MOVE *BLANK    P6711O  1
     C                     CALL 'P67'
     C                     PARM           P6701I
     C                     PARM           P6702I
     C                     PARM           P6703I
     C           P6711O    PARM           P6711O
     C*: RETURN PREVIOUS :
     C                     MOVE 'SC01'    SCID
     C                     SETON                     33
     C                     END
     C                     ELSE
     C*: RELEASE LOCK :*
     C                     MOVE 'NMPF'    P6701I 10
     C                     MOVE '21'      WNM01
     C                     MOVELDNM02     WNM02
     C                     MOVE *BLANK    WBLANK
     C                     MOVE WKEYS     P6702I 60
     C                     MOVE 'D'       P6703I  1
     C                     MOVE *BLANK    P6711O  1
     C                     CALL 'P67'
     C                     PARM           P6701I
     C                     PARM           P6702I
     C                     PARM           P6703I
     C           P6711O    PARM           P6711O
     C*: RETURN PREVIOUS :
     C                     MOVE 'SC01'    SCID
     C                     SETON                     33
     C                     END
     C                     ELSE
     C*: RELEASE LOCK :*
     C                     MOVE 'NMPF'    P6701I 10
     C                     MOVE '21'      WNM01
     C                     MOVELDNM02     WNM02
     C                     MOVE *BLANK    WBLANK
     C                     MOVE WKEYS     P6702I 60
     C                     MOVE 'D'       P6703I  1
     C                     MOVE *BLANK    P6711O  1
     C                     CALL 'P67'
     C                     PARM           P6701I
     C                     PARM           P6702I
     C                     PARM           P6703I
     C           P6711O    PARM           P6711O
     C*: RETURN :
     C                     SETON                     33
     C                     END
     C*
     C                     END
     C                     SETOF                     33
     C                     ENDSR
     C*==============================================================*
     C*          RTN230....DEL DATA
     C*==============================================================*
     C           RTN230    BEGSR
     C                     EXSR RTN820                     *MOV DTA
     C                     SETOF                     31    *NO INPUT
     C                     EXFMTDSPC2
     C           *IN03     IFEQ '0'
     C*
     C           *IN12     IFEQ '0'
     C*: DELETE FILE RECORD :
     C           KEYNM     CHAINNM0                  40    *NF
     C                     DELETNM0
     C                     END
     C*: RETURN PREVIOUS :
     C                     MOVE 'SC01'    SCID
     C                     END
     C*: RELEASE LOCK :*
     C                     MOVE 'NMPF'    P6701I 10
     C                     MOVE '21'      WNM01
     C                     MOVELDNM02     WNM02
     C                     MOVE *BLANK    WBLANK
     C                     MOVE WKEYS     P6702I 60
     C                     MOVE 'D'       P6703I  1
     C                     MOVE *BLANK    P6711O  1
     C                     CALL 'P67'
     C                     PARM           P6701I
     C                     PARM           P6702I
     C                     PARM           P6703I
     C           P6711O    PARM           P6711O
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN240....INQ DATA
     C*==============================================================*
     C           RTN240    BEGSR
     C                     EXSR RTN820                     *MOV DTA
     C                     SETOF                     31    *NO INPUT
     C                     EXFMTDSPC2
     C*
     C           *IN03     IFEQ '0'
     C*: RETURN PREVIOUS :
     C                     MOVE 'SC01'    SCID
     C                     END
     C                     ENDSR
     C*==============================================================*
     C*          RTN810....CHECK 'SC02' SCREEN
     C*==============================================================*
     C           RTN810    BEGSR
     C*: CHECK BLANK :
     C           DNM031    IFEQ *BLANK
     C                     SETON                     629399
     C                     END
     C   99                GOTO END810
     C*: CHECK VALUE :
     C           DNM032    IFNE '1'
     C           DNM032    ANDNE'2'
     C                     SETON                     639299
     C                     END
     C   99                GOTO END810
     C           END810    ENDSR
     C*==============================================================*
     C*          RTN820....MOVE DATA TO 'SC02' SCREEN
     C*==============================================================*
     C           RTN820    BEGSR
     C*:MOVE DATA TO SCREEN :
     C           KEYNM     CHAINNM0                  40     *NF
     C                     MOVELNM03      WNM03
     C*: RELEASE RECORD LOCK :
     C           *LOVAL    SETLLNM0
     C                     ENDSR
**
新增修改      刪除查詢
