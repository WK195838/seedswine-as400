      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      *    PROGRAM ID   : S@A14                                      *
      *    WRITTEN BY   : A1038   CATHY  SUN                         *
      *    DATE WRITTEN : 79/02/28                                   *
      *    UPDATE DATE  :                                            *
      *    SYSTEM       :                                            *
      *    SUBSYSTEM    : S@                                         *
      *    PROCEDURE    :                                            *
      *    PURPOSE      :程式代碼建立（選擇）                      *
      ****************************************************************
     FS@SPPF  UF  E           K        DISK                      A
     FS@TXPF  IF  E           K        DISK
     FS@A14D  CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
     I            DS
     I                                        1  10 ODOBNM
     I                                        1   2 SUBSYS
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C                     EXSR RTN110                     *WRITE SFL
     C                     EXSR RTN120                     *EXFMT SFL
      *
     C           *IN03     DOWEQ'0'
      *
     C           *IN25     IFEQ '0'
     C           *IN26     ANDEQ'0'
     C           RRN       ANDGT0
     C                     EXSR RTN140                     *PROCESS
     C                     SETON                     03
      *
     C                     ELSE
      *
     C           *IN25     IFEQ '1'                        *ROLLUP
     C                     SETON                     97
     C                     Z-ADDRRN       DRRN             *LAST PAGE  LL
     C                     END
      *
     C           *IN26     IFEQ '1'                        *ROLLDOWN
     C                     SETON                     98
     C                     SETOF                     53
     C                     Z-ADD1         DRRN             *FIRST PAGE LL
     C                     END
      *
     C                     END
     C  N03                EXSR RTN120                     *EXFMT SFL
     C                     END
      *
     C                     SETON                     LR
      *=========================================================*
      *          RTN110 :  INITIAL                              *
      *=========================================================*
     C           RTN110    BEGSR
      *: INTRIAL VALUE :
     C                     Z-ADD0         RRN
     C                     Z-ADD1         WRRN    30
     C                     Z-ADD1         DRRN
      *: CLEAR SFL DATA :
     C                     SETON                     52
     C                     WRITESFLCR
     C                     SETOF                     5254
      *
     C           *LOVAL    SETLLTX0                  53
      *WRITE SUBFILE
     C           *IN53     DOWEQ'0'
     C                     READ TX0                      53
     C           *IN53     IFEQ '0'
     C                     MOVE 'N'       DOPT
     C                     ADD  1         RRN
     C                     MOVELODOBTX    DDOBTX
     C                     WRITESFLSR                    53
     C                     END
     C                     END
      *
     C                     ENDSR
      *==============================================================*
      *          RTN120... EXFMT SFL                                 *
      *==============================================================*
     C           RTN120    BEGSR
      *: CHECK SFL DATA EXIST :
     C           RRN       IFGT 0
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     92    *NO RECORD
     C                     END
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC11
     C                     EXFMTSFLCR
     C                     SETOF                     5051
     C                     MOVEA*ALL'0'   *IN,60
      *
     C                     ENDSR
      *==============================================================*
      *          RTN140 :  PROCESS FILE
      *==============================================================*
     C           RTN140    BEGSR
     C                     READCSFLSR                    40
      *
     C           *IN40     DOWEQ'0'
      *
     C           DOPT      IFEQ 'Y'
     C           ODOBNM    CHAINSP0                  41
     C           *IN41     IFEQ '1'
     C                     MOVELODOBNM    SP01
     C                     MOVELDDOBTX    SP02
     C                     MOVELODLBNM    SP03
     C                     MOVELSUBSYS    SP04
     C                     MOVEL'N'       SP05
     C                     WRITESP0
     C                     END
     C                     END
     C                     SETOF                     54
     C                     UPDATSFLSR
     C                     READCSFLSR                    40
      *
     C                     END
     C           END140    ENDSR
