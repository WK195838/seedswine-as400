      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      *    PROGRAM ID   : S@A04                                      *
      *    WRITTEN BY   : A1046   JOHNSON CHEN                       *
      *    DATE WRITTEN : 79/02/09                                   *
      *    UPDATE DATE  :                                            *
      *    SYSTEM       :                                            *
      *    SUBSYSTEM    : S@                                         *
      *    PROCEDURE    :                                            *
      *    PURPOSE      :程式使用權設定－依使用者別                *
      ****************************************************************
     FS@SUPF  UF  E           K        DISK                      A
     FS@SPPF  IF  E           K        DISK
     FS@A04D  CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
      *
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
     C           KEYSU     KLIST
     C                     KFLD           WSU01  10
     C                     KFLD           WSU02  10
     C                     KFLD           WSU03  10
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C                     EXSR RTN010                     *INITIAL
     C           *IN03     DOWEQ'0'
      *
     C           SCID      CASEQ'SC01'    RTN100           *SCREEN 2
     C           SCID      CASEQ'SC02'    RTN200           *SCREEN 1
     C                     END
      *
     C                     END
     C                     SETON                     LR
      *==============================================================*
      *          RTN010....INTRIAL VALUE
      *==============================================================*
     C           RTN010    BEGSR
     C                     MOVEL'SC02'    SCID    4
     C                     MOVEL*BLANK    DSU01
     C                     MOVEL*BLANK    DSU02
     C                     MOVEL*BLANK    DSU03
     C                     MOVEL*BLANK    DSP04
     C                     ENDSR
      *==============================================================*
      *          RTN200....'SC02' SCREEN MAIN ROUTINE
      *==============================================================*
     C           RTN200    BEGSR
     C                     EXFMTDSPC1
      *: CLEAR ALL ERROR USED INDICATOR :
     C                     MOVEA*ALL'0'   *IN,60
      *
     C           *IN03     IFEQ '0'
     C                     EXSR RTN210
     C           *IN99     IFEQ '0'
     C           *IN09     IFEQ '1'
     C                     EXSR RTN220
     C                     ELSE
     C                     MOVE 'SC01'    SCID
     C                     END
     C                     END
     C                     END
      *
     C           END200    ENDSR
      *==============================================================*
      *   RTN210 : SC01 SCREEN ERROR CHECK ROUTINE
      *==============================================================*
     CSR         RTN210    BEGSR
      *
     C           DSU01     IFEQ *BLANK
     C                     SETON                     996098
     C                     GOTO END210
     C                     END
      *
     C                     CALL 'S@S11C'
     C                     PARM           CKVAL   1
     C                     PARM           DSU01  10
      *
     C           CKVAL     IFEQ '1'
     C                     SETON                     996097
     C                     GOTO END210
     C                     END
      *
     C   09                GOTO END210
      *
     C           END210    ENDSR
      *==============================================================*
      *          RTN220 : ALLUSE SET ALL PGM CAN BE USE BY USER
      *==============================================================*
     CSR         RTN220    BEGSR
     C                     MOVELDSU01     WSU01
     C                     MOVELDSU03     WSU03
      *                                                                   |
     C           *LOVAL    SETLLS@SPPF               40                   |
     C  N40                READ S@SPPF                   40               |
     C           *IN40     DOWEQ'0'                                    ---|
      *                                                                   |
     C           DSU03     IFNE *BLANK                                    |
     C           DSU03     ANDNESP03                                      |
     C                     GOTO NEX220                                    |
     C                     END
      *                                                                   |
     C           DSP04     IFNE *BLANK                                    |
     C           DSP04     ANDNESP04                                      |
     C                     GOTO NEX220                                    |
     C                     END
      *                                                                   |
     C                     MOVELSP01      WSU02
      *                                                                   |
     C           KEYSU     SETLLSU0                  41
     C  N41                READ SU0                      41
      *                                                                   |
     C                     MOVELWSU01     SU01
     C                     MOVELWSU02     SU02
     C                     MOVELSP03      SU03
     C                     MOVEL'Y'       SU04
     C                     Z-ADDDDATE     SUYY
     C                     MOVELUSER      SUZZ
     C                     CALL 'S@S09C'
     C                     PARM           SU04
     C                     PARM           SU03
     C                     PARM           SU02
     C                     PARM           SU01
     C                     PARM           COR     1
     C           COR       IFEQ 'N'
     C                     MOVELSU01      DSU01
     C                     MOVELSU02      DSU02
     C                     MOVELSU03      DSU03
     C                     MOVELSP04      DSP04
     C                     SETON                     806061
     C                     SETON                     6263
     C                     GOTO END220
     C                     END
     C           *IN41     IFEQ '1'
     C           SU01      ORNE WSU01
     C           SU02      ORNE WSU02
     C                     WRITESU0
     C                     ELSE
     C           DSU03     IFNE *BLANK
     C           KEYSU     CHAINSU0                  42
     C                     MOVELWSU01     SU01
     C                     MOVELWSU02     SU02
     C                     MOVELDSU03     SU03
     C                     MOVEL'Y'       SU04
     C                     Z-ADDDDATE     SUYY
     C                     MOVELUSER      SUZZ
     C           *IN42     IFEQ '1'
     C                     WRITESU0
     C                     ELSE
     C                     UPDATSU0
     C                     END
     C                     ELSE
     C                     UPDATSU0
     C                     END
     C                     END
      *
     C           NEX220    TAG
     C                     READ SP0                      40               |
     C                     END
      *
     C           END220    ENDSR
      *=============================================================*
      *          RTN100 :  SUBFILE ROUTINE                          *
      *=============================================================*
     C           RTN100    BEGSR
     C                     EXSR RTN110                     *WRITE SFL
     C                     EXSR RTN120                     *EXFMT SFL
      ***********
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
      ****
     C           *IN25     IFEQ '0'
     C           *IN26     ANDEQ'0'
     C           RRN       ANDGT0
     C                     EXSR RTN140                     *PROCESS
     C  N80                SETON                     12
      *
     C                     ELSE
      *
     C           *IN25     IFEQ '1'                        *ROLLUP
     C                     SETON                     97
     C                     Z-ADDRRN       DRRN             *LAST PAGE  LL
     C                     END
      *
     C           *IN26     IFEQ '1'                        *ROLLDOWN
     C                     SETON                     98
     C                     SETOF                     53
     C                     Z-ADD1         DRRN             *FIRST PAGE LL
     C                     END
     C                     END
      *
     C  N12                EXSR RTN120                     *EXFMT SFL
     C                     END
      ***********
     C                     MOVE 'SC02'    SCID
     C                     ENDSR
      *=========================================================*
      *          RTN110 :  INITIAL                              *
      *=========================================================*
     C           RTN110    BEGSR
      *: INTRIAL VALUE :
     C                     Z-ADD0         RRN
     C                     Z-ADD1         WRRN    30
     C                     Z-ADD1         DRRN
      *: CLEAR SFL DATA :
     C                     SETON                     52
     C                     WRITESFLCR
     C                     SETOF                     5254
      *
     C           DSU02     SETLLSP0                  53
     C  N53                READ SP0                      53
      *****WRITE SUBFILE
     C           *IN53     DOWEQ'0'
      *****
     C           DSU03     IFNE *BLANK
     C           DSU03     ANDNESP03                                      |
     C                     GOTO NEX110                                    |
     C                     END
      *                                                                   |
     C           DSP04     IFNE *BLANK                                    |
     C           DSP04     ANDNESP04                                      |
     C                     GOTO NEX110                                    |
     C                     END
      *
     C                     MOVELDSU01     WSU01
     C                     MOVELSP01      WSU02
     C                     MOVELSP03      WSU03
      *                                                                   |
     C           KEYSU     CHAINSU0                  41
     C           *IN41     IFEQ '1'
     C           SP05      IFEQ 'Y'
     C                     MOVE 'N'       DOPT
     C                     ELSE
     C                     MOVE 'Y'       DOPT
     C                     END
     C                     ELSE
     C                     MOVE SU04      DOPT
     C                     END
      *
     C                     ADD  1         RRN
     C                     WRITESFLSR                    53
      *****
     C           NEX110    TAG
     C           *IN53     IFEQ '0'
     C                     READ SP0                      53
     C                     END
      *
     C                     END
      ***********
     C                     ENDSR
      *==============================================================*
      *          RTN120... EXFMT SFL                                 *
      *==============================================================*
     C           RTN120    BEGSR
      *: CHECK SFL DATA EXIST :
     C           RRN       IFGT 0
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     92    *NO RECORD
     C                     END
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC11
     C                     EXFMTSFLCR
     C                     SETOF                     5051
     C                     MOVEA*ALL'0'   *IN,60
      *
     C                     ENDSR
      *==============================================================*
      *          RTN140 :  PROCESS FILE
      *==============================================================*
     C           RTN140    BEGSR
     C                     READCSFLSR                    40
      *
     C           *IN40     DOWEQ'0'
      ***********
     C                     CALL 'S@S09C'
     C                     PARM           DOPT
     C                     PARM           SP03
     C                     PARM           SP01
     C                     PARM           DSU01
     C                     PARM           COR
     C           COR       IFEQ 'Y'
     C                     MOVELDSU01     WSU01
     C                     MOVELSP01      WSU02
     C                     MOVELSP03      WSU03
     C           KEYSU     CHAINSU0                  41
     C                     MOVELWSU01     SU01
     C                     MOVELWSU02     SU02
     C                     MOVELWSU03     SU03
     C                     MOVELDOPT      SU04
     C                     Z-ADDDDATE     SUYY
     C                     MOVELUSER      SUZZ
     C*          DOPT      IFEQ 'Y'
     C           *IN41     IFEQ '1'
     C                     WRITESU0
     C                     ELSE
     C                     UPDATSU0
     C                     END
     C*                    ELSE
     C*          *IN41     IFEQ '0'
     C*                    DELETSU0
     C*                    END
     C*                    END
     C                     SETOF                     5464
     C                     ELSE
     C  N80                Z-ADDRRN       DRRN
     C                     SETON                     546480
     C                     END
     C                     UPDATSFLSR
     C                     READCSFLSR                    40
     C                     END
      ***********
     C           END140    ENDSR
