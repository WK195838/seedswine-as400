      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      *    PROGRAM ID   : S@A12                                      *
      *    WRITTEN BY   : A1046   JOHNSON CHEN                       *
      *    DATE WRITTEN : 79/02/28                                   *
      *    UPDATE DATE  :                                            *
      *    SYSTEM       :                                            *
      *    SUBSYSTEM    : S@                                         *
      *    PROCEDURE    :                                            *
      *    PURPOSE      :程式使用權隱藏                            *
      ****************************************************************
     FS@SPPF  UF  E           K        DISK
     FS@A12D  CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
      *
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C                     EXSR RTN010                     *INITIAL
     C           *IN03     DOWEQ'0'
      *
     C           SCID      CASEQ'SC01'    RTN100           *SCREEN 2
     C           SCID      CASEQ'SC02'    RTN200           *SCREEN 1
     C                     END
      *
     C                     END
     C                     SETON                     LR
      *==============================================================*
      *          RTN010....INTRIAL VALUE
      *==============================================================*
     C           RTN010    BEGSR
     C                     MOVEL'SC02'    SCID    4
     C                     MOVEL*BLANK    DSP01
     C                     MOVEL*BLANK    DSP04
     C                     ENDSR
      *==============================================================*
      *          RTN200....'SC02' SCREEN MAIN ROUTINE
      *==============================================================*
     C           RTN200    BEGSR
     C                     EXFMTDSPC1
      *: CLEAR ALL ERROR USED INDICATOR :
     C                     MOVEA*ALL'0'   *IN,60
      *
     C           *IN03     IFEQ '0'
     C           *IN09     IFEQ '1'
     C                     EXSR RTN220
     C                     ELSE
     C                     MOVE 'SC01'    SCID
     C                     END
     C                     END
      *
     C           END200    ENDSR
      *==============================================================*
      *          RTN220 : ALLUSE SET ALL PGM CAN BE USE BY USER
      *==============================================================*
     CSR         RTN220    BEGSR
      *                                                                   |
     C           *LOVAL    SETLLSP0                  40                   |
     C  N40                READ SP0                      40               |
      *                                                                   |
     C           *IN40     DOWEQ'0'                                    ---|
     C           SP05      IFEQ 'N'
     C                     CALL 'S@S13C'
     C                     PARM 'Y'       O1      1
     C                     PARM           SP01
     C                     PARM           SP03
     C                     PARM           COR     1
     C           COR       IFEQ 'Y'
     C                     MOVE 'Y'       SP05
     C                     UPDATSP0
     C                     ELSE
     C                     MOVELSP01      DSP01
     C                     MOVELSP04      DSP04
     C                     SETON                     806061
     C                     GOTO END220
     C                     END
     C                     END
     C                     READ SP0                      40               |
     C                     END
      *
     C           END220    ENDSR
      *=============================================================*
      *          RTN100 :  SUBFILE ROUTINE                          *
      *=============================================================*
     C           RTN100    BEGSR
     C                     EXSR RTN110                     *WRITE SFL
     C                     EXSR RTN120                     *EXFMT SFL
      ***********
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
      ****
     C           *IN25     IFEQ '0'
     C           *IN26     ANDEQ'0'
     C           RRN       ANDGT0
     C                     EXSR RTN140                     *PROCESS
     C  N80                SETON                     12
      *
     C                     ELSE
      *
     C           *IN25     IFEQ '1'                        *ROLLUP
     C                     SETON                     97
     C                     Z-ADDRRN       DRRN             *LAST PAGE  LL
     C                     END
      *
     C           *IN26     IFEQ '1'                        *ROLLDOWN
     C                     SETON                     98
     C                     SETOF                     53
     C                     Z-ADD1         DRRN             *FIRST PAGE LL
     C                     END
     C                     END
      *
     C  N12                EXSR RTN120                     *EXFMT SFL
     C                     END
      ***********
     C                     MOVE 'SC02'    SCID
     C                     ENDSR
      *=========================================================*
      *          RTN110 :  INITIAL                              *
      *=========================================================*
     C           RTN110    BEGSR
      *: INTRIAL VALUE :
     C                     Z-ADD0         RRN
     C                     Z-ADD1         WRRN    30
     C                     Z-ADD1         DRRN
     C           DSP01     IFEQ *BLANK
     C           DSP04     ANDNE*BLANK
     C                     MOVEL*BLANK    WSP01
     C                     MOVELDSP04     WSP01  10
     C                     ELSE
     C                     MOVEL*BLANK    WSP01
     C                     MOVELDSP01     WSP01
     C                     END
      *: CLEAR SFL DATA :
     C                     SETON                     52
     C                     WRITESFLCR
     C                     SETOF                     5254
      *
     C           WSP01     SETLLSP0                  53
     C  N53                READ SP0                      53
      *****WRITE SUBFILE
     C           *IN53     DOWEQ'0'
      ******
     C           DSP01     IFEQ *BLANK
     C           DSP04     ANDNE*BLANK                                    |
     C           SP04      ANDNEDSP04                                     |
     C                     SETON                     53
     C                     ELSE                                           |
     C                     MOVELSP05      DSP05                           |
     C                     ADD  1         RRN
     C                     WRITESFLSR                    53
      *
     C           *IN53     IFEQ '0'
     C                     READ SP0                      53
     C                     END
      *
     C                     END
      ******
     C                     END
      ***********
     C                     ENDSR
      *==============================================================*
      *          RTN120... EXFMT SFL                                 *
      *==============================================================*
     C           RTN120    BEGSR
      *: CHECK SFL DATA EXIST :
     C           RRN       IFGT 0
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     96    *NO RECORD
     C                     END
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC11
     C                     EXFMTSFLCR
     C                     SETOF                     5051
     C                     MOVEA*ALL'0'   *IN,60
      *
     C                     ENDSR
      *==============================================================*
      *          RTN140 :  PROCESS FILE
      *==============================================================*
     C           RTN140    BEGSR
     C                     READCSFLSR                    40
      *
     C           *IN40     DOWEQ'0'
      ***********
     C                     CALL 'S@S13C'
     C                     PARM           DSP05
     C                     PARM           SP01
     C                     PARM           SP03
     C                     PARM           COR
     C           COR       IFEQ 'Y'
     C           SP01      CHAINSP0                  41
     C                     MOVELDSP05     SP05
     C                     Z-ADDDDATE     SPYY
     C                     MOVELUSER      SPZZ
     C                     UPDATSP0
     C                     SETOF                     5462
     C                     ELSE
     C  N80                Z-ADDRRN       DRRN
     C                     SETON                     548062
     C                     END
     C                     UPDATSFLSR
     C                     READCSFLSR                    40
     C                     END
      ***********
     C           END140    ENDSR
