     H****************************************************************
     H*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     H*                                                              *
     H*    PROGRAM ID   : P77                                        *
     H*    PROGRAM NAME :切除文字前後空白                          *
     H*    AUTHOR       : SERMON                                     *
     H*    CREATE DATE  : 96/02/09                                   *
     H*    UPDATE DATE  :                                            *
     H****************************************************************
     E                    @S01      256  1
     I*---------------------------------------------------------------*
     I            DS
     I                                        1 256 @S01
     I                                        1 256 W7703
     C*===============================================================*
     C*                    DEFN
     C*===============================================================*
     C           *LIKE     DEFN P7701I    W7701
     C           *LIKE     DEFN P7701I    W7702
     C           *LIKE     DEFN P7702I    IDX
     C           *LIKE     DEFN P7702I    MAX
     C           *LIKE     DEFN P7702I    LEN
     C*===============================================================*
     C*                    MAIN ROUTINE
     C*===============================================================*
     C           *ENTRY    PLIST
     C                     PARM           P7701I256        *原字串
     C                     PARM           P7702I  30       *原字串長
     C                     PARM           P7703I  1        *處理方法
     C                     PARM W7703     P7701O256
     C                     PARM LEN       P7702O  30
     C                     MOVELP7701I    W7703
     C                     EXSR R0100
     C                     SELEC
     C           P7703I    WHEQ 'L'
     C                     EXSR R1000
     C           P7703I    WHEQ 'R'
     C                     EXSR R2000
     C                     OTHER
     C                     EXSR R3000
     C                     END
     C                     MOVELW7701     W7703
     C                     SETON                     LR
     C*===============================================================*
     C*                    INITIAL ROUTINE
     C*===============================================================*
     C           R0100     BEGSR
     C                     Z-ADD1         IDX
     C                     Z-ADDP7702I    MAX
     C                     MOVEL*BLANK    W7701
     C                     MOVEL*BLANK    W7702
     C                     ENDSR
     C*===============================================================*
     C*                    FUNCTION LTRIM
     C*===============================================================*
     C           R1000     BEGSR
     C           IDX       DOWLEMAX
     C           @S01,IDX  IFEQ ' '
     C                     ADD  1         IDX
     C                     ITER
     C                     ELSE
     C                     LEAVE
     C                     END
     C                     END
     C           MAX       SUB  IDX       LEN
     C                     ADD  1         LEN
     C                     MOVEA@S01,IDX  W7702
     C           LEN       SUBSTW7702:1   W7701
     C                     ENDSR
     C*===============================================================*
     C*                    FUNCTION RTRIM
     C*===============================================================*
     C           R2000     BEGSR
     C           MAX       DOWGEIDX
     C           @S01,MAX  IFEQ ' '
     C                     SUB  1         MAX
     C                     ITER
     C                     ELSE
     C                     LEAVE
     C                     END
     C                     END
     C           MAX       SUB  IDX       LEN
     C                     ADD  1         LEN
     C                     MOVEA@S01,IDX  W7702
     C           LEN       SUBSTW7702:1   W7701
     C                     ENDSR
     C*===============================================================*
     C*                    FUNCTION TRIM
     C*===============================================================*
     C           R3000     BEGSR
     C                     EXSR R1000
     C                     MOVELW7701     W7703
     C                     EXSR R0100
     C                     EXSR R2000
     C                     ENDSR
