     F*--------------------------------------------------------------*
     F*  1. AUTHOR      : SANDY_LAIW
     F*  2. CREATE DATE : 12/07/1989                                 *
     F*  3. FUNCTION    : TRANSLATE MAP TO DSPF                      *
     F*--------------------------------------------------------------*
     FQTXTSRC IF  E           K        DISK
     F            QTXTSRC                           KRENAMEQSRCPFR
     FXDSPF   O   E           K        DISK
     E*
     E                    ARY1       80  1
     E                    ARY2    1  23 70
     E                    ARY3       36  1
     E                    ARY4    1   1  1
     E                    ARY5    1   5 14
     I*--------------------------------------------------------------*
     I*  DS  FOR  DS1                                                *
     I*--------------------------------------------------------------*
     IDS1         DS
     I                                        1   5 DUMMY1
     I                                        6   6 CARD
     I                                        7   8 DUMMY2
     I                                        9  10 INOR
     I                                       11  16 DUMMY3
     I                                       17  17 REC
     I                                       18  18 DUMMY4
     I                                       19  28 NAME
     I                                       29  29 DUMMY5
     I                                       30  34 LEN
     I                                       35  35 TYPE
     I                                       36  37 DEC
     I                                       38  38 USAGE
     I                                       39  41 LINE
     I                                       42  44 BGPOS
     I                                       45  80 FUN
     I*--------------------------------------------------------------*
     I*  DS  FOR  $LEN                                               *
     I*--------------------------------------------------------------*
     I            DS
     I                                        1   1 $LEN1
     I                                        2   2 $LEN2
     I                                        1   20$LEN
     I*--------------------------------------------------------------*
     I*  DS  FOR  $DEC                                               *
     I*--------------------------------------------------------------*
     I            DS
     I                                        1   1 $DEC1
     I                                        2   2 $DEC2
     I                                        1   20$DEC
     I*--------------------------------------------------------------*
     I*  DS  FOR  $LINE                                              *
     I*--------------------------------------------------------------*
     I            DS
     I                                        1   1 $LINE1
     I                                        2   2 $LINE2
     I                                        1   20$LINE
     I*--------------------------------------------------------------*
     I*  DS  FOR  $POS                                               *
     I*--------------------------------------------------------------*
     I            DS
     I                                        1   1 $POS1
     I                                        2   2 $POS2
     I                                        1   20$POS
     I*--------------------------------------------------------------*
     I*  DS  FOR  OEOF                                               *
     I*--------------------------------------------------------------*
     IOEOF        DS
     I                                        1   1 OE
     I                                        2   2 OF
     C*--------------------------------------------------------------*
     C*  MAIN  SECTION                                               *
     C*--------------------------------------------------------------*
     C                     EXSR SR000
     C*
     C           1         DO   24        X       20
     C                     READ QSRCPFR                  46
     C           *IN46     IFEQ '1'
     C                     Z-ADD24        X
     C                     ELSE
     C           SRCDTA    IFNE *BLANK
     C                     EXSR SR010
     C                     END
     C                     END
     C*
     C                     END
     C*
     C*------------------- EXSR SR020
     C                     SETON                     LR
     C                     RETRN
     C*--------------------------------------------------------------*
     C*  SR000   MOVE COMMON SECTION TO XDSPF FILE                   *
     C*--------------------------------------------------------------*
     C           SR000     BEGSR
     C*
     C           1         DO   9         I       20
     C                     MOVELARY2,I    FIELD1
     C**  ERROR            MOVEAARY2,I    FIELD1
     C                     WRITEXDSPFR
     C                     END
     C*
oeof C                     MOVE ''      OEOF
     C                     Z-ADD75        CTINOR  20
     C                     Z-ADD0         $LEN    20
     C                     Z-ADD0         $DEC    20
     C                     MOVE 'FIRST'   $CH     5
     C*
     C           ED000     ENDSR
     C*--------------------------------------------------------------*
     C*  SR010   CHECK CHARACTER = ?                                 *
     C*--------------------------------------------------------------*
     C           SR010     BEGSR
     C*
     C                     MOVEASRCDTA    ARY1
     C           1         DO   80        Y       20
     C                     MOVEAARY1,Y    $CHAR   1
     C           $CHAR     CASEQ' '       SR100
     C*
     C           $CHAR     CASEQ'+'       SR200
     C           $CHAR     CASEQ'?'       SR200
     C*
     C           $CHAR     CASEQ'3'       SR300
     C           $CHAR     CASEQ'6'       SR300
     C           $CHAR     CASEQ'9'       SR300
     C*
     C           $CHAR     CASEQ'/'       SR400
     C           $CHAR     CASEQ','       SR400
     C           $CHAR     CASEQ'.'       SR400
     C*
     C           $CHAR     CASEQ'I'       SR500
     C           $CHAR     CASEQ'i'       SR500
     C           $CHAR     CASEQ'O'       SR500
     C           $CHAR     CASEQ'o'       SR500
     C           $CHAR     CASEQ'B'       SR500
     C           $CHAR     CASEQ'b'       SR500
     C*
     C           $CHAR     CASEQ'-'       SR600
     C*
     C           $CHAR     CASEQ'_'       SR700
     C*
     C           $CHAR     CASEQ'@'       SR800
     C*
     C           $CHAR     CASEQ'D'       SR900
     C           $CHAR     CASEQ'd'       SR900
     C           $CHAR     CASEQ'T'       SR900
     C           $CHAR     CASEQ't'       SR900
     C*
     C           $CHAR     CASEQOE        SR1000
     C           $CHAR     CASEQOF        SR1000
     C*
     C           $CHAR     CASEQARY4,1    SR1100
     C*
     C                     CAS            SR1200
     C                     END
     C                     END
     C*
     C           ED010     ENDSR
     C*--------------------------------------------------------------*
     C*  SR020   MOVE COMMON SECTION TO XDSPF FILE                   *
     C*--------------------------------------------------------------*
     C*----------SR020     BEGSR
     C*
MUST C*------------        MOVE *BLANK    FIELD1
     C*
     C*----------10        DO   23        I       20
     C*----------          MOVELARY2,I    FIELD1
     C*----------          WRITEXDSPFR
     C*----------          END
     C*
     C*----------ED020     ENDSR
     C*--------------------------------------------------------------*
     C*  SR100   IF CHARACTER = *BLANK                               *
     C*--------------------------------------------------------------*
     C           SR100     BEGSR
     C*
     C           $DATE     IFEQ 'YES'
     C                     MOVELARY5,1    $FUN
     C                     EXSR SR110
     C                     MOVE *BLANK    $@CH
     C                     MOVE *BLANK    $DATE   3
     C                     EXSR SR120
     C                     GOTO ED100
     C                     END
     C*
     C           $TIME     IFEQ 'YES'
     C                     MOVEL'TIME'    $FUN
     C                     EXSR SR110
     C                     MOVE *BLANK    $@CH
     C                     MOVE *BLANK    $TIME   3
     C                     EXSR SR120
     C                     GOTO ED100
     C                     END
     C*
     C           $VAR      IFEQ 'YES'
     C                     EXSR SR105
     C                     EXSR SR110
     C           $SETON    IFEQ 'YES'
     C                     EXSR SR130
     C                     MOVE *BLANK    $SETON  3
     C                     END
     C                     MOVE *BLANK    $VAR    3
     C                     MOVE *BLANK    $CH6    3
     C                     MOVE *BLANK    $SLASH  3
     C                     MOVE *BLANK    $COMMA  3
     C                     MOVE *BLANK    $DECIM
     C                     EXSR SR120
     C                     GOTO ED100
     C                     END
     C*
     C           $CONST    IFEQ 'YES'
     C           $OE       IFEQ 'YES'
     C           $OF       ANDNE'YES'
     C                     EXSR SR320
     C                     ELSE
     C                     ADD  1         Z       20
     C                     MOVELARY4,1    ARY3,Z
     C                     MOVEAARY3      $FUN
     C                     EXSR SR110
     C                     MOVEA*BLANK    ARY3
     C                     Z-ADD0         Z
     C                     MOVE *BLANK    $APOST  3
     C                     MOVE *BLANK    $CONST  3
     C                     MOVE *BLANK    $OE     3
     C                     MOVE *BLANK    $OF     3
     C                     MOVE 'FIRST'   $CH
     C                     EXSR SR120
     C                     END
     C                     END
     C*
     C           ED100     ENDSR
     C*--------------------------------------------------------------*
     C*  SR105   IF $VAR = 'YES'                                     *
     C*--------------------------------------------------------------*
     C           SR105     BEGSR
     C*
     C                     ADD  1         CTVAR   20
     C                     MOVEL'@'       $NAME1  3
     C                     MOVE CTVAR     $NAME1
     C                     MOVEL$NAME1    NAME
     C*
     C           $SLASH    IFEQ 'YES'
     C                     MOVELARY5,4    $FUN
     C                     GOTO ED105
     C                     END
     C*
     C           $COMMA    IFEQ 'YES'
     C                     MOVELARY5,3    $FUN
     C                     GOTO ED105
     C                     END
     C*
     C           $TYPE     IFNE 'A'
     C           $USAGE    ANDNE'I'
     C           $LEN      ANDNE0
     C                     MOVELARY5,2    $FUN
     C                     END
     C*
     C           ED105     ENDSR
     C*--------------------------------------------------------------*
     C*  SR110   MOVE DATA TO DATA BASE FILE                         *
     C*--------------------------------------------------------------*
     C           SR110     BEGSR
     C*
     C                     MOVE 'A'       CARD
     C*
     C           $LEN      IFEQ 0
     C                     MOVE *BLANK    LEN
     C                     ELSE
     C           $LEN      IFLE 9
     C                     MOVE $LEN2     LEN
     C                     ELSE
     C                     MOVE $LEN      LEN
     C                     END
     C                     END
     C*
     C                     MOVE $TYPE     TYPE
     C*
     C           $TYPE     IFEQ 'A'
     C           $LEN      OREQ 0
     C                     MOVE *BLANK    DEC
     C                     ELSE
     C           $DEC      IFLE 9
     C                     MOVE $DEC2     DEC
     C                     ELSE
     C                     MOVE $DEC      DEC
     C                     END
     C                     END
     C*
     C                     MOVE $USAGE    USAGE
     C*
     C           $LINE     IFEQ 0
     C                     MOVE *BLANK    LINE
     C                     ELSE
     C           $LINE     IFLE 9
     C                     MOVE $LINE2    LINE
     C                     ELSE
     C                     MOVE $LINE     LINE
     C                     END
     C                     END
     C*
     C           $POS      IFEQ 0
     C                     MOVE *BLANK    BGPOS
     C                     ELSE
     C           $POS      IFLE 9
     C                     MOVE $POS2     BGPOS
     C                     ELSE
     C                     MOVE $POS      BGPOS
     C                     END
     C                     END
     C*
     C                     MOVEL$FUN      FUN
     C*
     C                     MOVE DS1       FIELD1
     C                     WRITEXDSPFR
     C                     MOVE *BLANK    DS1
     C*
     C           ED110     ENDSR
     C*--------------------------------------------------------------*
     C*  SR120   INIT. FIELDS OF DATA                                *
     C*--------------------------------------------------------------*
     C           SR120     BEGSR
     C*
     C                     MOVE *BLANK    $NAME   6
     C                     Z-ADD0         $LEN    20
     C                     MOVE *BLANK    $TYPE   1
     C                     Z-ADD0         $DEC    20
     C                     MOVE *BLANK    $USAGE  1
     C                     Z-ADD0         $LINE   20
     C                     Z-ADD0         $POS    20
     C                     MOVE *BLANK    $FUN   36
     C*
     C           ED120     ENDSR
     C*--------------------------------------------------------------*
     C*  SR130   IF $SETON  = 'YES'                                  *
     C*--------------------------------------------------------------*
     C           SR130     BEGSR
     C*
     C                     MOVE 'A'       CARD
     C                     ADD  1         CTINOR  20
     C                     MOVE CTINOR    INOR
     C                     MOVELARY5,5    FUN
     C                     MOVELDS1       FIELD1
     C                     WRITEXDSPFR
     C*
     C                     MOVE *BLANK    DS1
     C*
     C           ED130     ENDSR
     C*--------------------------------------------------------------*
     C*  SR200   IF CHARACTER = '+'  or '?'                          *
     C*--------------------------------------------------------------*
     C           SR200     BEGSR
     C*
     C           $OE       IFEQ 'YES'
     C                     EXSR SR320
     C                     GOTO ED200
     C                     END
     C*
     C           $VAR      IFEQ 'YES'
MUST C           $CONST    OREQ 'YES'
     C                     EXSR SR100
     C                     END
     C*
     C           $CHAR     IFEQ '?'
     C                     MOVE 'YES'     $SETON
     C                     END
     C*
     C                     MOVE 'YES'     $VAR
     C                     Z-ADDX         $LINE
     C           Y         ADD  1         $POS    20
     C*
     C                     MOVE 'FIRST'   $FSTN   5
     C                     MOVE 'FIRST'   $FSTC   5
     C           ED200     ENDSR
     C*--------------------------------------------------------------*
     C*  SR300   IF CHARACTER = '3' or '6' or '9'                    *
     C*--------------------------------------------------------------*
     C           SR300     BEGSR
     C*
     C           $VAR      IFEQ 'YES'
     C                     ADD  1         $LEN
     C           $DECIM    IFEQ 'YES'
     C                     ADD  1         $DEC
     C                     END
     C           $FSTN     IFEQ 'FIRST'
     C                     EXSR SR310
     C                     MOVE *BLANK    $FSTN
     C                     END
     C                     ELSE
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR320
     C                     END
     C*
     C           ED300     ENDSR
     C*--------------------------------------------------------------*
     C*  SR310   CHECK  $USAGE                                       *
     C*--------------------------------------------------------------*
     C           SR310     BEGSR
     C*
     C           $CHAR     IFEQ '3'
     C                     MOVE 'I'       $USAGE
     C                     GOTO ED310
     C                     END
     C*
     C           $CHAR     IFEQ '6'
     C                     MOVE 'YES'     $CH6
     C                     MOVE 'O'       $USAGE
     C                     GOTO ED310
     C                     END
     C*
     C           $CHAR     IFEQ '9'
     C                     MOVE 'B'       $USAGE
     C                     END
     C*
     C           ED310     ENDSR
     C*--------------------------------------------------------------*
     C*  SR320   IF $CONST = 'YES'                                   *
     C*--------------------------------------------------------------*
     C           SR320     BEGSR
     C*
     C           $CH       IFEQ 'FIRST'
     C                     Z-ADDX         $LINE
     C                     Z-ADDY         $POS
     C                     Z-ADD1         Z
     C                     MOVEAARY4,1    ARY3,Z
     C                     MOVE *BLANK    $CH
     C                     END
     C*
MUST C           $OE       IFEQ 'YES'
MUST C           $OF       ANDNE'YES'
     C                     ADD  1         CT1000
     C                     END
     C*
     C           Z         IFEQ 33
     C           CT1000    ANDNE0
     C           $OF       ANDNE'YES'
     C                     EXSR SR320A
ERR  C**                   GOTO ED320
     C                     END
     C*
     C           Z         IFEQ 34
     C           CT1000    ANDNE0
     C           $OF       ANDNE'YES'
     C                     EXSR SR320B
ERR  C**                   GOTO ED320
     C                     END
     C*
     C                     ADD  1         Z
     C*
     C           Z         IFEQ 36
     C                     EXSR SR320C
     C                     GOTO ED320
     C                     END
     C*
     C           $APOST    IFEQ 'YES'
0413 C                     MOVEA$CHAR     ARY3,Z
0413 C                     ADD  1         Z
0413 C           Z         IFEQ 36
0413 C                     EXSR SR320C
0413 C                     ELSE
     C                     MOVEA$CHAR     ARY3,Z
MUST C                     MOVE *BLANK    $APOST
     C                     END
     C                     ELSE
     C                     MOVEA$CHAR     ARY3,Z
     C                     END
     C*
     C           ED320     ENDSR
     C*--------------------------------------------------------------*
     C*  SR320A  IF ARY3'S INDEX = 33  & CT1000 = 0                 *
     C*--------------------------------------------------------------*
     C           SR320A    BEGSR
     C*
     C           CT1000    DIV  2         $DIV    20
     C                     MVR            $REMER  20
     C*
     C           $REMER    IFEQ 0
     C                     ADD  1         Z
     C                     MOVELOF        ARY3,Z
     C                     ADD  1         Z
     C                     MOVEL'-'       ARY3,Z
     C                     MOVEAARY3      $FUN
     C                     EXSR SR110
     C                     EXSR SR120
     C                     MOVEA*BLANK    ARY3
     C                     Z-ADD1         Z
     C                     MOVELOE        ARY3,Z
MUST C                     Z-ADD0         CT1000
     C                     END
     C*
     C           ED320A    ENDSR
     C*--------------------------------------------------------------*
     C*  SR320B  IF ARY3'S INDEX = 34  & CT1000 = 0                 *
     C*--------------------------------------------------------------*
     C           SR320B    BEGSR
     C*
     C                     EXSR SR320A
     C*
     C           ED320B    ENDSR
     C*--------------------------------------------------------------*
     C*  SR320C  IF ARY3'S INDEX = 36                                *
     C*--------------------------------------------------------------*
     C           SR320C    BEGSR
     C*
     C                     MOVEL'-'       ARY3,Z
     C                     MOVEAARY3      $FUN
     C                     EXSR SR110
     C                     EXSR SR120
     C                     MOVEA*BLANK    ARY3
     C                     Z-ADD1         Z
     C           $APOST    IFEQ 'YES'
     C                     MOVEAARY4,1    ARY3,Z
MUST C                     MOVE *BLANK    $APOST
     C                     ELSE
     C                     MOVEL$CHAR     ARY3,Z
     C                     END
     C*
     C           ED320C    ENDSR
     C*--------------------------------------------------------------*
     C*  SR400   IF CHARACTER = '/' or ',' or  '.'                   *
     C*--------------------------------------------------------------*
     C           SR400     BEGSR
     C*
     C           $VAR      IFEQ 'YES'
     C                     EXSR SR410
     C                     ELSE
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR320
     C                     END
     C*
     C           ED400     ENDSR
     C*--------------------------------------------------------------*
     C*  SR410   CHECK  $SLASH , $COMMA , $DECIM                     *
     C*--------------------------------------------------------------*
     C           SR410     BEGSR
     C*
     C           $CHAR     IFEQ '/'
     C                     MOVE 'YES'     $SLASH  3
     C                     GOTO ED410
     C                     END
     C*
     C           $CHAR     IFEQ ','
     C                     MOVE 'YES'     $COMMA  3
     C                     GOTO ED410
     C                     END
     C*
     C           $CHAR     IFEQ '.'
     C                     MOVE 'YES'     $DECIM  3
     C                     Z-ADD0         $DEC
     C                     END
     C*
     C           ED410     ENDSR
     C*--------------------------------------------------------------*
     C*  SR500   IF CHARACTER = 'I' ,'O' , 'B' , 'i' , 'o' , 'b'     *
     C*--------------------------------------------------------------*
     C           SR500     BEGSR
     C*
     C           $VAR      IFEQ 'YES'
     C                     ADD  1         $LEN
     C                     MOVE 'A'       $TYPE
     C           $FSTC     IFEQ 'FIRST'
     C                     EXSR SR510
     C                     MOVE *BLANK    $FSTC
     C                     END
     C                     ELSE
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR320
     C                     END
     C*
     C           ED500     ENDSR
     C*--------------------------------------------------------------*
     C*  SR510   CHECK  $USAGE                                       *
     C*--------------------------------------------------------------*
     C           SR510     BEGSR
     C*
     C           $CHAR     IFEQ 'I'
     C           $CHAR     OREQ 'i'
     C                     MOVE 'I'       $USAGE
     C                     GOTO ED510
     C                     END
     C*
     C           $CHAR     IFEQ 'O'
     C           $CHAR     OREQ 'o'
     C                     MOVE 'O'       $USAGE
     C                     GOTO ED510
     C                     END
     C*
     C           $CHAR     IFEQ 'B'
     C           $CHAR     OREQ 'b'
     C                     MOVE 'B'       $USAGE
     C                     END
     C*
     C           ED510     ENDSR
     C*--------------------------------------------------------------*
     C*  SR600   IF CHARACTER = '-'                                  *
     C*--------------------------------------------------------------*
     C           SR600     BEGSR
     C*
     C           $VAR      IFNE 'YES'
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR320
     C                     END
     C*
     C           ED600     ENDSR
     C*--------------------------------------------------------------*
     C*  SR700   IF CHARACTER = '_'                                  *
     C*--------------------------------------------------------------*
     C           SR700     BEGSR
     C*
0308 C*          $OE       IFEQ 'YES'
0308 C*                    EXSR SR320
0308 C*                    GOTO ED700
0308 C*                    END
     C*
     C                     MOVE 'YES'     $CONST
     C                     ADD  1         Z
     C           Z         IFEQ 36
     C                     EXSR SR320C
     C                     END
     C                     MOVEL' '       ARY3,Z
     C*
     C           ED700     ENDSR
     C*--------------------------------------------------------------*
     C*  SR800   IF CHARACTER = '@'                                  *
     C*--------------------------------------------------------------*
     C           SR800     BEGSR
     C*
     C           $OE       IFEQ 'YES'
     C                     EXSR SR320
     C                     GOTO ED800
     C                     END
     C*
     C           $CONST    IFEQ 'YES'
     C                     EXSR SR100
     C                     END
     C*
     C                     MOVE 'YES'     $@CH    3
     C*
     C           ED800     ENDSR
     C*--------------------------------------------------------------*
     C*  SR900   IF CHARACTER = 'D'  or  'd'  or 'T' or 't'          *
     C*--------------------------------------------------------------*
     C           SR900     BEGSR
     C*
     C           $@CH      IFEQ 'YES'
     C                     EXSR SR910
     C                     Z-ADDX         $LINE
     C                     Z-ADDY         $POS
     C                     ELSE
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR320
     C                     END
     C*
     C           ED900     ENDSR
     C*--------------------------------------------------------------*
     C*  SR910   CHECK $DATE , $TIME                                 *
     C*--------------------------------------------------------------*
     C           SR910     BEGSR
     C*
     C           $CHAR     IFEQ 'D'
     C           $CHAR     OREQ 'd'
     C                     MOVE 'YES'     $DATE   3
     C                     GOTO ED910
     C                     END
     C*
     C           $CHAR     IFEQ 'T'
     C           $CHAR     OREQ 't'
     C                     MOVE 'YES'     $TIME   3
     C                     END
     C*
     C           ED910     ENDSR
     C*--------------------------------------------------------------*
     C*  SR1000  IF CHARACTER = OE  or  OF                           *
     C*--------------------------------------------------------------*
     C           SR1000    BEGSR
     C*
     C           $CHAR     IFEQ OE
     C                     Z-ADD0         CT1000  20
     C                     MOVE 'YES'     $OE
     C                     MOVE *BLANK    $OF
     C                     ELSE
     C                     MOVE 'YES'     $OF
     C                     END
     C*
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR320
     C*
     C           ED1000    ENDSR
     C*--------------------------------------------------------------*
     C*  SR1100  IF CHARACTER = '''                                  *
     C*--------------------------------------------------------------*
     C           SR1100    BEGSR
     C*
     C           $OE       IFEQ 'YES'
     C                     EXSR SR320
     C                     GOTO ED1100
     C                     END
     C*
     C                     MOVE 'YES'     $APOST
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR320
     C*
     C           ED1100    ENDSR
     C*--------------------------------------------------------------*
     C*  SR1200  IF CHARACTER = OTHERS                               *
     C*--------------------------------------------------------------*
     C           SR1200    BEGSR
     C*
     C                     MOVE 'YES'     $CONST
     C                     EXSR SR320
     C*
     C           ED1200    ENDSR
**     ARY2
     A*--------------------------------------------------------------*
     A*            泛太資技開發股份有限公司－版權所有              *
     A*--------------------------------------------------------------*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      MSGLOC(22)
     A                                      CF03(03 'RETURN')
     A*--------------------------------------------------------------*
     A          R DSPC1                     OVERLAY
     A*--------------------------------------------------------------*
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
 ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
**     ARY4
'
**     ARY5
DATE EDTCDE(Y)
EDTCDE(4)
EDTCDE(J)
EDTCDE(Y)
DSPATR(PC RI)
