      ****************************************************************
      * PROGRAM ID : P07                                             *
      * SYSTEM ID  : PTPLIB                                          *
      * FUNTCTION  :驗証西元年月日                                 *
      *                                                              *
      * PARAMETER : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             01   P0701I    I   *CHAR   8    YYYYMMDD(西元年)
      *             02   P0711O    O   *CHAR   1   驗証結果...     *
      *                                             Y=OK , N =ERROR  *
      *                                                              *
      * W-VARABLE : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             01   P0701     N   *DEC    4 0 西元年          *
      *             02   P0702(P07)N   *DEC    2 0 月份            *
      *             03   P0703     N   *DEC    2 0 日期            *
      *             04   P0704     N   *DEC    5 0 商數            *
      *             05   P0705     N   *DEC    4 0 餘數            *
      *             06   P0706     N   *CHAR   1   潤年(Y/N)       *
      *             07   P0707     N   *CHAR   1   驗証(Y/N)       *
      *             28   P07DAY,1  N   *DEC    2 0 DAYS PER MONTH    *
      *                                                            *
      *             39   P07DAY,12 N   *DEC    2 0 DAYS PER MONTH    *
      *                                                              *
      *                                                              *
      * LEAP YEAR CALCULATION RULE......                             *
      *                        (Y)                                   *
      *                         +- P0706=Y                           *
      *             MVR 400 =0  |            (Y)                     *
      *  LEAP YEAR -------------+             +- P0706=N             *
      *                         | MVR 100 =0  |         (Y)          *
      *                         +-------------+          +- P0706=Y  *
      *                        (N)            | MVR 4 =0 |           *
      *                                       +----------+           *
      *                                      (N)         |           *
      *                                                  +- P0706=N  *
      *                                                 (N)          *
      *                                                              *
      ****************************************************************
     E                    P07DAY 12  12  2 0
     IP070DS      DS
     I                                        1   40P0701
     I                                        1   20P071A
     I                                        3   40P071B
     I                                        5   60P0702
     I                                        7   80P0703
     IP07WK       DS
     I                                        1   60P07WKD
     I                                        1   20P07WMM
     I                                        3   40P07WDD
     I                                        5   60P07WYY
      *--------------------------------------------------------------*
      *                    MAIN PROGRAM                              *
      *--------------------------------------------------------------*
     C           *ENTRY    PLIST
     C                     PARM           P0701I  60
     C                     PARM           P0711O  1
     C                     MOVE P0701I    P07WK            .FOR DATASRUC
      *
     C                     CALL 'R@S005'
     C                     PARM 'GIANT/PT'S0051I  8
      *
     C           P07WYY    IFGT 87
     C                     MOVE 19        P071A
     C                     MOVE P07WYY    P071B
     C                     ELSE
     C                     MOVE 20        P071A
     C                     MOVE P07WYY    P071B
     C                     END
     C                     MOVE P07WDD    P0703
     C                     MOVE P07WMM    P0702
      *
     C                     EXSR P07LEP
     C                     EXSR P07CKD
      *
     C                     MOVE P0707     P0711O
     C                     RETRN
      *--------------------------------------------------------------*
      *          P07LEP....VERIFY LEAP YEAR                          *
      *--------------------------------------------------------------*
     C           P07LEP    BEGSR
     C           P0701     DIV  400       P0704   50
     C                     MVR            P0705   40
     C           P0705     IFEQ 0
     C                     MOVE 'Y'       P0706   1
     C                     ELSE
     C*
     C           P0701     DIV  100       P0704
     C                     MVR            P0705
     C           P0705     IFEQ 0
     C                     MOVE 'N'       P0706
     C                     ELSE
     C*
     C           P0701     DIV  4         P0704
     C                     MVR            P0705
     C           P0705     IFEQ 0
     C                     MOVE 'Y'       P0706
     C                     ELSE
     C                     MOVE 'N'       P0706
     C                     END
     C*
     C                     END
     C*
     C                     END
     C                     ENDSR
      *--------------------------------------------------------------*
      *          P07CKD....CHECK MONTH & DATE                        *
      *--------------------------------------------------------------*
     C           P07CKD    BEGSR
     C           P0702     IFGT 12
     C           P0702     OREQ 0
     C           P0703     OREQ 0
     C                     MOVE 'N'       P0707   1
     C                     ELSE
     C*
     C                     Z-ADDP0702     P07     20
     C           P0703     IFGT P07DAY,P07
     C                     MOVE 'N'       P0707
     C                     ELSE
     C                     MOVE 'Y'       P0707
     C                     END
     C*
     C           P0702     IFEQ 2
     C           P0706     ANDEQ'Y'
     C           P0703     ANDLE29
     C                     MOVE 'Y'       P0707
     C                     ELSE
     C                     END
     C*
     C                     END
     C                     ENDSR
**
312831303130313130313031
