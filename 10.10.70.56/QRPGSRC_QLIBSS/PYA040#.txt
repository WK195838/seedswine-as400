     H****************************************************************
     H*    PROGRAM NAME : PYA040$                                    *
     H*    AUTHOR       : A1034   STEPHANIE                          *
     H*    CREATE DATE  : 78/10/17                                   *
     H*    UPDATE DATE  :                                            *
     H*    SYSTEM       :財務系統                                  *
     H*    SUBSYSTEM    :人事薪資系統                              *
     H*    PROCEDURE    :                                            *
     H*    REMARK       :人事基本資料建檔                          *
     H*    INDIC DESCRIPTION :                                       *
     H*       27-29 -  DECIDE FORMAT ( 27-人事 28-薪資 29-履歷)*
     H*       31    -  CAN INPUT                                     *
     H*       33    -  CONTROL LOOP                                  *
     H*       35    -  SCREEN CONTROL (DOPT NE *BLANKS)              *
     H*       60-73 -  DSPATR                                        *
     H*       74-81 -  MESSAGE                                       *
     H*                                                              *
     H****************************************************************
     FEMPF    UF  E           K        DISK                      A
     FNMPF    IF  E           K        DISK
     FCMPF    IF  E           K        DISK
     FPYA040D$CF  E                    WORKSTN
     E*---------------------------------------------------------------
     E*: FUNCTION DATA :
     E                    WFUN    5   5  6
     I*---------------------------------------------------------------
     I            DS
     I                                        1  15 W1NM02
     I                                        1   1 DEM03
     I                                        2   6 DEM04
     I            DS
     I                                        1  15 W2NM02
     I                                        1   3 DEM05
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
     C*---------------------------------------------------------------
     C*          KLIST
     C*---------------------------------------------------------------
     C           KEY01     KLIST                           .EMPF
     C                     KFLD           DEM01
     C           KEY02     KLIST                           .NMPF
     C                     KFLD           W1NM01
     C                     KFLD           W1NM02
     C           KEY03     KLIST                           .NMPF
     C                     KFLD           W2NM01
     C                     KFLD           W2NM02
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR RTN010                     .INITIAL
     C           *IN03     DOWEQ'0'
     C*
     C           SCID      CASEQ'SC01'    RTN100
     C           SCID      CASEQ'SC02'    RTN200
     C                     END
     C*
     C                     END
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE         CALL BY MAIN ROUTINE
     C*==============================================================*
     C           RTN010    BEGSR
     C                     MOVE 'SC01'    SCID    4
     C                     MOVE *BLANK    DEM01
     C                     MOVE *BLANK    DITEM
     C                     MOVE *BLANK    DOPT
     C                     ENDSR
     C*==============================================================*
     C*          RTN100....'SC01' SCREEN MAIN ROUTINE            MAIN
     C*==============================================================*
     C           RTN100    BEGSR
     C                     EXFMTDSPC1
     C*
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C*
     C                     EXSR RTN110                     .SCR CHK
     C           *IN99     IFEQ '0'                        .CORRECT
     C                     MOVE 'SC02'    SCID
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*==============================================================*
     C*          RTN110....SCREEN CHECK                CALL BY RTN100
     C*==============================================================*
     C           RTN110    BEGSR
     C*
     C           DEM01     IFEQ *BLANK                     .CHK COMPANY
     C                     SETON                     607699
     C                     END
     C   99                GOTO END110
     C*                                                   .CHK ITEM
     C           DITEM     IFNE '1'                        .NOT人事
     C           DITEM     ANDNE'2'                        .NOT薪資
     C           DITEM     ANDNE'3'                        .NOT履歷
     C           DITEM     ANDNE*BLANK
     C                     SETON                     618399
     C                     END
     C   99                GOTO END110
     C*
     C           DOPT      IFEQ *BLANK                     .CHK FUNTION
     C                     SETON                     637699
     C                     END
     C   99                GOTO END110
     C*                                                   .FUNCTION ERR
     C           DOPT      IFNE '1'                        .NOT ADD
     C           DOPT      ANDNE'2'                        .NOT UPD
     C           DOPT      ANDNE'4'                        .NOT DLT
     C           DOPT      ANDNE'5'                        .NOT INQ
     C                     SETON                     638299
     C                     END
     C   99                GOTO END110
     C*
     C           KEY01     SETLLEMPF                     42
     C*
     C           DOPT      IFEQ '1'                        .ADD
     C           *IN42     IFEQ '1'                        .FOUND
     C                     SETON                     607899
     C                     END
     C                     END
     C   99                GOTO END110
     C*
     C           DOPT      IFNE '1'                        .UPD,DLT,INQ
     C           *IN42     IFEQ '0'                        .NR
     C                     SETON                     607999
     C                     END
     C                     END
     C   99                GOTO END110
     C*
     C           *IN42     IFEQ '1'                        .FOUND
     C           DITEM     IFNE ' '
     C           DOPT      ANDEQ'1'                        .ADD
     C                     SETON                     618199
     C                     END
     C                     END
     C   99                GOTO END110
     C*
     C           *IN42     IFNE '1'                        .NR
     C           DITEM     IFNE ' '
     C           DOPT      ANDEQ'4'                        .DLT
     C                     SETON                     618199
     C                     END
     C                     END
     C   99                GOTO END110
     C*
     C           DOPT      IFEQ '1'                        .ADD
     C                     EXSR RTN800                     .INIT SCR
     C                     END
     C*
     C           END110    ENDSR
     C*==============================================================*
     C*          RTN200....DECIDE SCREEN         CALL BY MAIN ROUTINE
     C*==============================================================*
     C           RTN200    BEGSR
     C*
     C           DITEM     IFEQ *BLANKS                    .FMT FLOW
     C                     Z-ADD1         WITEMS  10       .FMT 1 STARE
     C                     Z-ADD3         WITEME  10       .FMT 3 END
     C                     ELSE
     C                     MOVE DITEM     WITEMS
     C                     MOVE DITEM     WITEME
     C                     END
     C*
     C           WITEMS    DOWLEWITEME                     .CTL FMT FLOW
     C           *IN35     ANDEQ'0'
     C*
     C           WITEMS    IFEQ 1
     C                     SETON                     27    .FMT 1
     C                     EXSR RTN205                     .A/U/D/I
     C*
     C                     ELSE                            .NE 1
     C           WITEMS    IFEQ 2
     C                     SETON                     28    .FMT 2
     C                     EXSR RTN205                     .A/U/D/I
     C*
     C                     ELSE                            .NE 1,2
     C           WITEMS    IFEQ 3
     C                     SETON                     29    .FMT 3
     C                     EXSR RTN205                     .A/U/D/I
     C*
     C                     ELSE                            .NE 1,2,3
     C                     SETON                     35
     C*
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C                     SETOF                     35
     C                     MOVE 'SC01'    SCID
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN205....DECIDE FUNTION A/U/D/I           BY RTN200
     C*==============================================================*
     C           RTN205    BEGSR
     C*
     C                     MOVE DOPT      WNUM    10       .FUNCTION
     C                     MOVE WFUN,WNUM DFUN
     C*
     C           DOPT      CASEQ'1'       RTN210           .ADD
     C           DOPT      CASEQ'2'       RTN220           .UPD
     C           DOPT      CASEQ'4'       RTN230           .DEL
     C           DOPT      CASEQ'5'       RTN240           .INQ
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN210....ADD DATA                         BY RTN205
     C*==============================================================*
     C           RTN210    BEGSR
     C*
     C           *IN33     DOWEQ'0'                        .CTL LOOP
     C*
     C                     SETON                     31    .CAN INPUT
     C   27                EXFMTDSPC2                      .FMT 1
     C   28                EXFMTDSPC3                      .FMT 2
     C   29                EXFMTDSPC4                      .FMT 3
     C*
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C*
     C                     EXSR RTN810                     .CHK SCR
     C           *IN99     IFEQ '0'                        .CORRECT
     C                     EXSR RTN820                     .SCR TO FILE
     C   29                WRITEEM0                        .FMT 3 WRITE
     C                     ADD  1         WITEMS           .GO NEXT FMT
     C                     SETON                     33
     C                     END
     C*
     C                     ELSE                            .*IN12 ON
     C                     SUB  1         WITEMS           .GO PRV FMT
     C                     SETON                     33
     C                     END
     C*
     C                     ELSE                            .*IN03 ON
     C                     SETON                     3335  .RETURN
     C                     END
     C*
     C                     END                             .CTL LOOP
     C*
     C                     SETOF                     33    .CTL LOOP
     C                     SETOF                     272829.CTL FMT
     C                     ENDSR
     C*==============================================================*
     C*          RTN220....UPD DATA                    CALL BY RTN205
     C*==============================================================*
     C           RTN220    BEGSR
     C                     EXSR RTN830                     .FILE TO SCR
     C           *IN33     DOWEQ'0'                        .CTL LOOP
     C*
     C                     SETON                     31    .CAN INPUT
     C   27                EXFMTDSPC2                      .FMT 1
     C   28                EXFMTDSPC3                      .FMT 2
     C   29                EXFMTDSPC4                      .FMT 3
     C*
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C                     EXSR RTN810                     .CHK SCR
     C           *IN99     IFEQ '0'                        .CORRECT
     C           KEY01     CHAINEM0                  40
     C                     EXSR RTN820                     .SCR TO FILE
     C                     UPDATEM0
     C                     ADD  1         WITEMS           .GO NEXT FMT
     C                     SETON                     33
     C                     END
     C*
     C                     ELSE                            .*IN12 ON
     C           DITEM     IFEQ *BLANKS
     C                     SUB  1         WITEMS           .GO PRV FMT
     C                     ELSE
     C                     SETON                     35    .RETURN
     C                     END
     C*
     C                     SETON                     33
     C                     END
     C*
     C                     ELSE                            .*IN03 ON
     C*
     C                     SETON                     3335
     C                     END
     C*
     C                     END                             .CTL LOOP
     C                     SETOF                     33
     C                     SETOF                     272829
     C                     ENDSR
     C*==============================================================*
     C*          RTN230....DEL DATA                    CALL BY RTN205
     C*==============================================================*
     C           RTN230    BEGSR
     C                     EXSR RTN830                     .FILE TO SCR
     C                     SETOF                     31    .NO INPUT
     C                     EXFMTDSPC2
     C*
     C           *IN03     IFEQ '0'
     C*
     C           *IN12     IFEQ '0'
     C           KEY01     CHAINEM0                  40    .NR
     C                     DELETEM0
     C                     END
     C*
     C                     END
     C                     SETON                     35
     C                     ENDSR
     C*==============================================================*
     C*          RTN240....INQ DATA                    CALL BY RTN205
     C*==============================================================*
     C           RTN240    BEGSR
     C                     EXSR RTN830                     .FILE TO SCR
     C                     SETOF                     31    .NO INPUT
     C*
     C   27                EXFMTDSPC2                      .FMT 1
     C   28                EXFMTDSPC3                      .FMT 2
     C   29                EXFMTDSPC4                      .FMT 3
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     IFEQ '0'
     C                     ADD  1         WITEMS           .GO NEXT FMT
     C                     ELSE                            .*IN12 ON
     C           DITEM     IFEQ *BLANKS
     C                     SUB  1         WITEMS           .GO PRV FMT
     C                     ELSE
     C                     SETON                     35    .RETURN
     C                     END
     C                     END
     C*
     C                     ELSE                            .*IN03 ON
     C                     SETON                     35
     C                     END
     C*
     C                     SETOF                     272829
     C                     ENDSR
     C*===============================================================*
     C*          RTN800 - MOVE *BLANK TO DSPF
     C*===============================================================*
     C           RTN800    BEGSR
     C                     MOVE *BLANK    DEM02            .FMT 1
     C                     MOVE '1'       DEM03
     C                     MOVE *BLANK    DEM04
     C                     MOVE *BLANK    DEM05
     C                     MOVE *BLANK    DEM06
     C                     MOVE *BLANK    DEM07
     C                     MOVE *BLANK    DEM09
     C                     MOVE *BLANK    DEM10
     C                     MOVE '0'       DEM11
     C                     MOVE *BLANK    DEM12
     C                     MOVE *BLANK    DEM13
     C                     MOVE *BLANK    DEM14
     C                     MOVE *BLANK    DEM15
     C                     MOVE *BLANK    DEM16
     C                     MOVE 'S'       DEM17
     C                     MOVE *BLANK    DCM03
     C                     MOVE *BLANK    D1NM03
     C                     MOVE *BLANK    D2NM03
     C                     Z-ADD0         DEM08
     C                     Z-ADD0         DEM18
     C                     Z-ADD0         DEM19
     C                     Z-ADD0         DEM20
     C                     Z-ADD0         DEM21
     C                     Z-ADD0         DEM22
     C                     Z-ADD0         DEM23
     C                     Z-ADD0         DEM24
     C                     Z-ADD0         DEM25
     C                     Z-ADD0         DEM27
     C                     Z-ADD0         DEM29
     C                     Z-ADD0         DEM43
     C                     Z-ADDDDATE     DEM98
     C*
     C                     Z-ADD0         DEM30            .FMT 2
     C                     Z-ADD0         DEM31
     C                     Z-ADD0         DEM32
     C                     Z-ADD0         DEM33
     C                     Z-ADD0         DEM34
     C                     Z-ADD0         DEM35
     C                     Z-ADD0         DEM36
     C                     Z-ADD0         DEM37
     C                     Z-ADD0         DEM39
     C                     Z-ADD0         DEM40
     C                     Z-ADD0         DEM41
     C                     Z-ADD0         DEM42
     C                     Z-ADDEM43      DEM43
     C*
     C                     MOVE *BLANK    DEM45            .FMT 3
     C                     MOVE *BLANK    DEM46
     C                     MOVE *BLANK    DEM47
     C                     MOVE *BLANK    DEM48
     C                     MOVE *BLANK    DEM49
     C                     MOVE *BLANK    DEM50
     C                     MOVE *BLANK    DEM51
     C                     MOVE *BLANK    DEM52
     C                     MOVE *BLANK    DEM53
     C                     MOVE *BLANK    DEM54
     C                     ENDSR
     C*===============================================================*
     C*          RTN810 - CHECK DATA
     C*===============================================================*
     C           RTN810    BEGSR
     C           WITEMS    IFEQ 1                          .FMT 1
     C*
     C           DEM02     IFEQ *BLANK
     C                     SETON                     607699
     C                     END
     C   99                GOTO END810
     C*
     C           DEM03     IFEQ *BLANK
     C                     SETON                     637699
     C                     END
     C   99                GOTO END810
     C*
     C           DEM03     IFNE '1'
     C           DEM03     ANDNE'2'
     C                     SETON                     638099
     C                     END
     C   99                GOTO END810
     C*
     C           DEM04     IFEQ *BLANK
     C                     SETON                     617699
     C                     END
     C   99                GOTO END810
     C*
     C                     MOVE '01'      W1NM01  2        .DEPART DESC
     C           KEY02     SETLLNMPF                     42.EQ
     C           *IN42     IFEQ '0'                        .NR
     C                     SETON                     617799
     C                     END
     C   99                GOTO END810
     C*
     C           DEM05     IFEQ *BLANK
     C                     SETON                     627699
     C                     END
     C   99                GOTO END810
     C*
     C                     MOVE '02'      W2NM01  2        .TITLE DESC
     C           KEY03     SETLLNMPF                     42.EQ
     C           *IN42     IFEQ '0'                        .NR
     C                     SETON                     627799
     C                     END
     C   99                GOTO END810
     C*
     C           DEM11     IFEQ *BLANK
     C                     SETON                     667699
     C                     END
     C   99                GOTO END810
     C*
     C           DEM11     IFNE '0'
     C           DEM11     ANDNE'1'
     C                     SETON                     667899
     C                     END
     C   99                GOTO END810
     C*
     C           DEM06     IFEQ *BLANK
     C                     SETON                     647699
     C                     END
     C   99                GOTO END810
     C*
     C                     MOVE DEM06     P01I1  10
     C                     CALL 'P02'
     C                     PARM           P01I1
     C           P01O1     PARM           P01O1   1
     C           P01O2     PARM           P01O2   1
     C           P01O1     IFEQ 'N'
     C                     SETON                     647899
     C                     ELSE
     C                     MOVE P01O2     EM07
     C                     END
     C   99                GOTO END810
     C*
     C           DEM08     IFEQ *ZEROS
     C                     SETON                     737999
     C                     END
     C   99                GOTO END810
     C*
     C           DEM08     IFNE *ZEROS
     C                     Z-ADDDEM08     P02I1   60
     C                     CALL 'P04'
     C                     PARM           P02I1
     C           P02O1     PARM           P02O1   1
     C           P02O1     IFEQ 'N'
     C                     SETON                     737899
     C                     END
     C*
     C                     END
     C   99                GOTO END810
     C*
     C           DEM14     IFEQ *BLANK
     C                     SETON                     727699
     C                     END
     C   99                GOTO END810
     C*
     C           DEM17     IFEQ *BLANK
     C                     SETON                     657699
     C                     END
     C   99                GOTO END810
     C*
     C           DEM17     IFNE 'S'
     C           DEM17     ANDNE'M'
     C                     SETON                     657899
     C                     END
     C   99                GOTO END810
     C*
     C           DEM19     IFEQ *ZEROS
     C                     SETON                     677999
     C                     END
     C   99                GOTO END810
     C*
     C                     Z-ADDDEM19     P02I1   60
     C                     CALL 'P04'
     C                     PARM           P02I1
     C           P02O1     PARM           P02O1   1
     C           P02O1     IFEQ 'N'
     C                     SETON                     677899
     C                     END
     C   99                GOTO END810
     C*
     C           DEM20     IFNE *ZEROS
     C                     Z-ADDDEM20     P02I1   60
     C                     CALL 'P04'
     C                     PARM           P02I1
     C           P02O1     PARM           P02O1   1
     C           P02O1     IFEQ 'N'
     C                     SETON                     687899
     C                     END
     C*
     C                     END
     C   99                GOTO END810
     C*
     C           DEM21     IFNE *ZEROS
     C                     Z-ADDDEM21     P02I1   60
     C                     CALL 'P04'
     C                     PARM           P02I1
     C           P02O1     PARM           P02O1   1
     C           P02O1     IFEQ 'N'
     C                     SETON                     697899
     C                     END
     C*
     C                     END
     C   99                GOTO END810
     C*
     C           DEM22     IFNE *ZEROS
     C                     Z-ADDDEM22     P02I1   60
     C                     CALL 'P04'
     C                     PARM           P02I1
     C           P02O1     PARM           P02O1   1
     C           P02O1     IFEQ 'N'
     C                     SETON                     707899
     C                     END
     C*
     C                     END
     C   99                GOTO END810
     C*
     C           DEM23     IFNE *ZEROS
     C                     Z-ADDDEM23     P02I1   60
     C                     CALL 'P04'
     C                     PARM           P02I1
     C           P02O1     PARM           P02O1   1
     C           P02O1     IFEQ 'N'
     C                     SETON                     717899
     C                     END
     C*
     C                     END
     C   99                GOTO END810
     C*
     C           DEM24     IFNE *ZEROS
     C                     Z-ADDDEM24     P02I1   60
     C                     CALL 'P04'
     C                     PARM           P02I1
     C           P02O1     PARM           P02O1   1
     C           P02O1     IFEQ 'N'
     C                     SETON                     727899
     C                     END
     C*
     C                     END
     C   99                GOTO END810
     C*
     C                     END
     C*
     C           WITEMS    IFEQ 2                          .FMT 2
     C           DEM30     IFEQ 0
     C                     SETON                     607699
     C                     END
     C   99                GOTO END810
     C*
     C           DEM31     IFEQ 0
     C                     SETON                     617699
     C                     END
     C*
     C                     END
     C           END810    ENDSR
     C*===============================================================*
     C*          RTN820 - MOVE DSPF TO PF
     C*===============================================================*
     C           RTN820    BEGSR
     C           WITEMS    IFEQ 1                          .FMT 1
     C                     MOVE DEM01     EM01
     C                     MOVE DEM02     EM02
     C                     MOVE DEM03     EM03
     C                     MOVE DEM04     EM04
     C                     MOVE DEM05     EM05
     C                     MOVE DEM06     EM06
     C                     MOVE DEM09     EM09
     C                     MOVE DEM10     EM10
     C                     MOVE DEM11     EM11
     C                     MOVE DEM12     EM12
     C                     MOVE DEM13     EM13
     C                     MOVE DEM14     EM14
     C                     MOVE DEM15     EM15
     C                     MOVE DEM16     EM16
     C                     MOVE DEM17     EM17
     C                     MOVE *BLANK    EM97
     C                     MOVE USER      EM99
     C                     Z-ADDDEM08     EM08
     C                     Z-ADDDEM18     EM18
     C                     Z-ADDDEM19     EM19
     C                     Z-ADDDEM20     EM20
     C                     Z-ADDDEM21     EM21
     C                     Z-ADDDEM22     EM22
     C                     Z-ADDDEM23     EM23
     C                     Z-ADDDEM24     EM24
     C                     Z-ADDDEM25     EM25
     C                     Z-ADDDEM27     EM27
     C                     Z-ADDDEM28     EM28
     C                     Z-ADDDEM29     EM29
     C                     Z-ADDDEM43     EM43
     C                     Z-ADDDDATE     EM98
     C                     GOTO END820
     C*
     C                     END
     C*
     C           WITEMS    IFEQ 2                          .FMT 2
     C                     Z-ADDDEM30     EM30
     C                     Z-ADDDEM31     EM31
     C                     Z-ADDDEM32     EM32
     C                     Z-ADDDEM33     EM33
     C                     Z-ADDDEM34     EM34
     C                     Z-ADDDEM35     EM35
     C                     Z-ADDDEM36     EM36
     C                     Z-ADDDEM37     EM37
     C                     Z-ADDDEM38     EM38
     C                     Z-ADDDEM39     EM39
     C                     Z-ADDDEM40     EM40
     C                     Z-ADDDEM41     EM41
     C                     Z-ADDDEM42     EM42
     C                     Z-ADDDEM43     EM43
     C*
     C                     ELSE                            .FMT 3
     C*
     C                     MOVE DEM45     EM45
     C                     MOVE DEM46     EM46
     C                     MOVE DEM47     EM47
     C                     MOVE DEM48     EM48
     C                     MOVE DEM49     EM49
     C                     MOVE DEM50     EM50
     C                     MOVE DEM51     EM51
     C                     MOVE DEM52     EM52
     C                     MOVE DEM53     EM53
     C                     MOVE DEM54     EM54
     C*
     C                     END
     C           END820    ENDSR
     C*===============================================================*
     C*          RTN830 - MOVE PF TO DSPF
     C*===============================================================*
     C           RTN830    BEGSR
     C           KEY01     CHAINEM0                  40
     C           WITEMS    IFEQ 1                          .FMT 1
     C                     MOVE EM01      DEM01
     C                     MOVE EM02      DEM02
     C                     MOVE EM03      DEM03
     C                     MOVE EM04      DEM04
     C                     MOVE EM05      DEM05
     C                     MOVE EM06      DEM06
     C                     MOVE EM07      DEM07
     C                     MOVE EM09      DEM09
     C                     MOVE EM10      DEM10
     C                     MOVE EM11      DEM11
     C                     MOVE EM12      DEM12
     C                     MOVE EM13      DEM13
     C                     MOVE EM14      DEM14
     C                     MOVE EM15      DEM15
     C                     MOVE EM16      DEM16
     C                     MOVE EM17      DEM17
     C                     Z-ADDEM08      DEM08
     C                     Z-ADDEM18      DEM18
     C                     Z-ADDEM19      DEM19
     C                     Z-ADDEM20      DEM20
     C                     Z-ADDEM21      DEM21
     C                     Z-ADDEM22      DEM22
     C                     Z-ADDEM23      DEM23
     C                     Z-ADDEM24      DEM24
     C                     Z-ADDEM25      DEM25
     C                     Z-ADDEM27      DEM27
     C                     Z-ADDEM28      DEM28
     C                     Z-ADDEM29      DEM29
     C                     Z-ADDEM43      DEM43
     C                     Z-ADDEM98      DEM98
     C*
     C                     CALL 'P20'
     C                     PARM DEM19     P20I1   60
     C                     PARM DDATE     P20I2   60
     C                     PARM 0         P20I3   90
     C           P20I3     DIV  365       DYEAR   31H
     C*
     C           EM03      CHAINCMPF                 40
     C           *IN40     IFEQ '1'
     C                     MOVE *BLANK    DCM03
     C                     ELSE
     C                     MOVE CM03      DCM03
     C                     END
     C           *LOVAL    SETLLCMPF
     C*
     C                     MOVE '01'      W1NM01  2
     C           KEY02     CHAINNMPF                 40
     C           *IN40     IFEQ '1'
     C                     MOVE *BLANK    D1NM03
     C                     ELSE
     C                     MOVELNM03      D1NM03
     C                     END
     C*
     C                     MOVE '02'      W2NM01  2
     C           KEY03     CHAINNMPF                 40
     C           *IN40     IFEQ '1'
     C                     MOVE *BLANK    D2NM03
     C                     ELSE
     C                     MOVELNM03      D2NM03
     C                     END
     C*
     C           *LOVAL    SETLLNMPF
     C*
     C                     GOTO END830
     C                     END
     C*
     C           WITEMS    IFEQ 2                          .FMT 2
     C                     Z-ADDEM30      DEM30
     C                     Z-ADDEM31      DEM31
     C                     Z-ADDEM32      DEM32
     C                     Z-ADDEM33      DEM33
     C                     Z-ADDEM34      DEM34
     C                     Z-ADDEM35      DEM35
     C                     Z-ADDEM36      DEM36
     C                     Z-ADDEM37      DEM37
     C                     Z-ADDEM38      DEM38
     C                     Z-ADDEM39      DEM39
     C                     Z-ADDEM40      DEM40
     C                     Z-ADDEM41      DEM41
     C                     Z-ADDEM42      DEM42
     C                     Z-ADDEM43      DEM43
     C*
     C                     ELSE                            .FMT 3
     C*
     C                     MOVE EM45      DEM45
     C                     MOVE EM46      DEM46
     C                     MOVE EM47      DEM47
     C                     MOVE EM48      DEM48
     C                     MOVE EM49      DEM49
     C                     MOVE EM50      DEM50
     C                     MOVE EM51      DEM51
     C                     MOVE EM52      DEM52
     C                     MOVE EM53      DEM53
     C                     MOVE EM54      DEM54
     C*
     C                     END
     C           *LOVAL    SETLLEM0
     C           END830    ENDSR
**
新增修改      刪除查詢
