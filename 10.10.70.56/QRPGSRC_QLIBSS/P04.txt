      ****************************************************************
      * PROGRAM ID : P04                                             *
      * SYSTEM ID  : PTPLIB                                          *
      * FUNTCTION  :驗証中國年月日                                 *
      *                                                              *
      * PARAMETER : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             01   P0401I    I   *DEC(P) 6 0  YYMMDD(中國年) *
      *             02   P0411O    O   *CHAR   1   驗証結果...     *
      *                                             Y=OK , N =ERROR  *
      *                                                              *
      * W-VARABLE : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             00   P040DS    N   *CHAR   6    YYMMDD(中國年) *
      *             01   P0401C    N   *DEC    2 0 中國年          *
      *                  P0401     N   *DEC    4 0 西元年          *
      *             02   P0402(P04)N   *DEC    2 0 月份            *
      *             03   P0403     N   *DEC    2 0 日期            *
      *             04   P0404     N   *DEC    5 0 商數            *
      *             05   P0405     N   *DEC    4 0 餘數            *
      *             06   P0406     N   *CHAR   1   潤年(Y/N)       *
      *             07   P0407     N   *CHAR   1   驗証(Y/N)       *
      *             08   P04DAY,1  N   *DEC    2 0 DAYS PER MONTH    *
      *                                                            *
      *             19   P04DAY,12 N   *DEC    2 0 DAYS PER MONTH    *
      *                                                              *
      *                                                              *
      * LEAP YEAR CALCULATION RULE......                             *
      *                        (Y)                                   *
      *                         +- P0406=Y                           *
      *             MVR 400 =0  |            (Y)                     *
      *  LEAP YEAR -------------+             +- P0406=N             *
      *                         | MVR 100 =0  |         (Y)          *
      *                         +-------------+          +- P0406=Y  *
      *                        (N)            | MVR 4 =0 |           *
      *                                       +----------+           *
      *                                      (N)         |           *
      *                                                  +- P0406=N  *
      *                                                 (N)          *
      *                                                              *
      ****************************************************************
     E                    P04DAY 12  12  2 0
     IP040DS      DS
     I                                        1   20P0401C
     I                                        3   40P0402
     I                                        5   60P0403
      *--------------------------------------------------------------*
      *                    MAIN PROGRAM                              *
      *--------------------------------------------------------------*
     C           *ENTRY    PLIST
     C                     PARM           P0401I  60
     C                     PARM           P0411O  1
      *
     C                     MOVE P0401I    P040DS           .FOR DATASTRUC
     C                     Z-ADDP0401C    P0401   40
     C                     ADD  1911      P0401
      *
     C                     CALL 'R@S005'
     C                     PARM 'GIANT/PT'S0051I  8
      *
     C                     EXSR P04LEP
     C                     EXSR P04CKD
      *
     C                     MOVE P0407     P0411O
     C                     RETRN
      *--------------------------------------------------------------*
      *          P04LEP....VERIFY LEAP YEAR                          *
      *--------------------------------------------------------------*
     C           P04LEP    BEGSR
     C           P0401     DIV  400       P0404   50
     C                     MVR            P0405   40
     C           P0405     IFEQ 0
     C                     MOVE 'Y'       P0406   1
     C                     ELSE
     C*
     C           P0401     DIV  100       P0404
     C                     MVR            P0405
     C           P0405     IFEQ 0
     C                     MOVE 'N'       P0406
     C                     ELSE
     C*
     C           P0401     DIV  4         P0404
     C                     MVR            P0405
     C           P0405     IFEQ 0
     C                     MOVE 'Y'       P0406
     C                     ELSE
     C                     MOVE 'N'       P0406
     C                     END
     C*
     C                     END
     C*
     C                     END
     C                     ENDSR
      *--------------------------------------------------------------*
      *          P04CKD....CHECK MONTH & DATE                        *
      *--------------------------------------------------------------*
     C           P04CKD    BEGSR
     C           P0402     IFGT 12
     C           P0402     OREQ 0
     C           P0403     OREQ 0
     C                     MOVE 'N'       P0407   1
     C                     ELSE
     C*
     C                     Z-ADDP0402     P04     20
     C           P0403     IFGT P04DAY,P04
     C                     MOVE 'N'       P0407
     C                     ELSE
     C                     MOVE 'Y'       P0407
     C                     END
     C*
     C           P0402     IFEQ 2
     C           P0406     ANDEQ'Y'
     C           P0403     ANDLE29
     C                     MOVE 'Y'       P0407
     C                     ELSE
     C                     END
     C*
     C                     END
     C                     ENDSR
**
312831303130313130313031
