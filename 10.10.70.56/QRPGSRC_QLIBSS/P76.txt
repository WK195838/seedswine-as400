     H****************************************************************
     H* PROGRAM ID : P76                                             *
     H* FUNTCTION  :輸入中文值轉成J TYEP去除所有SINGLE BYTE      *
     H*                                                              *
     H* PARAMETER : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
     H*             ===  =======  ===  =====  ====  ===========      *
     H*             01   P7601I    I   *CHAR  82   輸入中文值      *
     H*             02   P7602I    I   *NUM   2,0  輸出中文位數    *
     H*                                            （有幾個中文字）*
     H*                                            （最多４０個字）*
     H*             02   P7601O    O   *CHAR  82   輸出中文值      *
     H****************************************************************
     E*
     E                    AI1        82  1               輸入中文值
     E                    AO1        82  1               輸出中文值
     E*
     IP7601I      DS
     I                                        1  82 AI1
     IP7601O      DS
     I                                        1  82 AO1
     I*
     I            DS
     I                                        1   2 SISO
     I                                        1   1 SI
     I                                        2   2 SO
     I*
     I            DS
     I                                        1   4 CSPACE
     I                                        2   2 CSPA1
     I                                        3   3 CSPA2
     C*==============================================================*
     C*                    PARM LIST                                 *
     C*==============================================================*
     C           *ENTRY    PLIST
     C                     PARM           P7601I 82
     C                     PARM           P7602I  20
     C                     PARM           P7601O 82
     C*==============================================================*
     C*                    MAIN PROGRAM                              *
     C*==============================================================*
     C                     EXSR R1000                      INIT
     C                     EXSR R2000                      MAIN
     C                     SETON                     LR
     C                     RETRN
     C*==============================================================*
     C*                    INIT PROCESS                              *
     C*==============================================================*
     C           R1000     BEGSR
     C*
     C                     MOVEL''      SISO
     C                     MOVEL'　'    CSPACE
     C*
     C                     MOVEL*BLANK    P7601O
     C           P7602I    IFGT 40
     C                     Z-ADD40        P7602I
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*                    MAIN PROCESS                              *
     C*==============================================================*
     C           R2000     BEGSR
     C*
     C                     Z-ADD1         L1      20
     C                     MOVELSI        AO1,1
     C                     Z-ADD0         B1      20
     C                     Z-ADD0         P1      20
     C*
     C           P1        DOWLT82
     C           B1        ANDLTP7602I
     C                     ADD  1         P1
     C*
     C           AI1,P1    IFEQ SI
     C                     SETOF                     42
     C           *IN42     DOWEQ'0'
     C                     EXSR R2100
     C                     END
     C                     END
     C*
     C                     END
     C*
     C           B1        DOWLTP7602I
     C                     ADD  1         B1
     C                     ADD  1         L1
     C                     MOVELCSPA1     AO1,L1
     C                     ADD  1         L1
     C                     MOVELCSPA2     AO1,L1
     C                     END
     C*
     C                     ADD  1         L1
     C                     MOVELSO        AO1,L1
     C*
     C                     ENDSR
     C*==============================================================*
     C*                    MOVE CHINESE                              *
     C*==============================================================*
     C           R2100     BEGSR
     C*
     C                     ADD  1         P1
     C           P1        IFGE 82
     C                     SETON                     42
     C                     GOTO E2100
     C                     END
     C*
     C           AI1,P1    IFEQ SO
     C                     SETON                     42
     C                     GOTO E2100
     C                     END
     C*
     C           B1        IFGE P7602I
     C                     SETON                     42
     C                     GOTO E2100
     C                     END
     C*
     C                     ADD  1         B1
     C                     ADD  1         L1
     C                     MOVELAI1,P1    AO1,L1
     C                     ADD  1         P1
     C                     ADD  1         L1
     C                     MOVELAI1,P1    AO1,L1
     C*
     C           E2100     ENDSR
