      ****************************************************************
      * 泛太資訊科技開發股份有限公司－版權所有      TEL:7313250    *
      ****************************************************************
      * PROGRAM ID : P67                                             *
      * SYSTEM ID  : PTPLIB                                          *
      * FUNTCTION  : RECORD LOCK & RELEASE                           *
      *                                                              *
      * PARAMETER : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             01   P6701I    I   *CHAR  10    FILE-NAME        *
      *             02   P6702I    I   *CHAR  60    COMPACT-KEY      *
      *             03   P6703I    I   *CHAR   1    'D' OR 'W'       *
      *             04   P6711O    O   *CHAR   1    'Y' OR 'N'       *
      *                                                              *
      ****************************************************************
     FPTC@PF  UF  E           K        DISK                      A
     I           SDS
     I                                      244 253 WJOB
     I                                      282 2870WTIME
     I           UDS
     I                                      101 110 WUSER
     C           KEYC@     KLIST                           *PTC@PF
     C                     KFLD           WC@01  10
     C                     KFLD           WC@02  60
      *==============================================================*
      *                    MAIN PROGRAM                              *
      *==============================================================*
     C           *ENTRY    PLIST
     C                     PARM           P6701I 10
     C                     PARM           P6702I 60
     C                     PARM           P6703I  1
     C                     PARM           P6711O  1
      *
     C                     CALL 'R@S005'
     C                     PARM 'GIANT/PT'S0051I  8
      *
     C                     EXSR P67CHK
      *
     C                     RETRN
      *==============================================================*
      *          P67CHK....CHECK VALUE FOR WRITE_IN OR DELET_OUT     *
      *==============================================================*
     C           P67CHK    BEGSR
      *
     C                     MOVE P6701I    WC@01
     C                     MOVE P6702I    WC@02
     C           KEYC@     SETLLC@0                      40*EQ
     C           *IN40     IFEQ '1'                        *RCD FOUND
     C           P6703I    IFEQ 'W'
     C                     MOVE 'N'       P6711O           *ERR
     C                     ELSE
     C                     EXSR P67DLT                     *DELETE DATA
     C                     MOVE 'Y'       P6711O           *RIGHT
     C                     END
     C                     ELSE                            *RCD NOT FOUND
     C           P6703I    IFEQ 'D'
     C                     MOVE 'N'       P6711O           *ERR
     C                     ELSE
     C                     EXSR P67WRT                     *WRT DATA
     C                     MOVE 'Y'       P6711O           *RIGHT
     C                     END
     C                     END
      *
     C                     ENDSR
      *==============================================================*
      *          P67WRT....WRITE_IN                                  *
      *==============================================================*
     C           P67WRT    BEGSR
      *
     C                     MOVE P6701I    C@01
     C                     MOVE P6702I    C@02
     C                     MOVE WUSER     C@03
     C                     MOVE WJOB      C@04
     C                     Z-ADDWTIME     C@05
      *
     C                     WRITEC@0
      *
     C                     ENDSR
      *==============================================================*
      *          P67DLT....DELETE_OUT                                *
      *==============================================================*
     C           P67DLT    BEGSR
      *
     C                     MOVE P6701I    WC@01
     C                     MOVE P6702I    WC@02
     C           KEYC@     CHAINC@0                  40    *NF
     C           *IN40     IFEQ '0'                        *RCD FOUND
     C                     DELETC@0
     C                     END
      *
     C                     ENDSR
