      ****************************************************************
      * PROGRAM ID : P22                                             *
      * SYSTEM ID  : PTPLIB                                          *
      * FUNTCTION  :計算該月日數                                   *
      *                                                              *
      * PARAMETER : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             01   P2201I    I   *DEC    4 0  YYMM(中國年月) *
      *             02   P2211O    O   *CHAR   1   驗証結果...     *
      *                                             Y=OK , N =ERROR  *
      *             03   P2212O    O   *DEC    2 0 該月日數        *
      *                                                              *
      * W-VARABLE : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             01   P2201C    N   *DEC    2 0 中國年          *
      *                  P2201     N   *DEC    4 0 西元年          *
      *             02   P2202(P22)N   *DEC    2 0 月份            *
      *             03   P2203     N   *DEC    2 0 日期            *
      *             04   P2204     N   *DEC    5 0 商數            *
      *             05   P2205     N   *DEC    4 0 餘數            *
      *             06   P2206     N   *CHAR   1   潤年(Y/N)       *
      *             07   P2207     N   *CHAR   1   驗証(Y/N)       *
      *             08   P22DAY,1  N   *DEC    2 0 DAYS PER MONTH    *
      *                                                            *
      *             19   P22DAY,12 N   *DEC    2 0 DAYS PER MONTH    *
      *                                                              *
      *                                                              *
      * LEAP YEAR CALCULATION RULE......                             *
      *                        (Y)                                   *
      *                         +- P2206=Y                           *
      *             MVR 400 =0  |            (Y)                     *
      *  LEAP YEAR -------------+             +- P2206=N             *
      *                         | MVR 100 =0  |         (Y)          *
      *                         +-------------+          +- P2206=Y  *
      *                        (N)            | MVR 4 =0 |           *
      *                                       +----------+           *
      *                                      (N)         |           *
      *                                                  +- P2206=N  *
      *                                                 (N)          *
      *                                                              *
      ****************************************************************
     E                    P22DAY 12  12  2 0
     IP220DS      DS
     I                                        1   20P2201C
     I                                        3   40P2202
      *--------------------------------------------------------------*
      *                    MAIN PROGRAM                              *
      *--------------------------------------------------------------*
     C           *ENTRY    PLIST
     C                     PARM           P2201I  40
     C                     PARM           P2211O  1
     C                     PARM           P2212O  20
     C                     MOVE P2201I    P220DS
     C                     MOVE P2201C    P2201   40
     C                     ADD  1911      P2201
      *
     C                     CALL 'R@S005'
     C                     PARM 'GIANT/PT'S0051I  8
      *
     C                     EXSR P22LEP
     C                     EXSR P22CKD
      *
     C                     MOVE P2207     P2211O
     C                     Z-ADDP22NUM    P2212O
     C                     RETRN
      *--------------------------------------------------------------*
      *          P22LEP....VERIFY LEAP YEAR                          *
      *--------------------------------------------------------------*
     C           P22LEP    BEGSR
     C           P2201     DIV  400       P2204   50
     C                     MVR            P2205   40
     C           P2205     IFEQ 0
     C                     MOVE 'Y'       P2206   1
     C                     ELSE
     C*
     C           P2201     DIV  100       P2204
     C                     MVR            P2205
     C           P2205     IFEQ 0
     C                     MOVE 'N'       P2206
     C                     ELSE
     C*
     C           P2201     DIV  4         P2204
     C                     MVR            P2205
     C           P2205     IFEQ 0
     C                     MOVE 'Y'       P2206
     C                     ELSE
     C                     MOVE 'N'       P2206
     C                     END
     C*
     C                     END
     C*
     C                     END
     C                     ENDSR
      *--------------------------------------------------------------*
      *          P22CKD....CHECK MONTH & DATE                        *
      *--------------------------------------------------------------*
     C           P22CKD    BEGSR
     C           P2202     IFGT 12
     C           P2202     OREQ 0
     C                     MOVE 'N'       P2207   1
     C                     Z-ADD0         P22NUM  20
     C                     ELSE
     C*
     C                     MOVE 'Y'       P2207
     C                     Z-ADDP2202     P22     20
     C                     Z-ADDP22DAY,P22P22NUM
     C*
     C           P2202     IFEQ 2
     C           P2206     ANDEQ'Y'
     C                     Z-ADD29        P22NUM
     C                     END
     C*
     C                     END
     C                     ENDSR
**
312831303130313130313031
