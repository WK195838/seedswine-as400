      ****************************************************************
      * PROGRAM ID : P01                                             *
      * SYSTEM ID  : PTPLIB                                          *
      * FUNTCTION  :驗証西元年月日                                 *
      *                                                              *
      * PARAMETER : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             01   P0101I    I   *CHAR   8    YYYYMMDD(西元年)
      *             02   P0111O    O   *CHAR   1   驗証結果...     *
      *                                             Y=OK , N =ERROR  *
      *                                                              *
      * W-VARABLE : SEQ  FIELD    I/O  TYPE   LEN   DESCRIPTION      *
      *             ===  =======  ===  =====  ====  ===========      *
      *             01   P0101     N   *DEC    4 0 西元年          *
      *             02   P0102(P01)N   *DEC    2 0 月份            *
      *             03   P0103     N   *DEC    2 0 日期            *
      *             04   P0104     N   *DEC    5 0 商數            *
      *             05   P0105     N   *DEC    4 0 餘數            *
      *             06   P0106     N   *CHAR   1   潤年(Y/N)       *
      *             07   P0107     N   *CHAR   1   驗証(Y/N)       *
      *             28   P01DAY,1  N   *DEC    2 0 DAYS PER MONTH    *
      *                                                            *
      *             39   P01DAY,12 N   *DEC    2 0 DAYS PER MONTH    *
      *                                                              *
      *                                                              *
      * LEAP YEAR CALCULATION RULE......                             *
      *                        (Y)                                   *
      *                         +- P0106=Y                           *
      *             MVR 400 =0  |            (Y)                     *
      *  LEAP YEAR -------------+             +- P0106=N             *
      *                         | MVR 100 =0  |         (Y)          *
      *                         +-------------+          +- P0106=Y  *
      *                        (N)            | MVR 4 =0 |           *
      *                                       +----------+           *
      *                                      (N)         |           *
      *                                                  +- P0106=N  *
      *                                                 (N)          *
      *                                                              *
      ****************************************************************
     E                    P01DAY 12  12  2 0
     IP010DS      DS
     I                                        1   40P0101
     I                                        5   60P0102
     I                                        7   80P0103
      *--------------------------------------------------------------*
      *                    MAIN PROGRAM                              *
      *--------------------------------------------------------------*
     C           *ENTRY    PLIST
     C                     PARM           P0101I  80
     C                     PARM           P0111O  1
     C                     MOVE P0101I    P010DS           .FOR DATASRUC
      *
     C                     CALL 'R@S005'
     C                     PARM 'GIANT/PT'S0051I  8
      *
     C                     EXSR P01LEP
     C                     EXSR P01CKD
      *
     C                     MOVE P0107     P0111O
     C                     RETRN
      *--------------------------------------------------------------*
      *          P01LEP....VERIFY LEAP YEAR                          *
      *--------------------------------------------------------------*
     C           P01LEP    BEGSR
     C           P0101     DIV  400       P0104   50
     C                     MVR            P0105   40
     C           P0105     IFEQ 0
     C                     MOVE 'Y'       P0106   1
     C                     ELSE
     C*
     C           P0101     DIV  100       P0104
     C                     MVR            P0105
     C           P0105     IFEQ 0
     C                     MOVE 'N'       P0106
     C                     ELSE
     C*
     C           P0101     DIV  4         P0104
     C                     MVR            P0105
     C           P0105     IFEQ 0
     C                     MOVE 'Y'       P0106
     C                     ELSE
     C                     MOVE 'N'       P0106
     C                     END
     C*
     C                     END
     C*
     C                     END
     C                     ENDSR
      *--------------------------------------------------------------*
      *          P01CKD....CHECK MONTH & DATE                        *
      *--------------------------------------------------------------*
     C           P01CKD    BEGSR
     C           P0102     IFGT 12
     C           P0102     OREQ 0
     C           P0103     OREQ 0
     C                     MOVE 'N'       P0107   1
     C                     ELSE
     C*
     C                     Z-ADDP0102     P01     20
     C           P0103     IFGT P01DAY,P01
     C                     MOVE 'N'       P0107
     C                     ELSE
     C                     MOVE 'Y'       P0107
     C                     END
     C*
     C           P0102     IFEQ 2
     C           P0106     ANDEQ'Y'
     C           P0103     ANDLE29
     C                     MOVE 'Y'       P0107
     C                     ELSE
     C                     END
     C*
     C                     END
     C                     ENDSR
**
312831303130313130313031
