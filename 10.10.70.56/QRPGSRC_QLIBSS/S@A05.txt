      ****************************************************************
      *   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
      *                                                              *
      *    PROGRAM ID   : S@A05                                      *
      *    WRITTEN BY   : A1046   JOHNSON CHEN                       *
      *    DATE WRITTEN : 79/02/10                                   *
      *    UPDATE DATE  :                                            *
      *    SYSTEM       :                                            *
      *    SUBSYSTEM    : S@                                         *
      *    PROCEDURE    :                                            *
      *    PURPOSE      :程式使用權設定－依程式別                  *
      ****************************************************************
     FS@SUPF  UF  E           K        DISK                      A
     FS@SPPF  IF  E           K        DISK
     FS@SULF01IF  E           K        DISK
     FS@A05D  CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
      *
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
     C           KEYSU     KLIST
     C                     KFLD           WSU01  10
     C                     KFLD           WSU02  10
     C                     KFLD           WSU03  10
     C           KEYSUL    KLIST
     C                     KFLD           LSU02  10
     C                     KFLD           LSU01  10
     C                     KFLD           LSU03  10
      *==============================================================*
      *                    MAIN ROUTINE
      *==============================================================*
     C                     EXSR RTN010                     *INITIAL
     C           *IN03     DOWEQ'0'
      *
     C           SCID      CASEQ'SC01'    RTN100           *SCREEN 2
     C           SCID      CASEQ'SC02'    RTN200           *SCREEN 1
     C                     END
      *
     C                     END
     C                     SETON                     LR
      *==============================================================*
      *          RTN010....INTRIAL VALUE
      *==============================================================*
     C           RTN010    BEGSR
     C                     MOVEL'SC02'    SCID    4
     C                     MOVEL*BLANK    DSU02
     C                     MOVEL*BLANK    DSU03
     C                     MOVEL*BLANK    WSU01
     C                     MOVEL*BLANK    WSU02
     C                     MOVEL*BLANK    WSU03
     C                     MOVEL*BLANK    LSU01
     C                     MOVEL*BLANK    LSU02
     C                     MOVEL*BLANK    LSU03
     C                     ENDSR
      *==============================================================*
      *          RTN200....'SC02' SCREEN MAIN ROUTINE
      *==============================================================*
     C           RTN200    BEGSR
     C                     EXFMTDSPC1
      *: CLEAR ALL ERROR USED INDICATOR :
     C                     MOVEA*ALL'0'   *IN,60
      *
     C           *IN03     IFEQ '0'
     C                     EXSR RTN210
     C           *IN99     IFEQ '0'
     C                     MOVE 'SC01'    SCID
     C                     END
     C                     END
      *
     C           END200    ENDSR
      *==============================================================*
      *   RTN210 : SC01 SCREEN ERROR CHECK ROUTINE
      *==============================================================*
     CSR         RTN210    BEGSR
      *
     C           DSU02     IFEQ *BLANK
     C                     SETON                     996098
     C                     GOTO END210
     C                     ELSE
     C           DSU02     CHAINSP0                  40
     C           *IN40     IFEQ '1'
     C                     SETON                     609799
     C                     ELSE
     C                     MOVELSP02      DSP02
     C                     MOVELSP03      DSP03
     C                     MOVELSP04      DSP04
     C                     MOVELSP05      DSP05   1
     C           *LOVAL    SETLLSP0
     C                     END
     C                     END
     C   99                GOTO END210
      *
     C           END210    ENDSR
      *=============================================================*
      *          RTN100 :  SUBFILE ROUTINE                          *
      *=============================================================*
     C           RTN100    BEGSR
     C                     EXSR RTN110                     *WRITE SFL
     C                     EXSR RTN120                     *EXFMT SFL
      ***********
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
      ****
     C           *IN25     IFEQ '0'
     C           *IN26     ANDEQ'0'
     C           RRN       ANDGT0
     C                     EXSR RTN130                     *CHECK SFL
      *
     C           *IN99     IFEQ '0'
     C                     EXSR RTN140                     *PROCESS
     C  N80                SETON                     12
     C                     END
      *
     C                     ELSE
      *
     C           *IN25     IFEQ '1'                        *ROLLUP
     C                     SETON                     97
     C                     Z-ADDRRN       DRRN             *LAST PAGE  LL
     C                     END
      *
     C           *IN26     IFEQ '1'                        *ROLLDOWN
     C                     SETON                     98
     C                     SETOF                     53
     C                     Z-ADD1         DRRN             *FIRST PAGE LL
     C                     END
     C                     END
      *
     C  N12                EXSR RTN120                     *EXFMT SFL
     C                     END
      ***********
     C                     MOVE 'SC02'    SCID
     C                     ENDSR
      *=========================================================*
      *          RTN110 :  INITIAL                              *
      *=========================================================*
     C           RTN110    BEGSR
      *: INTRIAL VALUE :
     C                     Z-ADD0         RRN
     C                     Z-ADD1         WRRN    30
     C                     Z-ADD1         DRRN
      *: CLEAR SFL DATA :
     C                     SETON                     52
     C                     WRITESFLCR
     C                     SETOF                     5254
      *
     C                     MOVELDSU02     LSU02
     C                     MOVEL*LOVAL    LSU01
     C           KEYSUL    SETLLSUL1                 40
     C  N40      DSU02     READESUL1                     40
      *****WRITE SUBFILE
     C           RRN       DOWLT200
     C                     ADD  1         RRN
     C           *IN40     IFEQ '0'
     C                     MOVE SU04      DOPT
     C                     MOVELSU01      DSU01
     C                     Z-ADDRRN       TRRN    30
     C                     SETON                     29
     C                     ELSE
     C*          DSP05     IFEQ 'N'
     C                     MOVE 'Y'       DOPT
     C*                    ELSE
     C*                    MOVE 'N'       DOPT
     C*                    END
     C                     MOVE *BLANK    DSU01
     C                     SETOF                     29
     C                     END
      *
     C                     WRITESFLSR                    53
      *****
     C           *IN53     IFEQ '0'
     C  N40      DSU02     READESUL1                     40
     C                     END
      *
     C                     END
      ***********
     C                     ENDSR
      *==============================================================*
      *          RTN120... EXFMT SFL                                 *
      *==============================================================*
     C           RTN120    BEGSR
      *: CHECK SFL DATA EXIST :
     C           RRN       IFGT 0
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     92    *NO RECORD
     C                     END
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC11
     C                     EXFMTSFLCR
     C                     SETOF                     5051
     C                     MOVEA*ALL'0'   *IN,60
      *
     C                     ENDSR
      *==============================================================*
      *          RTN130....SCREEN CHECK
      *==============================================================*
     C           RTN130    BEGSR
     C                     Z-ADD1         DRRN
     C                     Z-ADD1         RRN
      *
     C                     READCSFLSR                    40
     C                     SETON                     54
     C                     SETOF                     6263
      *********************
     C           *IN40     DOWEQ'0'
     C           RRN       IFGT TRRN
     C                     SETOF                     29
     C                     ELSE
     C                     SETON                     29
     C                     END
      *
     C           *IN99     IFEQ '1'
     C                     GOTO UPD130
     C                     END
      ***********
     C           DSU01     IFNE *BLANK
     C                     CALL 'S@S11C'
     C                     PARM           CKVAL   1
     C                     PARM           DSU01  10
      *
     C           CKVAL     IFEQ '1'
     C                     SETON                     996296
     C                     Z-ADDRRN       DRRN
     C                     GOTO UPD130
     C                     END
     C                     END
      *****
     C           UPD130    TAG
     C                     UPDATSFLSR
     C                     READCSFLSR                    40
     C                     SETON                     54
     C                     SETOF                     6263
     C                     END
      ***********
     C           END130    ENDSR
      *==============================================================*
      *          RTN140 :  PROCESS FILE
      *==============================================================*
     C           RTN140    BEGSR
     C                     Z-ADD1         RRN
     C                     READCSFLSR                    40
      *
     C           *IN40     DOWEQ'0'
      ***********
     C           DSU01     IFNE *BLANK
     C                     CALL 'S@S09C'
     C                     PARM           DOPT
     C                     PARM           DSP03
     C                     PARM           DSU02
     C                     PARM           DSU01
     C                     PARM           COR     1
     C           COR       IFEQ 'Y'
     C                     MOVELDSU01     WSU01
     C                     MOVELDSU02     WSU02
     C                     MOVELDSP03     WSU03
     C           KEYSU     CHAINSU0                  41
     C                     MOVELWSU01     SU01
     C                     MOVELWSU02     SU02
     C                     MOVELWSU03     SU03
     C                     MOVELDOPT      SU04
     C                     Z-ADDDDATE     SUYY
     C                     MOVELUSER      SUZZ
     C*          DOPT      IFEQ 'Y'
     C           *IN41     IFEQ '1'
     C                     WRITESU0
     C                     ELSE
     C                     UPDATSU0
     C                     END
     C*                    ELSE
     C*          *IN41     IFEQ '0'
     C*                    DELETSU0
     C*                    END
     C*                    END
     C                     SETOF                     626354
     C                     ELSE
     C  N80                Z-ADDRRN       DRRN
     C                     SETON                     548063
     C                     SETON                     6264
     C                     END
     C                     END
     C                     UPDATSFLSR
     C                     READCSFLSR                    40
     C                     END
      ***********
     C           END140    ENDSR
