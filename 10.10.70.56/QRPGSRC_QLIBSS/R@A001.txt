     F****************************************************************
     F*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     F*                                                              *
     F*    PROGRAM NAME : R@A001                                     *
     F*    AUTHOR       : A1062   JEANNY LIN                         *
     F*    CREATE DATE  : 79/05/11                                   *
     F*    UPDATE DATE  :                                            *
     F*    SYSTEM       :應用程式版權保護系統                      *
     F*    PURPOSE      :應用程式版權維護                          *
     F****************************************************************
     FR@RAPF  UF  E           K        DISK                      A
     FR@A001D CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLSR
     E                    WFUN    5   5  6
     E                    W2     35  35  1
     E                    W3A         8  1
     E                    W3B         8  1
     I            DS
     I                                        1   60DRA05
     I                                        1   6 WRA05
     I            DS
     I                                        1   70DRA07
     I                                        1   7 WRA07
     I            DS
     I                                        1  80 DRA06
     I                                        1   2 DRA601
     I                                        3   4 DRA602
     I                                        5   6 DRA603
     I                                        7   8 DRA604
     I                                        9  10 DRA605
     I                                       11  12 DRA606
     I                                       13  14 DRA607
     I                                       15  16 DRA608
     I                                       17  18 DRA609
     I                                       19  20 DRA610
     I                                       21  22 DRA611
     I                                       23  24 DRA612
     I                                       25  26 DRA613
     I                                       27  28 DRA614
     I                                       29  30 DRA615
     I                                       31  32 DRA616
     I                                       33  34 DRA617
     I                                       35  36 DRA618
     I                                       37  38 DRA619
     I                                       39  40 DRA620
     I                                       41  42 DRA621
     I                                       43  44 DRA622
     I                                       45  46 DRA623
     I                                       47  48 DRA624
     I                                       49  50 DRA625
     I                                       51  52 DRA626
     I                                       53  54 DRA627
     I                                       55  56 DRA628
     I                                       57  58 DRA629
     I                                       59  60 DRA630
     I                                       61  62 DRA631
     I                                       63  64 DRA632
     I                                       65  66 DRA633
     I                                       67  68 DRA634
     I                                       69  70 DRA635
     I                                       71  72 DRA636
     I                                       73  74 DRA637
     I                                       75  76 DRA638
     I                                       77  78 DRA639
     I                                       79  80 DRA640
     I           UDS
     I                                      101 110 USER
     I                                      111 1160DDATE
     C*==============================================================*
     C*                    KEY LIST FOR R@RAPF
     C*==============================================================*
     C           KEYRA     KLIST
     C                     KFLD           DRA01   2
     C                     KFLD           DRA02   1
     C*==============================================================*
     C*                    MAIN ROUTINE
     C*==============================================================*
     C                     EXSR RTN010                     *INIT RTN.
     C           *IN03     DOWEQ'0'
     C*
     C           SCID      CASEQ'SC01'    RTN100
     C           SCID      CASEQ'SC02'    RTN200
     C           SCID      CASEQ'SC03'    RTN300
     C                     END
     C*
     C                     END
     C                     SETON                     LR
     C*==============================================================*
     C*          RTN010....INITIAL VALUE
     C*==============================================================*
     C           RTN010    BEGSR
     C                     MOVE 'SC01'    SCID    4
     C                     MOVE *BLANK    DRA01
     C                     MOVE *BLANK    DRA02
     C                     MOVE '1'       DOPT
     C                     ENDSR
     C*==============================================================*
     C*          RTN100....'SC01' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           RTN100    BEGSR
     C                     EXFMTDSPC1
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'                        *03 OFF
     C*
     C           *IN18     IFEQ '0'                        *18 OFF
     C                     EXSR RTN110                     *SCR CHK
     C  N99                MOVE 'SC02'    SCID
     C                     ELSE                            *18 ON
     C                     MOVE 'SC03'    SCID
     C                     END                             *18 END
     C*
     C                     END                             *03 END
     C                     ENDSR
     C*==============================================================*
     C*          RTN110....SCREEN CHECK
     C*==============================================================*
     C           RTN110    BEGSR
     C*CHECK SCREEN
     C           DRA01     IFEQ *BLANK
     C                     SETON                     609899
     C                     END
     C   99                GOTO END110
     C*
     C           DOPT      IFNE '1'
     C           DRA02     ANDEQ*BLANK
     C                     SETON                     619899
     C                     END
     C   99                GOTO END110
     C*
     C           DOPT      IFEQ '1'
     C           DRA02     ANDNE*BLANK
     C                     SETON                     619799
     C                     END
     C   99                GOTO END110
     C*CHAIN DATA
     C           KEYRA     CHAINRA0                  40    *NF
     C           DOPT      IFEQ '1'
     C           *IN40     IFEQ '0'
     C                     SETON                     606196
     C                     SETON                     99
     C                     END
     C                     ELSE
     C           *IN40     IFEQ '1'
     C                     SETON                     606195
     C                     SETON                     99
     C                     END
     C                     END
     C*
     C           END110    ENDSR
     C*==============================================================*
     C*          RTN200....'SC02' SCREEN MAIN ROUTINE
     C*==============================================================*
     C           RTN200    BEGSR
     C*
     C                     MOVE DOPT      WNUM    10
     C                     MOVE WFUN,WNUM DFUN
     C*
     C                     EXSR RTN201                     *CLR FLDS.
     C           DOPT      CASEQ'1'       RTN210           *ADD
     C           DOPT      CASEQ'2'       RTN220           *UPD
     C           DOPT      CASEQ'4'       RTN230           *DEL
     C           DOPT      CASEQ'5'       RTN240           *INQ
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN201....CLEAR FIELDS
     C*==============================================================*
     C           RTN201    BEGSR
     C                     MOVE *BLANK    DRA03
     C                     MOVE *BLANK    DRA04
     C                     Z-ADD0         DRA05
     C                     MOVE *BLANK    DRA06
     C                     Z-ADD0         DRA07
     C                     MOVE *BLANK    DRA08
     C                     MOVE *BLANK    DRA09
     C                     ENDSR
     C*==============================================================*
     C*          RTN210....ADD DATA
     C*==============================================================*
     C           RTN210    BEGSR
     C                     SETON                     31    *CAN INPUT
     C                     EXSR RTN211                     *SET RA02
     C*
     C           *IN03     DOWEQ'0'                        *CTL LOOP
     C           SCID      ANDEQ'SC02'
     C                     EXFMTDSPC2
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'                        *03 12 OFF
     C           *IN12     ANDEQ'0'
     C                     EXSR RTN250                     *CHK SCR
     C*
     C           *IN06     IFEQ '0'                        *10 OFF
     C           *IN99     ANDEQ'0'                        *99 OFF
     C                     EXSR RTN270                     *SRC TO PF
     C                     WRITERA0
     C           *LOVAL    SETLLRA0
     C                     MOVE 'SC01'    SCID
     C                     END                             *99 END
     C*
     C                     END                             *03 12 END
     C*
     C   12                MOVE 'SC01'    SCID
     C                     END                             *LOOP END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN211....SET SEQUENCE NUMBER
     C*==============================================================*
     C           RTN211    BEGSR
     C           DRA01     SETGTRA0
     C                     READPRA0                      47*BOF
     C           *IN47     IFEQ '1'                        *1
     C           DRA01     ORNE RA01
     C                     MOVE '1'       DRA02
     C                     ELSE
     C                     Z-ADD1         WN      20
     C           RA02      LOKUPW2,WN                    31*EQ
     C*
     C           *IN31     IFEQ '1'                        *2
     C                     ADD  1         WN
     C                     MOVE W2,WN     DRA02
     C                     END                             *2
     C*
     C                     END                             *1
     C           *LOVAL    SETLLRA0
     C                     ENDSR
     C*==============================================================*
     C*          RTN220....UPD DATA
     C*==============================================================*
     C           RTN220    BEGSR
     C*
     C                     EXSR RTN260                     *PF TO SCR
     C                     SETON                     31    *CAN INPUT
     C*
     C           *IN03     DOWEQ'0'                        *CTL LOOP
     C           SCID      ANDEQ'SC02'
     C                     EXFMTDSPC2
     C                     MOVEA*ALL'0'   *IN,60
     C*
     C           *IN03     IFEQ '0'                        *03 12 OFF
     C           *IN12     ANDEQ'0'
     C                     EXSR RTN250                     *CHK SCR
     C*
     C           *IN06     IFEQ '0'                        *10 OFF
     C           *IN99     ANDEQ'0'                        *99 OFF
     C                     EXSR RTN270                     *SRC TO PF
     C                     UPDATRA0
     C           *LOVAL    SETLLRA0
     C                     MOVE 'SC01'    SCID
     C                     END                             *99 END
     C*
     C                     END                             *03 12 END
     C*
     C   12                MOVE 'SC01'    SCID
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN230....DEL DATA
     C*==============================================================*
     C           RTN230    BEGSR
     C*
     C                     EXSR RTN260                     *PF TO SCR
     C                     SETOF                     31    *NO INPUT
     C                     EXFMTDSPC2
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     ANDEQ'0'
     C                     DELETRA0
     C                     END
     C*
     C  N03                MOVE 'SC01'    SCID
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN240....INQ DATA
     C*==============================================================*
     C           RTN240    BEGSR
     C*
     C                     EXSR RTN260                     *PF TO SCR
     C                     SETOF                     31    *NO INPUT
     C                     EXFMTDSPC2
     C*
     C  N03                MOVE 'SC01'    SCID
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN250....CHECK 'SC02' SCREEN
     C*==============================================================*
     C           RTN250    BEGSR
     C*
     C           DRA03     IFEQ *BLANK
     C                     SETON                     609899
     C                     ELSE
     C                     EXSR RTN251
     C                     END
     C   99                GOTO END250
     C*
     C           DRA04     IFEQ *BLANK
     C                     SETON                     619899
     C                     END
     C   99                GOTO END250
     C*
     C           DRA05     IFEQ 0
     C                     SETON                     629799
     C                     END
     C   99                GOTO END250
     C*
     C           DRA05     IFNE 999999
     C                     CALL 'P04'
     C                     PARM DRA05     P041I   60
     C                     PARM           P041O   1
     C           P041O     IFEQ 'N'
     C                     SETON                     629699
     C                     END
     C                     END
     C   99                GOTO END250
     C*
     C           DRA08     IFEQ *BLANK
     C                     SETON                     639899
     C                     END
     C   99                GOTO END250
     C*
     C                     CALL 'R@S001'
     C                     PARM DRA03     S0011I  8
     C                     PARM DRA04     S0012I  4
     C                     PARM WRA05     S0013I  6
     C                     PARM DRA06     S0014I 80
     C           WRA07     PARM *BLANK    S0011O  7
     C*
     C           END250    ENDSR
     C*==============================================================*
     C*          RTN251....MOVE RIGHT
     C*==============================================================*
     C           RTN251    BEGSR
     C                     MOVEADRA03     W3A
     C                     MOVEA*BLANK    W3B
     C                     Z-ADD8         WA      10
     C                     Z-ADD8         WB      10
     C           WA        DOWGT0
     C           W3A,WA    IFNE *BLANK
     C                     MOVE W3A,WA    W3B,WB
     C                     SUB  1         WB
     C                     END
     C                     SUB  1         WA
     C                     END
     C                     MOVEAW3B       DRA03
     C           END251    ENDSR
     C*==============================================================*
     C*          RTN260....PF TO SCREEN
     C*==============================================================*
     C           RTN260    BEGSR
     C*
     C                     MOVE RA03      DRA03
     C                     MOVE RA04      DRA04
     C                     MOVE RA05      DRA05
     C                     MOVE RA06      DRA06
     C                     MOVE RA07      DRA07
     C                     MOVE RA08      DRA08
     C                     MOVE RA09      DRA09
     C*
     C           DOPT      IFEQ '5'
     C           *LOVAL    SETLLRA0
     C                     END
     C*
     C                     ENDSR
     C*==============================================================*
     C*          RTN270....SCREEN TO PF
     C*==============================================================*
     C           RTN270    BEGSR
     C*
     C                     MOVE DRA01     RA01
     C                     MOVE DRA02     RA02
     C                     MOVE DRA03     RA03
     C                     MOVE DRA04     RA04
     C                     MOVE DRA05     RA05
     C                     MOVE DRA06     RA06
     C                     MOVE DRA07     RA07
     C                     MOVE DRA08     RA08
     C                     MOVE DRA09     RA09
     C*
     C                     ENDSR
     C*=============================================================*
     C*          RTN300....SFL MAIN ROUTINE
     C*=============================================================*
     C           RTN300    BEGSR
     C                     SETOF                     12
     C           *IN03     DOWEQ'0'
     C           *IN12     ANDEQ'0'
     C*
     C           *IN25     IFEQ '0'
     C           *IN26     ANDEQ'0'
     C                     EXSR RTN310                     *INIT RTN.
     C                     EXSR RTN320                     *ADD SFL
     C                     END
     C*
     C           *IN25     IFEQ '1'                        *25 ON
     C                     SETON                     97    *LAST PAGE
     C                     Z-ADDRRN       DRRN
     C                     END
     C*
     C           *IN26     IFEQ '1'                        *26 ON
     C                     SETON                     98    *FIRST PAGE
     C                     Z-ADD1         DRRN
     C                     END                             *26 OFF
     C*
     C                     EXSR RTN330                     *EXFMT
     C                     END
     C*
     C   12                MOVE 'SC01'    SCID
     C                     ENDSR
     C*=============================================================*
     C*          RTN310....INITIAL
     C*=============================================================*
     C           RTN310    BEGSR
     C*INITIAL VALUES
     C                     Z-ADD*ZEROS    RRN
     C                     Z-ADD*ZEROS    DRRN
     C*SFL CLEAR
     C                     SETON                     52
     C                     WRITESFLCR
     C                     SETOF                     5253
     C*SETLL DATA
     C           KEYRA     SETLLRA0
     C                     ENDSR
     C*=============================================================*
     C*          RTN320....ADD SUBFILE DATA
     C*=============================================================*
     C           RTN320    BEGSR
     C           *IN53     DOWEQ'0'                        *53 DO
     C                     READ RA0                      46*NF
     C*
     C           *IN46     IFEQ '0'                        *46 IF
     C                     ADD  1         RRN
     C                     WRITESFLSR
     C                     ELSE                            *46 ELSE
     C                     SETON                     53
     C                     END                             *46 END
     C*
     C                     END                             *53 END
     C*
     C           *LOVAL    SETLLRA0
     C                     Z-ADD1         DRRN
     C                     ENDSR
     C*=============================================================*
     C*          RTN330....EXFMT DSPF
     C*=============================================================*
     C           RTN330    BEGSR
     C           RRN       IFGT *ZEROS
     C                     SETON                     50    *SFLDSP
     C                     ELSE
     C                     SETON                     96    *NO DATA
     C                     END
     C*
     C                     SETON                     51    *SFLDSPCTL
     C                     WRITEDSPC3
     C                     EXFMTSFLCR
     C                     MOVEA*ALL'0'   *IN,60
     C                     SETOF                     5051
     C                     ENDSR
**
新增修改      刪除查詢
**
123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ
